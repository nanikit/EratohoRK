;==================================================
;
;	범용 함수 두는 곳＠참조편
;		참조에 이용해 특정의 수치나 명칭 따위를 돌려주는 것
;		혹은, 상황에 따른 접두어 따위를 돌려주는 것
;
;		구상전용의 것도 있거나 않기도 하고.
;
;==================================================

;==================================================
;
;	TEQUIP·ITEM 관련의 참조
;
;==================================================

;-------------------------------------------------
;함수명:EQUIP_CARESS
;개  요점:지속 애무 장비품 취득 함수
;인  수:ARGS… 애무 종별을 넣으면 일치를 본다. /단락으로 복수 지정 가능. 생략 할까 공문자열이라면 TEQUIP에 의한 애무를 모두 표시.
;			  클리캡·오나홀등 지속 애무를 가져오지 않기 때문에 포함하지 않는다.
;       ARG… 대상의 캐릭터(생략 하면 조교 대상. 현재 조교 대상만 서포트)
;반환값:일치를 보는 경우, 일치하지 않으면 공문자열. 일치했는지 생략 했을 경우, 애무에 관련되는 TEQUIP 모든 것을/단락으로 나란해질 수 있던 것
;비  고:식 나카노세키수
;-------------------------------------------------
@EQUIP_CARESS(ARGS, ARG = -2)
#FUNCTIONS
#LOCALSSIZE 1
ARG = ARG != -2 ? ARG # MASTER
VARSET LOCALS
SIF TEQUIP:바이브
	LOCALS += "바이브/"
SIF TEQUIP:애널바이브
	LOCALS += "애널 바이브/"
SIF TEQUIP:애널비즈
	LOCALS += "애널 비즈/"
SIF TEQUIP:관장기＋플러그
	LOCALS += "관장기＋플러그/"
SIF TEQUIP:니플캡
	LOCALS += "니플캡/"
SIF TEQUIP:삼각목마
	LOCALS += "삼각 목마/"
SIF GETBIT(TEQUIP:자위중, 0)
	LOCALS += "자위중/"
IF TEQUIP:조교자페니스밴드
	SIF RANGE(TEQUIP:성교중, 1, 5) && TEQUIP:성교중 != 3
		LOCALS += "페니반 V성교중/"
	SIF TEQUIP:성교중 == 3
		LOCALS += "페니반 승마위중/"
	SIF TEQUIP:성교중 == 6
		LOCALS += "페니반 A성교중/"
ELSE
	SIF RANGE(TEQUIP:성교중, 1, 5) && TEQUIP:성교중 != 3
		LOCALS += "V성교중/"
	SIF TEQUIP:성교중 == 3
		LOCALS += "승마위중/"
	SIF TEQUIP:성교중 == 6
		LOCALS += "A성교중/"
ENDIF
IF TEQUIP:페니스밴드
	SIF RANGE(TEQUIP:성교봉사중, 1, 5) && TEQUIP:성교봉사중 != 3
		LOCALS += "페니반 V성교 봉사중/"
	SIF TEQUIP:성교봉사중 == 3
		LOCALS += "페니반역강간중/"
	SIF TEQUIP:성교봉사중 == 6
		LOCALS += "페니반 A성교 봉사중/"
ELSE
	SIF RANGE(TEQUIP:성교봉사중, 1, 5) && TEQUIP:성교봉사중 != 3
		LOCALS += "V성교 봉사중/"
	SIF TEQUIP:성교봉사중 == 3
		LOCALS += "역강간중/"
	SIF TEQUIP:성교봉사중 == 6
		LOCALS += "A성교 봉사중/"
ENDIF
IF PENIS(MASTER)
	SIF TEQUIP:조교대상C사용 == 1
		LOCALS += "자위중/"
	SIF TEQUIP:조교대상C사용 == 2
		LOCALS += "페라중/"
ELSE
	SIF TEQUIP:조교대상C사용 == 1
		LOCALS += "애무중/"
	SIF TEQUIP:조교대상C사용 == 2
		LOCALS += "쿤닐링구스중/"
ENDIF

SIF LOCALS == ""
	RETURNF ""

LOCALS = SUBSTRINGU(LOCALS, 0, STRLENSU(LOCALS)-1)
RETURNF \@ ARGS == "" || STRFIND(@"/%LOCALS%/", @"/%ARGS%/") == -1 ? LOCALS # \@


;-------------------------------------------------
;함수명:EFFECT
;개  요점:약제 따위의 효과를 나타내는 함수
;인  수:ARG:0… 약제의 효과량(TEQUIP)
;       ARG:1…0이라면 약중 강, 1이라면 소중다를 돌려준다
;반환값:효과의 크기를 나타내는 문자
;       10이상은 강, 5이상은 안, 5 미만은 미만
;비  고:식 나카노세키수
;-------------------------------------------------
@EFFECT(ARG:0, ARG:1)
#FUNCTIONS
SELECTCASE ARG:0
	CASE IS > 9
		RETURNF @"\@ ARG:1 ? 다# 강\@"
	CASE IS > 4
		RETURNF "중"
	CASEELSE
		RETURNF @"\@ ARG:1 ? 소# 미만\@"
ENDSELECT


;-------------------------------------------------
;함수명:GETINSERT
;개  요점:성교 상태를 나타내는 함수
;인  수:없음
;반환값:성교중=1, 성교 봉사중=2, 조수 성교중=3, 조수 성교 봉사중=4
;비  고:식 나카노세키수
;-------------------------------------------------
@GETINSERT(ARG:0, ARG:1)
#FUNCTION
SIF TEQUIP:성교중
	RETURNF 1
SIF TEQUIP:성교봉사중
	RETURNF 2
SIF TEQUIP:조수성교중
	RETURNF 3
SIF TEQUIP:조수성교봉사중
	RETURNF 4
RETURNF 0

;-------------------------------------------------
;함수명:EQUIPACTION_DESCRIPTION
;개  요점:TEQUIP 관계의 조교자의 행동 묘사
;인  수:없음
;반환값:TEQUIP 상태를 나타내는 캐릭터 라인
;비  고:식 나카노세키수
;사용예:%EQUIPACTION_DESCRIPTION()%
;-------------------------------------------------
@EQUIPACTION_DESCRIPTION
#FUNCTIONS
IF TEQUIP:안면승마 || TEQUIP:안면승마애널
	RETURNF @"%CALLNAME:MASTER%의 얼굴 위에 걸치면서"
ELSEIF TEQUIP:자위중
	SELECTCASE TEQUIP:자위중
		CASE 1
			RETURNF @"%CALLNAME:MASTER%가 자신을 위로하는 모양을 감상하면서"
		CASE 2
			RETURNF @"%CALLNAME:MASTER%에 자위를 과시하면서"
		CASE 3
			RETURNF @"%CALLNAME:MASTER%가 자신을 위로하는 모양을 감상하면서"
	ENDSELECT
ELSEIF TEQUIP:성교중
	RETURNF @"허리를 흔들어%CALLNAME:MASTER%를 밀어올리면서"
ELSEIF TEQUIP:성교봉사중
	IF TEQUIP:페니스밴드
		RETURNF @"완구의 페니스를\@ TEQUIP:성교봉사중 == 6 ? 애널# 질내\@로 맛보면서"
	ELSEIF TEQUIP:성교봉사중 == 6
		RETURNF @"애널에 삽입시킨 페니스를 단단히 조이면서"
	ELSE
		RETURNF @"질내의 페니스를 탐내면서"
	ENDIF
ENDIF


;-------------------------------------------------
;함수명:ITEM_TYPE
;개  요점:조교 도구 분류 함수
;인  수:ARG…ITEM 번호
;반환값:도구 분류 번호
;       (0. 비표시/1. 성구/2. 고문도구/3. 그 외)
;비  고:식 나카노세키수
;조교 도구 분류 테이블. 조교 도구의 분류 번호를 돌려준다.
;조교 도구 정돈 표시에 이용한다
;
;이름이 약간 DITEMTYPE라고 까다로울지도 모른다
;R#사양으로 변경@revkoishi(14/05/19)
;-------------------------------------------------
@ITEM_TYPE(ARG)
#FUNCTION
SELECTCASE ARG
	;0. 비표시
	CASE 9, IS >= 50
		RETURNF 0
	;1. 성구
	CASE 0 TO 3, 11 TO 14
		RETURNF 1
	;2. 고문도구
	CASE 20 TO 22, 30 TO 33, 40, 42
		RETURNF 2
ENDSELECT
;3. 그 외(현재미사용)
RETURNF 3


;-------------------------------------------------
;함수명:ITEM_TYPENAME
;개  요점:조교 도구 분류 나토리 이득 함수
;인  수:ARG… 조교 도구 분류 번호
;반환값:조교 도구 분류를 나타내는 캐릭터 라인
;       (0. 비표시/1. 성구/2. 고문도구/3. 그 외)
;비  고:식 나카노세키수
;조교 도구의 분류명을 돌려준다. 조교 도구 정돈 표시에 이용한다
;
;R#사양으로 변경@revkoishi(14/05/19)
;-------------------------------------------------
@ITEM_TYPENAME(ARG:0)
#FUNCTIONS
#LOCALSSIZE 10
SIF !STRLENS(LOCALS:0)
	SPLIT "비표시/성구/고문도구/그 외", "/", LOCALS:0, LOCAL:0
RETURNF LOCALS:(ARG:0)


;-------------------------------------------------
;함수명:GET_ITEMNAME
;개  요점:조교 도구 나토리 이득 함수
;인  수:ARG:0…ITEM 번호
;반환값:조교 도구명
;비  고:식 나카노세키수
;조교 도구의 이름을 돌려준다. 커스터마이즈 후의 이름의 처리 따위를 실시한다
;
;신규 작성@revkoishi(14/05/22)
;-------------------------------------------------
@GET_ITEMNAME(ARG:0)
#FUNCTIONS
SELECTCASE ARG:0
	CASE 1, 11 TO 14	;바이브/애널 바이브/클리캡/니플캡/오나홀
		RETURNF \@ ITEM:커스텀도구 ? 특주 # \@ + ITEMNAME:(ARG:0)
ENDSELECT
RETURNF ITEMNAME:(ARG:0)


;==================================================
;
;	방침·상태 변화 관련의 참조
;
;==================================================

;-------------------------------------------------
;함수명:CONDITION
;개  요점:상태치로부터 캐릭터 라인을 취득하는 함수
;인  수:ARG… 상태 플래그(TCVAR:상태 변화)
;반환값:상태를 나타내는 캐릭터 라인
;비  고:식 나카노세키수
;-------------------------------------------------
@CONDITION(ARG)
#FUNCTIONS
#LOCALSSIZE 10
SIF !STRLENS(LOCALS)
	SPLIT "통상/피폐/쇠약/무기력/몽롱/정욕/분노/싫증/울적", "/", LOCALS, LOCAL
RETURNF LOCALS:ARG

;-------------------------------------------------
;함수명:IS_CONDITION
;개  요점:ARG가 ARGS 상태라면 진을 돌려준다
;인  수:ARG… 캐릭터 등록 번호. 생략 하면 TARGET
;반환값:그 상태라면 1, 다르면 0
;비  고:식 나카노세키수
;-------------------------------------------------
@IS_CONDITION(ARG = -2, ARGS)
#FUNCTION
ARG = ARG != -2 ? ARG # TARGET
RETURNF ARGS == CONDITION(TCVAR:ARG:상태변화)

;-------------------------------------------------
;함수명:GET_CONDITION
;개  요점:ARG 상태 변화명을 취득
;인  수:ARG… 캐릭터 등록 번호. 생략 하면 TARGET
;반환값:상태를 나타내는 캐릭터 라인
;비  고:식 나카노세키수
;-------------------------------------------------
@GET_CONDITION(ARG = -2)
#FUNCTIONS
ARG = ARG != -2 ? ARG # TARGET
RETURNF CONDITION(TCVAR:ARG:상태변화)

;-------------------------------------------------
;함수명:T_COND
;개  요점:TARGET가 지정한 상태라면 진을 돌려준다
;인  수:ARGS… 상태명
;반환값:그 상태라면 1, 다르면 0
;비  고:식 나카노세키수
;-------------------------------------------------
@T_COND(ARGS)
#FUNCTION
RETURNF ARGS == CONDITION(TCVAR:상태변화)

;-------------------------------------------------
;함수명:M_COND
;개  요점:MASTER가 지정한 상태라면 진을 돌려준다
;인  수:ARGS… 상태명
;반환값:그 상태라면 1, 다르면 0
;비  고:식 나카노세키수
;-------------------------------------------------
@M_COND(ARGS)
#FUNCTION
RETURNF ARGS == CONDITION(TCVAR:MASTER:상태변화)

;-------------------------------------------------
;함수명:POLICY
;개  요점:조교 방침을 역당겨 하는 함수
;인  수:ARGS… 어느 방침인가(TODAY/오늘, NOW/지금, LAST/전)
;		ARG …ARGS에 오늘의 방침·이번 방침을 넣었을 경우 수치로부터 취할 수 있다
;반환값:방침명
;비  고:식 나카노세키수
;-------------------------------------------------
@POLICY(ARGS, ARG)
#FUNCTIONS
#LOCALSSIZE 20
;전반 6개는 단기 방침, 후반은 「오늘의 방침」
SPLIT "통상/휴게/소프트/노멀/하드/이상/통상/애널/벌/하드/러브러브/이상/야외/아이 만들기/휴게/안아/봉사/귀여워해", "/", LOCALS, LOCAL
IF ARGS == "오늘의 방침"
	RETURNF LOCALS:(ARG+6)
ELSEIF ARGS == "이번 방침"
	RETURNF LOCALS:ARG
ENDIF
SELECTCASE ARGS
	CASE "오늘", "TODAY"
		RETURNF LOCALS:(TFLAG:오늘의방침+6)
	CASE "지금", "NOW"
		RETURNF LOCALS:(TFLAG:턴방침)
	CASE "전", "LAST"
		RETURNF LOCALS:(TFLAG:전턴방침)
	CASEELSE
		CALLF PRINT_ERRORMESSAGE(@"@POLICY 에 무효인 방침 지정(%ARGS%)가 건네받았습니다")
		;종별무지정을 돌려준다
		RETURNF "종별무지정"
ENDSELECT

;-------------------------------------------------
;함수명:IS_POLICY
;개  요점:조교 방침 명칭과 합치하면 진을 돌려준다
;인  수:ARGS:0… 어느 방침인가(TODAY/오늘, NOW/지금, LAST/전)
;		ARGS:1… 방침 명칭(/단락으로 복수 대입이 가능합니다)
;반환값:진위치(1/0)
;비  고:식 나카노세키수
;-------------------------------------------------
@IS_POLICY(ARGS:0, ARGS:1)
#FUNCTION
#LOCALSSIZE 10
#DIMS LTEMPS
LTEMPS = %POLICY(ARGS)%
SPLIT ARGS:1, "/", LOCALS, LOCAL
IF LTEMPS == "종별무지정"
	CALLF PRINT_ERRORMESSAGE(@"@IS_POLICY 에 무효인 방침 지정(%ARGS%)가 건네받았습니다")
ELSEIF FINDELEMENT(LOCALS, LTEMPS, 0, LOCAL, 1) != -1
	RETURNF 1
ENDIF
RETURNF 0

;-------------------------------------------------
;함수명:SATIS_BONUS
;개  요점:오늘의 만족 보너스로부터 캐릭터 라인을 취득하는 함수
;인  수:ARG… 플래그(TCVAR:오늘의 만족 보너스)
;반환값:만족 보너스를 나타내는 캐릭터 라인
;비  고:식 나카노세키수
;-------------------------------------------------
@SATIS_BONUS(ARG)
#FUNCTIONS
#LOCALSSIZE 20
SIF !STRLENS(LOCALS)
	SPLIT "통상/애널/벌/하드/러브러브/이상/야외/아이 만들기/휴게/안아/봉사/귀여워해", "/", LOCALS, LOCAL
RETURNF LOCALS:ARG

;-------------------------------------------------
;함수명:IS_SATIS_BONUS
;개  요점:ARGS의 만족 보너스라면 진을 돌려준다
;인  수:ARGS… 만족 보너스명
;반환값:그 상태라면 1, 다르면 0
;비  고:식 나카노세키수
;-------------------------------------------------
@IS_SATIS_BONUS(ARGS)
#FUNCTION
RETURNF ARGS == SATIS_BONUS(TCVAR:오늘의만족보너스)

;-------------------------------------------------
;함수명:GET_SATIS_BONUS
;개  요점:오늘의 만족 보너스명을 취득
;인  수:없음
;반환값:만족 보너스를 나타내는 캐릭터 라인
;비  고:식 나카노세키수
;-------------------------------------------------
@GET_SATIS_BONUS
#FUNCTIONS
RETURNF SATIS_BONUS(TCVAR:오늘의만족보너스)

;-------------------------------------------------
;함수명:TENSION
;개  요점:BASE치를 대략적으로 나타내는 함수
;인  수:ARG:0… 캐릭터 등록 번호
;       ARG:1…BASE 번호
;반환값:BASE치를 대략적으로 나타내는 캐릭터 라인
;       50%이상은 높은,25%이상은 일반,25% 미만은 낮다
;비  고:식 나카노세키수
;조교자의 이성이나 만족 따위를 문자로 표현하는 경우에 사용
;-------------------------------------------------
@TENSION(ARG:0, ARG:1)
#FUNCTIONS
#LOCALSIZE 1
LOCAL = MAX(BASE:(ARG:0):(ARG:1), 0)
SIF LOCAL < MAXBASE:(ARG:0):(ARG:1) / 4
	RETURNF "낮다"
RETURNF @"\@ LOCAL < MAXBASE:(ARG:0):(ARG:1) / 2 ? 일반# 높다\@"

;-------------------------------------------------
;함수명:COM_RANK
;개  요점:REACT 부하로부터 캐릭터 라인을 취득하는 함수
;인  수:ARG…(TFLAG:REACT 부하)
;반환값:REACT 부하를 나타내는 캐릭터 라인
;-------------------------------------------------
@COM_RANK(ARG)
#FUNCTIONS
#LOCALSSIZE 10
SIF !STRLENS(LOCALS)
	SPLIT "자연스러운 행동/무리가 없는 행동/무리를 한 행동", "/", LOCALS, LOCAL
RETURNF LOCALS:ARG

;-------------------------------------------------
;함수명:GET_COM_RANK
;개  요점:REACT 부하 명칭과 합치하면 진을 돌려준다
;인  수:없음
;반환값:REACT 부하를 나타내는 캐릭터 라인
;비  고:식 나카노세키수
;-------------------------------------------------
@GET_COM_RANK()
#FUNCTIONS
RETURNF COM_RANK(TFLAG:REACT부하)


;==================================================
;
;	시스템 데이터 관련의 참조
;
;==================================================

;-------------------------------------------------
;기입 체크 함수
;	인수 0:표시 확률. 생략 하면 100%. 텐프레에서는 최초부터 0이 들어가 있다
;-------------------------------------------------
@구상기입체크(ARG = 100)
#FUNCTION
RETURNF PERCENT(ARG) ? ARG # 0

;-------------------------------------------------
;함수명:CAPACITY
;개  요점:있는 캐릭터의 수를 돌려준다
;인  수:없음
;반환값:있는 캐릭터의 수(MASTER를 포함한다)
;비  고:식 나카노세키수
;현상, 일시 퇴피중이 없기 때문에, CHARANUM와 아무것도 변함없다
;-------------------------------------------------
@CAPACITY
#FUNCTION
RETURNF CMATCH(CFLAG:92, 0)

;-------------------------------------------------
;함수명:GET_DAY
;개  요점:요일을 나타내는 캐릭터 라인을 취득하는 함수
;인  수:없음
;반환값:요일을 나타내는 캐릭터 라인(날/월/화/수/목/금/흙)
;비  고:식 나카노세키수
;-------------------------------------------------
@GET_DAY
#FUNCTIONS
#LOCALSSIZE 10
SIF !STRLENS(LOCALS)
	SPLIT "일/월/화/수/목/금/흙", "/", LOCALS, LOCAL
RETURNF LOCALS:(DAY % 7)

;-------------------------------------------------
;함수명:GET_SEASON
;개  요점:계절을 나타내는 캐릭터 라인을 취득하는 함수
;인  수:없음
;반환값:계절을 나타내는 캐릭터 라인(봄/하/추/겨울)
;비  고:식 나카노세키수
;-------------------------------------------------
@GET_SEASON
#FUNCTIONS
SWAP LOCAL:1, RESULT
SIF !STRLENS(LOCALS)
	SPLIT "봄/하/추/겨울", "/", LOCALS, LOCAL
RETURNF LOCALS:(FLAG:계절)

;-------------------------------------------------
;함수명:GET_TIME
;개  요점:시간을 나타내는 캐릭터 라인을 취득하는 함수
;인  수:없음
;반환값:시간을 나타내는 캐릭터 라인(낮/밤)
;비  고:식 나카노세키수
;-------------------------------------------------
@GET_TIME
#FUNCTIONS
RETURNF @"\@ TIME ? 밤# 낮\@"


