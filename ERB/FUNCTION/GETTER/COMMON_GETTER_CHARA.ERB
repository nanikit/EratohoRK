;==================================================
;
;	범용 함수 두는 곳＠참조편
;		참조에 이용해 특정의 수치나 명칭 따위를 돌려주는 것
;		혹은, 상황에 따른 접두어 따위를 돌려주는 것
;
;		구상전용의 것도 있거나 않기도 하고.
;
;==================================================


;==================================================
;
;	TALENT, 그 외 캐릭터 관련의 참조
;
;==================================================

;-------------------------------------------------
;캐릭터 사이 통칭 취득 함수@CNAME
;식중에서 사용하는 함수입니다. TARGET가 인수 0의 캐릭터를 부르는 통칭을 표시합니다. 생략 하면 MASTER
;인수 1을 넣으면 TARGET 이외로부터도 참조할 수 있습니다.
;빈번하게 불리므로 RESULT를 보호합니다.
;이런 완고한 처리를 이름 부를 때마다 실시하는 부디에 대해서는 우선 모른다. 뭐 무겁고 같은거 안 된다고 믿는다
;-------------------------------------------------
@CNAME(ARG = -2, ARG:1 = -2)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM LCOUNT, 1
#DIMS DYNAMIC 경칭대상, 100
#DIMS DYNAMIC 경칭내역, 100
#DIMS DYNAMIC 통칭대상, 100
#DIMS DYNAMIC 통칭내역, 100

;인수 생략의 처리
ARG = ARG != -2 ? ARG # MASTER
ARG:1 = ARG:1 != -2 ? ARG:1 # TARGET

;기본경칭의 처리
SPLIT CSTR:(ARG:1):기본경칭, "/", 경칭대상, LOCAL
FOR LCOUNT, 0, LOCAL
	경칭내역:LCOUNT = %REPLACE(REPLACE(SUBSTRING(경칭대상:LCOUNT, STRFIND(경칭대상:LCOUNT, "_")+1), "이름", NAME:ARG), "통칭", CALLNAME:ARG)%
	경칭대상:LCOUNT = %SUBSTRING(경칭대상:LCOUNT,0, STRFIND(경칭대상:LCOUNT, "_")-1)%
NEXT

;통칭의 분류
SPLIT CSTR:(ARG:1):호칭목록, "/", 통칭대상, LOCAL
FOR LCOUNT, 0, LOCAL
	통칭내역:LCOUNT = %REPLACE(REPLACE(SUBSTRING(통칭대상:LCOUNT, STRFIND(통칭대상:LCOUNT, "_")+1), "이름", NAME:ARG), "통칭", CALLNAME:ARG)%
	통칭대상:LCOUNT = %SUBSTRING(통칭대상:LCOUNT,0, STRFIND(통칭대상:LCOUNT, "_")-1)%
NEXT

;일시적으로 NAME를 넣는다
LOCALS = %NAME:ARG%
$NAMELESS_LOOP
;통칭 대상으로 있는 경우
IF MATCH(통칭대상, LOCALS)
	FOR LCOUNT, 0, 100
		IF 통칭대상:LCOUNT == LOCALS
			LOCALS = %통칭내역:LCOUNT%
			BREAK
		ENDIF
	NEXT
;대요정·소악마의 경우
ELSEIF LOCALS == "대요정" || LOCALS == "소악마"
	;그대로는 부를 수 없기 때문에 당신으로 해 한번 더 돌린다
	LOCALS = 당신
	GOTO NAMELESS_LOOP
;기본경칭의 처리
ELSE
	;CALLNAME로 한다
	LOCALS = %CALLNAME:ARG%
	;경칭 대상 검색
	FOR LCOUNT, 0, 100
		SELECTCASE 경칭대상:LCOUNT
			CASE ""
				BREAK
			CASE "기본"
				LOCALS = %경칭내역:LCOUNT%
			CASEELSE
				SIF TALENT:ARG:(경칭대상:LCOUNT)
					LOCALS = %경칭내역:LCOUNT%
		ENDSELECT
	NEXT
ENDIF
;RESULT를 복원
RESULT = LOCAL
RETURNF LOCALS

;-------------------------------------------------
;페니스 판정 함수@PENIS
;식중에서 사용하는 함수입니다. 페니스가 있는 경우 1을 돌려줍니다
;인수 0은 대상의 등록 번호. 시시한 내용
;인수의 캐릭터가 없는 경우 0으로 돌려주도록(듯이)했습니다
;
;↑의 사양은 정직 어떻게이겠지라고 생각하지 않는 것도 아니지만, 우선 하한 밖에 보지 않았기 때문에 수정@revkoishi(14/04/20)
;-------------------------------------------------
@PENIS(ARG)
#FUNCTION
RETURNF INRANGE(ARG, 0, CHARANUM - 1) ? TALENT:ARG:후타나리 || TALENT:ARG:남자 # 0
;RETURNF ARG < 0? 0 # TALENT:ARG:후타나리 || TALENT:ARG:남자

;-------------------------------------------------
;페니스밴드 판정 함수@PENISBAND
;식중에서 사용하는 함수입니다. 페니스밴드 사용중 1을 돌려줍니다
;파기 예정
;-------------------------------------------------
@PENISBAND(ARG)
#FUNCTION
RETURNF !(TALENT:ARG:후타나리 || TALENT:ARG:남자) && ITEM:페니스밴드

;-------------------------------------------------
;아라이먼트 판정 함수@ALI
;식중에서 사용하는 함수입니다. 아라이먼트가 인수 이상이라면 실로 됩니다
;인수 0은 아라이먼트의 수치. 0이라면 아라이먼트정취급
;인수 1을 생략 하면 TARGET. 지정하면 그 캐릭터를 봅니다. (FLAG:주인님)와 지정하는 것으로 EV111등에서도 사용할 수 있습니다
;-------------------------------------------------
@ALI(ARG, ARG:1 = -2)
#FUNCTION
SIF ARG:1 == -2
	ARG:1 = TARGET
RETURNF CFLAG:(ARG:1):아라이먼트 >= ARG

;-------------------------------------------------
;호감도 판정 함수@FAVOR
;식중에서 사용하는 함수입니다. 호감도가 지정 이상이라면 실로 됩니다. 다만, 0으로 했을 경우는 조건에 포함하지 않습니다
;인수 0은 조교 대상으로부터 조교자, 인수 1은 조교자로부터 조교 대상의 호감도입니다
;인수 2를 지정했을 경우, 조교자 대신에 그 캐릭터（주로 조수）를 봅니다
;-------------------------------------------------
@FAVOR(ARG, ARG:1, ARG:2 = -2)
#FUNCTION
SIF ARG:2 == -2
	ARG:2 = TARGET
RETURNF (!ARG || CFLAG:(ARG:2):M호의 >= ARG) && (!ARG:1 || CFLAG:(ARG:2):호의 >= ARG:1)

;-------------------------------------------------
;호감도 참조 함수@FAVOR2
;식중에서 사용하는 함수입니다. 호감도가 지정 이상이라면 실로 됩니다. 다만, 생략(-123456) 했을 경우는 호감도의 수치 자체를 돌려줍니다
;TARGET-ASSI간의 호감도는 존재하지 않기 때문에, 인수에 MASTER가 포함되지 않는 경우는 0으로 돌아옵니다
;-------------------------------------------------
@FAVOR2(ARG, ARG:1, ARG:2 = -123456)
#FUNCTION
;인수 부적당
SIF ARG < 0 || ARG:1 < 0 || !GROUPMATCH(MASTER, ARG, ARG:1)
	RETURNF 0
;반응을 일으키는 최소의 물리량이 있다
IF ARG:2 != -123456
	RETURNF ARG == MASTER ? CFLAG:(ARG:1):M호의 >= ARG:2 # CFLAG:ARG:호의 >= ARG:2
;반응을 일으키는 최소의 물리량이 없다
ELSE
	RETURNF ARG == MASTER ? CFLAG:(ARG:1):M호의 # CFLAG:ARG:호의
ENDIF

;-------------------------------------------------
;베이스 비율 산출 함수@BASERATIO
;식중에서 사용하는 함수입니다. 베이스치가 MAXBASE의 무엇％있을까 돌려줍니다
;인수 0은 등록 번호, 인수 1은 베이스 번호, 인수 2를 설정했을 경우는 그％이상이라면 진을 돌려줍니다
;-------------------------------------------------
@BASERATIO(ARG:0, ARG:1, ARG:2)
#FUNCTION
RETURNF ARG:2 ? BASE:(ARG:0):(ARG:1) * 100 / MAXBASE:(ARG:0):(ARG:1) >= ARG:2 # BASE:(ARG:0):(ARG:1) * 100 / MAXBASE:(ARG:0):(ARG:1)

;-------------------------------------------------
;소질 검색 함수@FINDTALENT
;식중에서 사용하는 함수입니다. 지정한 소질을 가지는 캐릭터를 검색합니다. 최초로 발견된 캐릭터의 등록 번호를 돌려줍니다
;없었던 경우, -1을 돌려줍니다
;인수는 캐릭터 라인입니다. 수치를 사용하는 경우는 TALENTNAME:xx를 인수로 해 불러 주세요
;-------------------------------------------------
@FINDTALENT(ARGS)
#FUNCTION
#LOCALSIZE 1
;FINDCHARA를 사용하고 싶은… 이지만 진위 판정을 할 수 없기 때문에 FOR를 돌린다
FOR LOCAL, 0, CHARANUM
	SIF TALENT:LOCAL:ARGS
		RETURNF LOCAL
NEXT
RETURNF -1

;-------------------------------------------------
;조수 번호 검색 함수@ASSISNO
;조수인 누군가가 지정한 캐릭터 번호라면, 그 조수의 등록 번호를 돌려줍니다.
;없는 경우, -1이 됩니다
;-------------------------------------------------
@ASSISNO(ARG)
#FUNCTION
#LOCALSIZE 1
FOR LOCAL, 0, 4
	SIF ASSI:LOCAL >= 0 && NO:(ASSI:LOCAL) == ARG
		RETURNF ASSI:LOCAL
NEXT
RETURNF -1
;-------------------------------------------------
;조수 소질 검색 함수@ASSISTALENT
;조수인 누군가가 지정한 TALENT를 인수 1의 값이상으로 가지고 있으면, 그 조수의 등록 번호를 돌려줍니다.
;없는 경우, -1이 됩니다
;-------------------------------------------------
@ASSISTALENT(ARG, ARG:1 = 1)
#FUNCTION
#LOCALSIZE 1
FOR LOCAL, 0, 4
	SIF ASSI:LOCAL >= 0 && TALENT:(ASSI:LOCAL):ARG >= ARG:1
		RETURNF ASSI:LOCAL
NEXT
RETURNF -1

;-------------------------------------------------
;조수 능력 검색 함수@ASSISABL
;조수인 누군가가 지정한 ABL를 인수 1의 값이상으로 가지고 있으면, 그 조수의 등록 번호를 돌려줍니다.
;없는 경우, -1이 됩니다
;-------------------------------------------------
@ASSISABL(ARG, ARG:1 = 1)
#FUNCTION
#LOCALSIZE 1
FOR LOCAL, 0, 4
	SIF ASSI:LOCAL >= 0 && ABL:(ASSI:LOCAL):ARG >= ARG:1
		RETURNF ASSI:LOCAL
NEXT
RETURNF -1

;-------------------------------------------------
;함수명:ASSIID
;개  카나메:조수 ID취득 함수
;인  수:없음
;반환값:ASSI로서 다루어지고 있는 조수의 ID(1～3)
;비  고:식 안의 함수
;ASSI에 들어가 있는 캐릭터(=구상주)가 조수 1이나 조수 2나 조수 3인지를 판별한다
;조수 구상으로 사용할지도
;-------------------------------------------------
@ASSIID
#FUNCTION
#LOCALSIZE 1
SIF ASSI < 0
	RETURNF 0
FOR LOCAL, 1, 4
	SIF ASSI == ASSI:LOCAL
		RETURNF LOCAL
NEXT

;-------------------------------------------------
;소질 판정 함수@GET_TALENT
;인수의 캐릭터가가 지정한 TALENT를 가지고 있으면, TALENT의 수치를 돌려줍니다.
;없는 경우, 0이 됩니다
;TALENT의 지정은 캐릭터 라인으로 실시합니다만, 번호에서도 할 수 있습니다. 그 경우""포위로 캐릭터 라인으로서 숫자를 넣는 것
;-------------------------------------------------
@GET_TALENT(ARG, ARGS)
#FUNCTION
#LOCALSIZE 1
IF GETNUM(TALENT, ARGS) >= 0
	LOCAL = GETNUM(TALENT, ARGS)
ELSE
	LOCAL = TOINT(ARGS)
ENDIF
RETURNF ARG >= 0 ? TALENT:(ASSI:1):LOCAL # 0


;-------------------------------------------------
;함수명:ABL_TYPE
;개  요점:능력 분류 함수
;인  수:ARG:0…ABL 번호
;반환값:능력 분류 번호
;       (0. 비표시/1. 기초/2. 감각/3. 성벽/4. 중독/5. 조교/6. 미분류)
;비  고:식 안의 함수
;능력 분류 테이블. 능력의 분류 번호를 돌려준다
;현재 분류에 밖에 사용하지 않았다
;
;신규 작성@revkoishi(14/05/26)
;-------------------------------------------------
@ABL_TYPE(ARG:0)
#FUNCTION
SELECTCASE ARG:0
	;0. 비표시(표시시키지 않는 능력) ※특별한 이유가 없는 한은 불필요한 테두리
	;CASE -1
	;	RETURNF 0
	;1. 기초
	CASE 0 TO 2
		RETURNF 1
	;2. 감각
	CASE 3 TO 6
		RETURNF 2
	;3. 성벽
	CASE 7 TO 11
		RETURNF 3
	;4. 중독
	CASE 12 TO 16
		RETURNF 4
	;5. 조교
	CASE 20 TO 28
		RETURNF 5
ENDSELECT
;6. 미분류(이것이 있는 것은 바람직하지 않다)
RETURNF 6


;-------------------------------------------------
;함수명:EXP_TYPE
;개  요점:경험 분류 함수
;인  수:ARG:0…EXP 번호
;반환값:경험 분류 번호
;       (0. 비표시/1. 쾌락/2. 절정/3. 상황/4. 능동/5. 수동/6. 이상/7. 미분류)
;비  고:식 안의 함수
;경험 분류 테이블. 경험의 분류 번호를 돌려준다
;현재 분류에 밖에 사용하지 않았다
;
;신규 작성@revkoishi(14/05/26)
;-------------------------------------------------
@EXP_TYPE(ARG:0)
#FUNCTION
SELECTCASE ARG:0
	;0. 비표시(표시시키지 않는 경험) ※특별한 이유가 없는 한은 불필요한 테두리
	;CASE -1
	;	RETURNF 0
	;1. 쾌락
	CASE 0 TO 3, 21, 30, 31
		RETURNF 1
	;2. 절정
	CASE 4 TO 6, 14
		RETURNF 2
	;3. 상황
	CASE 7, 20, 25, 26, 32, 40, 41
		RETURNF 3
	;4. 능동
	CASE 8, 10, 22 TO 24, 63
		RETURNF 4
	;5. 수동
	CASE 11 TO 13, 51, 60 TO 62
		RETURNF 5
	;6. 이상
	CASE 50, 52, 53, 55
		RETURNF 6
ENDSELECT
;7. 미분류(이것이 있는 것은 바람직하지 않다)
RETURNF 7


;-------------------------------------------------
;함수명:GET_SEX
;개  요점:성별 취득 함수
;인  수:ARG:0… 캐릭터 등록 번호
;반환값:성별을 나타내는 캐릭터 라인
;비  고:식 안의 함수
;
;GET_TALENTNAME를 사용하도록(듯이) 변경@revkoishi(14/04/28)
;-------------------------------------------------
@GET_SEX(ARG)
#FUNCTIONS
SIF TALENT:ARG:후타나리
	RETURNF GET_TALENTNAME(ARG, GETNUM(TALENT, "후타나리"))
RETURNF @"\@ TALENT:ARG:남자 ? %GET_TALENTNAME(ARG, GETNUM(TALENT, "남자"))% # 여자 아이 \@"


;-------------------------------------------------
;함수명:GET_TALENTNAME
;개  요점:소질 나토리 이득 함수
;인  수:ARG:0… 캐릭터 등록 번호
;　　　:ARG:1…TALENT 번호
;반환값:소질명
;비  고:식 안의 함수
;소질의 이름을 돌려준다. 상위 소질명의 처리 따위를 실시한다
;
;햇빛싫어함과 독심술의 처리를 추가, SQR분을 comment out@revkoishi(14/04/20)
;-------------------------------------------------
@GET_TALENTNAME(ARG:0, ARG:1)
#FUNCTIONS
SELECTCASE ARG:1
	;CASE 0		;처녀/처녀(재생)
	;	RETURNF TALENTNAME:0 + \@ ! INRANGE(TALENT:ARG:처녀, 0, 1) ? (재생) # \@
	;CASE 109	;거유/폭유
	;	RETURNF \@ TALENT:ARG:거유 == 2? 폭유 # %TALENTNAME:109% \@
	CASE 119	;햇빛싫어함/일광 골칫거리
		RETURNF "일광" + \@ TALENT:ARG:햇빛싫어함 == 2 ? 골칫거리 # 싫다 \@
	CASE 132	;(독심술) 공기를 읽는 정도의 능력/마음을 읽는 정도의 능력
		RETURNF \@ TALENT:ARG:독심술 == 2 ? 마음 # 공기 \@ + "를 읽는 정도의 능력"
ENDSELECT
RETURNF TALENTNAME:(ARG:1)



;-------------------------------------------------
;함수명:TALENT_TYPE
;개  요점:소질 분류 함수
;인  수:ARG…TALENT 번호
;반환값:소질 분류 번호
;       (0. 비표시/1. 성별/2. 성격/3. 체질/4. 기능/5. 후천 소질/6. 종족 특성/7. 미분류)
;비  고:식 안의 함수
;소질 분류 테이블. 소질의 분류 번호를 돌려준다
;소질 정돈 표시에 이용한다
;
;휘침성분을 추가, SQR분을 삭제, TALENT:220～222를 잠정적으로 비표시에@revkoishi(14/04/20)
;-------------------------------------------------
@TALENT_TYPE(ARG)
#FUNCTION
SELECTCASE ARG
	;0. 비표시(표시시키지 않는 소질) ※기본적으로는 SQR 유래범위인 것으로, 특별한 이유가 없는 한은 불필요한 테두리
	;CASE 220 TO 222	;TALENT:220～222에 데이터가 들어가는 문제가 회복되었으므로 comment out로@revkoishi(14/05/19)
	;	RETURNF 0
	;1. 성별
	CASE 0, 1, 121, 122
		RETURNF 1
	;2. 성격
	CASE 10 TO 17, 20 TO 37, 63, 65, 80 TO 88, 90, 92, 93
		RETURNF 2
	;3. 체질
	CASE 40 TO 43, 56, 60 TO 62, 64, 70, 71, 100 TO 114, 116, 118, 119, 123 TO 126
		RETURNF 3
	;4. 기능
	CASE 50 TO 53, 55, 57 TO 59, 66, 91, 117, 130, 132, 133, 135
		RETURNF 4
	;5. 후천 소질
	CASE 2, 3, 72 TO 79, 89, 131, 140
		RETURNF 5
	;6. 종족 특성
	CASE 54, 115, 119, 134, 136 TO 138
		RETURNF 6
ENDSELECT
;7. 미분류(이것이 있는 것은 바람직하지 않다)
RETURNF 7

;-------------------------------------------------
;함수명:TALENT_TYPENAME
;개  요점:소질 분류 나토리 이득 함수
;인  수:ARG…TALENT 분류 번호
;반환값:소질 분류를 나타내는 캐릭터 라인
;       (0. 비표시/1. 성별/2. 성격/3. 체질/4. 기능/5. 후천 소질/6. 종족 특성/7. 미분류)
;비  고:식 안의 함수
;소질의 분류명을 돌려준다. 소질 정돈 표시에 이용한다
;-------------------------------------------------
@TALENT_TYPENAME(ARG)
#FUNCTIONS
#LOCALSSIZE 10
SIF !STRLENS(LOCALS)
	SPLIT "비표시/성별/성격/체질/기능/후천 소질/종족 특성/미분류", "/", LOCALS, LOCAL
RETURNF LOCALS:ARG


;-------------------------------------------------
;주인님 수식 함수@TARGET_DESCRIPTION
;식중에서 사용하는 함수입니다. TARGET가 어떠한 상태인지를 묘사합니다… 우선 계속계에게만 대응
;인수는 어미. 「,」 「(이)면서」 「」 「라고」 「개개」 「」등에 대응
;-------------------------------------------------
@TARGET_DESCRIPTION(ARGS)
#FUNCTIONS
VARSET LOCALS
;성교중(조교자 삽입)
IF TEQUIP:성교중
	IF TEQUIP:조교자페니스밴드
		LOCALS += "몸에 댄 의욕형을"
	ELSE
		LOCALS += @"\@ TCVAR:TARGET:갈것같다 ? 당장갈것같다인 # \@페니스를"
	ENDIF
	;체위에 의해 분기
	SELECTCASE TEQUIP:성교중
		;정상위
		CASE 1
			LOCALS += @"체중을 맡겨%CALLNAME:MASTER%의 질내에 묻고"
		;후배위
		CASE 2
			LOCALS += @"배후로부터%CALLNAME:MASTER%의 질내에 묻고"
		;기승위
		CASE 3
			LOCALS += @"%CALLNAME:MASTER%의 암컷 구멍에 근원까지 물어지고"
		;대면좌위
		CASE 4
			LOCALS += @"밀착했다%CALLNAME:MASTER%의 질에 애무되고"
		;배면좌위
		CASE 5
			LOCALS += @"%CALLNAME:MASTER%에 삽입해 등을 껴안고"
		;애널섹스
		CASE 6
			LOCALS += @"%CALLNAME:MASTER%의 항문에 묻고"
	ENDSELECT
;자위·애무 계속중
ELSEIF TEQUIP:조교대상Ｃ사용 == 1
	LOCALS += @"\@ TCVAR:MASTER:갈것같다 ? 당장갈것같다인 # \@"
	LOCALS += @"%CALLNAME:MASTER%의\@ PENIS(MASTER) ? 페니스 # \@"
	SIF !PENIS(MASTER)
		LOCALS += @"\@ TEQUIP:페니스밴드 ? 의욕형 # 클리토리스 \@"
	LOCALS += "를 애무해"
;페라·쿤닐링구스 계속중
ELSEIF TEQUIP:조교대상Ｃ사용 == 2
	LOCALS += @"\@ TCVAR:MASTER:갈것같다 ? 당장갈것같다인 # \@%CALLNAME:MASTER%의\@ PENIS(MASTER) ? 페니스 # 클리토리스 \@를 빨고"
;성교중(조교 대상 삽입)
ELSEIF TEQUIP:성교봉사중
	IF TEQUIP:페니스밴드
		LOCALS += @"%CALLNAME:MASTER%의 몸에 댄 의욕형을"
	ELSE
		LOCALS += @"%CALLNAME:MASTER%의\@ TCVAR:MASTER:갈것같다 ? 당장갈것같다인 # \@페니스를"
	ENDIF
	;체위에 의해 분기
	SELECTCASE TEQUIP:성교봉사중
		;정상위
		CASE 1
			LOCALS += @"크게 벌어진 가랑이의 사이에 맞아들이고"
		;후배위
		CASE 2
			LOCALS += @"배후로부터 질내에 삽입되고"
		;기승위
		CASE 3
			LOCALS += @"미끈미끈의 질로 큐우큐우 짜내고"
		;대면좌위
		CASE 4
			LOCALS += @"체를 밀착시켜 질 안쪽으로 환영해"
		;배면좌위
		CASE 5
			LOCALS += @"등을 맡겨 만끽해"
		;애널섹스
		CASE 6
			LOCALS += @"장액에 미끌거리는 항문으로 억압"
	ENDSELECT
;조교자 자위중
ELSEIF GETBIT(TEQUIP:자위중, 1)
	IF TEQUIP:조교자페니스밴드
		LOCALS += "몸에 댄 의욕형을"
	ELSE
		LOCALS += @"\@ TCVAR:TARGET:갈것같다 ? 당장갈것같다인 # \@\@ PENIS(TARGET) ? 페니스 # 클리토리스 \@를"
	ENDIF
	LOCALS += "스스로 애무하는 것을 과시하고"
ENDIF


SIF LOCALS != ""
	LOCALS += ARGS
RETURNF LOCALS

;-------------------------------------------------
;함수명:PENIS_DESCRIPTION
;개  요점:페니스 묘사 함수
;인  수:없음
;반환값:페니스 상태를 나타내는 캐릭터 라인
;비  고:식 안의 함수
;어딘지 모르게 코드가 번잡하게 될 것 같은 예감이 했으므로 함수가능 해 보았다
;사용예
;PRINTFORML %CALLNAME:MASTER%의%PENIS_DESCRIPTION()%페니스가(이하 생략
;현상의 규모라면 AUTO_SPLIT에서도 충분하지만, 확장 가능성과 보수성을 생각해 STRDATA로 해 본다@revkoishi
;-------------------------------------------------
@PENIS_DESCRIPTION(ARG = -2)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
ARG = ARG != -2 ? ARG # MASTER
IF ARG == MASTER
	SELECTCASE TFLAG:ACT
		;애무계 ACT
		CASE 10 TO 19
			;쇠약해지고 있다
			IF TCVAR:MASTER:발기도 < 1000
				;사정 직후
				IF TCVAR:MASTER:사정후경과턴 == 1
					STRDATA LOCALS:0
						DATA 간지 얼마 안된
						DATA 갔던 바로 직후로 민감한
						DATA 조금 전 충분히 정령을 발했다
					ENDDATA
				;그 외
				ELSE
					STRDATA LOCALS:0
						DATA 쇠약해져 버렸다
						DATA 시들었다
						DATA 흠칫도 하지 않는다
					ENDDATA
				ENDIF
			;발기
			ELSE
				;사정 직후
				IF TCVAR:MASTER:사정후경과턴 == 1
					STRDATA LOCALS:0
						DATA 간신히 사정이 수습되었다
						DATA 쇠약을 모른다
						DATA 맥동이 수습되지 않는다
					ENDDATA
				;발기도의 파생
				ELSEIF TCVAR:MASTER:발기도 >= 1500
					STRDATA LOCALS:0
						DATA 터질듯이 노장 했다
						DATA 뒤로 젖혔다
						DATA 한계까지 발기했다
					ENDDATA
				ELSE
					STRDATA LOCALS:0
						DATA 노장 했다
						DATA 흘립 했다
						DATA 격분했다
					ENDDATA
				ENDIF
			ENDIF
	ENDSELECT
ELSE
	SELECTCASE TCVAR:ARG:발기
		CASE 0
			LOCALS:0 = 아직 부드럽다
		CASE 1
			LOCALS:0 = 서서히 경도를 더하고 있다
		CASE 2
			LOCALS:0 = 발기했다
		CASEELSE
			LOCALS:0 = 뒤로 젖혔다
	ENDSELECT
ENDIF
RETURNF LOCALS:0


;-------------------------------------------------
;함수명:PENIS_DESCRIPTION2
;개  요점:페니스 묘사 함수
;인  수:없음
;반환값:페니스 상태를 나타내는 캐릭터 라인
;비  고:식 안의 함수
;
;TFLAG:166=에너지 드레인
;피에너지 드레인중의 페니스 상태라고 하는 모로에 SQR 사양 함수인 것으로 comment out
;함수 정의만 살아 있는 것은 현상으로 호출해 개소가 있기 때문에 부실이라고 하는 의미도 있는 것이지만,
;그 중 누군가가 에너지 드레인 하게 되면(혹은 그 밖에 이용 방법 있으면)
;그 때에 또 생각합시다라는 것으로@revkoishi(14/04/20)
;-------------------------------------------------
@PENIS_DESCRIPTION2(ARG = -2)
#FUNCTIONS
#LOCALSSIZE 1
[SKIPSTART]
ARG = ARG != -2? ARG # MASTER
LOCALS:0 = 
IF ARG == MASTER && TFLAG:166
	IF BASE:MASTER:사정
		STRDATA LOCALS:0
			DATA 가고 있을 뿐이 되었다
			DATA 사정을 계속한다
			DATA 정액이 질질 흘림이 되었다
		ENDDATA
	ELSE
		STRDATA LOCALS:0
			DATA 공격를 계속한다
			DATA 가고 있을 뿐이 되었다
		ENDDATA
	ENDIF
ENDIF
[SKIPEND]
RETURNF LOCALS:0


;-------------------------------------------------
;함수명:OPPAI_DESCRIPTION
;개  요점:말이 아니고 마음으로 이해해라
;인  수:ARG… 캐릭터 등록 번호
;반환값:꿈
;비  고:식 안의 함수
;사용예:%OPPAI_DESCRIPTION(TARGET)%
;-------------------------------------------------
@OPPAI_DESCRIPTION(ARG = -2)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
ARG = ARG != -2 ? ARG # MASTER
;SQR 유래 부분에 대해 comment out. 폭유는 향후도 없다고 생각되지만 참고에 남겨 둔다@revkoishi(14/04/20)
;SIF TALENT:ARG:거유 == 2	;폭유
;	RETURNF RAND_SPLIT("터질듯한 가슴/대박력의 버스트/충분히로 한 질감의 가슴")
IF TALENT:ARG:거유	;거유
	STRDATA LOCALS:0
		DATA 풍만한 가슴
		DATA 자랑의 버스트
		DATA 매력적인 풍부한 부푼 곳
		DATA 신선하게 흔들리는 쌍구
		DATA 휘어지게 흔들리는 과실
		DATA 큰 유방
		DATA 포근한 가슴
		DATA 탄력이 풍부한 풍부한 가슴
	ENDDATA
ELSEIF TALENT:ARG:빈유	;빈유
	STRDATA LOCALS:0
		DATA 소극적인 가슴
		DATA 사랑스러운 가슴
		DATA 사랑스럽게 발기시킨 유두
		DATA 손바닥에 들어갈 것 같은 사랑스러운 유방
		DATA 얇은 유방
		DATA 자그마한 부푼 곳
	ENDDATA
ELSE					;보통젖
	STRDATA LOCALS:0
		DATA 부드러운 가슴
		DATA 균정을 취할 수 있던 가슴
		DATA 예쁜 가슴
		DATA 신선하고 붙은 좋은 가슴
		DATA 밸런스를 잡힌 아름다운 유방
		DATA 아름다운 곡선의 쌍구
		DATA 형 좋게 균정미를 유지한 유방
	ENDDATA
ENDIF
RETURNF LOCALS:0

;-------------------------------------------------
;함수명:VAGINA_DESCRIPTION
;인  수:ARG… 캐릭터 등록 번호
;반환값:꿈
;비  고:식 안의 함수
;사용예:%VAGINA_DESCRIPTION(TARGET)%
;-------------------------------------------------
@VAGINA_DESCRIPTION(ARG = -2)
#FUNCTIONS
#LOCALSSIZE 1
ARG = ARG != -2 ? ARG # MASTER
IF TALENT:ARG:작은체형	;작은체형 체형
	STRDATA LOCALS:0
		DATA 어린 균열
		DATA 어린 성기
		DATA 미발달의 세로 스지
	ENDDATA
ELSE				;일반
	STRDATA LOCALS:0
		DATA 음순
		DATA 여성기
		DATA 비순
	ENDDATA
ENDIF
RETURNF LOCALS:0

;-------------------------------------------------
;함수명:NIPPLE_DESCRIPTION
;개  요점:유두 묘사 함수
;인  수:캐릭터 번호. 생략 하면 MASTER
;반환값:캐릭터 라인
;비  고:식 안의 함수
;사용예:%CALLNAME:MASTER%의%NIPPLE_DESCRIPTION(MASTER)%유두 운운…
;-------------------------------------------------
@NIPPLE_DESCRIPTION(ARG = -2)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
ARG = ARG != -2 ? ARG # MASTER
SELECTCASE PALAM:ARG:쾌Ｂ
	CASE IS >= 8500
		STRDATA LOCALS:0
			DATA 절정에의 기대에 떨린다
			DATA 핀과 다 날카로워졌다
			DATA 움찔움찔 떨린다
		ENDDATA
	CASE IS >= 4000
		STRDATA LOCALS:0
			DATA 다 부풀어 올랐다
			DATA 딱딱하게 응어리졌다
			DATA 튈듯이 부풀어 올랐다
		ENDDATA
	CASEELSE
		STRDATA LOCALS:0
			DATA 희미하게 부풀어 올랐다
			DATA 조금 부푼 곳을 늘리고 있다
			DATA 아직도 부드럽다
		ENDDATA
ENDSELECT
RETURNF LOCALS:0

;-------------------------------------------------
;함수명:CLITORIS_DESCRIPTION
;개  요점:Ｃ묘사 함수
;인  수:캐릭터 번호. 생략 하면 MASTER
;반환값:캐릭터 라인
;비  고:식 안의 함수
;사용예:%CALLNAME:MASTER%의%CLITORIS_DESCRIPTION(MASTER)%음핵 운운…
;-------------------------------------------------
@CLITORIS_DESCRIPTION(ARG = -2)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
ARG = ARG != -2 ? ARG # MASTER
SELECTCASE PALAM:ARG:쾌Ｃ
	CASE IS >= 8500
		STRDATA LOCALS:0
			DATA 절정에의 기대에 떨린다
			DATA 흠칫흠칫 맥박친다
		ENDDATA
	CASE IS >= 4000
		STRDATA LOCALS:0
			DATA 다 부풀어 올랐다
			DATA 충혈해 버렸다
			DATA 튈듯이 부풀어 올랐다
		ENDDATA
	CASEELSE
		STRDATA LOCALS:0
			DATA 희미하게 부풀어 올랐다
			DATA 조금 부풀어 오르고 있다
			DATA 자그마하게 부풀어 올랐다
		ENDDATA
ENDSELECT
RETURNF LOCALS:0

;-------------------------------------------------
;함수명:WET_DESCRIPTION
;개  요점:Ｖ묘사 함수
;인  수:캐릭터 번호. 생략 하면 MASTER
;반환값:캐릭터 라인
;비  고:식 안의 함수（v30601. 001 현재, MASTER만 참조 가능）
;사용예:%WET_DESCRIPTION(MASTER)%여성기(ry
;-------------------------------------------------
@WET_DESCRIPTION(ARG = -2)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
ARG = ARG != -2 ? ARG # MASTER
SELECTCASE PALAM:ARG:윤활
	CASE IS >= 1500
		LOCALS:0 = "뜨거운 밀액의 흘러넘치기 시작한다"
	CASE IS >= 1000
		LOCALS:0 = "애액의 흘러넘치기 시작한다"
	CASE IS >= 500
		LOCALS:0 = "흠뻑 젖었다"
	CASEELSE
		LOCALS:0 = "촉촉히 습기찼다"
ENDSELECT
RETURNF LOCALS:0

;-------------------------------------------------
;함수명:VAGINA_SMELL
;개  요점:성기의 냄새 묘사 함수
;인  수:캐릭터 번호. 생략 하면 MASTER
;반환값:캐릭터 라인
;비  고:식 안의 함수（v30601. 001 현재, MASTER만 참조 가능）
;사용예:%VAGINA_SMELL(MASTER)%를 감돌게 한다(ry
;-------------------------------------------------
@VAGINA_SMELL(ARG = -2)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
ARG = ARG != -2 ? ARG # MASTER
SELECTCASE PALAM:ARG:윤활
	CASE IS >= 1000
		LOCALS:0 = "농후한 여자의 향기"
	CASE IS >= 500
		LOCALS:0 = "요시카인 향기나"
	CASEELSE
		LOCALS:0 = "맑고 깨끗한 소녀의 향기"
ENDSELECT
RETURNF LOCALS:0
;eraWIZ를 봐 만들고 싶어졌다
;이렇게 말하는지, 한정 없음 구상전용으로 MASTER의 체격등으로 분기 만들어도 괜찮은 것인지도
;작은체형 체형이라면 어린 취급이고

;-------------------------------------------------
;함수명:VAGINA_DESCRIPTION2
;개  요점:Ｖ묘사 함수
;인  수:없음
;반환값:캐릭터 라인
;비  고:식 안의 함수
;사용예:%VAGINA_DESCRIPTION2(MASTER)%
;-------------------------------------------------
@VAGINA_DESCRIPTION2(ARG)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
STRDATA LOCALS:0
	DATA 비순
	DATA 비밀의 틈
	DATA 비밀스런 장소
	DATA 미육
	DATA 꽃잎
ENDDATA
RETURNF LOCALS:0

;-------------------------------------------------
;함수명:PANTY_DESCRIPTION
;개  요점:팬티 묘사 함수
;인  수:없음
;반환값:캐릭터 라인
;비  고:식 안의 함수（v30601. 001 현재, MASTER만 참조 가능）
;사용예:%PANTY_DESCRIPTION(MASTER)%팬티(ry
;-------------------------------------------------
@PANTY_DESCRIPTION(ARG = -2)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
ARG = ARG != -2 ? ARG # MASTER
SELECTCASE PALAM:ARG:윤활
	CASE IS >= 1500
		LOCALS:0 = "뜨거운 꿀로 질척질척 젖었다"
	CASE IS >= 1000
		LOCALS:0 = "밀액으로 흠뻑 젖었다"
	CASE IS >= 500
		LOCALS:0 = "꿀로 더러워졌다"
	CASEELSE
		LOCALS:0 = "흠뻑에 젖었다"
ENDSELECT
RETURNF LOCALS:0
;드로와 입은 캐릭터의 가능성을 알아차렸지만, 출력상은 관계없기 때문에 별로 좋다

;-------------------------------------------------
;함수명:DHARNESS_DESCRIPTION
;개  요점:페니스밴드 묘사 함수
;인  수:ARG… 캐릭터 등록 번호
;반환값:캐릭터 라인
;비  고:식 안의 함수
;사용예:%DHARNESS_DESCRIPTION(TARGET)%
;-------------------------------------------------
@DHARNESS_DESCRIPTION(ARG)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
STRDATA LOCALS:0
	DATA 무기질인 무기
	DATA 멋없는 완구
	DATA 거짓의 강직
	DATA 유사품의 쐐기
ENDDATA
RETURNF LOCALS:0

;-------------------------------------------------
;함수명:ANUS_DESCRIPTION
;개  요점:아누스 묘사 함수
;인  수:ARG… 캐릭터 등록 번호
;반환값:캐릭터 라인
;비  고:식 안의 함수
;사용예:%ANUS_DESCRIPTION(TARGET)%
;-------------------------------------------------
@ANUS_DESCRIPTION(ARG)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
STRDATA LOCALS:0
	DATA 부정의 꽃잎
	DATA 부정의 구멍
	DATA 부정의 오므라짐
	DATA 아누스
	DATA 골짜기의 꽃봉오리
	DATA 금단의 고기구멍
ENDDATA
RETURNF LOCALS:0

;-------------------------------------------------
;함수명:PLEASURE_DESCRIPTION
;개  요점:쾌락 상태 묘사 함수
;인  수:ARG… 캐릭터 등록 번호
;반환값:캐릭터 라인
;비  고:식 안의 함수
;사용예:%PLEASURE_DESCRIPTION(TARGET)%
;-------------------------------------------------
@PLEASURE_DESCRIPTION(ARG)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
STRDATA LOCALS:0
	DATA 신체를 움찔움찔 진동시키면서
	DATA 전신을 삐걱삐걱 진동시키면서
	DATA 목을 좌우에 흔들면서
	DATA 손을 입술에 주어 손가락을 씹으면서
	DATA 가는 어깨를 진동시키면서
ENDDATA
RETURNF LOCALS:0

;-------------------------------------------------
;함수명:Ｃ묘사
;개  요점:ARG의Ｃ묘사 함수
;반환값:캐릭터 라인
;비  고:식 안의 함수
;사용예:%Ｃ묘사(MASTER)%
;-------------------------------------------------
@Ｃ묘사(ARG = -2)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
ARG = ARG != -2 ? ARG # MASTER
IF PENIS(ARG)
	STRDATA LOCALS:0
		DATA 페니스
		DATA 쐐기
		DATA 노장
	ENDDATA
ELSE
	STRDATA LOCALS:0
		DATA 클리토리스
		DATA 음핵
		DATA 육아
		DATA 성감의 급소
	ENDDATA
ENDIF
RETURNF LOCALS:0

;-------------------------------------------------
;함수명:SEMEN_DESCRIPTION
;개  요점:SPERM는 아니고 SEMEN네요 하고 말한다
;인  수:ARG… 캐릭터 등록 번호
;반환값:캐릭터 라인
;비  고:식 안의 함수
;사용예:%SEMEN_DESCRIPTION(MASTER)%
;-------------------------------------------------
@SEMEN_DESCRIPTION(ARG)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
STRDATA LOCALS:0
	DATA 관능의 트로미
	DATA 흰 토액
	DATA 오탁의 액체
	DATA 흰 정령
	DATA 백탁액
	DATA 미숙한 점액
ENDDATA
RETURNF LOCALS:0

;-------------------------------------------------
;함수명:AFFECTION
;개  요점:ARGS:0으로 MASTER와 ARG:0과의 호의, 호감도를 참조한다
;인  수:ARGS:0… 캐릭터 라인
;인  수:ARG :0… 캐릭터 번호. 생략 하면 TARGET
;-------------------------------------------------
@AFFECTION(ARGS:0, ARG:0 = -2)
#FUNCTION
ARG:0 = ARG:0 != -2 ? ARG:0 # TARGET
SELECTCASE ARGS:0
	CASE "INFINITE"
		RETURNF FAVOR(10000, 10000, ARG:0) ;호의·호감도 10000
	CASE "상사상애"
		RETURNF FAVOR(3000, 3000, ARG:0) ;호의·호감도 3000
	CASE "맹목적으로 사랑"
		RETURNF FAVOR(0, 3000, ARG:0) ;호감도 3000
	CASE "몹시 사랑함"
		RETURNF FAVOR(0, 2000, ARG:0) ;호감도 2000
	CASE "권애"
		RETURNF FAVOR(0, 1000, ARG:0) ;호감도 1000
	CASE "괴뢰"
		RETURNF FAVOR(2500, 0, ARG:0) ;호의 2500
	CASE "조교 완료"
		RETURNF FAVOR(2000, 0, ARG:0) ;호의 2000
	CASE "조교가 끝난 상태"
		RETURNF FAVOR(1000, 0, ARG:0) ;호의 1000
	CASE "눈을 뜸"
		RETURNF FAVOR(500, 0, ARG:0) ;호의 500
	CASE "공포"
		RETURNF !FAVOR(200, 0, ARG:0) && MARK:MASTER:트라우마 >= 3 ;호의 200 미만·트라우마 Lv3 이상
	CASE "순응"
		RETURNF FAVOR(100, 100, ARG:0) ;호의·호감도 100
	CASE "흥미"
		RETURNF FAVOR(0, 500, ARG:0) ;호감도 500
	CASE "반항"
		RETURNF !FAVOR(1, 0, ARG:0) && MARK:(ARG:0):반항각인 >= 1 ;호의 0 이하·반항각인 Lv1 이상
	CASE "폭군"
		RETURNF !FAVOR(-100, 0, ARG:0) && FAVOR(0, 1000, ARG:0) ;호의-100·호감도 1000
	CASE "혐오"
		RETURNF !FAVOR(-100, 0, ARG:0) ;호의-100
	CASE "불구대천"
		RETURNF !FAVOR(-500, 0, ARG:0) ;호의-500
	CASE "천적"
		RETURNF !FAVOR(-1000, 0, ARG:0) ;호의-1000
	CASEELSE
		CALLF PRINT_ERRORMESSAGE(@"@AFFECTION에 무효인 정의(%ARGS%)가 건네받았습니다")
		RETURNF -1
ENDSELECT

;--------------------------------------------------
;궁합 검색 함수@RELATION_QUERY
;	인수 0:대상의 캐릭터 번호（등록 번호는 아니다. 수중에 존재하고 있을 필요는 없다）
;	인수 1:검색 조건（0=지정하지 않는, 1=수중에 없는 캐릭터만 선택하는, 2=수중에 있는 캐릭터만 선택한다）
;대상 캐릭터와 전캐릭터와의 궁합을 검색해, 가장 궁합의 좋은 상대를 추출합니다
;--------------------------------------------------
;반환값 일람
;	RESULT:발견된 캐릭터와의 궁합（0=발견 불가, 1=대상에의 궁합 101～199, 2=대상에의 궁합 200이상）
;	RESULT:1:발견한 캐릭터의 캐릭터 번호（발견 불가의 경우=-1）
;	RESULT:2:발견한 캐릭터의 등록 번호（수중에 없는 경우=-1）
;	RESULTS:발견한 캐릭터의 통칭
;	RESULTS:1:발견한 캐릭터의 이름
;--------------------------------------------------
@RELATION_QUERY(ARG, ARG:1)
#DIM LCOUNT, 1
#LOCALSIZE 7
;검색 대상의 등록 번호를 추출
LOCAL = GETCHARA(ARG, 1)
LOCAL:6 = 0
IF LOCAL == -1
	ADDCHARA ARG
	LOCAL = CHARANUM - 1
	LOCAL:6 = 1
ENDIF
LOCAL:1 = -1
IF ARG:1 == 2
	;수중으로부터 검색
	FOR LCOUNT, 0, CHARANUM
		LOCAL:2 = LOCAL:1 < 0 ? 100 # RELATION:LOCAL:(LOCAL:1)
		LOCAL:3 = NO:LCOUNT < 0 || NO:LCOUNT >= VARSIZE("RELATION") ? 100 # RELATION:LOCAL:(NO:LCOUNT)
		SIF LOCAL:2 == 0
			LOCAL:2 = 100
		SIF LOCAL:3 == 0
			LOCAL:3 = 100
		;동치의 경우 랜덤으로 남긴다
		;뒤로 물러나는 편이 큰 경우 그것을 보존
		SIF (LOCAL:2 == LOCAL:3 && RAND:2 && LOCAL:1 >= 0) || LOCAL:2 < LOCAL:3
			LOCAL:1 = NO:LCOUNT
	NEXT
ELSE
	;전체로부터 검색
	;궁합을 총검색해 캐릭터 번호와 궁합치를 추출
	FOR LCOUNT, 0, VARSIZE("RELATION")
		;원래 CSV가 없으면 보는 필요없음
		SIF EXISTCSV(LCOUNT, 0)  == 0
			CONTINUE
		;캐릭터가 있든가 확인해, 있을까 개검색 조건=1이라면 다음에 난다
		SIF GETCHARA(LCOUNT, 1) != -1 && ARG:1 == 1
			CONTINUE
		LOCAL:2 = LOCAL:1 < 0 ? 100 # RELATION:LOCAL:(LOCAL:1)
		LOCAL:3 = RELATION:LOCAL:LCOUNT
		SIF LOCAL:2 == 0
			LOCAL:2 = 100
		SIF LOCAL:3 == 0
			LOCAL:3 = 100
		;동치의 경우 랜덤으로 남긴다
		;뒤로 물러나는 편이 큰 경우 그것을 보존
		SIF ((LOCAL:2 == LOCAL:3 && RAND:2 && LOCAL:1 >= 0) || LOCAL:2 < LOCAL:3) && LCOUNT < 149
			LOCAL:1 = LCOUNT
	NEXT
ENDIF
SIF LOCAL:6
	DELCHARA CHARANUM - 1

;이름과 등록 번호를 취득
RESULT:1 = LOCAL:1
IF LOCAL:1 >= 0
	RESULT:2 = GETCHARA(LOCAL:1, 1)
	IF RESULT:2 >= 0
		RESULTS = %CALLNAME:(RESULT:2)%
		RESULTS:1 = %NAME:(RESULT:2)%
	ELSE
		RESULTS = %CSVCALLNAME(LOCAL:1, 0)%
		RESULTS:1 = %CSVNAME(LOCAL:1, 0)%
	ENDIF
ENDIF
;수치에 응해 RETURN
SIF RELATION:LOCAL:(LOCAL:1) > 199
	RETURN 2
SIF RELATION:LOCAL:(LOCAL:1) > 100
	RETURN 1
RETURN 0
