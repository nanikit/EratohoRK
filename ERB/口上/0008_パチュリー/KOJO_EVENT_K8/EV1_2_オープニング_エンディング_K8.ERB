;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	パチュリー口上 オープニング、エンディング口上ファイル
;
;	オープニング, エンディングイベント口上のファイル
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;========================================================================
;EV1, オープニング口上
;========================================================================
;・口上主(TARGET)が主人である場合にのみ表示される
;・戻り値を2とした場合はOP後の本体側のタイトルロゴは表示されない
;・ノーマルモード以外の場合、調教対象がNO:MASTER == 0(いわるゆるあなた)でない可能性あり
;-------------------------------------------------
;関連変数…
;FLAG:モード(0=通常/1=サンドボックス/9=エキストラ)
;-------------------------------------------------
@KOJO_EVENT_K8_1(ARG)
;-------------------------------------------------
;CNAMEやCSTR:召喚可能助手、その他各種代入に向いた箇所の一つです
;コマンド封印も個別に可能ですが、本体側の意向として(一応)隠し機能にしたいという事情もあり
;判る人はどうぞ、という事で解説はしない方向で
;-------------------------------------------------
;口上を表示した上で、終端をRETURN 0とする事で本体側の汎用OPに渡す事も可能だったりします
;汎用OPに飛ばした際はタイトルロゴが表示されます
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;ノーマルモード時
;RESULT値(1,OPを表示した後本体のタイトルロゴを表示/2, 本体のタイトルロゴを表示しない)
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && !FLAG:モード
	;MASTERの呼び名代入
	CSTR:(TARGET):呼び方リスト = あなた_貴方/レミリア_レミィ/フランドール_フラン/小悪魔_小悪魔
	;他の箇所(例えばEVENT11)等で条件式を組んでINPUTを絡ませて代入、という事も可能

	;-------------------------------------------------
	;各種封印処理
	;-------------------------------------------------
	;会話は制御しにくいので一部除いて封印
	CFLAG:1000 = 100 ;今の気分を聞く
	CFLAG:1001 = 100 ;自分への気持ちを聞く
	CFLAG:1002 = 100 ;性についての話をする
	CFLAG:1003 = 100 ;優しく慰める
	CFLAG:1004 = 100 ;厳しく脅す
	CFLAG:1008 = 100 ;怒鳴る
	CFLAG:1009 = 100 ;不気味に笑う

	;アナル舐めは主人がやることではない？
	;パイズリや素股は快感が労力に見合わないので封印
	CFLAG:1014 = 100 ;アナル舐め
	CFLAG:1016 = 100 ;性器を擦り合う
	CFLAG:1017 = 100 ;パイズリ

	;アイテムを使わない羞恥系は自慰以外封印
	CFLAG:1041 = 100 ;秘貝開帳
	CFLAG:1042 = 100 ;自慰みせつけ
	CFLAG:1045 = 100 ;放尿

	;手は出させない方針
	;こっちもパイズリ素股封印
	CFLAG:1050 = 100 ;手で愛撫を強制
	CFLAG:1052 = 100 ;パイズリ
	CFLAG:1053 = 100 ;素股
	CFLAG:1057 = 100 ;胸愛撫強制

	;罵倒は会話系と同じく封印
	CFLAG:1066 = 100 ;罵倒

	;アイテムを使わない異常系は封印
	CFLAG:1070 = 100 ;フィストファック
	CFLAG:1071 = 100 ;アナルフィスト
	CFLAG:1072 = 100 ;両穴フィスト
	CFLAG:1073 = 100 ;放置プレイ

	;「自分で動きなさい」みたいなのも考えるけど
	;奴隷は下においておくはず
	;パチュリーと正常位するなら恋人としてしたい
	CFLAG:1095 = 100 ;正常位させる
	CFLAG:1096 = 100 ;後背位させる
	CFLAG:1099 = 100 ;アナルセックスさせる

	;デフォルトのストーリーを踏襲
	;但し、ストーリーの解釈が合っているかどうか微妙
	;詰める必要大いにあり
	$OPENING_LOOP
	CALL WAIT_3
	PRINTFORMW 「ようやくお目覚めね」
	PRINTL 
	CALL CHARA_CUTIN(CSTR:二つ名, CSTR:登場名, CSTR:ローマ字)
	PRINTL 
	PRINTFORMDW 薄暗い部屋で%CALLNAME:MASTER%を迎えたのは、日陰臭い魔法使いだった。
	PRINTL 
	PRINTFORML 「ここは調教室」
	PRINTFORMW 「貴方は私の奴隷」
	PRINTL 
	PRINTFORML 「今日から貴方に性的快感と絶頂をあげるから」
	PRINTFORMW 「その体から精力を吐き出しなさい」
	PRINTL 
	PRINTFORMW 「それは私にとっては現実にも幻想にも非ざる貴重な魔力なの」
	PRINTL 
	PRINTFORMW 「使い途は教えてあげない」
	PRINTDL 
	CALL FPRINTL("[0]目の前の頭のおかしい魔法使いから逃げ出す", 0xFF0000)
	PRINTDL [1]嫌な予感がするので脱出を諦める
	CALL INPUTINT(0, 1)
	IF RESULT == 0
		PRINTFORMDW 恐怖に駆られて部屋の出口へ走り出した瞬間、
		PRINTFORMDW %CALLNAME:MASTER%の足元から触手が滑り出し、たちまち四肢を絡め取る。
		PRINTFORMDW 触手に呑み込まれてゆく%CALLNAME:MASTER%を尻目に
		PRINTFORMDW パチュリーは一仕事を終えた顔で部屋から出て行った…
		PRINTFORMDW GAME OVER
		CALL BGM_CUTIN("プレイヤーズスコア")
		PRINTDL [0]再挑戦
		PRINTDL [1]終了
		CALL INPUTINT(0, 1)
		IF RESULT == 0
			CLEARLINE LINECOUNT
			GOTO OPENING_LOOP
		ELSE
			QUIT
		ENDIF
	ENDIF
	PRINTFORML 「賢明な判断に感謝するわ」
	PRINTFORMW 「この空間では貴方の『能力』は通用しないから力関係は明白よ」
	CALL BGM_CUTIN(CSTR:曲１)
	PRINTL 
	PRINTFORMW 「口で説明するより体に言い聞かせる方が早い――」
	PRINTL 

	REDRAW 0
	SETFONT "ＭＳ 明朝"
	FONTSTYLE 3

	CALL FULLCUTIN(40, 17, "eratohoЯeverse――","　","　","　　　　　　――調教を始めるわ")
	
	PRINTFORMW 
	RETURN 2
ENDIF

;将来的に他のモードが追加された場合はFLAG:モードを決めうちした方が良いかも知れません
;━━━━━━━━━━━━━━━━━━━━━━━━
;ノーマルモード以外の場合
;RESULT値(1,OPを表示した後本体のタイトルロゴを表示/2, 本体のタイトルロゴを表示しない)
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(0) && FLAG:モード > 0
	PRINTFORMW 
	RETURN 1
ENDIF

;========================================================================
;EV2, エンディングイベント口上
;========================================================================
;・開始30日以降、日時の更新時にシステム側からEVENT2が呼び出される
;・戻り値1以上が返ると本体側でFLAG:END達成が立ち、以降はシステム側から呼び出される事はない
;・以前は戻り値1以外はFLAG:END達成が立たない仕様でしたが、現在はRESULT > 0ならフラグが立つようになっています
;・必要に応じてFIRSTEVENTを使っておくと万が一の保険にはなりますし、仕様変更にも強いです
;-------------------------------------------------
;　最短で呼び出されるのが30日というだけなので、その他の部分では口上側で任意に条件設定が可能です
;　IF 口上記入チェック() &&で条件で区切るなり、IF 口上記入チェック()で大分岐にした上で、小分岐内で個別に条件設定も出来ます
;　階層イベント形式にして表示させた上でRETURN 0。最後にRETURN 1で。なんて事も可能ではあったり
;　可能性は無限にあるので、条件設定は各口上作者に委ねたいと思います
;-------------------------------------------------
@KOJO_EVENT_K8_2(ARG)
;-------------------------------------------------
;将来的にはCFLAG:依存度による本体側のED分岐なんていう代物の構想もあったり
;その際には、本体側の処理を呼ぶ/殺すも口上側で任意に設定が可能という方向性
;とはいえ、いつになるのか、実現するかも定かではないので、現状は各自頑張って下さいということで
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;エンディング
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(0)
	PRINTFORMW 
	;戻り値は1で決めうちしておきます
	RETURN 1
ENDIF
