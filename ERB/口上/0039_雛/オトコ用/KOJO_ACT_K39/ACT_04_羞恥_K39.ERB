;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;雛口上　羞恥系アクション口上ファイル

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


;################
;羞恥系 ACT40～45
;################
[SKIPSTART]
	#TFLAG:ACT
		40=自慰
		41=秘貝開帳
		42=自慰みせつけ
		43=羞恥プレイ
		44=野外プレイ
		45=放尿
		46=コスプレ(未実装))
	#TFLAG:ACT分類 == 4に該当
	#汎用リアクションにおいてはMENUMATCH(TFLAG:ACT, "羞恥")で拾う事が可能
	#ACTによっては拒否が可能であったりなかったり
[SKIPEND]


;/////////////
;ACT40, 自慰//
;/////////////
[SKIPSTART]
	#オナニーしてみせろと命令する
	#この段階で命令に従うか否かは未定
	#REACT分類が消極的従う/平然と従う/積極的従う/受け入れる以外の場合は、全てREACT分類"拒否"扱い
	#ターン方針が"異常"だと発生しない
	#押し倒し中である場合は発生しない
	#すでに自慰中である場合は発生しない
	#TEQUIP(縄/三角木馬)装着中には発生しない
	#性交中/性交奉仕中には発生しない
	#このACTに派生はない

	*地の文に分岐はなし
	*@TARGET_DESCRIPTIONで状況描写が入る場合あり
	*調教対象のペニスの有無、ペニス無しの場合はペニスバンドの有無による書き分けが考えられる
	*調教対象がオトコであれば快Ｂは発生しない
[SKIPEND]
@KOJO_MALE_ACT_K39_40

;調教対象がオトコである場合 //オトコ用口上なので本来は必要ない条件。でも保険はかけておきたい的なアレ
;==========================
IF TALENT:MASTER:オトコ && PENIS(MASTER)
	;初回 //オナホ装着中なら弾く
	;====
	IF !TEQUIP:オナホール && FIRSTACT(0)
		;調教対象に自慰経験アリ
		IF EXP:MASTER:自慰経験 > 0 || EXP:MASTER:調教自慰経験 > 0
			{
			PRINTFORMW 「さて、自分でするところを見せて\@ 
			GET_KFLAG_K39("相思相愛", (FLAG:ご主人様)) 
			? もらおうかしら # もらいましょうか \@」
			}
			{
			PRINTFORMW 「別に初めてって訳じゃない\@ 
			GET_KFLAG_K39("相思相愛", (FLAG:ご主人様)) 
			? ないでしょう # ないですよね \@？」
			}
			{
			PRINTFORMW 「人前でした経験があるかどうかは知\@ 
			GET_KFLAG_K39("相思相愛", (FLAG:ご主人様)) 
			? らないけど # りませんけど\@」
			}
		;調教対象に自慰経験無し
		ELSE
			{
			CALL HTMLPRINTW(
			@"「え？ もしかして…%HTMLFONT("<b>『初めて』</b>", , COLOR2_K39("WHITE"))%" + 
			@"\@ GET_KFLAG_K39("相思相愛", (FLAG:ご主人様)) ? なの # なんですか \@？」")
			}
			{
			PRINTFORMW 「男の\@ 
			TALENT:MASTER:小柄 
			? 子 
			# 人 
			\@は皆、早い段階からこういう事してるものと思っていた\@ 
			GET_KFLAG_K39("相思相愛", (FLAG:ご主人様)) 
			? んだけど 
			# のですけど \@…」
			}
			{
			PRINTFORMW 「あ、でも……これってある意味、酷い偏見\@ 
			GET_KFLAG_K39("相思相愛", (FLAG:ご主人様)) ? よね # ですよね \@」
			}
		ENDIF
		CALL FADE_TO_CLEARLINE(10, "「ふふ……」")
		RETURN 1
	ENDIF

	;オナホで初回 //調教対象に自慰経験がある場合に限定
	;============
	IF TEQUIP:オナホール && GROUPMATCH(1, EXP:MASTER:自慰経験, EXP:MASTER:調教自慰経験) > 1 && FIRSTACT(1)
		{
		PRINTFORMW 「いい機会\@ 
		GET_KFLAG_K39("相思相愛", (FLAG:ご主人様)) 
		? だ 
		# です 
		\@から、その、えっと……オナホール\@ 
		!GET_KFLAG_K39("相思相愛", (FLAG:ご主人様)) 
		? ですか 
		# 
		\@？」
		}
		{
		PRINTFORMW 「それを使って、自分てシてみせてもら\@ 
		GET_KFLAG_K39("相思相愛", (FLAG:ご主人様)) 
		? おうかしら # いましょうか \@」
		}
		{
		PRINTFORMW 「それが、その道具本来の使い方なん\@ 
		GET_KFLAG_K39("相思相愛", (FLAG:ご主人様)) 
		? だろうし。ね？ 
		# でしょうし \@」
		}
		RETURN 1
	ENDIF

	;お仕置きモード //ONCEで調教内に一度のみ表示
	;==============
	IF TFLAG:お仕置きフラグ && ONCEACT(0)
		;好意、好感度で派生
		IF GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))
			PRINTFORMW 「ほら、自分でシてみせて」
			PRINTFORMW 「これはお仕置きなんだから、ちゃんとしないと駄目よ？」
		ELSE
			PRINTFORMW 「さぁ、自慰をしてください」
			PRINTFORMW 「恥ずかしがっても許してあげませんよ…？」
		ENDIF
		CALL FADE_TO_CLEARLINE(10, "「ふふふ……」")
		RETURN 1
	ENDIF

	[SKIPSTART]
	//現在の仕様だとすでに自慰中の場合は実行判定で規制される。即ち、発生し得ない！
	//ただこれについては、調教対象がイきそうな場合にのみ等、一部条件下に於いてはACT_ABLEを通しても良いような気がしてる

	;すでに自慰中かつ調教対象がイキそうかつ我慢フラグ無しの場合
	;==========================================================
	IF TCVAR:MASTER:イきそう && !TCVAR:MASTER:我慢 && GETBIT(TEQUIP:自慰中, 0)
		;好意、好感度で派生
		IF GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))
			PRINTFORMW 「……そんなに激しくシゴいちゃって」
			CALL HTMLPRINTW(@「いいよ……最期まで見ててあげる…%HTML_PS_K39()%」")
		ELSE
			PRINTFORMW 「そんなに激しく扱くものなんですね？」
			PRINTFORMW 「私もなんだか変な気持ちになってしまいます…」
		ENDIF
		RETURN 1
	ENDIF
	[SKIPEND]

	;調教者も自慰中
	;==============
	IF GETBIT(TEQUIP:自慰中, 1)
		;好意、好感度で派生
		IF GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))
			PRINTFORMW 「ねぇどうせなら…、%CNAME(MASTER, TARGET)%も自分でシてみせてよ」
			PRINTFORMW 「私一人でよくなっちゃうのも、なんかアレだし…」
		ELSE
			CALL HTMLPRINTW(@"「見てるだけではつまらないですよね？ ふふ…%HTML_PS_K39()%」")
			PRINTFORMW 「いいですよ。%CNAME(MASTER, TARGET)%が自分でするところ、私に見せて下さい」
		ENDIF
		RETURN 1
	ENDIF

	;オナホール装着中
	;================
	IF TEQUIP:オナホール
		;好意、好感度で派生
		IF GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))
			PRINTFORMW 「ほら、自分でシてみせて？」
			CALL HTMLPRINTW(@"「ソレを使ってどうやるのか、じっくり見させてもらうから%HTML_PS_K39()%」")
		ELSE
			PRINTFORMW 「さ、ソレで扱いて見せてください」
			PRINTFORMW 「今更恥ずかしがる事なんてないんですよ？」
			CALL FADE_TO_CLEARLINE(15, "「ふふ……」")
		ENDIF
		RETURN 1
	ENDIF

	;その他・条件なし //汎用ACTにはパスしない
	;================
	IF 口上記入チェック(100)
		;好意、好感度で派生
		IF GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))
			PRINTFORMW 「ねぇ、自分でシてみせて？」
			PRINTFORM 「%CNAME(MASTER, TARGET)%がイクまで、ちゃんと見ててあげるから
		ELSE
			PRINTFORMW 「では、自慰をしてみせてください」
			PRINTFORM 「どういう風にするのか、じっくり見させてもらいます
		ENDIF
		CALL PRINT_SYMBOL_K39
		RETURN 1
	ENDIF
ENDIF


;/////////////////
;ACT41, 秘貝開帳//
;/////////////////
[SKIPSTART]
	#調教者が調教対象の秘貝を開帳する。所謂「くぱぁ」
	#調教対象がオトコの場合は発生しない
	#TFLAG:お仕置きフラグが立っていると発生しない
	#ターン方針が"異常"の場合は発生しない
	#TEQUIP:三角木馬装着中は発生しない
	#押し倒し中は発生しない
	#性交中/性交奉仕中は発生しない
	#このACTに派生はない
	#現在リアクション後の@ACTION_APPLY2に処理なし

	*TEQUIP(顔面騎乗/自慰中)は解除される
	*下半身全部強制脱衣の処理あり
[SKIPEND]
@KOJO_MALE_ACT_K39_41
;オトコ用口上なので不要
RETURN 0


;/////////////////////
;ACT42, 自慰見せつけ//
;/////////////////////
[SKIPSTART]
	#調教者が見せ付けるように自慰を始める
	#TFLAG:お仕置きフラグが立っていると発生しない
	#押し倒し中には発生しない
	#調教者がすでに自慰中である場合は発生しない
	#性交中/性交奉仕中には発生しない

	*調教者欲望、技巧、調教レベル、調教者技巧、器用な指先素質に応じて快Ｃ/快ＢSOURCE増加
	*バイブ所持の場合の処理はv1.020時点では封印されている
[SKIPEND]
@KOJO_MALE_ACT_K39_42

;調教者にペニスがある場合もしくは調教者がペニスバンド装着中である場合
SIF PENIS(TARGET) || TEQUIP:調教者ペニスバンド
	RETURN 0

;調教者にペニスがない場合
;========================
IF !PENIS(TARGET)
	;初回
	;====
	IF FIRSTACT(0)
		{
		PRINTFORMW 「こんなことするトコ%CNAME(MASTER, TARGET)%に見られるのは、これが初めて\@ 
		GET_KFLAG_K39("相思相愛", (FLAG:ご主人様)) 
		? よね # ですね \@…」
		}
		;好意、好感度で派生
		IF GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))
			PRINTFORML 「まぁ確かに…、ちょっと恥ずかしいけど」
			PRINTFORMW 「どうせなら、しっかり見ていて。でないと私…、馬鹿みたいだし……」
		ELSE
			PRINTFORMW 「この厄神のいやらしい姿、しっかり見ていて下さいね……」
		ENDIF
		RETURN 1
	ENDIF

	;調教対象も自慰中
	;================
	IF GETBIT(TEQUIP:自慰中, 0)
		;好意、好感度で派生
		IF GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))
			PRINTFORMW 「見せ合いっこなんて変態みたい…」
			PRINTFORMW 「でも、%RAND_SPLIT("どうしようもな/凄")%く興奮しちゃうの。ホント駄目な神様ね、私……」
		ELSEIF AFFECTION("溺愛")
			PRINTFORMW 「こんなの見てたら私、もう我慢できません…」
			PRINTFORMW 「\@ !RAND:3 ? あの…、 # \@%CNAME(MASTER, TARGET)%は、こんな私を軽蔑しますか…？」
		ELSE
			CALL HTMLPRINTW(@"「%CNAME(MASTER, TARGET)%一人にさせておく訳にもいきませんね%HTML_PS_K39()%」")
			PRINTFORMW 「さぁ、私が自分でするところも見せてあげます……」
		ENDIF
		RETURN 1
	ENDIF

	;その他・条件なし //汎用ACTにはパスしない
	;================
	IF 口上記入チェック(100)
		;好意、好感度で派生
		IF GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))
			PRINTFORMW 「人前で、自分でして興奮しちゃうなんて最低よね…」
			PRINTFORMW 「でも、それでもいいの。もう止められないの…」
		ELSE
			PRINTFORMW 「\@ TENSION(TARGET, 8) == "高い" ? うふ、 # \@目を逸らしては駄目ですよ？」
			PRINTFORMW 「私が自分で慰める情けない姿、しっかり見て下さいね…」
		ENDIF
		RETURN 1
	ENDIF
ENDIF


;///////////////////
;ACT43, 羞恥プレイ//
;///////////////////
[SKIPSTART]
	#調教対象を大鏡の前に連れて行く
	#TFLAG:お仕置きフラグが立っていると発生しない
	#ターン方針が"異常"だと発生しない
	#押し倒し中には発生しない
	#TEQUIP:三角木馬装着中には発生しない
	#性交中/性交奉仕中には発生しない
	#既に羞恥プレイ中なら、羞恥プレイ終了になる
[SKIPEND]
@KOJO_MALE_ACT_K39_43 ;TFLAG:ACT派生(0=開始/1=終了)
#LOCALSIZE 1
VARSET LOCAL
LOCAL:0 = GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))

;ACT43_0, 羞恥プレイ開始
;=======================
IF !TFLAG:ACT派生
	;アイマスク装着中
	;================
	IF TEQUIP:アイマスク
		{
		PRINTFORMW 「\@ 
		LOCAL:0 
		? ねぇ、判る 
		# 判りますか 
		\@？ %CNAME(MASTER, TARGET)%は今、鏡の前にいる\@ 
		LOCAL:0 
		? の 
		# んです \@」
		}
		{
		PRINTFORMW 「目隠しで何も見えないでしょうけど…、自分がどういう状況なのかは知っておいて\@ 
		!LOCAL:0 
		? ください 
		# \@」
		}
		{
		PRINTFORMW 「想像力\@ 
		FIRSTACT(0) 
		? が豊かなら 
		# 次第で 
		\@、これはこれで愉しめる状況\@ 
		LOCAL:0 
		? なんじゃないかしら？ 
		# ではあると思いますけど \@」
		}
		CALL FADE_TO_CLEARLINE(15, @"「\@ FIRSTACT(1) ? ふふ # くすくす \@……」")
		RETURN 1
	ENDIF

	;その他・好意、好感度で派生
	;==========================
	IF GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))
		SELECTCASE RANDACT(2, 0)
			CASE 0
				CALL FADEIN(8, "「鏡から目を逸らしては駄目よ」")
				PRINTFORMW 「ほら、もっと良く見て。どう？　ちょっと興奮してきたんじゃない？」
				CALL FADE_TO_CLEARLINE(15, "「クスクス…」")
			CASE 1
				PRINTFORMW 「ねぇ、こーいうのって興奮しない？」
				PRINTFORMW 「私は凄く興奮しちゃう。人に見られてるみたいで、ゾクゾクしちゃうの……」
		ENDSELECT
	ELSE
		PRINTFORMW 「ほら、鏡を見て下さい…」
		PRINTFORMW 「目を逸らさず、ちゃんと見てないと駄目ですよ\@ TENSION(TARGET, 8) == "高い" ? ？ # …… \@」
	ENDIF
	RETURN 1
ENDIF

;ACT43_1, 羞恥プレイ終了
;=======================
IF TFLAG:ACT派生 == 1
	PRINTFORMW 「愉しんでもらえた\@ LOCAL:0 ? かしら # でしょうか \@」
	;調教対象の状態変化によって派生
	SELECTCASE GET_CONDITION(MASTER)
		CASE "疲弊", "衰弱"
			PRINTFORMW 「…って。随分やつれ\@ LOCAL:0 ? ちゃったわ # てしまいました \@ね」
		CASE "無気力"
			{
			PRINTFORMW 「…って、どうした\@ 
			LOCAL:0 
			? の 
			# んでしょう 
			\@？ すっかり気の抜けた顔になっ\@ 
			LOCAL:0 
			? ちゃ 
			# てしま 
			\@って…」
			}
		CASE "朦朧"
			CALL HTMLPRINTW(@"「あら？ 心ここにあらずって感じ\@ !LOCAL:0 ? です # \@ね…%HTML_PS_K39()%」")
		CASE "情欲"
			{
			CALL HTMLPRINTW(
			@"「\@ TENSION(TARGET, 8) == "高い" ? ふふ、 # \@興奮冷めやらぬって感じ" + 
			@"\@ !LOCAL:0 ? です # \@ね？ 聞くだけ野暮だったかしら%HTML_PS_K39()%」")
			}
		CASE "怒り"
			{
			PRINTFORMW 「\@ LOCAL:0 
			? なによ%RAND_SPLIT("ー/")%、 
			# \@機嫌悪そう\@ 
			!LOCAL:0 
			? です 
			# 
			\@ね。そんなにこれが嫌だったの\@ 
			!LOCAL:0 ? ですか # \@？」
			}
		CASE "退屈"
			PRINTFORMW 「%CNAME(MASTER, TARGET)%にとっては、退屈なだけだったみたい\@ !LOCAL:0 ? です # \@ね…」
		CASE "鬱屈"
			PRINTFORMW 「んー。ちょっと様子がおかしい\@ LOCAL:0 ? わ # です \@ね……」
		CASE "通常"
			PRINTFORMW 「ふふ、お疲れさま\@ !LOCAL:0 ? でした # \@」
	ENDSELECT
	RETURN 1
ENDIF


;///////////////////
;ACT44, 野外プレイ//
;///////////////////
[SKIPSTART]
	#調教対象に首輪をつけて外に連れ出す
	#ターン方針が"ソフト", "ノーマル"だと発生しない
	#押し倒し中には発生しない
	#TEQUIP(縄/三角木馬)装着中には発生しない
	#性交中/性交奉仕中には発生しない
	#既に野外プレイ中なら、野外プレイ終了になる
[SKIPEND]
@KOJO_MALE_ACT_K39_44 ;TFLAG:ACT派生(0=開始/1=終了)

;ACT44_0, 野外プレイ開始
;=======================
IF !TFLAG:ACT派生 && FLAG:オートモード表示設定 < 1 ;オートモード簡略表示なら弾く
	;好意、好感度で派生
	IF GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))
		PRINTFORMW 「ねぇ、たまには気分を変えてちょっと出掛けてみる？」
		PRINTFORMW 「行きたいところがあれば言ってね」
		PRINTL
		PRINTFORMDL [0] 命蓮寺
		PRINTFORMDL [1] 魔法の森
		PRINTL
		CALL INPUTINTD(0, 1)
		;オートなら止める
		SIF FLAG:オートモード > 0
			FORCEWAIT
		SELECTCASE RESULT
			CASE 0
				PRINTFORMW 「命蓮寺って……ねぇ、お寺でえっちな事がシたいの？」
				PRINTFORMW 「もうっ、ほんとうにいやらしい人ね……」
			CASE 1
				PRINTFORMW 「魔法の森にいきたいの？ でもあそこは瘴気が、って…」
				PRINTFORMW 「私がなんとかするとか思ってる？ ちょっと厄神を勘違いしてない？ それ…」
		ENDSELECT
	ELSE
		PRINTFORMW 「たまには外出するのもいいと思いませんか？」
		PRINTFORMW 「行きたいところがあれば言ってくださいね」
		PRINTL
		PRINTL [0] 霧の湖
		PRINTL [1] 香霖堂
		PRINTL
		CALL INPUTINTD(0, 1)
		;オートなら止める
		SIF FLAG:オートモード > 0
			FORCEWAIT
		SELECTCASE RESULT
			CASE 0
				PRINTFORMW 「霧の湖、ですか。 釣りでもしたいんでしょうか？」
				PRINTFORMW 「それもいいですけど、お愉しみのあとにしてもらいますよ？ うふ…」
			CASE 1
				PRINTFORML 「香霖堂って確か古道具屋でしたか。何か買いたいものでもあるんですか？」
				PRINTFORMW 「それとも、ただ店主に見せつけたいとか？」
				PRINTFORMW 「いやらしい人ですね♪ うふ…」
		ENDSELECT
	ENDIF
	RETURN 1
ENDIF

;ACT44_1, 野外プレイ終了
;=======================
IF TFLAG:ACT派生 == 1 && FLAG:オートモード表示設定 < 1 ;オートモード簡略表示なら弾く
	;好意、好感度で派生
	IF GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))
		PRINTFORMW 「ねぇ、どうだった？ %CNAME(MASTER, TARGET)%は愉しんでもらえたかしら？」
		PRINTL
		PRINTFORMDL [0] 人に見られて興奮した！
		PRINTFORMDL [1] 二人っきりで愛し合う方が好き
		PRINTL
		CALL INPUTINTD(0, 1)
		;オートなら止める
		SIF FLAG:オートモード > 0
			FORCEWAIT
		SELECTCASE RESULT
			CASE 0
				PRINTFORML 「うふっ、ほんとうにいやらしい人」
				CALL HTMLPRINTW(@"「まぁ、私も人の事は言えないかもだけど…%HTML_PS_K39()%」")
			CASE 1
				PRINTFORML 「あら、そうなの…？」
				PRINTFORMW 「それなら最初に言ってくれればいいのに…」
		ENDSELECT
	ELSE
		PRINTFORMW 「外はどうでした？」
		PRINTFORMW 「愉しんでもらえたでしょうか？」
		PRINTL
		PRINTFORMDL [0] また連れてって！
		PRINTFORMDL [1] もう勘弁して…
		PRINTL
		CALL INPUTINTD(0, 1)
		;オートなら止める
		SIF FLAG:オートモード > 0
			FORCEWAIT
		SELECTCASE RESULT
			CASE 0
				PRINTFORML 「愉しんでもらえたみたいで何よりです」
				CALL HTMLPRINTW(@"「いいですよ。また一緒に出かけましょうね%HTML_PS_K39()%」")
			CASE 1
				PRINTFORMW 「そうですか…。いい気分転換になると思ったのですが……」
		ENDSELECT
	ENDIF
	RETURN 1
ENDIF


;/////////////
;ACT45, 放尿//
;/////////////
[SKIPSTART]
	#おしっこしてみせろと命令する
	#この段階では命令に従うかどうか確定していない
	#REACT分類が消極的従う/平然と従う/積極的従う/受け入れる以外の場合は、全てREACT分類"拒否"扱い
	#ターン方針が"ソフト"だと発生しない
	#TEQUIP(三角木馬/ペニスバンド)装着中には発生しない
	#押し倒し中には発生しない
	#性交中/性交奉仕中には発生しない
	#尿意(BASE:MASTER:尿意)が5000以下だと発生しない

	*調教対象の尿意ゲージがセットアップされるのはTALENT:MASTER:おもらし癖を所持している場合に限られる
	*即ち、おもらし癖のない調教対象の場合、ACT自体が発生し得ない。はず…
[SKIPEND]
@KOJO_MALE_ACT_K39_45 ;ACT派生なし
#LOCALSIZE  1
#LOCALSSIZE 1
VARSET LOCAL
VARSET LOCALS
LOCAL:0   = GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))
LOCALS:0 = 「\@ LOCAL:0 ? クスクス # ふふ \@……」

;調教対象がオトコの場合
;======================
IF TALENT:MASTER:オトコ ;オトコ用口上なのでこの条件分岐は必須ではないが、この手の条件は書いておきたい的なアレ
	;初回
	;====
	IF FIRSTACT(0)
		PRINTFORMW 「さて、ここでおしっこをしてもら\@ LOCAL:0 ? おうかしら # いましょうか \@」
		{
		PRINTFORMW 「男の\@ 
		TALENT:MASTER:小柄 
		? 子 # 人 
		\@がどういう風にするのか、じっくり鑑賞させて\@ 
		LOCAL:0 
		? 頂くわ # もらいます \@」
		}
		;無茶なことを言う…感はまぁある
		PRINTFORMW 「ま、そう\@ LOCAL:0 ? ねぇ # ですね \@。これも厄祓いの一環と思ってもらえれば」
		CALL FADE_TO_CLEARLINE(10, @"%LOCALS:0%")
		WAIT
		RETURN 1
	ENDIF

	;その他・好意、好感度で派生
	;==========================
	IF GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))
		PRINTFORML 「ね、%CNAME(MASTER, TARGET)%がするとこ見せて」
		PRINTFORMW 「あら、別に汚いなんて思わないわよ？」
	ELSE
		PRINTFORML 「ではここでしてみせてもらいましょうか」
		PRINTFORMW 「これも厄を祓う為と思ってくださいね？」
	ENDIF
	CALL FADE_TO_CLEARLINE(10, @"%LOCALS%")
	WAIT
	RETURN 1
ENDIF
