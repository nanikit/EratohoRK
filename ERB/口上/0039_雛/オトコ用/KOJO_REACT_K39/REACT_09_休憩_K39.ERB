;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;雛口上 休憩系個別リアクション口上ファイル

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;#################
;休憩系 REACT90~92
;#################
[SKIPSTART]
	#TFLAG:ACT
		90 = 休ませる
		91 = 栄養剤を飲ませる
		92 = 治療する

・ACT分類:休憩系の場合、会話系COM(COM0~COM8)中心の選択肢となりますが
・状況によってはその他のものが選択肢に含まれる可能性もあります
・※持続効果のあるTEQUIPで愛撫系反応が選択肢となる場合があります
[SKIPEND]


;///////////////////////////////////
;REACT90, 休ませる・個別リアクション
;///////////////////////////////////
[SKIPSTART]
想定されるCOM…
	会話系反応
		COM:沈黙する/気弱く応答する/無愛想に応答する/愛想よく応答する/
			強気に応答する/皮肉を言う/もっとハードにして/許しを乞う/気持ち良くして
	愛撫系反応
		COM:快感を我慢する/快感を受け入れる/気持ち良い！
	苦痛系反応
		COM:痛みを我慢する/悲鳴を上げる

・地の文はACT派生で分岐
・REACT分類が受け入れるの場合のみ処理が異なる
[SKIPEND]
@KOJO_MALE_REACT_K39_90 ;TFLAG:ACT派生(0=普通に休んだ/1=道具のせいであまり休めなかった/2=勝手に自慰をし始めた)
#DIM  DYNAMIC FAV_RATING
#DIMS DYNAMIC SYMBOL
#DIMS DYNAMIC STRING
#DIMS DYNAMIC MSTR
FAV_RATING = GET_KFLAG_K39("相思相愛")
SYMBOL    '= HTML_PS_K39()
STRING    '= RAND_SPLIT("ふふ/うふ/ふふっ/うふっ/くすっ/クスクス") + "…"
MSTR      '= CNAME(MASTER, TARGET)

;REACT90_0, 普通に休んだ // 負荷2は弾く
;=======================
IF !TFLAG:ACT派生 && TFLAG:REACT負荷 < 2
	;COM1, 気弱く応答する
	IF IS_NOWCOMNAME("気弱く応答する") && TFLAG:REACT派生 < 1
		PRINTFORMW 「\@ FAV_RATING ? あまり元気がないみたい… # 元気がありませんね？ \@」
		PRINTFORMW 「\@ FAV_RATING ? ねぇ、まだ疲れが残ってるの？ # まだ疲れが残ってるのでしょうか \@」
		RETURN 1
	ENDIF

	;COM2, 無愛想に応答する
	IF IS_NOWCOMNAME("無愛想に応答する")
		{
		PRINTFORMW 「\@ FAV_RATING 
		? なにそれ、%RAND_SPLIT("機嫌でも悪いの…？/なんで不貞腐れてる訳？")% 
		# そういう態度は%RAND_SPLIT("良くありませんよ？/感心しませんね…")% \@」
		}
		RETURN 1
	ENDIF

	;COM3, 愛想よく応答する
	IF IS_NOWCOMNAME("愛想よく応答する")
		SELECTCASE RANDREACT(3, 30)
			CASE 0
				CALL HTMLPRINTW(@"「%STRING%、元気いっぱいって感じ\@ FAV_RATING ? ね # ですね \@%SYMBOL%」")
			CASE 1
				PRINTFORMW 「\@ FAV_RATING ? ほら # さあ \@、もっと楽にして\@ FAV_RATING ? ？ # ください \@」
				CALL HTMLPRINTW(@"「肩を揉んであげ\@ FAV_RATING ? るから # ます \@%SYMBOL%」")
			CASE 2
				PRINTFORMW 「すっかり元気にな\@ FAV_RATING ? ったわね # りましたね \@」
				{
				CALL HTMLPRINTW(@"「\@ FAV_RATING ? じゃぁ # では \@続きを頑張\@ FAV_RATING 
				? %RAND_SPLIT("ってもらうわよ/ってね")% 
				# %RAND_SPLIT("ってもらいますよ/りましょうか")% \@%SYMBOL%」")
				}
		ENDSELECT
		RETURN 1
	ENDIF

	;COM4, 強気に応答する
	IF IS_NOWCOMNAME("強気に応答する") && TFLAG:REACT派生 < 1
		{
		PRINTFORMW 「\@ FAV_RATING 
		? なにそれ？ 急に強気になっちゃって 
		# 私、そういうのはあまり好きではありません \@…」
		}
		RETURN 1
	ENDIF

	;COM6, もっとハードにして
	IF IS_NOWCOMNAME("もっとハードにして") && TFLAG:REACT派生 < 1
		PRINTFORMW 「すっかり元気になったみたいで何より\@ FAV_RATING ? だ # です \@けど…」
		CALL HTMLPRINTW(@"「そんなこと言って…、後悔しても知\@ FAV_RATING ? らないわよ # りませんよ \@%SYMBOL%」")
		CALL FADE_TO_CLEARLINE(15, @"「%STRING%」")
		RETURN 1
	ENDIF

	;COM8, 気持ちよくして
	IF IS_NOWCOMNAME("気持ち良くして") && TFLAG:REACT派生 < 1
		PRINTFORMW 「%STRING%、元気になったと思ったらそれ\@ FAV_RATING ? な訳 # ですか \@？」
		{
		CALL HTMLPRINTW(@"「尤も…、%MSTR%に言われるまでもなく、最初からそのつもり\@ 
		FAV_RATING ? だけど # ですけど \@%SYMBOL%」")
		}
		RETURN 1
	ENDIF
ENDIF

;REACT90_1, 道具のせいであまり休めなかった
;=========================================
IF TFLAG:ACT派生 == 1
	;相思相愛
	IF FAV_RATING
		PRINTFORML 「やだ、ごめんなさい。落ち着かなかった？」
		PRINTFORMW 「…って、今更言うのはちょっと白々しいかな」
	;その他
	ELSE
		PRINTFORML 「気が付かなくてごめんなさい。落ち着かなかったですか？」
		PRINTFORMW 「って、今頃言っても遅いですよね」
	ENDIF
	CALL FADE_TO_CLEARLINE(15, @"「%STRING%」")
	RETURN 1
ENDIF

;REACT90_2, 勝手に自慰をし始めた
;===============================
IF TFLAG:ACT派生 == 2
	;調教対象の自慰中毒Lv6以上
	IF ABL:MASTER:自慰中毒 > 6
		SELECTCASE RANDREACT(2, 40)
			CASE 0
				PRINTFORMW 「…………」
				{
				PRINTFORMW 「いまさら何を言っても無駄だと思\@ 
				FAV_RATING ? うけど # いますけど\@、敢えて言わせて\@ 
				!FAV_RATING ? ください # \@…」
				}
				{
				CALL HTMLPRINTW(@"「%HTMLFONT(@"<b>いい加減にし\@ FAV_RATING 
				? なさいよ%RAND_SPLIT("っ/")% # てください \@！</b>", , ,)%」")
				}
			CASE 1
				PRINTFORMW 「どうせ言ってもやめる気はないんで\@ FAV_RATING ? しょ # すよね \@？」
				{
				PRINTFORMW 「\@ FAV_RATING ? もうっ！ # \@本当にどうしようもない人\@ 
				FAV_RATING ? %RAND_SPLIT("ね/")%… # ですね… \@」
				}
		ENDSELECT
		RETURN 1
	ENDIF

	;調教対象の自慰中毒Lv3以上
	IF ABL:MASTER:自慰中毒 > 3
		PRINTFORMW 「私とするより自分でする方がいい\@ FAV_RATING ? の # んですか \@！？」
		{
		PRINTFORMW 「それに…、そんな事\@ FAV_RATING 
		? してちゃ休憩にならないでしょ！ 
		# をしていては、休憩になりません… \@」
		}
		RETURN 1
	ENDIF

	;その他
	SELECTCASE RANDREACT(2, 60)
		CASE 0
			PRINTFORMW 「それ\@ FAV_RATING ? じゃ # では\@休憩にな\@ FAV_RATING ? らないでしょう # りませんよね \@？」
			PRINTFORMW 「本当に困った人\@ FAV_RATING ? ね # ですね\@…」
		CASE 1
			CALL FADEIN(15, @"「は？ %MSTR%…、いったい何を……」")
			PRINTFORMW 「そんな事して良いなんて、言った覚えは\@ FAV_RATING ? ないんですけどー！？ # ありませんけど… \@」
	ENDSELECT
	RETURN 1
ENDIF


;///////////////////////////////////////////
;REACT91, 栄養剤を飲ませる・個別リアクション
;///////////////////////////////////////////
[SKIPSTART]
想定されるCOM…
	会話系反応
		COM:沈黙する/気弱く応答する/無愛想に応答する/愛想よく応答する/強気に応答する/皮肉を言う/もっとハードにして/許しを乞う/気持ち良くして
	奉仕系反応
		COM:拒否する

・地の文はTFLAG:ACT派生で分岐
[SKIPEND]
@KOJO_MALE_REACT_K39_91 ;TFLAG:ACT派生(0=栄養剤を飲んだ/1=栄養剤を捨てた)
#DIM  DYNAMIC FAV_RATING
#DIMS DYNAMIC SYMBOL
#DIMS DYNAMIC STRING
#DIMS DYNAMIC MSTR
#DIMS DYNAMIC YOU
#DIMS DYNAMIC TGT
FAV_RATING = GET_KFLAG_K39("相思相愛")
SYMBOL    '= HTML_PS_K39()
STRING    '= RAND_SPLIT("ふふ/うふ/ふふっ/うふっ/くすっ/クスクス") + "…"
MSTR      '= CNAME(MASTER, TARGET)
YOU       '= CALLNAME:MASTER
TGT       '= CALLNAME:TARGET

;栄養剤とかいう謎ドリンク――
;飲むと回復するという以外特に設定もないので好きな様に扱えばいいかと

;REACT91_0, 栄養剤を飲んだ
;=========================
IF !TFLAG:ACT派生
	;積極的に従った
	IF IS_COMGRONAME("平然と従う/積極的従う/受け入れる") && TFLAG:REACT負荷 < 2
		SELECTCASE RANDREACT(3, 0)
			CASE 0
				PRINTFORMW 「%STRING%、そんなに美味しかった\@ !FAV_RATING ? ですか # \@？」
			CASE 1
				CALL HTMLPRINTW(@"「%STRING%、気に入ってもらえたみたい\@ !FAV_RATING ? です # \@ね%SYMBOL%」")
			CASE 2
				PRINTFORMW 「厄ルトをどうやって作っているの\@ FAV_RATING ? かって # か、ですか \@？」
				PRINTL 「それは――
				IF RAND:3
					CALL FADEIN(8, "　　　　　ひ")
					CALL FADEIN(8, "　　　　　　み")
					CALL FADEIN(8, "　　　　　　　つ")
					CALL HTMLPRINTW(@"　　　　　　　　　――\@ FAV_RATING ? よ # です\@%SYMBOL%」")
				ELSE
					PRINTFORML 「――知らない方が良い\@ FAV_RATING ? わ # と思います\@。きっと%MSTR%は……」
					PRINTFORMW 「聞いた事を後悔する\@ FAV_RATING ? と思うから # でしょうから \@」
				ENDIF
				CALL FADE_TO_CLEARLINE(15, @"「%STRING%」")
				{
				PRINTFORMDW \@ FIRSTREACT(0) 
				? %TGT%は結局、%YOU%に厄ルトの秘密を教えてくれなかった 
				# %RAND_SPLIT(@"%TGT%にまたしても、厄ルトの秘密をはぐらかされてしまった/" + 
				"結局のところ、%TGT%は%YOU%に厄ルトの秘密を教える気はなさそうだ…")% \@…
				}
		ENDSELECT
	;その他
	ELSE
		PRINTFORMW 「\@ FAV_RATING ? なんかそれ…、嫌々飲んでない？ # %MSTR%の口には合わなかったみたいですね… \@」
	ENDIF
	RETURN 1
ENDIF

;REACT91_1, 栄養剤を捨てた
;=========================
IF TFLAG:ACT派生 == 1
	{
	PRINTFORMW 「\@ FAV_RATING 
	? %RAND_SPLIT("酷い…、折角作ったのに…/ちょ、ちょっと！ もったいない事しないでよ！")% 
	# %RAND_SPLIT("そんな勿体無い事、やめてください/随分ひどい事をするんですね")%… \@」
	}
	RETURN 1
ENDIF


;///////////////////////////////////
;REACT92, 治療する・個別リアクション
;///////////////////////////////////
[SKIPSTART]
想定されるCOM…
	会話系反応
		COM:沈黙する/気弱く応答する/無愛想に応答する/愛想よく応答する/
		強気に応答する/皮肉を言う/もっとハードにして/許しを乞う/気持ち良くして
	奉仕系反応
		COM:暴れる

・地の文はTFLAG:ACT派生で分岐
[SKIPEND]
@KOJO_MALE_REACT_K39_92 ;TFLAG:ACT派生(0=体の隅々まで撫で回して治療した/1=暴れたため治療できなかった)

;REACT92_0, 体の隅々まで撫で回して治療した
;=========================================
IF !TFLAG:ACT派生
	;好意好感度で派生
	IF GET_KFLAG_K39("相思相愛")
		CALL HTMLPRINTW(@"「これでも一応神様なんだから、まぁ%RAND_SPLIT("これ位の事/このくらい")%はね？ ふふ%HTML_PS_K39()%」")
		PRINTFORMDW そう言い放った%CALLNAME:TARGET%の表情は、どこか得意げだ…
	ELSE
		PRINTFORMW 「どうです？ 少しは回復できたでしょうか」
	ENDIF
	RETURN 1
ENDIF

;REACT92_1, 暴れたため治療できなかった
;=====================================
IF TFLAG:ACT派生 == 1
	IF GET_KFLAG_K39("相思相愛")
		PRINTFORMW 「%RAND_SPLIT("私を困らせて、さぞや楽しいんでしょうね…/そうやって、暴れる元気はあるのね……")%」
		PRINTFORMDW そう言い放つ%CALLNAME:TARGET%の背後に、黒く禍々しいオーラが漂う…
	ELSE
		PRINTFORML 「――困りましたね。私に治療されるのがそんなに嫌ですか？」
		PRINTFORMW 「それとも私、信用されていないんでしょうか……」
	ENDIF 
	RETURN 1
ENDIF
