;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

;	雛口上　オープニング口上ファイル

;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
[SKIPSTART]
	VCSでバージョン管理する以上ファイルヘッダーの履歴は不要
	面倒なだけだし、時々忘れるし、切り捨てられて然るべき文化
	過去の物もこの際バッサリ切り捨てる

	最近はミニマリズムに傾倒
	必要なものは最低限確保した上で可能な限りシンプルにしたい意向

	エディタ Sublime Text3
	テーマ   Spacegray
	この環境で最も美しく見えるように！
[SKIPEND]

;EV1, オープニング口上
;#####################
[SKIPSTART]
	口上主(TARGET)が主人である場合にのみ表示される
	戻り値を2とした場合はOP後の本体側のタイトルロゴは表示されない
	ノーマルモード以外の場合、調教対象がNO:MASTER == 0(いわるゆるあなた)でない可能性あり
[SKIPEND]

@KOJO_MALE_EVENT_K39_1(ARG)
#LOCALSIZE 1
VARSET LOCAL

;//////////////////////////
;//モードに依らない共通処理
;//////////////////////////

;GameBase判定処理 //望ましくない動作環境は蹴る
CALL CHK_GAMEBASE_K39

;呼び名を代入 //代入しない場合、雛はデフォルトで「あんた」と呼んでくるCSVの設定ｗ
;============
;プレイヤーが名前を設定している場合
IF NO:MASTER == 0 && CALLNAME:MASTER != "あなた"
	IF TALENT:MASTER:オトコ          ;//名前入力済みかつ男
		CSTR:(TARGET):呼び方リスト = %NAME:MASTER%_貴方/%CSTR:(TARGET):呼び方リスト%
	ELSEIF TALENT:MASTER:ふたなり    ;//名前入力済みかつふたなり
		CSTR:(TARGET):呼び方リスト = %NAME:MASTER%_貴女/%CSTR:(TARGET):呼び方リスト%
	ELSE                             ;//名前入力済みかつそれ以外（女）
		CSTR:(TARGET):呼び方リスト = %NAME:MASTER%_貴女/%CSTR:(TARGET):呼び方リスト%
	ENDIF
;調教対象がオトコの場合
ELSEIF TALENT:MASTER:オトコ
	CSTR:(TARGET):呼び方リスト = あなた_貴方/%CSTR:(TARGET):呼び方リスト%
;調教対象がふたなりの場合
ELSEIF TALENT:MASTER:ふたなり
	CSTR:(TARGET):呼び方リスト = あなた_貴女/%CSTR:(TARGET):呼び方リスト%
;その他の場合
ELSE
	CSTR:(TARGET):呼び方リスト = あなた_貴女/%CSTR:(TARGET):呼び方リスト%
ENDIF

;各種封印処理
;============
;ACT類
CFLAG:1035 = 100 ;//アナルセックスを封印(事実上、全ての性交系を封印)
CFLAG:1075 = 100 ;//ペニスバンド装着を封印
CFLAG:1070 = 100 ;//どうせ出ないだろうけど、その他容認できない異常系(1070~1074)を封印
CFLAG:1071 = 100 ;//
CFLAG:1072 = 100 ;//
CFLAG:1074 = 100 ;//

;満足ボーナス
CFLAG:抱いてほしい封印 = 1

;////////////////////
;//以下モード毎の処理
;////////////////////

;ノーマルモード
;==============
IF !FLAG:モード ;(0=通常/1=サンドボックス/9=エキストラ)
	[SKIPSTART]
	;OPスキップ処理 //コメントアウト
	;==============
	PRINTFORMDW 　オープニングをスキップしますか？
	PRINTL
	PRINTFORMDL [0] はい
	PRINTFORMDL [1] いいえ
	CALL INPUTINTD(0, 1)
	SELECTCASE RESULT
		CASE 0
			GOTO SKIP
		CASE 1
			PRINTL
	ENDSELECT
	[SKIPEND]

	;OP開始
	;======
	;カットイン用のフォントをセットする
	CALL SETFONT_K39
	;現在のREDRAWを保存
	LOCAL:0 = CURRENTREDRAW()
	;全画面カットインの為にREDRAW 0で描画を抑制する
	REDRAW 0
	;コンフィグをオーバーライド
	CALLF ANIME_CONFIG("TIMES", 1)
	;全画面カットイン2 通常のFULLCUTINより若干早い
	{
	CALL FULLCUTIN2(
	33, 
	17, 
	"――この気持ちに気付かなければ", 
	"　", 
	"　", 
	"　　　　　　こんなに苦しまずにいられたのに")
	}
	;REDRAWを戻す
	REDRAW LOCAL:0
	;フォントを戻す
	CALL RESETFONT_K39
	CALL TWAIT3_K39

	;////////////////////////////////////////////
	;//雛のモノローグはデフォルトカラーで表示する
	;////////////////////////////////////////////

	;CALL FADE_BGCOLOR_K39(10, 0x140A00, 0x00001E, 0, 0)
	PRINTFORMDL 出会いは突然だった
	PRINTFORMDW それが只の偶然だったのか、或いは必然だったのか、この私には分からない――
	;キャラカットイン
	CALL CHARA_CUTIN(CSTR:二つ名, CSTR:登場名, CSTR:ローマ字)
	PRINTL
	PRINTFORMDL それでも一つだけはっきりしている事がある
	PRINTFORMDW あの日以来、私は孤独を知り、愛を知り、憎しみを知った――
	PRINTL
	PRINTFORMDL 棄てられ、流された流し雛より転じて神となったこの身
	PRINTFORMDW 人の厄を祓い、集める厄神は自分の意思に関わらず、周囲の者を不幸にしてしまう
	PRINTFORMDL 故に、人であれ妖であれ周囲に寄り付く者はなかったが、それは当然の事として受け入れていた
	PRINTFORMDW 一人でいるのには慣れている。孤独を友とし、ただ淡々と厄を祓い集め、人々を厄災から守る
	PRINTL
	PRINTFORMDW 何時かこの身が朽ち果てるまで、そういった日々が、ただ静かに過ぎ行くものと思っていた
	;フェード、コンフィグ参照しての行削除、REDRAWによる描画抑制を全て１つで賄う
	CALL FADE_TO_CLEARLINE(30, "あの出会いの時迄は――")
	CALL TWAIT1_K39
	;BGM_CUTINは引数0を正にして♪ではなくＢＧＭで表示
	CALL BGM_CUTIN(CSTR:曲２, 1)   ;//厄神様の通り道　～ Dark Road
	PRINTFORMDW 厄を集めたいつもの帰り道、普段と変わらぬ、見慣れた長閑な風景
	PRINTFORMDW だがその日は何かが違っていた。何か只ならぬ気配を感じた――
	PRINTL
	PRINTFORMDW 程なくその気配の正体が見えてきた。あれは厄、それもかなりのものだ
	PRINTFORMDW そして渦巻く黒い厄の中心に人影らしきものが見える――
	PRINTL
	PRINTFORMDW 意識を失っているようだが、これだけの厄に塗れていては無理からぬ事
	PRINTFORMDW しかし普通の人間が、これほどの厄を溜め込むのは見た事が無いし、少し奇妙な感じがする
	PRINTFORMDW それでも人の厄災を祓うのはこの私の務め、見捨てておける訳もない
	PRINTL
	PRINTFORMDW 近寄ってみると、どうやら気絶しているだけで命に別状はないようだ
	PRINTFORMDW そしてその手には既に原型を留めない”何か”が握られている
	PRINTFORMDW 不審に思いつつも、今はそんな事に構っている場合ではない
	PRINTFORMDW そっと揺り動かしてみると程なくして意識を取り戻し、こちらの存在に気が付いたようだった
	PRINTL
	PRINTFORMW 「私は鍵山雛、人の厄を祓う厄神です」
	PRINTFORMW 「%CNAME(MASTER, TARGET)%の厄災、私が全て引き受けましょう」
	CALL BGM_CUTIN(CSTR:曲１, 1) ;//ＢＧＭカットイン 運命のダークサイド
	PRINTL
	;引数が多いものは引数毎に改行した方が可読性が向上する(だろう)的な考え
	{
	CALL FADE(
	40, 
	GETCOLOR(), 
	GETBGCOLOR(), 
	"　　私は無邪気にそう言い放った", 
	"　　　　　　　　　この先に待ち受ける運命も知らずに――")
	}
	;コンフィグを参照して必要なら行削除を行いさらにREDRAW 0で削除中の描画を抑制する
	CALL FADE_CLINE(1)
	CALL TWAIT3_K39
	;$SKIP

	;OP経過判定。後のイベントはOPを経過していない場合は排他にする
	;現状オトコ用なので大した意味はないけど
	KFLAG:(FLAG:ご主人様):50 = 1
	;RESULT値内訳(0.表示しない/1,OPを表示した後本体のタイトルロゴを表示/2, OPを表示後に本体のタイトルロゴを表示しない)
	RETURN 2
ENDIF


;サンドボックスまたはエキストラモード時
;======================================
;ノーマルモード以外の場合は本体の汎用OPに飛ばす
;汎用OPに飛ばした際はこの箇所の戻り値に関わらずタイトルロゴが表示される
IF FLAG:モード != 0
	;RESULT値0を返して汎用OPにパスする
	RETURN 0
ENDIF