;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

;	雛口上　調教者行動前イベント口上ファイル

;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

;EV12, 調教者行動前口上
;######################
[SKIPSTART]
	・TRAIN_PROCCES.ERB内、@TRAIN_CANCELから呼び出される
	・口上主のパラメータ表示の直後に表示。調教指示は既に決定されている
	・調教中止宣言の差し替えや、調教ターン開始前にご主人様から一言、のように書くことも可能
	・午前、午後の調教開始ではなく、調教ターンの開始前には毎回呼び出される箇所
	
	・その為もあって、調教中止時と経過時間0以外の箇所はIF 口上記入チェック(25)等として確率表示する事を推奨
	・調教中止時にのみ地の文が存在する
	・文章表示後に本体側のDRAWLINE処理あり
[SKIPEND]


@KOJO_MALE_EVENT_K39_12(ARG)
[SKIPSTART]
	調教中止 ARG:1 ARG:2 ARG:3 FIRSTEVENT(0)～
	疲労、もしくはご褒美による調教中止の場合の口上
	RESULT値1の場合は口上表示後本体側でPRINTLの空行が入る
	RESULT値2にすると、地の文を表示しない

	#引数内訳
		ARG=(0.調教可/1.口上主の疲労で中止/2.調教対象の疲労で中止/3.ご褒美による中止)

	#RESULT値
		RETURN=(1, 口上の後に地の文を表示(非推奨)、2, 口上のみ表示)
[SKIPEND]
VARSET LOCALS
{
LOCALS:0 '= RAND:3 
? HTML_HEART(, COLOR2_K39("PINK")) 
# HTML_EIGHTHNOTE()
}


;ご褒美による中止//見返してご褒美と疲労による中止逆じゃね？ と思ったので入れ替える
;================
IF ARG == 3 && KFLAG:50 == 1 && TALENT:TARGET:ご主人様 && FLAG:オートモード表示設定 < 1 ;//オートモード簡略表示の際は弾く
	;相思相愛
	;========
	IF GET_KFLAG_K39("相思相愛", (FLAG:ご主人様)) && !RAND:2 ;1/2で真
		;昼間
		IF !TIME
			PRINTFORMW 「気分転換にちょっとその辺でも散歩しない？」
			PRINTFORMW 「%CNAME(MASTER, TARGET)%も疲れてるし、あまり遠出はできないけど、ねぇ、どこが良い？」
			PRINTL
			PRINTFORMDL [0] 山に行きたい
			PRINTFORMDL [1] 川に行きたい
			;PRINTFORMDL [2] 家でのんびりしたい
			CALL INPUTINTD(0, 1)
			;オートなら止める
			SIF FLAG:オートモード > 0
				FORCEWAIT
			SELECTCASE RESULT
				CASE 0
					PRINTFORML 「山ね、いいわよ」
					PRINTFORMW 「あまり奥まで行かなければ、哨戒天狗も邪魔しないと思うし」
					CALL TWAIT_3
					;厄神様の通り道　～ Dark Road
					CALL BGM_CUTIN(CSTR:曲２, 1)
					{
					CALL HTMLPRINTW(@"「%HTMLFONT(
					"花の色は うつりにけりな いたづらに 我が身世にふる ながめせしまに", 
					"ＭＳ 明朝")%～」")
					}
					PRINTFORMDW %CALLNAME:TARGET%は上機嫌で和歌を詠んでいた。それはどこか聞き覚えのあるものだった
					PRINTL
					PRINTFORML 「あら、知ってるみたいね？」
					PRINTFORMDW %CALLNAME:TARGET%は微かに微笑みながらそう言った
					PRINTL
					PRINTFORML 「外界と共有してるものがあるってちょっと素敵ね」
					CALL HTMLPRINTW(@"「まぁ、この歌の内容は、私には当てはまらないんだけどね%LOCALS:0%」")
					PRINTL
					SELECTCASE FLAG:季節
						CASE 0     ;春
							CALL HTMLPRINTW(@"「ほら、見て！ 桜がとっても綺麗…%LOCALS:0%」")
							PRINTFORMDW 頬を撫でる心地良いそよ風と、微かな木々のざわめきを楽しみながら、二人は散歩を満喫した…
						CASE 1     ;夏
							CALL HTMLPRINTW(@"「流石に昼間は暑いわねぇ。でもこの山の緑は夏ならではね%LOCALS:0%」")
							PRINTFORMDW 日差しは強かったが、夏特有の風景を楽しみながら、二人は散歩を満喫した…
						CASE 2     ;秋
							CALL HTMLPRINTL(@"「紅葉は綺麗だし風は涼しくて気持ち良いし、やっぱり秋は最高ね%LOCALS:0%」")
							PRINTFORMW 「そういえば、あの姉妹は元気にしているのかしら…？」
							PRINTFORMDW 燃えるような紅葉と、涼しい秋風を楽しみながら、二人は散歩を満喫した…
						CASE 3     ;冬
							PRINTFORML 「ねぇ寒くない？　私は大丈夫だからいいけど…」
							PRINTFORMW 「あっ！　見て。雪が降ってきたわよ。これは結構積もりそうねー」
							PRINTFORMDW 小雪が舞い、辺りはかなり寒かったが、山の冬景色を楽しみながら、二人は散歩を満喫した…
					ENDSELECT
					PRINTL
					PRINTFORML 「うふっ、楽しかったわね」
					PRINTFORMW 「ねぇ、今度は川に行ってみない？」
				CASE 1
					PRINTFORMW 「川にいきたいの？　そう…、いいわよ。行きましょう」
					PRINTFORML 「そうそう、河童って見た事ある？」
					PRINTFORMW 「もしかしたら、会えるかもしれないわよ？　うふっ」
					CALL TWAIT_3
					CALL BGM_CUTIN(CSTR:曲２, 1)
					{
					CALL HTMLPRINTW(@"「%HTMLFONT(
					"春たてば　消ゆる氷の残りなく　君が心はわれにとけなむ～", 
					"ＭＳ 明朝")%」")
					}
					PRINTFORMDW %CALLNAME:TARGET%は上機嫌で和歌を詠んでいた。それはあまり聞き覚えがないものだった
					PRINTL
					PRINTFORML 「何よ、これ知らないの？」
					PRINTFORMDW %CALLNAME:TARGET%はちょっと不機嫌そうだった…
					PRINTL
					PRINTFORML 「――まったく。鈍いというか、鈍感というか……」
					PRINTFORMDW %CALLNAME:TARGET%は何事か一人で呟いている…
					PRINTL
					SELECTCASE FLAG:季節
						CASE 0     ;春
							PRINTFORMW 「川の水もまだちょっと冷たいかしらね？」
						CASE 1     ;夏
							CALL HTMLPRINTW(@"「足元だけでも川に入るのもいいかも知れないわね%LOCALS:0%」")
						CASE 2     ;秋
							PRINTFORMW 「そろそろ水も冷たくなってきた感じかしらね？」
						CASE 3     ;冬
							PRINTFORMW 「流石にこの季節に川に入るのはどうかと思うわよ？」
					ENDSELECT
					PRINTFORMDL 結局、河童の姿を目にする事は無かったが、二人は川沿いの散策を十分に楽しんだ
					PRINTFORMDW 未だ自然の色濃く残るこの土地に、あなたは少しずつ魅了されていくのを感じていた…
					PRINTL
					PRINTFORML 「どう？　楽しんでもらえたかしら」
					PRINTFORMW 「じゃ、今度は山に行ってみる？」
			ENDSELECT
			CALL TWAIT_3
			PRINTFORML 「あら、もうこんな時間。そろそろ食事の準備をしないとね」
			PRINTFORMW 「ほら、%CNAME(MASTER, TARGET)%もちゃんと手伝ってくれないと駄目よ？」
		;夜間
		ELSE
			PRINTFORMW 「ねぇ見て！ \@ GROUPMATCH(FLAG:季節, 0, 2) ? 月 # 星 \@がとっても綺麗…」
			{
			PRINTFORMW 「折角だから、\@ 
			GROUPMATCH(FLAG:季節, 0, 2) 
			? 軽く月見と洒落込むのはどう 
			# 星空鑑賞なんてどうかしら 
			\@？」
			}
			PRINTL
			PRINTFORMDL [0] ＯＫ！　\@ GROUPMATCH(FLAG:季節, 0, 2) ? 月見をしよう # 星空鑑賞と洒落込もう \@
			PRINTFORMDL [1] \@ GROUPMATCH(FLAG:季節, 0, 2) ? 月見しながら # 星空の下で \@イチャイチャしたい！
			CALL INPUTINTD(0, 1)
			;オートなら止める
			SIF FLAG:オートモード > 0
				FORCEWAIT
			SELECTCASE RESULT
				CASE 0
					PRINTFORMW 「あら、ちょっと意外」
					PRINTFORMW 「風流を楽しむ心があるなんて、少し見直したわよ？　うふふっ」
					PRINTL
					SELECTCASE FLAG:季節
						CASE 0     ;春
							PRINTFORML 「こうして見ると、朧月も悪くないわね」
							PRINTFORMW 「なんて言えばいいのか、上手く言葉が見つからないけど…」
						CASE 1     ;夏
							PRINTFORML 「月もいいけど、夏といえばやっぱり天の川よね」
							PRINTFORMW 「ねぇ、射手座って判る？ ほら、あそこに見えるやつ」
							PRINTFORMW 「あの方角が、私達の銀河系の中心らしいわよ」
							PRINTFORML 「えっ、なぜそんな事知ってるのかって？」
							PRINTFORMW 「さて、どうしてかしら…ね？ ふふ」
						CASE 2     ;秋
							{
							CALL HTMLPRINTL(@"「%HTMLFONT(
							"秋風に　たなびく雲の　絶え間より　もれ出づる月の　影のさやけさ", 
							"ＭＳ 明朝")%～」")
							}
							PRINTFORMW 「思わず歌っちゃうくらい月が綺麗ねー」
							PRINTL
							PRINTFORMW 「ねぇ折角だから…、%CNAME(MASTER, TARGET)%も何か歌ってみてよ」
							PRINTFORMW 「ふふ…ま、無理にとは言わないけど」
						CASE 3     ;冬
							PRINTFORML 「冬の星空ってホント綺麗…。手を伸ばせば届きそうな気がするわ」
							PRINTFORMDW %CALLNAME:TARGET%は空に手を差し伸ばしながらそう言った…
							PRINTFORMW 「ま、流石に…厄神でもそんな事はできっこないけど。ふふ」
					ENDSELECT
					{
					PRINTFORMDW \@ 
					GROUPMATCH(FLAG:季節, 0, 2) 
					? 月見 # 満天の星空 
					\@を楽しみながら会話も弾み、二人は楽しいひとときを過ごした\@ 
					INRANGE(FLAG:季節, 1, 2) 
					? … # \@
					}
					;季節が春もしくは冬の場合に表示
					IF GROUPMATCH(FLAG:季節, 0, 3)
						PRINTL
						{
						CALL HTMLPRINTW(@"「%HTMLFONT(
						"色もなき心を人にそめしより　うつろはむとは思ほえなくに", 
						"ＭＳ 明朝")%～」")
						}
						PRINTFORMDL 歌を詠んだ%CALLNAME:TARGET%と、つかのま視線が交差する
						PRINTFORMDW 言葉は要らなかった、必要無かった
						PRINTFORMDW 視線の先に情愛の念を絡め合い、二人はただ見つめあった…
					ENDIF
				CASE 1
					PRINTFORMW 「……まったく。風情が無いにも程があるわね！」
					PRINTFORMW 「でも……ちょっとだけなら、いいよ？」
					PRINTL
					SELECTCASE FLAG:季節
						CASE 0     ;春
							PRINTFORMW 「まだ夜風がちょっと冷たい感じかしらね？」
						CASE 1     ;夏
							CALL HTMLPRINTW(@"「夜風が涼しくて気持ちいいわね%LOCALS:0%」")
						CASE 2     ;秋
							CALL HTMLPRINTW(@"「秋に見る月は最高ねー。ススキと月見団子も用意したから完璧ね%LOCALS:0%」")
						CASE 3     ;冬
							CALL HTMLPRINTW(@"「冬の夜空の月も、なかなか風情があっていいわね%LOCALS:0%　ちょっと寒いけど」")
					ENDSELECT
					PRINTFORMDW 互いに童心に帰ったかのようにじゃれあいながら、二人は楽しいひとときを過ごした…
			ENDSELECT
			CALL TWAIT_3
			PRINTFORML 「あら、もうこんな時間。そろそろ寝ないと」
			PRINTFORMW 「一緒に寝てもいいけれど、イタズラしては駄目だからね？　クスクス…」
		ENDIF
		RETURN 2
	ENDIF

	;汎用処理
	;========
	PRINTFORM 「今日の\@ !TIME ? 昼 # 夕 \@食は
	;昼
	SIF !TIME
		{
		PRINTFORM %RAND_SPLIT(
		"ピリ辛肉味噌丼/カルボナーラリゾット/明太子パスタ/ミートボールのオープンオムレツ/" + 
		"サバ味噌キャベツ丼/野菜たっぷり炒飯/じゃがいもベーコンチーズ炒め/ピーマンそぼろランチ")%
		}
	;夜
	SIF TIME
		{
		PRINTFORM %RAND_SPLIT(
		"豚ニラもやし炒め/大豆ドライカレー/スペアリブ煮込み/明太マヨのクリームリゾット/イワシのフライ/" + 
		"ローストチキン/鶏もも肉とにんにくの煮物/煮込みハンバーグ/鶏もも肉のトマト煮/豚こま肉の回鍋肉")%
		}
	PRINTFORMW に\@ GET_KFLAG_K39("相思相愛", (FLAG:ご主人様)) ? するわ # しましょう \@」
	{
	CALL HTMLPRINTW(
	@"「%CNAME(MASTER, TARGET)%はゆっくり休んでいて\@ 
	!GET_KFLAG_K39("相思相愛", (FLAG:ご主人様)) 
	? ください # \@ね%LOCALS:0%」")
	}
	RETURN 2
ENDIF

;調教者の疲労による中止
;======================
IF ARG == 1
	;RETURNを2以下にした場合は以下の地の文が表示される。表示タイミングは口上表示の後
	;//調教の時間が来たが、入ってきた%CALLNAME:TARGET%はなにやら疲れている様子で、
	;//今回は休みだと言って出ていった…
	RETURN 0
ENDIF

;調教対象の疲労による中止
;========================
IF ARG == 2 && KFLAG:50 == 1 && TALENT:TARGET:ご主人様 ;雛がご主人様かつOPを経由している場合にのみ表示
	;相思相愛
	;========
	IF GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))
		;初回
		;====
		IF FIRSTEVENT(0)
			;口授ではたてから取材を受けてるとこの挿絵ではテーブルと椅子の洋間だった感。むしろ紅茶の方が似合いそうだったｗ
			PRINTFORML 「そうそう、新しい茶葉が手に入ったの」
			CALL BGM_CUTIN(CSTR:14, 1)
			CALL HTMLPRINTW(@"「丁度いい機会だからお茶を淹れるわね%LOCALS:0%」")
			PRINTFORMW 「これは本山茶って言うんだけど、%CNAME(MASTER, TARGET)%は飲んだ事ある？」
			PRINTFORMDW %CALLNAME:TARGET%は日本茶に拘りがあるようで、とりわけ本山茶を気に入ってるようだった
			PRINTFORMDW そうして茶に関する薀蓄を聞かされながら、時は静かに過ぎていった……
			RETURN 2
		ENDIF

		;初回以外
		;========
		CALL BGM_CUTIN("厄災流れ", 1)
		;階層型イベント 0から順に消費する
		SELECTCASE ORDEREVENT(0)
			CASE 0
				PRINTFORML 「この前出したお茶だけど、気に入って貰えた？」
				PRINTFORMW 「あれは弱火入れといってちょっと高級なお茶なのよ」
				PRINTFORMW 「お茶の味と香りを仕上げるのに、火入れってのは凄く大切で……」
				PRINTFORMDW 茶を飲みながらのお茶談義に花を咲かせ、二人は楽しいひとときを過ごした…
			CASE 1
				PRINTFORMW 「なんで日本茶に拘るのかって？」
				PRINTFORML 「だって八百万の神が紅茶とかコーヒーって似合わないでしょ？」
				PRINTFORMW 「今でこそ幻想郷でも手に入るようになったけど、元々は無かったものだし…」
				PRINTL
				PRINTFORML 「まぁそんな事より、とりあえずこのお茶を飲んでみて？」
				PRINTFORMW 「これは炭火遠赤仕上げといって、馥郁な香りが持続するって評判らしいんだけど…」
				PRINTFORMDW そうして時は過ぎていった…
			CASE 2
				PRINTFORMW 「ねぇ、外の世界には大きな茶畑があるんでしょう？」
				PRINTFORMW 「ここにも茶畑はあるけれど、皆小規模なのよね」
				PRINTFORMW 「一度でいいから視界一面に広がる茶畑を見てみたいわ」
				PRINTL
				PRINTFORMW 「それは山の斜面であったり、平地に広がるものであったり、きっと見事な光景なんでしょうね」
				PRINTFORMDW 見知らぬ土地に想いを馳せる%CALLNAME:TARGET%の瞳は輝きつつも、それは微かな寂しさも漂わせていた…
			CASE 3
				PRINTFORMW 「はいこれ、ほうじ茶よ。たまにはこういうのも良いと思って」
				PRINTFORMW 「勿論本山産よ。小石を熱して炭火焼き風に仕上げたものらしいんだけど…」
				PRINTFORMDW %CALLNAME:TARGET%の茶に対する探究心には限りがないようだった…
			;3以降は汎用処理する
			CASEELSE
				{
				PRINTFORML 「%RAND_SPLIT(
				"時にはこうして、のんびり過ごすのも悪くないわね/" + 
				"考えてみると…、何もしないで過ごす時間って、凄く贅沢なのかも知れないわね/" + 
				"う～ん。なんかこう…、ぼーっとしてると眠くなってきちゃったわ…")%」
				}
				PRINTFORMDW そうして二人は、ゆったりとした時間を過ごした…
		ENDSELECT
		RETURN 2
	ENDIF

	;その他
	;======
	PRINTW 「どうぞゆっくり休んでいて下さい」
	{
	PRINTFORMW 「私は%RAND_SPLIT(
	"ちょっと厄集めに行ってきますので/" + 
	"集めた厄を神様に渡してきますので/" +
	"ちょっと出掛けてきます")%」
	}
	{
	PRINTFORMD %CALLNAME:TARGET%はそう言い残すと、%RAND_SPLIT(
	"いそいそと/" + 
	"少し慌しく/" + 
	"微かに気だるげな表情を見せながら")%
	}
	PRINTDW 出掛けていった…
	RETURN 2
ENDIF

;/////////////////////////
;//以下、ARG:0(調教可)相当
;/////////////////////////

[SKIPSTART]
	TRAIN_PROCCES.ERB内、@BEFORE_ACTから呼び出される
	この時点でACTはすでに決定しているがACT派生の決定はなされていない
	NOWEXのリセットはなされていない
	これらの箇所を抜けた後にACT開始になる
[SKIPEND]

;調教開始直後 FIRSTEVENT(10)～
;#############################
[SKIPSTART]
	経過時間が0である場合に表示される
	その回の調教開始直後に一度きりで、以後その調教中には表示されない
	この箇所を抜けた後にACT開始になる
[SKIPEND]

IF !TFLAG:経過時間
	;EVENT_001直後
	;=============
	IF (KFLAG:0 == 1 || KFLAG:1 == 1) && FIRSTEVENT(10)
		PRINTFORMW 「ごめんなさい。ちょっと変な雰囲気になっちゃったわね」
		PRINTFORMW 「気を取り直していつも通りにしたいんだけど……いい？」
		PRINTFORMW 「じゃ、そういう訳でよろしくお願いね？」
		RETURN 1
	ENDIF

	;調教者が少し疲れている
	;======================
	IF CFLAG:消耗 > 2
		PRINTFORMDW %CALLNAME:TARGET%は少し疲れているようだ…
		IF GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))
			PRINTFORMW 「んー、ちょっと疲れてるんだけど」
			PRINTFORMW 「やめる訳にもいかない%RAND_SPLIT("わね…/のよね…/でしょう？ ふふ")%」
		ELSE
			PRINTFORMW 「体調が優れなくても、厄祓いをやめる訳にはいきませんね…」
		ENDIF
		RETURN 1
	ENDIF

	;調教対象が少し疲れている
	;========================
	IF CFLAG:MASTER:消耗 > 2
		IF GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))
			PRINTFORMW 「ちょっと疲れが残ってるみたいね」
			PRINTFORMW 「少しだけなら大丈夫？」
		ELSE
			PRINTFORMW 「あまり体調が良くないみたいですね？」
			PRINTFORMW 「これも厄を祓う為と思って、少しの間辛抱してください」
		ENDIF
		RETURN 1
	ENDIF

	;その他
	;======
	IF GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))
		PRINTFORMW 「準備はいい%RAND_SPLIT("かしら/")%？」
		PRINTFORMW 「じゃ、始め%RAND_SPLIT("ましょ♪/るわよ！ ふふっ")%」
	ELSE
		PRINTFORMW 「準備は宜しいで%RAND_SPLIT("しょうか/すか")%？」
		PRINTFORMW 「では始め%RAND_SPLIT("ましょう/させてもらいますね…")%」
	ENDIF
	RETURN 1
ENDIF

;お仕置き FIRSTEVENT(20)～
;#########################
[SKIPSTART]
	TFLAG:お仕置きフラグが立っている場合に表示される箇所
	お仕置きフラグが立っていても、加虐系以外のACTになる場合がある

	加虐系ACT、派生する事により加虐系に含まれる可能性のあるイラマチオ
	衣装の変更を要求する/不気味に笑う/アナルバイブ/後背位/アナルセックス/自慰/野外プレイ/放尿/足舐め強制
	異常系ACT/性交奉仕系ACT
	上記のACT以外はTFLAG:お仕置きフラグによってACT_ABLEで規制される
[SKIPEND]

;IF 口上記入チェック(0) && TFLAG:お仕置きフラグ && ONCEEVENT(20)
;ENDIF

;お仕置き中
;==========
;口上記入チェック(確率％, 入力した数字の確率で口上を表示)
IF 口上記入チェック(25) && TFLAG:お仕置きフラグ
	;前回ACTがお仕置き宣言かつ今回ACTが加虐系
	;========================================
	IF IS_PREVACTNAME("お仕置きと宣言する") && MENUMATCH(TFLAG:ACT, "加虐")
		{
		PRINTFORMW 「\@ 
		GET_KFLAG_K39("相思相愛", (FLAG:ご主人様)) 
		? まだお仕置きが必要みたいね… 
		# もう少しお仕置きが必要みたいですね… \@」
		}
		RETURN 1
	ENDIF

	;前回MENUが加虐系
	;================
	IF IS_PREVMENUNAME("加虐")
		{
		PRINTFORMW 「\@ 
		GET_KFLAG_K39("相思相愛", (FLAG:ご主人様)) 
		? ねぇ、%RAND_SPLIT("少しは反省した？/ちょっとは反省して…")% 
		# %RAND_SPLIT("少しは反省したでしょうか/ちょっとは反省してください…")% \@」
		}
		RETURN 1
	ENDIF
ENDIF


;膣内、腸内射精直後 FIRSTEVENT(30)～
;###################################
[SKIPSTART]
	NOWEX:射精ないしNOWEX:MASTER:射精で射精種の参照も可能だったりしますが
	EVENT20, 絶頂口上に同種の分岐があると被る可能性もあります

	#関連変数…
		TFLAG:前回中出し(1=Ｖ性交/2=Ｖ性交奉仕/3=Ｖ性交(空射)/4=Ｖ性交奉仕(空射)
                         5=Ａ性交/6=Ａ性交奉仕/7=Ａ性交(空射)/8=Ａ性交奉仕(空射))

		NOWEX:射精      (1=空射精/2=射精/3=大量射精/4=射精(早漏)/5=前立腺射精)
[SKIPEND]

;膣内/腸内射精直後(調教対象→調教者)
;===================================
;口上記入チェック（確率％, 入力した数字の確率で口上を表示）
IF 口上記入チェック(25) && GROUPMATCH(TFLAG:前回中出し, 2, 4, 6, 8) > 0
	;空射精
	;======
	IF GROUPMATCH(TFLAG:前回中出し, 4, 8)
		{
		CALL HTMLPRINTW(@"「\@ 
		GET_KFLAG_K39("相思相愛", (FLAG:ご主人様)) 
		? ねぇ、もう出ないの？　空っぽ？ 
		# 搾り取り過ぎてしまいましたかね%HTML_PS_K39()% \@」")
		}
		RETURN 1
	ENDIF

	;その他
	;======
	IF GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))
		PRINTFORMW 「これだけ射精してもまだいけそな感じね…」
		CALL HTMLPRINTW(@"「出し惜しみなんて許さないわよ？ 空っぽになるまで搾り取ってあげる%HTML_PS_K39()%」")
	ELSE
		PRINTFORMW 「これだけ出しても硬くて…まだいけそうですね？」
		{
		CALL HTMLPRINTW(
		@"「%RAND_SPLIT(
		"全部私の中に出すまで続けますよ/" + 
		"では、全部出すまで続けますよ")%" +
		@"%HTML_PS_K39()%」")
		}
	ENDIF
	RETURN 1
ENDIF

;性交中・性交奉仕中 FIRSTEVENT(40)～
;###################################
[SKIPSTART]
	#関連変数…
		TEQUIP:性交中    //調教者が調教対象に挿入中 (1=正常位/2=後背位/3=騎乗位/4=対面座位/5=背面座位/6=アナルセックス)
		TEQUIP:性交奉仕中//調教対象が調教者に挿入中 内容は上記のものと同じ

	#関連関数…
		V_SEX (=TARGET, TEQUIP:性交中 < 6/=MASTER, TEQUIP:性交奉仕中 < 6)と同義
		A_SEX (=TARGET, TEQUIP:性交中 == 6/MASTER, TEQUIP:性交奉仕中 == 6)と同義
[SKIPEND]

;性交奉仕中(調教対象→調教者)
;============================
IF 口上記入チェック(25) && TEQUIP:性交奉仕中
	;Ａ性交奉仕中
	;============
	IF A_SEX(MASTER)
		;今回のACTが性交奉仕系
		IF MENUMATCH(TFLAG:ACT, "性奉")
			;調教者Ａ感覚派生
			SELECTCASE ABL:TARGET:Ａ感覚
				CASE IS >= 5
					{
					CALL HTMLPRINTW(
					@"「%RAND_SPLIT("や/ん/ふ/あ")%ぁぁ、ダメぇっ" + 
					@"%HTML_AH_K39()%　おかしくなっちゃうっ%HTML_PS2_K39()%」")
					}
					{
					CALL HTMLPRINTW(
					@"「おしり、突かれて%RAND_SPLIT("ぇ/")% ッ……" + 
					@"%RAND_SPLIT("バカ/変")%になっちゃうっ%RAND_SPLIT("っ！/")%！！」")
					}
				CASE IS >= 3
					{
					CALL HTMLPRINTW(
					@"「は%RAND_SPLIT("ぅ/ぁ/")%っ、%RAND_SPLIT("あ/ん/")%んっ……" + 
					@"あっ、あ%RAND_SPLIT("ぁ/ぅ/")%っ%HTML_PS2_K39()%」")
					}
					{
					CALL HTMLPRINTW(
					@"「どうして、おしり……こんなにッ…%HTML_AH_K39()%　" + 
					@"%RAND_SPLIT("んぁ/あ/ふ")%ぁっ%HTML_PS2_K39()%」")
					}
				CASEELSE
					{
					CALL HTMLPRINTW(
					@"「%RAND_SPLIT("は/あ/う/く/ん")%ぅっ、んっ%HTML_AH_K39()%　" + 
					@"くっ…あっ、%RAND_SPLIT("ひ/あ/う")%ぅっ……%HTML_PS2_K39()%」")
					}
			ENDSELECT
		;その他
		ELSE
			;調教者Ａ感覚派生
			SELECTCASE ABL:TARGET:Ａ感覚
				CASE IS >= 5
					{
					CALL HTMLPRINTW(
					@"「%RAND_SPLIT("ひ/あ/ふ/ん")%ぁっ%HTML_AH_K39()%　" + 
					@"いきなり、そんな…奥までッ%HTML_PS2_K39()%」")
					}
					CALL HTMLPRINTW(@"「い、痛いのにっ！ おしり、気持ちよくって！ あ゛ぁぁっ%HTML_PS2_K39()%」")
				CASE IS >= 3
					{
					CALL HTMLPRINTW(
					@"「%RAND_SPLIT("ん/あ/く")%っ、あぁっ" + 
					@"%HTML_AH_K39()%　おしり、気持ちよくってッ！」")
					}
				CASEELSE
					{
					CALL HTMLPRINTW(
					@"「%RAND_SPLIT("ん/あ/あぅ/んん")%っ、くっ" + 
					@"%HTML_AH_K39()%　お願い…もっと、ゆっくり……」")
					}
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF

	;Ｖ性交奉仕中
	;============
	IF V_SEX(MASTER)
		;今回のACTが性交奉仕系
		IF MENUMATCH(TFLAG:ACT, "性奉")
			;調教者Ｖ感覚派生
			SELECTCASE ABL:TARGET:Ｖ感覚
				CASE IS >= 5
					{
					CALL HTMLPRINTW(
					@"「%RAND_SPLIT("や/あ/ん/ふ")%ぁぁ、ダメぇっ" + 
					@"%HTML_AH_K39()%　おかしくなっちゃうっ！」")
					}
					{
					CALL HTMLPRINTW(
					@"「そんなに突いたらッ…%HTML_AH_K39()%　" + 
					@"バカになっちゃうっ%RAND_SPLIT("っ！/")%！」")
					}
				CASE IS >= 3
					{
					CALL HTMLPRINTW(
					@"「%RAND_SPLIT("は/あ/ん")%ぅっ、あんっ…" + 
					@"%HTML_AH_K39()%　あっ、あぁっ%HTML_PS2_K39()%」")
					}
					{
					CALL HTMLPRINTW(
					@"「%RAND_SPLIT("なか/ナカ")%が、擦れてっ！ ゾクゾク" + 
					@"%RAND_SPLIT("が、とまらなくっ/しちゃっ")%てッ……あぁっ%HTML_PS2_K39()%」")
					}
				CASEELSE
					{
					CALL HTMLPRINTW(
					@"「%RAND_SPLIT("は/あ/ん/う")%ぅっ、" + 
					@"%RAND_SPLIT("ん/あ/はぁ/…")%っ%HTML_AH_K39()%　くっ…あっ、" + 
					@"%RAND_SPLIT("ひ/あ/ん")%ぅっ……」")
					}
			ENDSELECT
		;その他
		ELSE
			;調教者Ｖ感覚派生
			SELECTCASE ABL:TARGET:Ｖ感覚
				CASE IS >= 5
					{
					CALL HTMLPRINTW(
					@"「%RAND_SPLIT("ひ/あ/う/ん/")%ぁっ" + 
					@"%HTML_AH_K39()%　いきなり、そんな…奥までッ！」")
					}
					{
					CALL HTMLPRINTW(
					@"「%RAND_SPLIT("ダメぇぇ/やぁぁ/ぅあぁ")%っ" + 
					@"%HTML_AH_K39()%　そんなに、奥突いちゃっ！ " + 
					@"あ゛ぁぁっ、やぁぁっ%HTML_PS2_K39()%」")
					}
				CASE IS >= 3
					{
					CALL HTMLPRINTW(
					@"「%RAND_SPLIT("あ/ん/ふぁ")%っ、あぁっ" + 
					@"%HTML_AH_K39()%　突いてッ！ もっと激しくっ" + 
					@"%RAND_SPLIT("…ッ/")%！」")
					}
				CASEELSE
					{
					CALL HTMLPRINTW(
					@"「%RAND_SPLIT("んっ、く/あ…ん/はぅ/あぅ")%っ" + 
					@"%HTML_AH_K39()%　お願い…もっと、ゆっくり……」")
					}
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
ENDIF


;絶頂した直後 FIRSTEVENT(50)～
;#############################
[SKIPSTART]
	調教者、調教対象が絶頂した場合に表示される口上
	個別の部位別絶頂を拾いたいなら、IF 口上記入チェック(100) && NOWEX:Ｃ絶頂等の分岐を追加してください
	射精種はNOWEX:射精/NOWEX:MASTER:射精で参照する事が可能です

	#関連変数…
		NOWEX:射精(1=空射精/2=射精/3=大量射精/4=射精(早漏)/5=前立腺射精)

	#関連関数…
		TRUECHECK 真になるものがいくつあるかを返す
[SKIPEND]

;調教者が絶頂した直後
;====================
IF TRUECHECK(NOWEX:Ｃ絶頂, NOWEX:Ｖ絶頂, NOWEX:Ａ絶頂, NOWEX:Ｂ絶頂) > 0
	;深くイった
	;==========
	IF (NOWEX:Ｃ絶頂 + NOWEX:Ｖ絶頂 + NOWEX:Ａ絶頂 + NOWEX:Ｂ絶頂) > 1
		;多重絶頂
		IF TRUECHECK(NOWEX:Ｃ絶頂, NOWEX:Ｖ絶頂, NOWEX:Ａ絶頂, NOWEX:Ｂ絶頂) > 1
			PRINTFORMW 「知らない、こんなの、知らないよぉ……」
			PRINTFORMD %CALLNAME:TARGET%は粘液に塗れた体を横たえ
		;強絶頂
		ELSE
			PRINTFORMW 「ダメぇ、こんなのダメぇ……」
			PRINTFORMD %CALLNAME:TARGET%はうわ言のように呟きながら
		ENDIF
		PRINTDW 、絶頂の余韻に浸っている…
		RETURN 1
	ENDIF

	;条件なし	30%
	;==============
	IF 口上記入チェック(30)
		SIF !RAND:3
			{
			CALL HTMLPRINTW(@"「はぁ……はぁ……はぁ……\@ 
			RAND:2 
			? %HTML_HEART(, COLOR2_K39("PINK"))% 
			# \@」")
			}
		PRINTFORMDW %CALLNAME:TARGET%は絶頂の余韻に浸りながら、呼吸を整えている…
		RETURN 1
	ENDIF
ENDIF

;調教対象が絶頂した直後 30%で真
;==============================
;//性交・自慰中TEQUIPは既に解除されている
IF 口上記入チェック(30) && TRUECHECK(NOWEX:MASTER:Ｃ絶頂, NOWEX:MASTER:Ｖ絶頂, NOWEX:MASTER:Ａ絶頂, NOWEX:MASTER:Ｂ絶頂)
	;射精させた後
	;============
	IF NOWEX:MASTER:射精 && !TFLAG:抜かず
		IF GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))
			CALL HTMLPRINTW(@"「出したあとヒクヒクしてるのが可愛い%HTML_PS_K39()%」")
			PRINTFORMW 「ねぇ、口で綺麗にしてあげようか？」
		ELSE
			PRINTFORMW 「満足してもらえましたか？」
			CALL HTMLPRINTW(@"「でも、まだ終わりじゃないですよ%HTML_PS_K39()%」")
		ENDIF
		RETURN 1
	ENDIF

	;その他
	;======
	IF GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))
		PRINTFORMW 「イっちゃった%RAND_SPLIT("の？/みたいね/ようね")%」
		{
		CALL HTMLPRINTW(
		@"「でも、\@ 
		!TIME 
		? まだいけるでしょう？ 
		# 夜はこれからよ。まだ愉しませてね%LOCALS:0% \@」")
		}
	ELSE
		PRINTFORMW 「イってしまったんです%RAND_SPLIT("か？/ね♪")%」
	ENDIF
	RETURN 1
ENDIF

;調教方針変更 FIRSTEVENT(60)
;###########################
[SKIPSTART]
	調教方針が変わったときに表示する
	あくまでも方針なので行動を細かく決定するものではない

	#関連変数…
		TFLAG:  ターン方針      このターンの調教方針(1=休憩/2=ソフト/3=ノーマル/4=ハード/5=異常)
		TFLAG:前ターン方針      前ターンの調教方針(内容はターン方針と同じ)

	#関連関数…
		IS_POLICY               調教方針名称と合致すれば真を返す
[SKIPEND]

;25%で真
IF 口上記入チェック(25) && TFLAG:ターン方針 != TFLAG:前ターン方針
	;ターン方針派生
	;==============
	SELECTCASE POLICY("今")
		;ターン方針1:休憩（確実に体力回復行動になる）
		;===========================================
		CASE "休憩"
			{
			PRINTFORMW 「\@ 
			GET_KFLAG_K39("相思相愛", (FLAG:ご主人様)) 
			? ちょっと疲れたわね…ねぇ、%CNAME(MASTER, TARGET)%は大丈夫？ 
			# 疲れましたね…%CNAME(MASTER, TARGET)%は大丈夫ですか？ \@」
			}
		;ターン方針2:ソフト
		;==================
		CASE "ソフト"
			{
			CALL HTMLPRINTW(@"「\@ 
			GET_KFLAG_K39("相思相愛", (FLAG:ご主人様)) 
			? ねぇ、こっちに来て。可愛がってあげるから 
			# ちょっと甘えたいんじゃないですか？ さ、こちらへ \@%HTML_PS_K39()%」")
			}
			PRINTFORMDW %CALLNAME:TARGET%はあなたを優しく抱き寄せた…
		;ターン方針3:ノーマル
		;====================
		CASE "ノーマル"
			{
			PRINTFORMW 「\@ 
			GET_KFLAG_K39("相思相愛", (FLAG:ご主人様)) 
			? やっぱり普通が一番かな… 
			# やはり普通がいいでしょうか\@」
			}
		;ターン方針4:ハード
		;==================
		CASE "ハード"
			IF GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))
				PRINTFORML 「こういうのはあまり好きじゃないけど…」
				PRINTFORMW 「%CNAME(MASTER, TARGET)%は好きなんでしょ？　うふっ」
			ELSE
				PRINTFORMW 「少し激しくいきますよ……」
			ENDIF
		;ターン方針5:異常
		;================
		CASE "異常"
			PRINTW 「…………」
			PRINTFORMDW %CALLNAME:TARGET%の目つきが怪しい……
	ENDSELECT
	RETURN 1
ENDIF

;条件なし FIRSTEVENT(70)～
;#########################
[SKIPSTART]
	・ここまでの条件にヒットしなかった場合に表示される箇所
	
	#関連変数…
		BASE    (0, 体力/1, 気力/2, 射精/3, 母乳/4, 尿意/5, 理性/6, 興味/7, 苛立ち/8, 満足)

	#関連関数…
		TENSION 引数で指定したBASE値を表す(50%以上は高い、25%以上は一般、25%未満は低い)
[SKIPEND]

;1/4で真
IF 口上記入チェック(25)
	;満足80％以上
	;============
	IF BASERATIO(TARGET, 8, 80)
		IF GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))
			PRINTFORML 「%RAND_SPLIT("う/ふ")%ふ%RAND_SPLIT("っ/")%、%RAND_SPLIT("中々いい感じ/悪くないわ")%ね」
			CALL HTMLPRINTW(@"「さぁ、もっと愉しみましょ%HTML_PS_K39()%」")
		ELSE
			PRINTFORML 「%RAND_SPLIT("なかなかいい/悪くない")%ですね」
			CALL HTMLPRINTW(@"「さ、まだ続けますよ%HTML_PS_K39()%」")
		ENDIF
		RETURN 1
	ENDIF

	;調教者がイきそう
	;================
	IF TCVAR:TARGET:イきそう
		SELECTCASE CFLAG:TARGET:C絶回数
			CASE IS > 4
				PRINTFORMW （やだ私、また……こんなにイったらバカになりそう…）
			CASE IS > 1
				PRINTFORMW （やだっ、またイきそう……）
			CASEELSE
				PRINTFORMW （もうだめ…イっちゃいそう……）
		ENDSELECT
		RETURN 1
	ENDIF

	;苛立ち50％以上
	;==============
	IF TENSION(TARGET, 7) == "高い"
		{
		PRINTFORMW 「\@ 
		GET_KFLAG_K39("相思相愛", (FLAG:ご主人様)) 
		? あまり困らせないで 
		# 思うようにいきませんね\@…」
		}
		RETURN 1
	ENDIF

	;相思相愛（好意3000以上・好感度3000以上）
	;=======================================
	IF GET_KFLAG_K39("相思相愛", (FLAG:ご主人様))
		{
		CALL HTMLPRINTW(@"「\@ 
		!RAND:2 
		? ねぇねぇ、次は何する？ 
		# うふっ、楽しいわね%HTML_PS_K39()% \@」")
		}
		RETURN 1
	ENDIF

	;その他
	;======
	{
	CALL HTMLPRINTW(@"「\@ 
	!RAND:2 
	? 次は何をしましょうかね%HTML_PS_K39()% 
	# そうですね、では次は… \@」")
	}
	RETURN 1
ENDIF