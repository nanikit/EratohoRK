;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	雛口上 会話系反応、汎用リアクション口上ファイル
;
;Version		Reason
;1.11			COMファイルを系統別に分割
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;=====================================================================
;会話系反応 COM0番台
;=====================================================================
;・主にACT分類:会話系/休憩系の際に発生する
;・発生はトリガーイベント製。特定のACTがトリガーとなる
;・ACT分類:会話についてはREACTで完結させるのが望ましい
;-------------------------------------------------

;//今は時間的余裕がないので、COMをきっちり見直すのはまたの機会に(2014/12/30)

;────────────────────────────────────
;COM0, 沈黙する
;────────────────────────────────────
;・従ったことにはならない
;・ACT1,今の気分を聞く/自分への気持ちを聞くの場合はREACT派生1,"質問に答えない"となりお仕置きポイント加算
;・REACT印象:悪印象小
;・依存度変化(-1, -1)
;-------------------------------------------------
;・ACTが今の気分を聞く/自分への気持ちを聞くの場合は派生1,質問に答えない扱い
;・その他の場合は派生0,ただの沈黙
;-------------------------------------------------
;トリガーする行動や状況…
;ACT分類0:会話
;	ACT:今の気分を聞く/自分への気持ちを聞く/性についての話をする/優しく慰める
;		厳しく脅す/お仕置きと宣言する/怒鳴る/不気味に笑う
;ACT分類2:道具
;	ACT:
;ACT分類4:羞恥
;	ACT:羞恥プレイ/野外プレイ
;ACT分類6:加虐
;	ACT:縄/アイマスク/ボールギャグ/罵倒/三角木馬
;ACT分類7:異常
;	ACT:放置プレイ/ペニスバンド装着
;ACT分類9:休憩
;	ACT:休ませる/栄養剤を飲ませる/治療する
;--------------------------------------------------
;・TFLAG:REACT派生(0=ただの沈黙/1=質問に答えない)
;-------------------------------------------------
@KOJO_MALE_COM_K39_0
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;無理をした行動
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && GET_COM_RANK() == "無理をした行動"
	PRINTFORMW 「\@ GET_KFLAG_K39("相思相愛") ? そこまで無理して喋らない理由が何かあるの # 無理してまで黙る必要なんて無いと思いますけどね \@？」
	IF GET_KFLAG_K39("相思相愛")
		PRINTFORMW 「\@ FIRSTCOM(0) ? 良く判らないんだけど # %CNAME(MASTER, TARGET)%って、時々そういう事するわよね\@…」
	ELSE
		PRINTFORMW 「\@ FIRSTCOM(0) ? 何を考えているのでしょうか # %CNAME(MASTER, TARGET)%、時々そういう事しますよね\@…」
	ENDIF
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;REACT派生１：質問に答えない
;━━━━━━━━━━━━━━━━━━━━━━━━
;ACT, 今の気分を聞く/自分への気持ちを聞くの場合に派生する
;-------------------------------------------------
IF 口上記入チェック(100) && TFLAG:REACT派生 == 1
	PRINTFORMW 「\@ GET_KFLAG_K39("相思相愛") ? ねぇ…、黙っていれば誤魔化せるとか思ってる？ # そうやって誤魔化すつもりなんですね… \@」
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;REACT派生０：ただの沈黙
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && !TFLAG:REACT派生
	;罵倒時を想定
	;=================================================
	;ACT分類：加虐
	;=================================================
	IF MENUMATCH(TFLAG:ACT, "加虐")
		;相思相愛
		IF GET_KFLAG_K39("相思相愛")
			PRINTFORMW 「確かにちょっと言い過ぎたかもしれないけど…」
			PRINTFORMW 「ねぇ、何か言ってよ……」
		;それ以外
		ELSE
			SELECTCASE RANDCOM(3, 60)
				CASE 0
					PRINTFORMW 「あの、お願いですから何か言ってください…」
				CASE 1
					PRINTFORMW 「ちょっと言い過ぎましたか…」
				CASE 2
					PRINTFORMW 「本当は何か言いたいのではないですか？」
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類：異常
	;=================================================
	;IF MENUMATCH(TFLAG:ACT, "異常")
		;元々放置プレイ想定であったのでREACTに移設
	;ENDIF
	
	;=================================================
	;ACT分類：休憩
	;=================================================
	IF MENUMATCH(TFLAG:ACT, "休憩")
		;相思相愛
		IF GET_KFLAG_K39("相思相愛")
			PRINTFORMW 「黙っちゃってどうしたの？」
			PRINTFORMW 「それだけ疲れてるのかしら…」
		;それ以外
		ELSE
			PRINTFORMW 「喋る気力が無いほど疲れてしまったのでしょうか？」
		ENDIF
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他（汎用処理）
	;-------------------------
	IF GET_KFLAG_K39("相思相愛")
		SELECTCASE RANDCOM(2, 0)
			CASE 0
				PRINTFORMW 「黙ってたら何も分からないんだけど…？」
				PRINTFORMW 「ねぇ、何か言って…」
			CASE 1
				PRINTFORMW 「何か喋りたくない理由でもあるの？」
		ENDSELECT
	ELSE
		SELECTCASE RANDCOM(2, 1)
			CASE 0
				PRINTFORMW 「困りましたね…」
				PRINTFORMW 「何か言ってくれないと、私どうしたらいいか…」
			CASE 1
				PRINTFORMW 「何か喋りたくない理由でもあるのですか？」
		ENDSELECT
	ENDIF
	RETURN 1
ENDIF

;────────────────────────────────────
;COM1, 気弱く応答する
;────────────────────────────────────
;・REACT分類は派生によって変化
;・ACT("厳しく脅す/お仕置きと宣言する/怒鳴る/押し倒す")で一定条件を満たすとREACT派生1,哀願に派生
;・REACT印象:好印象小
;・依存度変化(0, +1)
;-------------------------------------------------
;トリガーする行動や状況…
;ACT分類:0:会話
;	ACT:今の気分を聞く/自分への気持ちを聞く/性についての話をする/優しく慰める
;		厳しく脅す/お仕置きと宣言する/怒鳴る/不気味に笑う/衣装の変更を要求する/合意を求める
;ACT分類:1:愛撫
;	ACT:押し倒す
;ACT分類:2:道具
;	ACT:
;ACT分類:3:性交
;	ACT
;ACT分類:4:羞恥
;	ACT:野外プレイ
;ACT分類:5:奉仕
;	ACT:
;ACT分類:6:加虐
;	ACT:罵倒
;ACT分類:7:異常
;	ACT:放置プレイ/ペニスバンド装着
;ACT分類:9:休憩
;	ACT:休ませる/栄養剤を飲ませる/治療する
;ACT分類:10:性交奉仕
;	ACT:
;TEQUIP:ボールギャグは弾かれる
;-------------------------------------------------
;・TFLAG:REACT派生(0=気弱く応答する/1=哀願する)
;-------------------------------------------------
@KOJO_MALE_COM_K39_1
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;REACT派生１：哀願する
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:REACT派生 == 1
	CALL FADEIN(10, @"「\@ GET_KFLAG_K39("相思相愛") ? ねぇ、悪いけど # あの、申し訳ありませんが \@…」")
	SELECTCASE GET_COM_RANK()
		CASE "自然な行動", "無理のない行動"
			PRINTFORMW 「こんなことやめて、って言われても\@ GET_KFLAG_K39("相思相愛") ? 困るんですけど？ # 困りますね… \@」
		CASE "無理をした行動"
			PRINTFORMW 「もうやめて、って言われても\@ GET_KFLAG_K39("相思相愛") ? 困るわ # 困ってしまいますね \@…」
	ENDSELECT
	;COM,許しを乞うにパス
	;CALL KOJO_MALE_COM_K39_7
	;RETURN RESULT
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;REACT派生０：気弱く応答
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && !TFLAG:REACT派生
	;=================================================
	;無理をした行動
	;=================================================
	IF GET_COM_RANK() == "無理をした行動"
		IF GET_KFLAG_K39("相思相愛")
			PRINTFORMW 「%RAND_SPLIT("わざわざ/そうやって/ねぇ…、")%元気のない振りをする意味が何かあるの？」
			PRINTFORM 「単に、かまって欲しいだけかも知れないけど
			CALL PRINT_SYMBOL_K39
		ELSE
			PRINTFORMW 「\@ TENSION(MASTER, 0) == "高い" ? それ程疲れてるようにも見えませんが # 確かに、ちょっとお疲れのようですが \@…」
			PRINTFORMW 「その反応…、ちょっと無理があるのではないですか？ ふふっ」
		ENDIF
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	IF GET_KFLAG_K39("相思相愛")
		SELECTCASE RANDCOM(2, 0)
			CASE 0
				PRINTFORMW 「ねぇ、どうしちゃったの…？」
			CASE 1
				PRINTFORMW 「体調でも悪いの？　もう終わりにする？」
		ENDSELECT
	ELSE
		PRINTFORMW 「どうしたのでしょう…？」
		PRINTFORMW 「元気がありませんね…」
	ENDIF
	RETURN 1
ENDIF
;COM1, 気弱く応答する

;────────────────────────────────────
;COM2, 無愛想に応答する
;────────────────────────────────────
;・REACT分類:なし
;・従ったことにはならない
;・お仕置きポイントが加算される
;・REACT印象:悪印象小
;・依存度変化(-2, -2)
;-------------------------------------------------
;トリガーする行動や状況…
;ACT分類:0:会話
;	ACT:今の気分を聞く/自分への気持ちを聞く/性についての話をする/優しく慰める
;		厳しく脅す/お仕置きと宣言する/怒鳴る/不気味に笑う/衣装の変更を要求する/合意を求める
;ACT分類:1:愛撫
;	ACT:押し倒す
;ACT分類:2:道具
;	ACT:
;ACT分類:3:性交
;	ACT
;ACT分類:4:羞恥
;	ACT:野外プレイ
;ACT分類:5:奉仕
;	ACT:
;ACT分類:6:加虐
;	ACT:罵倒
;ACT分類:7:異常
;	ACT:放置プレイ/ペニスバンド装着
;ACT分類:9:休憩
;	ACT:休ませる/栄養剤を飲ませる/治療する
;ACT分類:10:性交奉仕
;	ACT:
;TEQUIP:ボールギャグは弾かれる
;--------------------------------------------------
;・REACT派生なし
;-------------------------------------------------
@KOJO_MALE_COM_K39_2
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;COM2, 無愛想に応答する
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100)
	;=================================================
	;無理をした行動
	;=================================================
	IF 口上記入チェック(100) && GET_COM_RANK() == "無理をした行動"
		IF GET_KFLAG_K39("相思相愛")
			CALL HTMLPRINTL(@"「普通なら%HTMLFONT(@"<b>%RAND_SPLIT("可愛くない/何それ？")%</b>", ,)%ってとこなんだけど…」")
			PRINTFORMW 「何か無理してるみたいで、ちょっと面白いわね？」
			PRINTFORML 「わざとそんな態度して、私の気を引こうとしてるって考えると…」
			PRINTFORM 「むしろちょっと可愛いかも…、ってね
			CALL PRINT_SYMBOL_K39
		ELSE
			PRINTFORMW 「そういう態度、ちょっと%RANDTEXT_K39("どうかと思いますけどね", "どうなんでしょう…")%」
			PRINTFORMW 「無理してまでする必要があるんですかね？ %RANDTEXT_K39("ふふ", "くすっ")%…」
		ENDIF
		RETURN 1
	ENDIF

	;-------------------------
	;その他
	;-------------------------
	IF GET_KFLAG_K39("相思相愛")
		SELECTCASE RANDCOM(3, 0)
			CASE 0
				PRINTFORMW 「やめてよ、そういうの…」
			CASE 1
				PRINTFORMW 「それ、反抗期か何かなの？」
			CASE 2
				PRINTFORMW 「本当は何か言いたい事でもあるんじゃないの…？」
		ENDSELECT
	ELSEIF AFFECTION("嫌悪")
		PRINTFORMW 「駄目ですよ？　そういうの…」
		PRINTFORMW 「私、愛想の悪い子は嫌いです」
	ELSE
		SELECTCASE RANDCOM(3, 1)
			CASE 0
				PRINTFORMW 「何か怒らせるような事でも、言ってしまったでしょうか…？」
			CASE 1
				PRINTFORMW 「機嫌悪そうですね…」
			CASE 2
				PRINTFORMW 「あら、ご機嫌斜めですね…」
		ENDSELECT
	ENDIF
	RETURN 1
ENDIF
;COM2, 無愛想に応答する

;────────────────────────────────────
;COM3, 愛想よく応答する
;────────────────────────────────────
;・REACT分類:積極的従う
;・REACT印象:好印象大
;・依存度変化(+3, +1)
;-------------------------------------------------
;トリガーする行動や状況…
;ACT分類:0:会話
;	ACT:今の気分を聞く/自分への気持ちを聞く/性についての話をする/優しく慰める
;		厳しく脅す/お仕置きと宣言する/怒鳴る/衣装の変更を要求する/合意を求める
;ACT分類:1:愛撫
;	ACT:押し倒す
;ACT分類:2:道具
;	ACT:
;ACT分類:3:性交
;	ACT
;ACT分類:4:羞恥
;	ACT:野外プレイ
;ACT分類:5:奉仕
;	ACT:
;ACT分類:6:加虐
;	ACT:罵倒
;ACT分類:7:異常
;	ACT:放置プレイ/ペニスバンド装着
;ACT分類:9:休憩
;	ACT:休ませる/栄養剤を飲ませる/治療する
;ACT分類:10:性交奉仕
;	ACT:
;TEQUIP:ボールギャグは弾かれる
;--------------------------------------------------
;・REACT派生なし
;-------------------------------------------------
@KOJO_MALE_COM_K39_3
;-------------------------------------------------

;━━━━━━━━━━━━━━━━━━━━━━━━
;COM3, 愛想よく応答する
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100)
	;=================================================
	;無理をした行動
	;=================================================
	IF GET_COM_RANK() == "無理をした行動"
		;この段階でTFLAG:REACT負荷 == 2になる可能性はないんじゃないか、という気がするけど放置する事も出来ず
		IF GET_KFLAG_K39("相思相愛")
			SELECTCASE RANDCOM(3, 0)
				CASE 0
					PRINTFORMW 「…凄く嘘っぽい。それ、本心からじゃない癖に……」
				CASE 1
					PRINTFORMW 「無理して愛想いい振りされたところで、私…、全然嬉しくないんだけど？」
				CASE 2
					PRINTFORMW 「それで機嫌をとってるつもり？」
					PRINTFORMW 「%CNAME(MASTER, TARGET)%が無理してるのがミエミエで、むしろ傷付くんだけど…」
			ENDSELECT
		;事実上、こちらがメインとなるだろうな的な
		ELSE
			PRINTFORMW 「そうですね…、そういう態度%RANDTEXT_K39("が非常に好ましい", "を嬉しく思う")%のは確かなんですが」
			SELECTCASE RANDCOM(4, 1)
				CASE 0
					PRINTFORMW 「%CNAME(MASTER, TARGET)%、ちょっと無理してませんか？」
				CASE 1
					PRINTFORMW 「ご機嫌取りなのがみえみえで、ちょっと…」
				CASE 2
					PRINTFORMW 「ちょっとわざとらしいですね…」
				CASE 3
					PRINTFORMW 「%CNAME(MASTER, TARGET)%の本心からではないのは、判っているつもりですよ…」
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類：加虐
	;=================================================
	IF MENUMATCH(TFLAG:ACT, "加虐")
		;相思相愛
		IF GET_KFLAG_K39("相思相愛")
			SELECTCASE RANDCOM(2, 0)
				CASE 0
					PRINTFORMW 「こんな事酷い事されてるのに嬉しそうね？」
					PRINTFORM 「ま、悦んで貰えてるみたいで何よりだけど
					CALL PRINT_SYMBOL_K39
				CASEELSE
					PRINTFORMW 「痛い事されてるのに楽しそうね？」
					PRINTFORM 「ほんと、どうしようもない人
					CALL PRINT_SYMBOL_K39
			ENDSELECT
		;それ以外
		ELSE
			SELECTCASE RANDCOM(2, 1)
				CASE 0
					PRINTFORMW 「酷い事されてるのに嬉しそうですね」
					PRINTFORM 「本当に困った人です
					CALL PRINT_SYMBOL_K39
				CASE 1
					PRINTFORMW 「あの、それってやせ我慢じゃないんですか？」
					PRINTFORMW 「私が言うのも何ですが、ちょっと心配になってしまいます…」
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類：休憩
	;=================================================
	IF MENUMATCH(TFLAG:ACT, "休憩")
		;相思相愛
		IF GET_KFLAG_K39("相思相愛")
			PRINTFORM 「すっかり元気になったみたいね
			CALL PRINT_SYMBOL_K39
		;それ以外
		ELSE
			PRINTFORM 「元気一杯ってところでしょうか
			CALL PRINT_SYMBOL_K39
		ENDIF
		RETURN 1
	ENDIF
	
	;会話系分岐を廃棄して汎用処理に充てる
	;-------------------------
	;その他
	;-------------------------
	IF GET_KFLAG_K39("相思相愛")
		PRINTFORM 「うふふっ、可愛い…
		CALL PRINT_SYMBOL_K39
	ELSE
		PRINTFORM 「素直な子は大好きですよ
		CALL PRINT_SYMBOL_K39
	ENDIF
	RETURN 1
ENDIF
;COM3, 愛想よく応答する

;────────────────────────────────────
;COM4, 強気に応答する
;────────────────────────────────────
;・REACT分類:なし
;・従ったことにはならない
;・ACT,押し倒すの場合はREACT派生1,"押し倒しに屈しない"に派生
;・REACT印象:悪印象小
;・依存度変化(-1, -3)
;-------------------------------------------------
;トリガーする行動や状況…
;ACT分類:0:会話
;	ACT:今の気分を聞く/自分への気持ちを聞く/性についての話をする/優しく慰める
;      :厳しく脅す/お仕置きと宣言する/怒鳴る/不気味に笑う/衣装の変更を要求する/合意を求める
;ACT分類:1:愛撫
;	ACT:押し倒す
;ACT分類:2:道具
;	ACT:
;ACT分類:3:性交
;	ACT
;ACT分類:4:羞恥
;	ACT:
;ACT分類:5:奉仕
;	ACT:
;ACT分類:6:加虐
;	ACT:罵倒
;ACT分類:7:異常
;	ACT:放置プレイ/ペニスバンド装着
;ACT分類:9:休憩
;	ACT:休ませる/栄養剤を飲ませる/治療する
;ACT分類:10:性交奉仕
;	ACT:
;TEQUIP:ボールギャグは弾かれる
;--------------------------------------------------
;・TFLAG:REACT派生(0=強気に応答/1=押し倒しに屈しない)
;-------------------------------------------------
@KOJO_MALE_COM_K39_4
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;REACT派生１：押し倒しに屈しない
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:REACT派生 == 1
	;相思相愛
	IF GET_KFLAG_K39("相思相愛")
		PRINTFORM 「随分強気ね？ でも駄目よ…、%RAND_SPLIT("ここで/今更")%やめる訳にもいかないから
	;それ以外
	ELSE
		PRINTFORMW 「あら、怖いですね」
		PRINTFORM 「でも駄目ですよ。やめてあげません
	ENDIF
	CALL PRINT_SYMBOL_K39
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;REACT派生０：強気に応答
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && !TFLAG:REACT派生
	;=================================================
	;無理をした行動
	;=================================================
	IF GET_COM_RANK() == "無理をした行動"
		IF GET_KFLAG_K39("相思相愛")
			PRINTFORMW 「%RAND_SPLIT("無駄に元気があり余ってるのも/そういうのって、普通ならちょっと")%考え物なんだけど…」
			PRINTFORMW 「でも、そんな態度の割りにはちょっと余裕無さ過ぎじゃない？」
		ELSE
			PRINTFORMW 「そうですね…、普段ならあまりいい気はしませんけど」
			PRINTFORM 「%CNAME(MASTER, TARGET)%が無理してるのが伝わって、ちょっと微笑ましいですね
			CALL PRINT_SYMBOL_K39
		ENDIF
		;フェードアウト
		CALL FADE(20, COLOR2_K39("HINA"), COLOR2_K39("black"), @"「\@ GET_KFLAG_K39("相思相愛") ? くすくす # ふふっ \@…」")
		CALL FADE_CLINE(1)
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類：加虐
	;=================================================
	IF MENUMATCH(TFLAG:ACT, "加虐")
		;相思相愛
		IF GET_KFLAG_K39("相思相愛")
			PRINTFORMW 「私を怒らせて、もっと虐めて欲しいとかって考えてる？」
		;それ以外
		ELSE
			SELECTCASE RANDCOM(2, 60)
				CASE 0
					PRINTFORMW 「まだ自分の状況が良く分かってないみたいですね」
				CASE 1
					PRINTFORMW 「そんな事言っても止めてあげませんよ？」
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類：休憩
	;=================================================
	IF 口上記入チェック(100) && MENUMATCH(TFLAG:ACT, "休憩")
		;相思相愛
		IF GET_KFLAG_K39("相思相愛")
			SELECTCASE RANDCOM(2, 90)
				CASE 0
					PRINTFORMW 「何か面白くない事でもあるの？」
				CASE 1
					PRINTFORMW 「休もうって言ってるのに、そんな言い方は無いと思う…」
			ENDSELECT
		;それ以外
		ELSE
			SELECTCASE RANDCOM(3, 91)
				CASE 0
					PRINTFORMW 「今そんな態度をしても、あまり意味が無いと思うのですが…」
				CASE 1
					PRINTFORMW 「素直に休んでもいいと思うのですけど…」
				CASE 2
					PRINTFORMW 「そんな言い方しなくても…」
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	IF GET_KFLAG_K39("相思相愛")
		PRINTFORMW 「そういう言い方は無いと思うんだけど…」
	ELSE
		SELECTCASE RANDCOM(2, 0)
			CASE 0
				PRINTFORMW 「随分元気なんですね？　まだまだ余裕があるという事でしょうか」
			CASE 1
				PRINTFORMW 「こう見えても私は神なんですよ？」
				PRINTFORMW 「あまりそういう事は言わない方が…」
		ENDSELECT
	ENDIF
	RETURN 1
ENDIF
;COM4, 強気に応答する

;────────────────────────────────────
;COM5, 皮肉を言う
;────────────────────────────────────
;・ACT,今の気分を聞く/自分への気持ちを聞くの場合はREACT分類:拒否
;・それ以外の場合はREACT分類:平然と従うになる
;・お仕置きポイント加算
;・REACT印象:悪印象小
;・依存度変化(-3, -1)
;-------------------------------------------------
;トリガーする行動や状況…
;ACT分類:0:会話
;	ACT:今の気分を聞く/自分への気持ちを聞く/優しく慰める
;		厳しく脅す/お仕置きと宣言する/合意を求める
;ACT分類:1:愛撫
;	ACT:
;ACT分類:2:道具
;	ACT:
;ACT分類:3:性交
;	ACT
;ACT分類:4:羞恥
;	ACT:羞恥プレイ/野外プレイ
;ACT分類:5:奉仕
;	ACT:
;ACT分類:6:加虐
;	ACT:縄/アイマスク/ボールギャグ/罵倒/三角木馬
;ACT分類:7:異常
;	ACT:放置プレイ/ペニスバンド装着
;ACT分類:9:休憩
;	ACT:休ませる/栄養剤を飲ませる/治療する
;ACT分類:10:性交奉仕
;	ACT:
;TEQUIP:ボールギャグは弾かれる
;--------------------------------------------------
;・REACT派生なし
;-------------------------------------------------
@KOJO_MALE_COM_K39_5
;-------------------------------------------------

;━━━━━━━━━━━━━━━━━━━━━━━━
;COM5, 皮肉を言う
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100)
	;=================================================
	;無理をした行動
	;=================================================
	IF GET_COM_RANK() == "無理をした行動"
		IF GET_KFLAG_K39("相思相愛")
			SELECTCASE RANDCOM(2, 930)
				CASE 1
					PRINTFORMW 「ふーん。皮肉、ねぇ？」
					PRINTFORMW 「どうせなら、もっと余裕のある時に言った方がいいんじゃない？」
					PRINTFORMW 「尤も…、無理してまで今言う%RANDTEXT_K39("事な", "必要がある")%のか、ちょっと疑問だけどね\@ T_COND("怒り") ? ！ # \@」
				CASE 1
					PRINTFORMW 「こう見えても私…、一応神様なんですけどね\@ T_COND("怒り") ? ！ # ？ \@」
					PRINTFORMW 「%CNAME(MASTER, TARGET)%、もしかして…その事忘れてない？ ただ…」
					PRINTFORMW 「その余裕の無い姿を見てると、聞かなかった事にしてあげてもいいって思うわ」
					CALL FADEIN(13, "「ちょっとだけ、ね」")
					TWAIT 500, 0
					CALL FADE(20, COLOR2_K39("HINA"), COLOR2_K39("black"), "「くすくす…」")
					CALL FADE_CLINE(1)
			ENDSELECT
		ELSE
			SELECTCASE RANDCOM(2, 931)
				CASE 0
					PRINTFORMW 「皮肉、ですか…。でもそうですね？ 正直に言わせてもらうと…」
					PRINTFORM 「無理してまでそんな事言わなくてもいいのに。…って、私は思いますけどね
				CASE 1
					PRINTFORMW 「ふふ、ちょっと面白いですね。え？ どうしてかって…」
					PRINTFORM 「%CNAME(MASTER, TARGET)%が相当無理してるの、判ってしまうんですよね
			ENDSELECT
			CALL PRINT_SYMBOL_K39
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類：加虐
	;=================================================
	IF MENUMATCH(TFLAG:ACT, "加虐")
		;相思相愛
		IF GET_KFLAG_K39("相思相愛")
			PRINTFORMW 「今の%CNAME(MASTER, TARGET)%の状況がまさに皮肉って感じがするけど？」
			PRINTFORMW 「でも、ちょっと面白かったわよ。クスクス」
		;その他
		ELSE
			PRINTFORMW 「こんな状態で皮肉を言っても、意味が無いとは思いませんか？」
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類：休憩
	;=================================================
	IF MENUMATCH(TFLAG:ACT, "休憩")
		PRINTFORMW 「\@ GET_KFLAG_K39("相思相愛") ? 今そんな事言うなんて、ちょっとどうかと思う # 随分捻くれているんですね \@…」
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	IF GET_KFLAG_K39("相思相愛")
		SELECTCASE RANDCOM(2, 0)
			CASE 0
				PRINTFORMW 「あんまり面白くないけど？」
			CASE 1
				PRINTFORMW 「ねぇ、そういうのやめて…」
		ENDSELECT
	ELSE
		SELECTCASE RANDCOM(2, 1)
			CASE 0
				PRINTFORMW 「私、皮肉は嫌いです…」
			CASE 1
				PRINTFORMW 「それ、面白いと思っているのですか？」
		ENDSELECT
	ENDIF
	RETURN 1
ENDIF
;COM5, 皮肉を言う

;────────────────────────────────────
;COM6, もっとハードにして
;────────────────────────────────────
;・REACT分類:受け入れる
;・調教者の素質、BASE:苛立ち等によってREACT派生1,「この恥知らずのメス豚め！」になる
;・REACT派生1の場合はお仕置きポイント加算
;・REACT印象:0から遠ざかる
;・依存度変化(0, +3)
;-------------------------------------------------
;トリガーする行動や状況…
;ACT分類:0:会話
;	ACT:今の気分を聞く/自分への気持ちを聞く/優しく慰める
;		お仕置きと宣言する
;ACT分類:1:愛撫
;	ACT:手で愛撫/胸愛撫/口で愛撫/キス/性器を擦り合う/パイズリする
;ACT分類:2:道具
;	ACT:Ｃ愛撫道具/ニプルキャップ
;ACT分類:3:性交
;	ACT:正常位
;ACT分類:4:羞恥
;	ACT:羞恥プレイ/野外プレイ
;ACT分類:5:奉仕
;	ACT:
;ACT分類:6:加虐
;	ACT:スパンキング/縄/アイマスク/ボールギャグ/三角木馬
;ACT分類:7:異常
;	ACT:放置プレイ/ペニスバンド装着
;ACT分類:9:休憩
;	ACT:休ませる/栄養剤を飲ませる/治療する
;ACT分類:10:性交奉仕
;	ACT:
;TEQUIP:ボールギャグは弾かれる
;--------------------------------------------------
;・TFLAG:REACT派生(0=ふーん/1=この恥知らずのメス豚め！)
;-------------------------------------------------
@KOJO_MALE_COM_K39_6
;-------------------------------------------------
VARSET LOCALS
LOCALS '= RAND_SPLIT("いやら/イヤラ/浅ま")

;━━━━━━━━━━━━━━━━━━━━━━━━
;REACT派生１：この恥知らず！（お仕置きポイント加算）
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:REACT派生 == 1
	IF GET_KFLAG_K39("相思相愛")
		PRINTFORMW 「%LOCALS%しい%RAND_SPLIT("わ/わね/")%…」
		PRINTFORMW 「見境無くそんな事言ってるとお仕置きなんだけど…」
		PRINTFORMW 「%CNAME(MASTER, TARGET)%はそれでもいいの？」
	ELSE
		PRINTFORMW 「%LOCALS%しいですね…」
		PRINTFORMW 「少し%CNAME(MASTER, TARGET)%を%RAND_SPLIT("矯正する/躾けなおす")%必要がありそうですね」
	ENDIF
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;REACT派生０：もっとハードにして
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && !TFLAG:REACT派生
	;=================================================
	;無理をした行動
	;=================================================
	IF 口上記入チェック(100) && GET_COM_RANK() == "無理をした行動"
		IF GET_KFLAG_K39("相思相愛")
			PRINTFORMW 「そういうのって、無理してまで言う事じゃないと思うけどね？」
			PRINTFORMW 「後で後悔しても知らないから…」
		ELSE
			PRINTFORMW 「そういう事、あまり軽々しく言わない方が…」
			PRINTFORM 「それが%CNAME(MASTER, TARGET)%の本心ではないのが判るだけに、余計にそう思いますね
			CALL PRINT_SYMBOL_K39
		ENDIF
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他(汎用処理)
	;-------------------------
	IF RAND:3
		CALL hmm_K39
	ELSE
		;相思相愛
		IF GET_KFLAG_K39("相思相愛")
			SELECTCASE RANDCOM(2, 0)
				CASE 0
					PRINTFORMW 「本当にいいの？」
					PRINTFORM 「うふふっ、興奮しちゃう
					CALL PRINT_SYMBOL_K39
				CASEELSE
					PRINTFORM 「仕様のない人ね
					CALL PRINT_SYMBOL_K39
					PRINTFORMW 「じゃ、もっと凄い事しましょうか」
			ENDSELECT
		;それ以外
		ELSE
			SELECTCASE RANDCOM(2, 1)
				CASE 0
					PRINTFORMW 「いいですよ」
					PRINTFORM 「%CNAME(MASTER, TARGET)%の厄、たっぷり抜いてあげます
					CALL PRINT_SYMBOL_K39
				CASEELSE
					PRINTFORM 「普通ではもう満足出来ないみたいですね
					CALL PRINT_SYMBOL_K39
			ENDSELECT
		ENDIF
	ENDIF
	RETURN 1
ENDIF
;COM6, もっとハードにして

;────────────────────────────────────
;COM7, 許しを乞う
;────────────────────────────────────
;・REACT分類:許しを乞う。派生した場合はREACT分類:なし
;・調教者の素質、BASE:理性、アライメントによってREACT派生1,「意見を出すなんていい度胸ね！」になる
;・ターン方針が異常の場合は派生1になりやすい
;・派生1の場合はお仕置きポイント加算
;・REACT印象:0に近づく
;・依存度変化(0, +2)
;-------------------------------------------------
;トリガーする行動や状況…
;ACT分類:0:会話
;	ACT:今の気分を聞く/自分への気持ちを聞く/性についての話をする/優しく慰める
;		厳しく脅す/お仕置きと宣言する/怒鳴る/不気味に笑う
;ACT分類:1:愛撫
;	ACT:アナル愛撫/アナル舐め/押し倒す
;ACT分類:2:道具
;	ACT:アナルバイブ/アナルビーズ/ペニスバンド
;ACT分類:3:性交
;	ACT:後背位/背面座位/アナルセックス
;ACT分類:4:羞恥
;	ACT:自慰/秘貝開帳/羞恥プレイ/野外プレイ/放尿
;ACT分類:5:奉仕
;	ACT:足舐め強制
;ACT分類:6:加虐
;	ACT:スパンキング/鞭/針/三角木馬/浣腸セット
;ACT分類:7:異常
;	ACT:放置プレイ/ペニスバンド装着/フィストファック/アナルフィスト/両穴フィスト/公衆肉便器プレイ
;ACT分類:9:休憩
;	ACT:休ませる/栄養剤を飲ませる/治療する
;ACT分類:10:性交奉仕
;	ACT:逆レイプ
;
;TEQUIP:ボールギャグは弾かれる
;--------------------------------------------------
;・TFLAG:REACT派生(0=ふーん/1=意見を出すなんていい度胸ね！)
;-------------------------------------------------
@KOJO_MALE_COM_K39_7
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;REACT派生１：意見を出すなんていい度胸ね！（お仕置きポイント加算）
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:REACT派生 == 1
	;相思相愛
	IF GET_KFLAG_K39("相思相愛")
		PRINTFORMW 「そんな事言われると…」
		PRINTFORMW 「もっと意地悪したくなっちゃうんだけど…」
		CALL PRINT_SYMBOL_K39
	;それ以外
	ELSE
		PRINTFORMW 「今頃そんな事言われても困りますね…」
	ENDIF
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;REACT派生０：許しを乞う
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && !TFLAG:REACT派生
	;=================================================
	;無理をした行動
	;=================================================
	IF GET_COM_RANK() == "無理をした行動"
		IF GET_KFLAG_K39("相思相愛")
			PRINTFORMW 「わざわざ無理してまで、そんな媚びた態度する事ないのに」
			PRINTFORMW 「大体何よ、許してって…」
		ELSE
			PRINTFORMW 「その見え透いた媚びた態度、あまり感心しませんね」
			PRINTFORMW 「どうせ本心からではないのでしょう？」
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;「ふーん」(5%)
	;=================================================
	IF 口上記入チェック(5)
		CALL hmm_K39
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類：会話
	;=================================================
	IF MENUMATCH(TFLAG:ACT, "会話")
		;相思相愛
		IF GET_KFLAG_K39("相思相愛")
			PRINTFORMW 「許してって…」
			PRINTFORMW 「人が話してるのに、それはないんじゃない？」
		;それ以外
		ELSE
			PRINTFORMW 「私と話すのも嫌、という事なんですか？」
			PRINTFORMW 「それはちょっと悲しいですね…」
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類：愛撫
	;=================================================
	IF MENUMATCH(TFLAG:ACT, "愛撫")
		;相思相愛
		IF GET_KFLAG_K39("相思相愛")
			PRINTFORMW 「どうして…」
			PRINTFORMW 「私に触られるのがそんなに嫌？」
		;それ以外
		ELSE
			PRINTFORMW 「私に触られるのが嫌なんですか？」
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類：道具
	;=================================================
	IF MENUMATCH(TFLAG:ACT, "道具")
		PRINTFORMW 「\@ GET_KFLAG_K39("相思相愛") ? これがそんなに嫌だったの # 道具を使われるのが、そんなに嫌だったのですか \@？」
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類：性交
	;=================================================
	IF MENUMATCH(TFLAG:ACT, "性交")
		;相思相愛
		IF GET_KFLAG_K39("相思相愛")
			SELECTCASE RANDCOM(2, 300)
				CASE 0
					PRINTFORMW 「私にシて欲しいんじゃなかったの？」
				CASE 1
					PRINTFORMW 「ねぇ、今更やめられると思う？　クスクス」
			ENDSELECT
		;それ以外
		ELSE
			SELECTCASE RANDCOM(2, 301)
				CASE 0
					PRINTFORMW 「私に突かれるのが、そんなに嫌ですか？」
				CASE 1
					PRINTFORMW 「ご免なさい。私、やめられそうにありません♪」
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類：羞恥
	;=================================================
	IF MENUMATCH(TFLAG:ACT, "羞恥")
		;相思相愛
		IF GET_KFLAG_K39("相思相愛")
			SELECTCASE RANDCOM(2, 400)
				CASE 0
					PRINTFORMW 「そんなに恥ずかしいのが嫌なの？」
				CASE 1
					PRINTFORMW 「もっと恥ずかしい事したらどうなっちゃうのか…」
					PRINTFORMW 「ちょっと気になるわね♪」
			ENDSELECT
		;それ以外
		ELSE
			SELECTCASE RANDCOM(2, 401)
				CASE 0
					PRINTFORMW 「恥ずかしいのは嫌いなんでしょうか」
				CASE 1
					PRINTFORMW 「そんな事言われると、もっと恥ずかしい事してあげたくなりますね♪」
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類：奉仕
	;=================================================
	IF MENUMATCH(TFLAG:ACT, "奉仕")
		;相思相愛
		IF GET_KFLAG_K39("相思相愛")
			PRINTFORMW 「ねぇ、今更それはないと思うけど？」
		;それ以外
		ELSE
			PRINTFORMW 「私に触るのも嫌、という事でしょうか…」
			PRINTFORMW 「流石にこれは傷つきますね…」
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類：加虐
	;=================================================
	IF MENUMATCH(TFLAG:ACT, "加虐")
		;相思相愛
		IF GET_KFLAG_K39("相思相愛")
			SELECTCASE RANDCOM(2, 600)
				CASE 0
					PRINTFORMW 「こういうの好きだったんじゃないの？」
				CASE 1
					PRINTFORMW 「許してほしかったら、どうすればいいか良く考えて…」
			ENDSELECT
		;それ以外
		ELSE
			SELECTCASE RANDCOM(2, 601)
				CASE 0
					PRINTFORMW 「痛いのは嫌ですか？」
				CASE 1
					PRINTFORMW 「困りますね、今更許してなんて…」
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類：異常
	;=================================================
	IF MENUMATCH(TFLAG:ACT, "異常")
		;相思相愛
		IF GET_KFLAG_K39("相思相愛")
			SELECTCASE RANDCOM(2, 700)
				CASE 0
					PRINTFORMW 「確かに気持ちは分からないでもないけど…」
				CASE 1
					PRINTFORMW 「今頃言われても困るんだけど？　うふふ」
			ENDSELECT
		;それ以外
		ELSE
			SELECTCASE RANDCOM(2, 701)
				CASE 0
					PRINTFORMW 「流石にこれは嫌だったんでしょうか」
				CASE 1
					PRINTFORMW 「困りますね、今更許してなんて…」
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類：休憩
	;=================================================
	IF MENUMATCH(TFLAG:ACT, "休憩")
		;相思相愛
		IF GET_KFLAG_K39("相思相愛")
			SELECTCASE RANDCOM(2, 900)
				CASE 0
					PRINTFORMW 「休憩したくないの？」
				CASE 1
					PRINTFORMW 「許してって…」
					PRINTFORMW 「ちょっと意味が分からないんだけど？」
			ENDSELECT
		;それ以外
		ELSE
			SELECTCASE RANDCOM(2, 901)
				CASE 0
					PRINTFORMW 「許してって今言う事ですか？」
				CASE 1
					PRINTFORMW 「休憩するのも嫌なんでしょうか…」
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類：性奉
	;=================================================
	IF MENUMATCH(TFLAG:ACT, "性奉")
		;相思相愛
		IF GET_KFLAG_K39("相思相愛")
			SELECTCASE RANDCOM(2, 950)
				CASE 0
					PRINTFORMW 「それ、ちょっと酷くない？」
				CASE 1
					PRINTFORMW 「何それ？　シたくないって事？」
			ENDSELECT
		;それ以外
		ELSE
			SELECTCASE RANDCOM(2, 951)
				CASE 0
					PRINTFORMW 「私と肌を合わせるのがそんなに嫌ですか？」
				CASE 1
					PRINTFORMW 「今そんな事言われるのは、流石に傷つきますね…」
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
ENDIF
;COM7, 許しを乞う

;────────────────────────────────────
;COM8, 気持ち良くして
;────────────────────────────────────
;・REACT分類:受け入れる
;・調教者の素質、BASE:理性、アライメントによってREACT派生1,「この恥知らずのメス豚め！」になる
;・派生1の場合はお仕置きポイント加算
;・REACT印象:0から遠ざかる
;・依存度変化(0, +2)
;-------------------------------------------------
;トリガーする行動や状況…
;ACT分類:0:会話
;	ACT:今の気分を聞く/自分への気持ちを聞く/性についての話をする/優しく慰める/合意を求める
;ACT分類:1:愛撫
;	ACT:アナル愛撫/アナル舐め/押し倒す
;ACT分類:2:道具
;	ACT:ペニスバンド
;ACT分類:3:性交
;	ACT:
;ACT分類:4:羞恥
;	ACT:自慰みせつけ/羞恥プレイ/野外プレイ/放尿
;ACT分類:5:奉仕
;	ACT:
;ACT分類:6:加虐
;	ACT:罵倒
;ACT分類:7:異常
;	ACT:放置プレイ/ペニスバンド装着
;ACT分類:9:休憩
;	ACT:休ませる/栄養剤を飲ませる/治療する
;ACT分類:10:性交奉仕
;	ACT:
;
;TEQUIP:ボールギャグは弾かれる
;-------------------------------------------------
;・TFLAG:REACT派生(0=ふーん/1=この恥知らずのメス豚め！)
;-------------------------------------------------
@KOJO_MALE_COM_K39_8
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;REACT派生１：この恥知らず！（お仕置きポイント加算）
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:REACT派生 == 1
	IF GET_KFLAG_K39("相思相愛")
		PRINTFORMW 「いやらしい…」
		PRINTFORMW 「見境無くそんな事言ってるとお仕置きなんだけど…」
		PRINTFORMW 「%CNAME(MASTER, TARGET)%はそれでもいいの？」
	ELSE
		PRINTFORMW 「浅ましいですね…」
		PRINTFORMW 「少し%CNAME(MASTER, TARGET)%を矯正する必要がありそうですね」
	ENDIF
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;REACT派生０：気持ち良くして
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && !TFLAG:REACT派生
	;=================================================
	;無理をした行動
	;=================================================
	IF GET_COM_RANK() == "無理をした行動"
		IF GET_KFLAG_K39("相思相愛")
			PRINTFORMW 「ねぇ、本当はそんなこと言う気分じゃないんでしょう？」
			PRINTFORMW 「嘘ついたって駄目。ちゃんと判%RANDTEXT_K39("って", "＿")%るんだから…」
		ELSE
			PRINTFORMW 「それが%CNAME(MASTER, TARGET)%の本心からの言葉なら嬉し%RANDTEXT_K39("いの", "く思うところ")%ですが…」
			PRINTFORMW 「どうやらちょっと違うみたいですね？」
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類：会話
	;=================================================
	IF MENUMATCH(TFLAG:ACT, "会話")
		;相思相愛
		IF GET_KFLAG_K39("相思相愛")
			SELECTCASE RANDCOM(2, 0)
				CASE 0
					PRINTFORMW 「うふ、興奮しちゃったの？」
					PRINTFORM 「じゃ、どんな事して欲しいか言ってみて
				CASE 1
					PRINTFORMW 「いいわよ？　クスクス…」
					PRINTFORM 「もっと気持ち良い事しましょうね
			ENDSELECT
			CALL PRINT_SYMBOL_K39
		;それ以外
		ELSE
			SELECTCASE RANDCOM(2, 1)
				CASE 0
					PRINTFORMW 「話していて興奮してしまったのでしょうか」
					PRINTFORM 「可愛いですね
					CALL PRINT_SYMBOL_K39
				CASE 1
					PRINTFORM 「ええ、いいですよ
					CALL PRINT_SYMBOL_K39
					PRINTFORMW 「では、どんな事して欲しいですか？　うふふ」
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類：羞恥
	;=================================================
	IF MENUMATCH(TFLAG:ACT, "羞恥")
		;相思相愛
		IF GET_KFLAG_K39("相思相愛")
			PRINTFORMW 「もっと恥ずかしい事して欲しいって事？」
			PRINTFORMW 「ふふっ、じゃ次はアレやってみる？」
		;それ以外
		ELSE
			PRINTFORM 「いいですよ。もっと恥ずかしい事してあげます
			CALL PRINT_SYMBOL_K39
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類：加虐
	;=================================================
	IF MENUMATCH(TFLAG:ACT, "加虐")
		;相思相愛
		IF GET_KFLAG_K39("相思相愛")
			PRINTFORMW 「もっと虐めて欲しいの？　まだ物足りないの？」
			PRINTFORM 「まったく……もうどうしようもない感じね
		;それ以外
		ELSE
			PRINTFORMW 「罵倒されてるのに気持ち良くして、ですか？」
			PRINTFORM 「本当にいやらしい人ですね
		ENDIF
		CALL PRINT_SYMBOL_K39
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類：異常
	;=================================================
	IF MENUMATCH(TFLAG:ACT, "異常")
		;相思相愛
		IF GET_KFLAG_K39("相思相愛")
			PRINTFORMW 「一人で居るのが寂しくなっちゃったの？」
			PRINTFORM 「クスクス、可愛いわね
		;それ以外
		ELSE
			PRINTFORMW 「あら、寂しくなってしまったんですか？」
			PRINTFORM 「可愛いですね
		ENDIF
		CALL PRINT_SYMBOL_K39
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類：休憩
	;=================================================
	IF MENUMATCH(TFLAG:ACT, "休憩")
		;相思相愛
		IF GET_KFLAG_K39("相思相愛")
			SELECTCASE RANDCOM(2, 900)
				CASE 0
					PRINTFORMW 「そんなに焦らなくてもいいのに♪　クスクス…」
					PRINTFORM 「とりあえずしっかり休んで、体力を回復して貰わないとね
				CASE 1
					PRINTFORMW 「ええ、勿論良いわよ」
					PRINTFORM 「休憩が終わったら、また愉しませてあげる
			ENDSELECT
		;それ以外
		ELSE
			SELECTCASE RANDCOM(2, 901)
				CASE 0
					PRINTFORMW 「うふっ、いいですよ」
					PRINTFORM 「休憩が終わったら、また愉しませてあげます
				CASE 1
					PRINTFORMW 「もう待ちきれないんですか？」
					PRINTFORM 「そんなに慌てなくてもいいのに
			ENDSELECT
		ENDIF
		CALL PRINT_SYMBOL_K39
		RETURN 1
	ENDIF
	
	;=================================================
	;その他(汎用処理)
	;=================================================
	IF 口上記入チェック(100)
		CALL hmm_K39
		RETURN 1
	ENDIF
ENDIF
;COM8, 気持ち良くして