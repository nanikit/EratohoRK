;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	eratohoЯeverse　橙口上
;
;		「基本設定・ライセンス・ターミナル」ファイル
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;　※※※※※※他者による修正、加筆、改変についての但し書き※※※※※※

;　オリジナル作者以外が手を入れる場合は、可能な限りオリジナル作者の許可を求めること。
;　オリジナル作者とコンタクトが取れない場合には以下の但し書きに従ってUPできるかどうか判断してください。
;
;　　◎修正（誤字、脱字、構文のバグ等の修正、本体の仕様変更への対応など）
;　　　→OK
;
;　　◎加筆（未記入箇所、分岐等の増補）
;　　　→OK
;
;　　◎改変（上書き、消去を含む改変）
;　　　→OK
;
;　　◎収録・再配布（本体への同梱、口上まとめへの追加など）
;　　　→OK

;　※※※※※※※※※※※※※※※※※※※※※※※※※※※※※※※※※※

;━━━━━━━━━━━━━━━━━━━━━━━━
;地の文のアニメーション効果設定
;━━━━━━━━━━━━━━━━━━━━━━━━
;・地の文でアニメーション効果を行う直前に呼ばれる
;・RETURN 1すると絶頂時地の文のアニメーション効果（振動・フラッシュの二種）を強制非表示にする
;・口上側でエフェクトを多用するなどで競合する場合に設定
;・戻り値を変数や条件式にすることで、特定の場面のみオフにすることも一応できる
;・現時点でシステム側で設定されている効果は調教対象(MASTER)に対してのみ
;-------------------------------------------------
@KOJO_TRAIN_MESSAGE_ANIME_K13
RETURN 0

;━━━━━━━━━━━━━━━━━━━━━━━━
;排他のチェック
;━━━━━━━━━━━━━━━━━━━━━━━━
;・条件を満たす調教対象の場合、未記入でも一般口上を表示しない設定
;・Y1の箇所を(MALEYOU/TWINYOU/PENISYOU/FEMALEYOU/NO{キャラ番号}/MALE/TWIN/PENIS/FEMALE)等にする事で対象以外を排他にできる
;・指定口上同士の排他設定はできない
;・機能を生かすならRETURN 1
;-------------------------------------------------
@KOJO_Y1_EXCLUSION_K13
RETURN 0

;━━━━━━━━━━━━━━━━━━━━━━━━
;口上色設定
;━━━━━━━━━━━━━━━━━━━━━━━━
;・ここで設定したものが口上表示時にEVENT_K内でCALL KOJO_COLOR(TARGET)で呼び出され
;・@KOJO_ACTなり@KOJO_REACT終了時にはシステム側でRESETCOLORされる
;-------------------------------------------------
;・SETCOLOR命令はＲ(赤)Ｇ(緑)Ｂ(青)の順で文字の表示色を指定できる
;・各値は0～255までの範囲で設定できる。数字が大きいほど、その色の度合いが強くなる
;・色彩についてよくわからなければ「色見本」などで検索してみるのがよい
;・デフォルトは灰色
;・アライメントが負だと色が変わる等、自由に条件で分岐することもできる
;・RGB HexまたはDecどちらの表記でも可
;-------------------------------------------------
@KOJO_COLOR_K13
SETCOLOR 0xFF7040

;━━━━━━━━━━━━━━━━━━━━━━━━
;口上存在判定
;━━━━━━━━━━━━━━━━━━━━━━━━
@KOJO_K13
;対象限定口上の有無をRESULTSに代入。複数代入可。キャラ限定はNOXではなくKXで代入
RESULTS = 
;口上の作者名。複数人の場合連名で記述すること
RESULTS:1 = SBの人
;一般口上があればRETURN 1。無い場合はRETURN 0
RETURN 1

;━━━━━━━━━━━━━━━━━━━━━━━━
;あらすじ(DIGEST)
;━━━━━━━━━━━━━━━━━━━━━━━━
;・この箇所が表示される時点では調教者としては決定していない
;・当該キャラは一時的にTARGETになっており、@KOJO_COLORで設定した文字色で表示される
;・文字色をデフォルトで表示させたい場合は、PRINTD系なりPRINTFORMD系を使う必要がある
;・FADEIN, FADEOUTは@KOJO_COLORで設定した色で表示される。これはFULLCUTIN系も同様
;-------------------------------------------------
;・FLAG:モードで各モード毎に記述する事も可
;・関数分けによる排他も可
;-------------------------------------------------
@KOJO_DIGEST_K13
#DIM current_time
#DIM current_month
#DIM current_date

current_time = GETTIME()
current_month = (current_time / 100000000000) % 100
current_date = (current_time / 1000000000) % 100

RESETCOLOR
PRINTFORML 　　―― 人が猫を飼うなら、猫が人を飼ったっていい ――
PRINTFORML 　　そんな化け猫橙の一時の思いつきからペットとして監禁されてしまったあなた
PRINTFORML 　　果たしてあなたは橙の苛烈な『躾』に耐え忍び、人としての尊厳を守ることが出来るのか？
PRINTFORML 　　全米が震撼したその驚愕の結末とは！！？
PRINTFORML 　　{current_month}月{current_date}日上映開始予定！！  Emueraの起動を急げ！
IF NO:MASTER != 0
	PRINTL 
	PRINTL 
	PRINTFORML 　　ん？　主人公があなたじゃない？
	PRINTFORML 　　……まあ、そんな時でもほどほどに整合性は合うと思うから気にするな！
ENDIF
WAIT
RETURN 1

;━━━━━━━━━━━━━━━━━━━━━━━━
;注意事項(ATTENTION)
;━━━━━━━━━━━━━━━━━━━━━━━━
;・仕様的にはあらすじに準ずる
;-------------------------------------------------
@KOJO_ATTENTION_K13
RESETCOLOR
PRINTFORML 　　どちらかといえば原作寄り(？)の、生意気で高慢な橙の口上です
PRINTFORML 　　雰囲気的にもかなり陰鬱な方向性で仕上げております
PRINTFORMW 　　可愛い系とか元気系の橙を期待している方はご注意下さいませ……
RETURN 1

;━━━━━━━━━━━━━━━━━━━━━━━━
;コンフィグ設定
;━━━━━━━━━━━━━━━━━━━━━━━━
;・RESULTは初期値-1になっています。コンフィグ項目に対応した設定をしてください。
;・コメントアウト部分、-1になっている部分は、設定を変更しません。
;-------------------------------------------------
@KOJO_SETUP_K13

;主人公の読心能力(0=読めない、1=空気が、2=心が)
RESULT:1 = -1

;素質の表示分類(0=番号順、1=系統別)
RESULT:2 = -1

;日常イベント(0=オフ、1=オン)
RESULT:3 = 0

;調教者の妊娠判定(0=オフ、1=オン)
RESULT:4 = -1

;調教対象の妊娠判定(0=オフ、1=愛()があるほど妊娠しやすい、2=普通にオン)
RESULT:5 = -1

;AI自由度（SANDBOX用）(0=余計なことしない、1=人員追加、2=技巧上昇、4=技能取得、8=道具錬成、15=全てを自動で行う)
;現状ではRESULTに入れた数値がbitとして指定されるので、bit和での指定も可能。ただし将来的に仕様変更の可能性あり
RESULT:6 = -1

;口上表示設定(0=表示しない、1=表示する)
RESULT:10 = 1


;アニメ設定(0=オフ、1=オン)
;キャラ登場カットイン
RESULT:11 = -1
;ＢＧＭカットイン
RESULT:12 = -1
;PALAM獲得アニメ
RESULT:13 = -1
;脱衣アニメ
RESULT:14 = -1
;昼夜の背景色変化
RESULT:15 = -1
;全画面カットイン
RESULT:16 = -1
;フラッシュ効果
RESULT:17 = -1
;振動効果
RESULT:18 = -1
;文字のスライド
RESULT:19 = -1
;文字のフェードイン・アウト
RESULT:20 = -1

RETURN 1

;━━━━━━━━━━━━━━━━━━━━━━━━
;その他口上で使用する関数など
;━━━━━━━━━━━━━━━━━━━━━━━━

;-------------------------------------------------
;呼び名を返す関数
;-------------------------------------------------
@FUNC_K13_CALLNAME()
#FUNCTIONS
SELECTCASE NO:MASTER
	CASE 21
		RETURNF "藍様"
	CASE 22
		RETURNF "紫様"
ENDSELECT
RETURNF "あなた"

;-------------------------------------------------
;調教対象の性別を見て、男ならARGS:0、女ならARGS:1を返す関数
;-------------------------------------------------
@FUNC_K13_SEXSELECT(ARGS:0, ARGS:1)
#FUNCTIONS
IF TALENT:MASTER:オトコ
	RETURNF ARGS:0
ENDIF
RETURNF ARGS:1

;-------------------------------------------------
;昼なら「こんにちは」夜なら「こんばんは」を返す関数
;-------------------------------------------------
@FUNC_K13_GREET()
#FUNCTIONS
IF TIME == 0
	RETURNF "こんにちは"
ENDIF
RETURNF "こんばんは"

;-------------------------------------------------
;選択肢を表示する関数(最大8つまで)
;選択された選択肢の番号を戻り値として返す(0～7)
;-------------------------------------------------
@FUNC_K13_ASK(ARGS:0, ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5, ARGS:6, ARGS:7)
#DIM cur_redraw
#DIM count_i
#DIM max_select

max_select = 0
FOR count_i, 0, 8
	IF ARGS:count_i != ""
		PRINTBUTTON @"[{count_i}] - %ARGS:count_i%", count_i
		PRINTL 
		max_select ++
	ELSE
		BREAK
	ENDIF
NEXT

IF max_select <= 0
	RETURN 0
ENDIF

cur_redraw = CURRENTREDRAW()

$INPUT_LOOP
INPUT

REDRAW 0

IF RESULT >= 0 && RESULT < max_select
	REDRAW cur_redraw
	RETURN RESULT
ENDIF
GOTO INPUT_LOOP

;-------------------------------------------------
;恋人イベントを消化していれば1を返す関数
;-------------------------------------------------
@FUNC_K13_ISLOVER()
#FUNCTION
RETURNF VAR_K13:24 == 6

;-------------------------------------------------
;爪切りイベント
;ARGは指で弄る対象部位の設定(0=膣/1=アナル)
;-------------------------------------------------
@FUNC_K13_CUTCLAW(ARG)
PRINTFORMW 「じゃーん！　これを見なさいっ！」
PRINTFORMW %CALLNAME%は%CALLNAME:MASTER%の顔の前で両手を拡げると、くるりと掌を裏返して手の甲を見せつけてきた
PRINTFORMW 鋭く伸びた真紅の爪が、ずらりと並んでいるが……
PRINTFORMW 「ほら、片方の手の、人差し指と中指だけ、爪が切ってあるでしょ？」
PRINTFORMW たしかに、よく見るとその二本だけ爪が切られ、先端が丸くなめらかに処理されている
PRINTFORMW 「爪が生えたままだと%FUNC_K13_CALLNAME()%を躾けるのに不便だからね。こうして、綺麗にしておいたのよ」
PRINTFORMW %CALLNAME%は得意気にフフン、と笑った
PRINTL 
PRINTFORMW だが、『躾けるのに不便』ということは、つまるところ、その指を%CALLNAME:MASTER%の調教に使うということ……
PRINTFORMW 「あ、ひょっとして、この指をどんなことに使うのか気になるの？」
IF ARG
	PRINTFORMW 「それはもちろん、%FUNC_K13_CALLNAME()%のお尻の穴を開発するために使うのよ」
	PRINTL 
	PRINTFORMW 「……というわけで、早速だけど尻を出しなさい」
	PRINTFORMW 「大丈夫、大丈夫♪　壊さない程度には優しくしてあげるわ」
ELSE
	PRINTFORMW 「それはもちろん、%FUNC_K13_CALLNAME()%のオマンコを可愛がるために使うのよ」
	PRINTL 
	PRINTFORMW 「……というわけで、早速だけど股を開きなさい」
	PRINTFORMW 「大丈夫、大丈夫♪　ソコは大切な場所だから、ちゃんと優しく扱ってあげる」
ENDIF
