;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	アリス口上(対象限定) 会話系アクション口上ファイル
;
;		会話系アクション口上をまとめたファイル
;
;		EXTRAモードでNO:9咲夜をMASTERに選択した場合のみ表示
;
;Version		Reason
;1.70			@口上記入チェック仕様に。その他細かな修正
;1.62			一部条件の見直し。誤字、脱字修正
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;========================================================================
;会話系 ACT0～9
;========================================================================
;・TFLAG:ACT(0=今の気分を聞く/1=自分への気持ちを聞く/2=性についての話をする/3=優しく慰める/4=厳しく脅す/5=衣装の変更を要求する/6=合意を求める/7=お仕置きと宣言する/8=怒鳴る/9=不気味に笑う)
;・TFLAG:ACT分類 == 0に該当
;・ACT9, 不気味に笑うは異常系ACT分類から派生する場合がある
;・汎用リアクションにおいてはMENUMATCH(TFLAG:ACT, "会話")で拾う事が可能
;-------------------------------------------------
;・戻り値(RESULT)について
;・システム側では口上側のRESULT(終端のRETURN 1など)によって口上が表示されたのか否か、
;・また箇所毎の特有の挙動(媚薬等口上をスキップなど)を制御しています
;・@口上記入チェックの場合代入処理を省略できますが、RESULT値は口上側で決めうちする必要があります
;・特別な理由がない限り、テンプレとしてはRETURN 1を記述しておきます
;-------------------------------------------------
;関連変数…
;FIRSTACT
;初回ACT判定関数
;TFLAG:ACT（TFLAG:90）を自動で参照、現在実行されているACTごとに番号が独立して保存される
;-------------------------------------------------
;────────────────────────────────────
;NO9_ACT0, 今の気分を聞く FIRSTACT(0) RANDACT(0)(1)
;────────────────────────────────────
;・今の気分について聞いてくる
;・調教対象の反応に応じてREACT以降でACT派生への代入が行われる
;・ターン方針が"異常"なら発生しない
;-------------------------------------------------
@KOJO_NO9_MS_ACT_K14_0
;-------------------------------------------------
;@EQUIPACTION_DESCRIPTIONが絡むところは、とりあえず書き分けないとって感じ？
;まぁ面倒だし、並び順も頭が痛いところ。一部TEQUIP分岐は何処かに統合してもよさそうだが
;というかこれ、REACTも相応にシチュエーション絞ったもの書く必要があるなぁ
;━━━━━━━━━━━━━━━━━━━━━━━━
;NO9_ACT0, 今の気分を聞く
;RESULT値（=0, 汎用ACTに流れる、1, これ以上表示しない、2, 表示して媚薬等口上をスキップ）
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100)
	;=============================================
	;初回
	;=============================================
	IF 口上記入チェック(100) && FIRSTACT(0)
		PRINTFORMW 「ねぇ、気分はどうかしら？」
		PRINTFORMW 「そんなに固くなる事ないわ。大人しくしてれば可愛がってあげるから」
		RETURN 1
	ENDIF
	
	;=============================================
	;性交系で膣内射精の後
	;=============================================
	IF 口上記入チェック(100) && TFLAG:前回中出し == 1
		PRINTFORMW 「%CNAME(MASTER, TARGET)%に中出し…最高ね♪」
		PRINTFORMW 「ねぇ、私に征服された気分はいかがかしら？」
		RETURN 1
	ENDIF
	
	;=============================================
	;イキそう
	;=============================================
	IF 口上記入チェック(100) && TCVAR:MASTER:イきそう
		;-------------------------------------------------
		;イキそうなのを我慢している
		;-------------------------------------------------
		IF 口上記入チェック(100) && TCVAR:MASTER:イきそう > 5
			PRINTFORMW 「あら、頑張るわね？」
			PRINTFORM 「%CNAME(MASTER, TARGET)%がいつまで耐えられるか、試してみるのも悪くないわね
			CALL PRINT_SYMBOL_K14
			RETURN 1
		ENDIF
		
		;-------------------------------------------------
		;焦らし中
		;-------------------------------------------------
		IF 口上記入チェック(100) && TCVAR:MASTER:焦らし度 > 1
			PRINTFORMW 「ねぇ、焦らされてる気分はどう？」
			PRINTFORMW 「簡単にはイかせないわよ、悪いけど。フフ…」
			RETURN 1
		ENDIF
		
		;-------------------------------
		;その他
		;-------------------------------
		PRINTFORMW 「ふふ、イキそうなのね？」
		PRINTFORMW 「でも、簡単にイかせては面白くないのよね」
		RETURN 1
	ENDIF
	
	;=============================================
	;ボールギャグ装着中
	;=============================================
	IF 口上記入チェック(100) && TEQUIP:ボールギャグ
		PRINTFORMW 「ふふ、良い格好ね。今の気分はいかがかしら？」
		PRINTFORMW 「と言っても答えられないでしょうけど、ね♪」
		RETURN 1
	ENDIF
	
	;=============================================
	;アイマスク装着中
	;=============================================
	IF 口上記入チェック(100) && TEQUIP:アイマスク
		PRINTFORMW 「視界を奪われた気分はどうかしら？」
		RETURN 1
	ENDIF
	
	;=============================================
	;顔面騎乗中
	;=============================================
	IF 口上記入チェック(100) && TRUECHECK(TEQUIP:顔面騎乗, TEQUIP:顔面騎乗アナル) > 0
		PRINTFORMW 「ねぇ咲夜。私にこんな事されてる気分はどうかしら？」
		PRINTFORMW 「でもこの状態では答えられないかしらね？ フフフ…」
		RETURN 1
	ENDIF
	
	;=============================================
	;自慰中
	;=============================================
	IF 口上記入チェック(100) && TEQUIP:自慰中
		;-------------------------------------------------
		;調教対象が自慰中
		;-------------------------------------------------
		IF 口上記入チェック(100) && GETBIT(TEQUIP:自慰中, 0)
			SELECTCASE ABL:MASTER:露出癖
				;露出癖3以上
				CASE IS >= 3
					PRINTFORMW 「私に見られて余計に興奮してるみたいね？」
				;その他
				CASEELSE
					PRINTFORMW 「ねぇ咲夜。まだ見られるのは恥ずかしい？」
			ENDSELECT
			RETURN 1
		ENDIF
		
		;-------------------------------------------------
		;調教者が自慰中
		;-------------------------------------------------
		IF 口上記入チェック(100) && GETBIT(TEQUIP:自慰中, 1)
			;アイマスク装着中
			IF TEQUIP:アイマスク
				SELECTCASE RANDACT(2, 0)
					CASE 0
						PRINTFORMW 「ねぇ咲夜。聞こえるかしら？ この音…」
					CASE 1
						PRINTFORMW 「見えなくても分かるでしょう？ 今、私が何をしているか…」
				ENDSELECT
			;その他
			ELSE
				SELECTCASE RANDACT(2, 1)
					CASE 0
						PRINTFORMW 「私のこんな姿見せ付けられて、どんな気分かしら？」
					CASE 1
						PRINTFORMW 「ねぇ咲夜。自分でもシたくならない？」
				ENDSELECT
			ENDIF
			RETURN 1
		ENDIF
	ENDIF
	
	;=============================================
	;性交中
	;=============================================
	IF 口上記入チェック(100) && TEQUIP:性交中
		SELECTCASE TEQUIP:性交中
			;正常位, 騎乗位
			CASE 1, 3
				;前COM派生処理
				SELECTCASE TRAINNAME:PREVCOM
					;腰を振る
					CASE "腰を振る"
						PRINTFORMW 「そんなに腰振っちゃって、愉しんでもらえてるみたいね？」
					;積極的反応
					CASE "快感を受け入れる", "積極的やる", "愛情を込めてやる"
						PRINTFORMW 「ふふ、愉しんでもらえてるみたいね」
					;その他
					CASEELSE
						PRINTFORMW 「愉しんでないみたいね。あまりシたくないの？」
				ENDSELECT
			;後背位
			CASE 2
				;前COM派生処理
				SELECTCASE TRAINNAME:PREVCOM
					;腰を振る
					CASE "腰を振る"
						PRINTFORMW 「後ろから犯されるのが興奮するのかしら？ ねぇ咲夜…」
					;積極的反応
					CASE "快感を受け入れる", "積極的やる", "愛情を込めてやる"
						PRINTFORMW 「貴女…こういう風に犯されるのが好きみたいね？」
					;その他
					CASEELSE
						PRINTFORMW 「後ろからされるのは嫌？」
				ENDSELECT
			;対面座位, 背面座位
			CASE 4, 5
				;前COM派生処理
				SELECTCASE TRAINNAME:PREVCOM
					;腰を振る
					CASE "腰を振る"
						PRINTFORM 「すっかり興奮してるみたいね？ 抱っこされてると興奮するのかしら、ねぇ咲夜
						CALL PRINT_HEARTCBW_K14(COLOR2_K14("pink"), , , COLOR2_K14("ALICE"))
					;積極的反応
					CASE "快感を受け入れる", "積極的やる", "愛情を込めてやる"
						PRINTFORMW 「この格好でされるの、好きみたいね？」
					;その他
					CASEELSE
						PRINTFORMW 「気分が乗らないみたいね？ あまりシたくないのかしら」
				ENDSELECT
			;アナルセックス
			CASE 6
				;前COM派生処理
				SELECTCASE TRAINNAME:PREVCOM
					;腰を振る
					CASE "腰を振る"
						PRINTFORMW 「ふふ、おしりがそんなに気持ち良いのかしら？」
					;積極的反応
					CASE "快感を受け入れる", "積極的やる", "愛情を込めてやる"
						PRINTFORMW 「貴女…おしりでするのが好きなのね？ ふふっ」
					;その他
					CASEELSE
						PRINTFORMW 「おしりでするのは嫌？」
				ENDSELECT
		ENDSELECT
		RETURN 1
	ENDIF
	
	;=============================================
	;三角木馬使用中
	;=============================================
	IF 口上記入チェック(100) && TEQUIP:三角木馬
		PRINTFORMW 「私だって思うのよ？ こんな状況で気分なんて聞くべきじゃないって」
		PRINTFORMW 「フフフ…」
		RETURN 1
	ENDIF
	
	;=============================================
	;調教対象の状態が怒り
	;=============================================
	IF 口上記入チェック(100) && M_COND("怒り")
		PRINTFORMW 「随分機嫌が悪そうね？」
		PRINTFORMW 「一体何がそんなに面白くないのかしら」
		RETURN 1
	ENDIF
	
	;通常ならもっと上層でいいかもだけれどTEQUIP系を食いたくないとかそんな感じで
	;=============================================
	;その調教の最初のアクション
	;=============================================
	IF 口上記入チェック(100) && TFLAG:経過時間 == 1
		;相思相愛
		IF AFFECTION("相思相愛")
			PRINTFORMW 「ねぇ、気分はどう？ と、いうか…」
			PRINTFORMW 「私が話しかけてるんだから、もっと嬉しそうな顔しなさいよ！」
		;それ以外
		ELSE
			PRINTFORMW 「正直、%CNAME(MASTER, TARGET)%の今の気分とかに興味ないの…」
			PRINTFORMW 「私のやりたいようにさせて頂くわ…　ウフフ…」
		ENDIF
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	PRINTFORMW 「ねぇ咲夜。気分はいかが？」
	RETURN 1
ENDIF
;会話系は根本的に見直す必要あり

;────────────────────────────────────
;NO9_ACT1, 自分への気持ちを聞く
;────────────────────────────────────
;・自分のことをどう思うか聞く
;・調教対象の反応に応じてREACT以降でACT派生への代入が行われる
;・ターン方針が"異常"なら発生しない
;・話題『好意』が既出となり、以後、同一調教中では抑制される
;-------------------------------------------------
@KOJO_NO9_MS_ACT_K14_1
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;NO9_ACT1, 自分への気持ちを聞く
;RESULT値（=0, 汎用ACTに流れる、1, これ以上表示しない、2, 表示して媚薬等口上をスキップ）
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100)
	;=================================================
	;初回
	;=================================================
	;IF 口上記入チェック(0) && FIRSTACT(0)
	;ENDIF
	
	;=================================================
	;その調教の最初のアクション
	;=================================================
	IF 口上記入チェック(100) && TFLAG:経過時間 == 1 && ONCEACT(0)
		PRINTFORMW 「ねぇ…ちょっと聞かせて欲しいんだけど」
		PRINTFORMW 「%CNAME(MASTER, TARGET)%、私の事どう思ってるのかしら？」
		RETURN 1
	ENDIF
	
	;=================================================
	;顔面騎乗中
	;=================================================
	;IF 口上記入チェック(0) && TRUECHECK(TEQUIP:顔面騎乗, TEQUIP:顔面騎乗アナル) > 0
	;ENDIF
	
	;=================================================
	;自慰中
	;=================================================
	;IF 口上記入チェック(0) && TEQUIP:自慰中
	;ENDIF
	
	;=================================================
	;性交中
	;=================================================
	;IF 口上記入チェック(0) && TEQUIP:性交中
	;ENDIF

	;-------------------------
	;その他
	;-------------------------
	PRINTFORMW 「%CNAME(MASTER, TARGET)%は私の事どう思ってるのかしら？」
	PRINTFORMW 「ねぇ、聞かせて頂戴…」
	RETURN 1
ENDIF

;────────────────────────────────────
;NO9_ACT2, 性についての話をする
;────────────────────────────────────
;・調教者が性についての知識を語り始める
;・TFLAG:お仕置きフラグが立っていると発生しない
;・ターン方針が"異常"だと発生しない
;・調教者に性知識が無いと発生しない
;・話題『知識』が既出となり、以後、同一調教内では抑制される
;-------------------------------------------------
@KOJO_NO9_MS_ACT_K14_2
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;NO9_ACT2, 性についての話をする
;RESULT値（=0, 汎用ACTに流れる、1, これ以上表示しない、2, 表示して媚薬等口上をスキップ）
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(0)
[SKIPSTART]
	;=================================================
	;初回
	;=================================================
	IF 口上記入チェック(0) && FIRSTACT(0)
	ENDIF
	;=================================================
	;顔面騎乗中
	;=================================================
	IF 口上記入チェック(0) && TRUECHECK(TEQUIP:顔面騎乗, TEQUIP:顔面騎乗アナル) > 0
	ENDIF
	;=================================================
	;自慰中
	;=================================================
	IF 口上記入チェック(0) && TEQUIP:自慰中
	ENDIF
	;=================================================
	;性交中
	;=================================================
	IF 口上記入チェック(0) && TEQUIP:性交中
	ENDIF
	;=================================================
	;その調教の最初のアクション
	;=================================================
	IF 口上記入チェック(0) && TFLAG:経過時間 == 1
	ENDIF
[SKIPEND]
ENDIF

;────────────────────────────────────
;NO9_ACT3, 優しく慰める
;────────────────────────────────────
;・調教者が優しい言葉で慰めてくる
;・TFLAG:お仕置きフラグが立っていると発生しない
;・ターン方針が"ハード", "異常"だと発生しない
;・話題『慰め』が既出となり、以後、同一調教内では抑制される
;-------------------------------------------------
@KOJO_NO9_MS_ACT_K14_3
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;NO9_ACT3, 優しく慰める 
;RESULT値（=0, 汎用ACTに流れる、1, これ以上表示しない、2, 表示して媚薬等口上をスキップ）
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100)
	;=================================================
	;性交系で膣内射精の後
	;=================================================
	IF 口上記入チェック(100) && TFLAG:前回中出し == 1
		PRINTFORMW 「心配しないで…」
		PRINTFORMW 「デきちゃったら、ちゃんと責任とってあげるから♪」
		RETURN 1
	ENDIF
	
	[SKIPSTART]
	;==================================================
	;顔面騎乗中
	;==================================================
	IF 口上記入チェック(0) && TRUECHECK(TEQUIP:顔面騎乗, TEQUIP:顔面騎乗アナル) > 0
	ENDIF
	
	;==================================================
	;自慰中
	;==================================================
	IF 口上記入チェック(0) && TEQUIP:自慰中
		PRINTW
	ENDIF
	
	;==================================================
	;性交中
	;==================================================
	IF 口上記入チェック(0) && TEQUIP:性交中
	ENDIF
	
	;==================================================
	;イキそう
	;==================================================
	IF 口上記入チェック(0) && TCVAR:MASTER:イきそう
		;-------------------------------------------------
		;イキそうなのを我慢している
		;-------------------------------------------------
		IF 口上記入チェック(0) && TCVAR:MASTER:イきそう > 5
			PRINTFORMW 「我慢なんてしないでイってしまいなさい」
			PRINTFORMW 「でも、耐えてる姿はちょっと可愛いかも…ふふふ」
			RETURN 1
		ENDIF
		
		;-------------------------------------------------
		;焦らし中
		;-------------------------------------------------
		IF 口上記入チェック(0) && TCVAR:MASTER:焦らし度 > 1
			PRINTFORMW 「どう？ 焦らされるのは辛いかしら？」
			PRINTFORML 「私の気が済んだら、イかせてあげてもいいわよ」
			PRINTFORMW 「フフフ……」
			RETURN 1
		ENDIF
		
		;-------------------------------
		;その他
		;-------------------------------
		PRINTFORMW 「イキそうなのね？」
		PRINTFORMW 「待ってて…すぐにイかせてあげるから♪」
		RETURN 1
	ENDIF
	[SKIPEND]
	
	;-------------------------
	;その他
	;-------------------------
	IF AFFECTION("相思相愛")
		PRINTFORMDW アリスは咲夜の頭を優しく撫でた…
		PRINTFORMW 「ふふ、よしよし♪」
	ELSE
		PRINTFORMW 「私だってあまり酷いはしたくないのよ、咲夜」
		PRINTFORMW 「大人しくしてれば、悪いようにはしないわ」
	ENDIF
	RETURN 1
ENDIF

;────────────────────────────────────
;ACT4, 厳しく脅す
;────────────────────────────────────
;・厳しい言葉で脅す
;・TFLAG:お仕置きフラグが立っていると発生しない
;・ターン方針が"ソフト", "異常"だと発生しない
;・話題『脅す』が既出となり、以後、同一調教内では抑制される
;-------------------------------------------------
@KOJO_NO9_MS_ACT_K14_4
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;NO9_ACT4, 厳しく脅す
;RESULT値（=0, 汎用ACTに流れる、1, これ以上表示しない、2, 表示して媚薬等口上をスキップ）
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100)
	;=================================================
	;初回
	;=================================================
	IF 口上記入チェック(100) && FIRSTACT(0)
		PRINTFORMW 「覚えておいて頂戴」
		PRINTFORMW 「%CNAME(MASTER, TARGET)%をどうするか、全て私次第だって事」
		PRINTFORMW 「この意味が分からない程、馬鹿じゃない事を期待してるわ…」
		RETURN 1
	ENDIF
	
	;=================================================
	;性交系で膣内射精の後
	;=================================================
	IF 口上記入チェック(100) && TFLAG:前回中出し == 1
		PRINTFORMW 「\@ IS_PREVCOMNAME("中に出さないで！") ? 無理やり # %CNAME(MASTER, TARGET)%に \@中出し、最高ね。フフ」
		PRINTFORM 「%CNAME(MASTER, TARGET)%がちゃんと孕むまで、毎日膣内に注いであげるわ
		CALL PRINT_SYMBOL_K14
		RETURN 1
	ENDIF
	
	;=================================================
	;イキそう
	;=================================================
	IF 口上記入チェック(100) && TCVAR:MASTER:イきそう
		;-------------------------------------------------
		;イキそうなのを我慢している
		;-------------------------------------------------
		IF 口上記入チェック(100) && TCVAR:MASTER:イきそう > 5
			PRINTFORMW 「無駄な抵抗はやめたらどう…？」
			PRINTFORMW 「どうせすぐイっちゃうんだから」
			RETURN 1
		ENDIF
		
		;-------------------------------------------------
		;焦らし中
		;-------------------------------------------------
		IF 口上記入チェック(100) && TCVAR:MASTER:焦らし度 > 1
			SELECTCASE RANDACT(2, 0)
				CASE 0
					PRINTFORMW 「あら、イキそうなの？」
					PRINTFORMW 「駄目よ、咲夜。もう少し我慢して頂戴…」
				CASE 1
					PRINTFORMW 「イきたくてたまらない、って顔ね？」
					PRINTFORMW 「このままずっと焦らしてたら%CNAME(MASTER, TARGET)%、どうなっちゃうかしら。ねぇ？」
			ENDSELECT
			CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), "「フフ……」")
			CLEARLINE 1
			RETURN 1
		ENDIF
	ENDIF
	
	;=================================================
	;アイマスク装着中
	;=================================================
	IF 口上記入チェック(100) && TEQUIP:アイマスク
		PRINTFORMW 「視界を奪った貴女を放置したらどうなるかしら？」
		PRINTFORMW 「ちょっと面白そうではあるわね。流石にやらないけど」
		CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"「\@ TENSION(TARGET, 8) == "高い" ? クスクス # フフフ \@……」")
		CLEARLINE 1
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	PRINTFORMW 「大人しくしてれば、悪いようにはしないわ」
	PRINTFORMW 「無駄な抵抗はやめた方が賢明よ」
	RETURN 1
ENDIF

;────────────────────────────────────
;NO9_ACT5, 衣装の変更を要求する
;────────────────────────────────────
;・調教者が衣装の変更を要求してくる
;・調教対象の反応に応じて、何枚脱ぐか、あるいは脱がないのかにREACT以降で派生する
;・TFLAG:ACT派生(0=服を脱ぐように命令/1=服を着るように命令)
;・TEQUIP:押し倒し中には発生しない
;・TEQUIP:縄が真になっていると発生しない
;-------------------------------------------------
@KOJO_NO9_MS_ACT_K14_5
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;NO9_ACT5_0, 服を脱ぐように命令
;RESULT値（=0, 汎用ACTに流れる、1, これ以上表示しない、2, 表示して媚薬等口上をスキップ）
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && !TFLAG:ACT派生
	;=================================================
	;初回
	;=================================================
	IF 口上記入チェック(100) && FIRSTACT(0)
		PRINTFORMW 「さ、服を脱いで頂戴」 
		PRINTFORMW 「%CNAME(MASTER, TARGET)%の全て、見せてもらうわ♪」
		RETURN 1
	ENDIF
	
	;=================================================
	;調教者が調教対象を溺愛
	;=================================================
	IF 口上記入チェック(100) && AFFECTION("溺愛")
		PRINTFORMW 「さぁ、脱いで…　私に全部見せて…」
		PRINTFORMW 「ふふっ…　興奮しちゃう♪」
		RETURN 1
	ENDIF

	;-------------------------
	;その他
	;-------------------------
	PRINTFORML 「じゃぁ、脱いでもらおうかしら」
	PRINTFORMW 「恥ずかしがる事なんて無いわ」
	PRINTFORMW 「ゆっくり…私によく見えるように、ね♪」
	RETURN 1
ENDIF

;このあたり凡庸だな
;━━━━━━━━━━━━━━━━━━━━━━━━
;NO9_ACT5_1, 服を着るように命令
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:ACT派生 == 1
	PRINTFORMW 「気が変わったわ。ねぇ、服を着て頂戴」
	PRINTFORMW 「どうしてかって？ 別に理由なんてないわよ」
	RETURN 1
ENDIF

;────────────────────────────────────
;NO9_ACT6, 合意を求める
;────────────────────────────────────
;・初めてを貰ってもいいかと聞く
;・初めて＝処女or童貞(両方の場合もある)
;・既に合意を得ている場合は発生しない
;・調教対象が処女でも童貞でもない場合は発生しない
;・TFLAG:お仕置きフラグが立っていると発生しない
;・ターン方針が"異常"だと発生しない
;・話題『合意』が既出となり、以後、同一調教内では抑制される
;-------------------------------------------------
;@KOJO_NO9_MS_ACT_KXX_6
;-------------------------------------------------
;咲夜さん処女じゃないんでｗ
;-------------------------------------------------

;────────────────────────────────────
;NO9_ACT7, お仕置きと宣言する
;────────────────────────────────────
;・これからお仕置きすると言い出す
;・このACTが発生している時点でTFLAG:お仕置きフラグは立っている
;・ターン方針が"ソフト", "異常"だと発生しない
;・TEQUIP:押し倒し中時には発生しない
;-------------------------------------------------
@KOJO_NO9_MS_ACT_K14_7
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;NO9_ACT7, お仕置きと宣言する
;RESULT値（=0, 汎用ACTに流れる、1, これ以上表示しない、2, 表示して媚薬等口上をスキップ）
;━━━━━━━━━━━━━━━━━━━━━━━━
;COM側の処理と上手く繋がるよう調整すべき
;1.70以降に一度見直すか…
;------------------------------------------------
IF 口上記入チェック(100)
	;宣言したら実行してもらわないとね
	TCVAR:行動予約 = 60
	;=================================================
	;前COMがが「無愛想に応答する/強気に応答する/嫌がる/暴れる/拒否する」のいずれか
	;=================================================
	IF 口上記入チェック(100) && IS_PREVCOMNAME("無愛想に応答する/強気に応答する/嫌がる/暴れる/拒否する")
		PRINTFORMW 「まだ自分の立場が判ってないようね？ ……やれやれだわ」
		PRINTFORMW 「%CNAME(MASTER, TARGET)%の場合、言葉で伝えるより、もっと直接的な方法がいいのかしら。ねぇ？」
		RETURN 1
	ENDIF
	
	;=================================================
	;前COMが「気持ち良くして」
	;（この恥知らず！）
	;=================================================
	IF 口上記入チェック(100) && IS_PREVCOMNAME("気持ち良くして")
		PRINTFORMW 「貴女がそんな事言うなんてねぇ、咲夜」
		PRINTFORMW 「でも、今の立場を弁えてもらわないと困るわ」
		RETURN 1
	ENDIF
	
	;=================================================
	;前COMが「もっとハードにして」
	;（この豚め！）
	;=================================================
	IF 口上記入チェック(100) && IS_PREVCOMNAME("もっとハードにして")
		PRINTFORMW 「呆れたわね、咲夜。貴女がそんな事言うなんて」
		PRINTFORMW 「本当に今の状況がわかっているのかしら？」
		RETURN 1
	ENDIF
	
	;=================================================
	;前COMが「許しを乞う」
	;（許してもらえませんでした）
	;=================================================
	IF 口上記入チェック(100) && IS_PREVCOMNAME("許しを乞う")
		PRINTFORMW 「駄目よ、許すつもりなんてないもの」
		PRINTFORMW 「少しお仕置きが必要みたいね」
		RETURN 1
	ENDIF
	
	;=================================================
	;前COMが「自慰し始める」
	;（人の話を聞け！）
	;=================================================
	IF 口上記入チェック(100) && IS_PREVCOMNAME("自慰し始める")
		PRINTFORMW 「何を勝手に始めちゃってるのかしら」
		PRINTFORMW 「これはお仕置きが必要かしらね」
		;TCVAR:行動予約 = 60
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	PRINTFORMW 「身の程を弁えないこの殺人ドールには…」
	PRINTFORMW 「どうやらお仕置きが必要みたい、ね」
	RETURN 1
ENDIF

;────────────────────────────────────
;NO9_ACT8, 怒鳴る
;────────────────────────────────────
;・大声で怒鳴る
;・ターン方針が"ソフト", "ノーマル"では発生しない
;・TFLAG:お仕置きフラグに関わらず、発生する可能性がある（お仕置き中にさらに怒鳴る可能性等）
;-------------------------------------------------
@KOJO_NO9_MS_ACT_K14_8
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;NO9_ACT8, 怒鳴る
;RESULT値（=0, 汎用ACTに流れる、1, これ以上表示しない、2, 表示して媚薬等口上をスキップ）
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100)
	;=================================================
	;前COMがが「無愛想に応答する/強気に応答する/嫌がる/暴れる/拒否する」のいずれか
	;=================================================
	IF 口上記入チェック(100) && IS_PREVCOMNAME("無愛想に応答する/強気に応答する/嫌がる/暴れる/拒否する")
		PRINTFORMW 「ホント、逆らってばかりでイライラするわ…」
		PRINTFORMW 「いい加減、頭にくるのよ！ そういうの！」
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	PRINTFORMW 「本当…、可愛くないわ。%CNAME(MASTER, TARGET)%…」
	PRINTFORMW 「その反抗的な顔もイライラするのよ！」 
	RETURN 1
ENDIF

;────────────────────────────────────
;NO9_ACT9, 不気味に笑う
;────────────────────────────────────
;・不気味な微笑を浮かべる
;・異常系から発生する場合もある
;・今日の方針が"イチャイチャ"だと発生しない
;・話題『嗤う』が既出となり、以後、同一調教内では抑制される
;-------------------------------------------------
@KOJO_NO9_MS_ACT_K14_9
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;NO9_ACT9, 不気味に笑う
;RESULT値（=0, 汎用ACTに流れる、1, これ以上表示しない、2, 表示して媚薬等口上をスキップ）
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100)
	;=================================================
	;アイマスク装着中
	;=================================================
	IF 口上記入チェック(100) && TEQUIP:アイマスク
		PRINTFORMDW 視界を奪われた%CALLNAME:MASTER%の耳に、アリスの不気味な笑い声が届く…
		RETURN 1
	ENDIF
	
	;MENUMATCH等を通すと派生に対応してしまうのでむしろTFLAGそのままがいいとかそんなところ
	;まぁもう少しやりようがある感じではある
	;笑い系はBASEやらFAVORやらMENUMATCHで判定して関数化してもいいのかもしれないとか思いついたけど後回しにする
	;=================================================
	;異常系から発生
	;=================================================
	IF 口上記入チェック(100) && TFLAG:ACT分類 == 7
		PRINTFORMDW アリスの不気味な笑い声が部屋に響いている…
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	PRINTFORMDW アリスの不気味な笑い声が部屋中に響き渡る…
	RETURN 1
ENDIF
;会話系はちょっと梃入れする必要があるよね