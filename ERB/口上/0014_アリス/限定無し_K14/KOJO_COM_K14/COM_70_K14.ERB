;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;		アリス口上 特殊な反応汎用リアクション口上ファイル
;
;Version		Reason
;1.71			新規作成
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;=====================================================================
;特殊な反応 COM70～
;=====================================================================

;──────────────────────────────────
;COM70, イかせて！
;──────────────────────────────────
;・REACT分類:受け入れる
;・REACT印象:好印象大
;・依存度変化(+10, +6)
;-------------------------------------------------
;選択可能な条件…
;・焦らし中ではない
;・TCVAR:MASTER:イきそうが立っている
;・愛撫系ACT(キス/押し倒し以外)かつ各部位のPALAMが5000以上である
;※上記条件に適う場合は可
;-------------------------------------------------
;ACTに於いてTCVAR:MASTER:焦らし度 == 1(イかせる)であってもCOM70の選択でTCVAR:MASTER:焦らし度 > 1(焦らす)になる場合あり
;現在の仕様ではこの手の変化を参照する術はない。本体側の対応が望まれる！
;-------------------------------------------------
@KOJO_COM_K14_70
;-------------------------------------------------
;・TFLAG:REACT派生(0=聞き流す/1=イかせてあげる/2=イかせてあげない）
;-------------------------------------------------

LOCALS = \@ AFFECTION("相思相愛") ? クスクス # フフフ \@

;━━━━━━━━━━━━━━━━━━━━━━━━
;派生2, イかせてあげない
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:REACT派生 == 2
	;TEQIPも参照する
	;=================================================
	;アナルセックス
	;=================================================
	IF 口上記入チェック(100) && (IS_NOWACTNAME("アナルセックスさせる") || A_SEX(MASTER))
		;取りあえず枠を区切って汎用処理に落ちるように
		;-------------------------------------------------
		;ペニスバンド装着中
		;-------------------------------------------------
		;IF 口上記入チェック(0) && TEQUIP:ペニスバンド
		;ENDIF
		
		;竿付きの場合のみ落ちるように
		;-------------------------------------------------
		;通常の性交奉仕
		;-------------------------------------------------
		IF 口上記入チェック(100) && PENIS(MASTER)
			;〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓
			;今日の方針が子作り
			;〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓
			IF 口上記入チェック(100) && IS_POLICY("今日", "子作り")
				PRINTFORMW 「ねぇ…子作りするって決めた日におしりに出したいって、ちょっとどうなの？」
				PRINTFORMW 「普通、少しは我慢しようと思わない？ ま、そんな訳だから…」
				PRINTFORM 「簡単にはイかせてあげないから、ね
				CALL PRINT_HEARTCBW_K14(COLOR2_K14("pink"), , , COLOR2_K14("ALICE"))
				RETURN 1
			ENDIF
			
			;-----------------------
			;その他
			;-----------------------
			IF AFFECTION("相思相愛")
				PRINTFORMW 「おしりに出したいって言うなら別に構わないし、嫌って訳でもないわ。ただ…」
				PRINTFORMW 「なんとなく意地悪したい気分なのよね、ふふっ」
			ELSE
				PRINTFORMW 「んっ！ おしりの中に出すのはいいけど…」
				PRINTFORMW 「もう少し頑張って貰うわ、悪いけど、ね…」
			ENDIF
			RETURN 1
		ENDIF
	ENDIF
	
	;=================================================
	;性交奉仕中
	;=================================================
	IF 口上記入チェック(100) && (IS_NOWACTNAME("逆レイプ/正常位させる/後背位させる/対面座位させる/背面座位させる") || V_SEX(MASTER))
		;現状、専用口上を書く余裕がないんでチェック落として後回し
		;-------------------------------------------------
		;ペニスバンド装着中
		;-------------------------------------------------
		;IF 口上記入チェック(0) && TEQUIP:ペニスバンド
		;ENDIF
		
		;竿付きの場合のみ落ちるように
		;-------------------------------------------------
		;通常の性交奉仕
		;〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓
		IF 口上記入チェック(100) && PENIS(MASTER)
			;〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓
			;今日の方針が子作り
			;記入チェック
			LOCAL:1 = 1
			;〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓
			IF 口上記入チェック(100) && IS_POLICY("今日", "子作り")
				PRINTFORMW 「ダメよ。いくら今日は子作りだからって、まだ早すぎるもの」
				PRINTFORM 「もう少し愉しませてくれたら、考えてもいいけど…ね
				CALL PRINT_HEARTCBW_K14(COLOR2_K14("pink"), , , COLOR2_K14("ALICE"))
				RETURN 1
			ENDIF
			
			;-----------------------
			;その他
			;-----------------------
			IF AFFECTION("相思相愛")
				PRINTFORMW 「そうよね？ 私の中に出したわよね？ でも、ふふふ」
				PRINTFORMD アリスは
				;後背位以外
				IF IS_NOWACTNAME("逆レイプ/正常位させる/対面座位させる/背面座位させる") || GROUPMATCH(TEQUIP:性交奉仕中, 1, 3, 4, 5) > 0
					PRINTFORMDW %CALLNAME:MASTER%の耳元に口を寄せ囁いた…
				;後背位
				ELSEIF IS_NOWACTNAME("後背位させる") || TEQUIP:性交奉仕中 == 2
					PRINTFORMDW 振り返りながら%CALLNAME:MASTER%に向かって言った…
				ENDIF
				PRINTFORM 「まだイかせないわよ。　わ・る・い・け・ど
				CALL PRINT_SYMBOL_K14
			ELSE
				PRINTFORMW 「…ん。私の中にぶちまけたいのは判るけど……」
				PRINTFORMW 「まだダメよ。もっと愉しませて…」
			ENDIF
			RETURN 1
		ENDIF
	ENDIF
	
	;-------------------------------
	;漏れたものはここで処理
	;-------------------------------
	SELECTCASE RANDCOM(3, 0)
		CASE 0
			PRINTFORM 「そう言われても、すぐにイかせては面白くないのよね
		CASE 1
			PRINTFORMW 「そんな事言ったからって、この私が簡単にイかせてあげると思う？」
			PRINTFORM 「ふふ…むしろ余計意地悪したくなっちゃうんだけど、ね
		CASE 2
			PRINTFORMW 「そんな事言われると、ちょっと意地悪したくなるのよね…」
			PRINTFORM 「イけそうでイけないもどかしさ、たっぷり愉しんで頂戴
	ENDSELECT
	CALL PRINT_SYMBOL_K14
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;派生1, イかせてあげる
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:REACT派生 == 1
	;=================================================
	;アナルセックス
	;=================================================
	IF 口上記入チェック(100) && (IS_NOWACTNAME("アナルセックスさせる") || A_SEX(MASTER))
		;汎用処理にまわす
		;-------------------------------------------------
		;ペニスバンド装着中
		;-------------------------------------------------
		;IF 口上記入チェック(0) && TEQUIP:ペニスバンド
		;ENDIF
		
		;竿付きの場合のみ落ちるように
		;-------------------------------------------------
		;通常の性交奉仕
		;-------------------------------------------------
		IF 口上記入チェック(100) && PENIS(MASTER)
			;〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓
			;今日の方針が子作り
			;〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓
			IF 口上記入チェック(100) && IS_POLICY("今日", "子作り")
				PRINTFORMW 「ねぇ、それはいいけど…今日は子作りのはずだったわよね」
				PRINTFORMW 「だからほどほどにして、ちゃんと残しておきなさいよ？」
				PRINTFORMW 「でないと、後でお仕置きするから……%LOCALS%」
				RETURN 1
			ENDIF
			
			;-----------------------
			;その他
			;-----------------------
			IF AFFECTION("相思相愛")
				PRINTFORMW 「いいわ、私のおしりでイかせておげる」
				PRINTFORM 「枯れ果てるまで搾り取ってあげるから、覚悟して
				CALL PRINT_HEARTCBW_K14(COLOR2_K14("pink"), , , COLOR2_K14("ALICE"))
			ELSE
				PRINTFORMW 「いいわよ、射精して…」
				PRINTFORMW 「私のおしりに思いっきりぶちまけて…」
			ENDIF
			RETURN 1
		ENDIF
	ENDIF
	
	;=================================================
	;Ｖ性交奉仕中
	;=================================================
	IF 口上記入チェック(100) && (IS_NOWACTNAME("逆レイプ/正常位させる/後背位させる/対面座位させる/背面座位させる") || V_SEX(MASTER))
		;とりあえず汎用処理まで落ちて貰う
		;-------------------------------------------------
		;ペニスバンド装着中
		;-------------------------------------------------
		;IF 口上記入チェック(0) && TEQUIP:ペニスバンド
		;ENDIF
		
		;竿付きの場合のみ落ちるように
		;-------------------------------------------------
		;通常の性交奉仕
		;-------------------------------------------------
		IF 口上記入チェック(100) && PENIS(MASTER)
			;〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓
			;今日の方針が子作り
			;〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓〓
			IF 口上記入チェック(100) && IS_POLICY("今日", "子作り")
				PRINTFORMW 「…ん、わかった」
				PRINTFORMD アリスは膣内の亀頭を子宮に密着させると%CALLNAME:MASTER%
				;正常位/逆レイプ/対面座位
				IF IS_NOWACTNAME("逆レイプ/正常位させる/対面座位させる") || GROUPMATCH(TEQUIP:性交奉仕中, 1, 3, 4) > 0
					PRINTFORMDW の耳元で囁いた…
				;後背位/背面座位
				ELSEIF IS_NOWACTNAME("後背位させる/背面座位させる") || GROUPMATCH(TEQUIP:性交奉仕中, 2, 5) > 0
					PRINTFORMDW に振り返りながら言った…
				ENDIF
				PRINTFORMW 「私をママにして…、%CNAME(MASTER, TARGET)%の子供を孕ませて…」
				RETURN 1
			ENDIF
			
			;-----------------------
			;その他
			;-----------------------
			IF AFFECTION("相思相愛")
				PRINTFORMW 「いいわよ、イかせてあげる……」
				PRINTFORMW 「ほら、私の中に注ぎ込んで…」
			ELSE
				PRINTFORMW 「今それを言うって事は、私の中に出したいって事よね？」
				PRINTFORMW 「いいわよ……%CNAME(MASTER, TARGET)%に責任をとる覚悟があるなら、ね…」
			ENDIF
			RETURN 1
		ENDIF
	ENDIF
	
	;=================================================
	;性交、性交奉仕中以外
	;=================================================
	IF 口上記入チェック(100) && TRUECHECK(!TEQUIP:性交奉仕中, !TEQUIP:性交中, !TEQUIP:助手性交奉仕中, !TEQUIP:助手性交中) > 0
		;-------------------------------------------------
		;今日の方針が子作り
		;-------------------------------------------------
		IF 口上記入チェック(100) && IS_POLICY("今日", "子作り")
			PRINTFORMW 「イかせてあげるのはいいけど、今日は子作りって事を忘れちゃ駄目よ？」
			PRINTFORMW 「ちゃんと残しておかないと、お仕置きするから……ふふっ」
			RETURN 1
		ENDIF
		
		;-------------------------------------------------
		;相思相愛
		;-------------------------------------------------
		IF 口上記入チェック(100) && AFFECTION("相思相愛")
			SELECTCASE RANDCOM(3, 1)
				CASE 0
					PRINTFORMW 「ふふ…%CNAME(MASTER, TARGET)%の頼みなら断れないわね」
					PRINTFORM 「いいわ、お望み通りイかせてあげる…
					CALL PRINT_SYMBOL_K14
				CASE 1
					PRINTFORMW 「\@ !TEQUIP:アイマスク ? 涙目で # \@そんな事言われると流石に、ね」
					PRINTFORM 「さぁいいわよ。%CNAME(MASTER, TARGET)%がイクところ、私に見せて頂戴
					CALL PRINT_HEARTCBW_K14(COLOR2_K14("pink"), , , COLOR2_K14("ALICE"))
				CASE 2
					PRINTFORMW 「そうね？ 流石に限界みたいだし。ただし、今日は…」
					PRINTFORM 「\@ PENIS(MASTER) ? 徹底的に搾り取ってあげるつもりだから # 思いっきりイき狂わせてあげるつもりだから \@、覚悟して
					CALL PRINT_HEARTCBW_K14(COLOR2_K14("pink"), , , COLOR2_K14("ALICE"))
			ENDSELECT
			RETURN 1
		ENDIF
		
		;---------------------
		;その他
		;---------------------
		SELECTCASE RANDCOM(3, 2)
			CASE 0
				PRINTFORMW 「いいわよ、イきなさい…」
			CASE 1
				PRINTFORMW 「そうね、そろそろイかせてあげてもいいわ」
				PRINTFORM 「\@ PENIS(MASTER) ? 徹底的に搾り取ってあげるから # 思いっきりイき狂わせてあげるから \@、覚悟して頂戴…
				CALL PRINT_SYMBOL_K14
			CASE 2
				PRINTFORMW 「いいわ、イかせてあげる」
				PRINTFORM 「%CNAME(MASTER, TARGET)%が無様に果てるとこ、私に見せて頂戴
				CALL PRINT_SYMBOL_K14
		ENDSELECT
		RETURN 1
	ENDIF
ENDIF

;要求を聞き流すシチュエーション
;━━━━━━━━━━━━━━━━━━━━━━━━
;派生0, 「ふーん」
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:REACT派生 == 0
	;2/3の確率で表示
	IF RAND:3
		SELECTCASE TENSION(TARGET, 8)
			CASE "高い"
				PRINTFORMW 「あら、そんなにイきたいの？」
				PRINTFORMW 「でも……そう言われても、ねぇ？ %LOCALS%…」
			CASE "一般"
				PRINTFORMW 「そう、そうなの。で…、それがなにか？」
				PRINTFORMW 「%LOCALS%…」
			CASE "低い"
				PRINTFORMW 「ん？ 何か言ったかしら。よく聞こえなかったわ」
				PRINTFORMW 「%LOCALS%…」
		ENDSELECT
	;1/3の確率で表示
	ELSE
		SELECTCASE RANDCOM(3, 3)
			CASE 0
				PRINTFORMW 「『イかせてください』、でしょう？」
				PRINTFORMW 「人に物を頼む時は良く考えて欲しいものね？」
			CASE 1
				PRINTFORMW 「ふふ、そんな泣きそうな顔したってダメよ」
				PRINTFORMW 「どうするかは私次第。私がその気になるまで大人しく待つ事ね？」
			CASE 2
				PRINTFORMW 「…まったく。浅ましいにも程があるわね」
				PRINTFORMW 「頭の中イク事しか考えられない醜悪で無様な\@ TALENT:MASTER:オトコ ? マゾ豚 # メス豚 \@、ってとこかしら」
		ENDSELECT
	ENDIF
	RETURN 1
ENDIF