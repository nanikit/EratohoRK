;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;		アリス口上 奉仕系汎用リアクション口上ファイル
;
;Version		Reason
;1.71			新規作成
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;=====================================================================
;奉仕系反応 COM40番台
;=====================================================================
;・調教者への愛撫を含む行動で見られる
;・ACT分類:奉仕+トリガーイベント制
;-------------------------------------------------
;────────────────────────────────────
;COM40, 拒否する
;────────────────────────────────────
;・REACT分類:拒否
;・REACT印象:悪印象大
;・お仕置きポイント追加
;・依存度変化(-2, -4)
;-------------------------------------------------
;トリガーする行動や状況…
;ACT分類:0:会話
;	ACT:衣装の変更を要求する/合意を求める
;ACT分類:1:愛撫
;	ACT:
;ACT分類:2:道具
;	ACT:
;ACT分類:3:性交
;	ACT:騎乗位
;ACT分類:4:羞恥
;	ACT:自慰/放尿
;ACT分類:5:奉仕
;	ACT:手で愛撫を強制/口で愛撫を強制/パイズリ/素股/足舐め強制/胸愛撫強制
;ACT分類:6:加虐
;	ACT:罵倒
;ACT分類:7:異常
;	ACT:
;ACT分類:9:休憩
;	ACT:栄養剤を飲ませる
;ACT分類:10:性交奉仕
;	ACT:正常位させる/後背位させる/対面座位させる/背面座位させる/アナルセックスさせる
;-------------------------------------------------
;・REACT派生なし
;-------------------------------------------------
;=================================================
@KOJO_COM_K14_40
;=================================================
;━━━━━━━━━━━━━━━━━━━━━━━━
;COM40, 拒否する
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && !TFLAG:REACT派生
	;=================================================
	;ACT分類が性交
	;=================================================
	IF 口上記入チェック(100) && MENUMATCH(TFLAG:ACT, "性交")
		;-------------------------------------------------
		;無理をした行動
		;-------------------------------------------------
		IF 口上記入チェック(100) && GET_COM_RANK() == "無理をした行動"
			;実はまんざらでもない
			IF COMCOR_POSI()
				PRINTFORMW 「そうやって逆らう事で、私を誘ってる%RAND_SPLIT("のかしら/つもり？")%」
				PRINTFORMW 「ねぇ、本当は嫌じゃないんでしょう？ ほら、正直に言いなさいよ……」
			;実は嫌
			ELSE
				PRINTFORMW 「なによ、嫌なの！？ シたくないって訳？」
				PRINTFORMW 「ホント%CNAME(MASTER, TARGET)%って気紛れで、面倒臭い人ね？」
				PRINTFORMW 「ま、私も人のこと言えないけど…」
			ENDIF
			RETURN 1
		ENDIF
		
		;---------------------
		;その他
		;---------------------
		SELECTCASE RANDCOM(2, 0)
			CASE 0
				PRINTFORMW 「この期に及んで拒否するとか、いい度胸してるわね…」
				PRINTFORMW 「許し難いわ、%CNAME(MASTER, TARGET)%……」
			CASE 1
				PRINTFORMW 「そうやって、あくまでも拒否するつもりなら…」
				PRINTFORMW 「私も考えなくてはいけないわね……」
		ENDSELECT
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類が羞恥
	;=================================================
	IF 口上記入チェック(100) && MENUMATCH(TFLAG:ACT, "羞恥")
		;-------------------------------------------------
		;無理をした行動
		;-------------------------------------------------
		IF 口上記入チェック(100) && GET_COM_RANK() == "無理をした行動"
			;実はまんざらでもない
			IF COMCOR_POSI()
				PRINTFORMW 「ただ拒否するにしては、迷いがあるようにみえるんだけど」
				PRINTFORMW 「本当のとこはどうなのかしら。ねぇ？」
			;実は嫌
			ELSE
				PRINTFORMW 「ま、そうよねぇ。こんな恥ずかしい事、普通は断るわよね」
				PRINTFORMW 「でも私…、%CNAME(MASTER, TARGET)%の意思を尊重するつもりは無いのよ」
			ENDIF
			RETURN 1
		ENDIF
		
		;---------------------
		;その他
		;---------------------
		;無理のない行動
		IF TFLAG:REACT負荷
			PRINTFORMW 「へぇ、そういう態度するわけね。結構なことで」
			PRINTFORMW 「でも%CNAME(MASTER, TARGET)%…、それなりの覚悟はできてるんでしょうね……」
		;自然な行動
		ELSE
			PRINTFORMW 「どうあっても私の言うこと聞くつもりはなさそうね」
			PRINTFORMW 「見上げた度胸だけど…、%CNAME(MASTER, TARGET)%の為にならないわよ。そういうの」
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;無理をした行動
	;=================================================
	IF 口上記入チェック(100) && GET_COM_RANK() == "無理をした行動"
		IF COMCOR_POSI()
			SELECTCASE RANDCOM(2, 10)
				CASE 0
					PRINTFORMW 「へぇ、そう…、嫌なの。でもおかしいわね？」
					PRINTFORMW 「そうやって拒絶する事自体、%CNAME(MASTER, TARGET)%にとっては辛そうに見えるんだけど」
					PRINTFORMW 「私の気のせい？ じゃ、ないわよねぇ。フフフ…」
				CASE 1
					PRINTFORMW 「迷った末の拒絶、ねぇ。一つ言っておくけど……」
					PRINTFORMW 「心の内がどうであれ、%CNAME(MASTER, TARGET)%が拒絶したって事実に変わりはないのよ」
					CALL FADEIN(8, "「面白くないわ、本当に……」")
					WAIT
			ENDSELECT
		ELSE
			SELECTCASE RANDCOM(2, 11)
				CASE 0
					PRINTFORMW 「あらあら、随分余裕がないわねぇ。それが%CNAME(MASTER, TARGET)%の精一杯だったのかしら」
					PRINTFORMW 「そんなに隙をみせてると、相手に付け込まれるわよ…」
					PRINTFORMW 「と言ったところで、もう手遅れなのよね。残念だけど」
				CASE 1
					PRINTFORMW 「自分から拒否したくせにおどおどしちゃって…」
					PRINTFORMW 「%CNAME(MASTER, TARGET)%…、一体何なのよ\@ TENSION(TARGET, 7) == "高い" ? ！？ # …… \@」
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	IF RAND:3
		LOCALS = \@ T_COND("怒り") ? ！ # \@
		;無理のない行動
		IF TFLAG:REACT負荷
			;BASE:7, 苛立ち派生
			SELECTCASE TENSION(TARGET, 7)
				CASE "高い"
					PRINTFORMW 「%CNAME(MASTER, TARGET)%…、私をイラつかせる才能があるわね」
					PRINTFORMW 「本当、許し難いわ…%LOCALS%」
				CASE "一般"
					PRINTFORMW 「少しは自分の立場を弁えて欲しいものだけど」
					PRINTFORMW 「気に入らないわ…%LOCALS%」
				CASE "低い"
					PRINTFORMW 「そう…、私の言う事が聞けないっていうのね」
					PRINTFORMW 「生意気だわ…%LOCALS%」
			ENDSELECT
		;自然な行動
		ELSE
			SELECTCASE TENSION(TARGET, 7)
				CASE "高い"
					PRINTFORMW 「へぇ？ 私の要求を拒絶したからには、それなりの覚悟はできてるんでしょうねぇ」
					PRINTFORMW 「今に思い知らせてやるから……」
				CASE "一般"
					PRINTFORMW 「身の程も弁えず、調子に乗ってると後悔する事になるわよ」
					PRINTFORMW 「%CNAME(MASTER, TARGET)%、人形の癖に生意気なのよ…%LOCALS%」
				CASE "低い"
					PRINTFORMW 「この私にそんな態度とるなんて、いい度胸ね。でも…」
					PRINTFORMW 「調子に乗ってると、痛い目に遭わせるから…」
			ENDSELECT
		ENDIF
	ELSE
		IF 0
		ELSEIF (AFFECTION("相思相愛") || CFLAG:TARGET:依存度 > CFLAG:MASTER:依存度) && !T_COND("怒り")
			PRINTFORMW 「なによそれ… 一体どうしちゃったのよ…」
			PRINTFORMW 「ねぇ、私の事嫌いになちゃったの…？」
			PRINTFORMDW アリスは涙を堪えながら、肩を震わせている…
		ELSEIF AFFECTION("溺愛") && !T_COND("怒り")
			PRINTFORMW 「なによ、それ…」
			PRINTFORMW 「一体どうしちゃったのよ…」
		ELSEIF AFFECTION("眷愛") && !T_COND("怒り")
			PRINTFORMW 「私の言う事が聞けないっていうの…？」
			PRINTFORMW 「一体どういうつもりなのか、はっきり聞かせて貰おうじゃない」
		ELSEIF AFFECTION("嫌悪") || (!ALI(0) && !RAND:3) || T_COND("怒り")
			PRINTFORMW 「本当に役立たずな駄犬ね！」
			PRINTFORMW 「私にそんな態度をとった事、後悔させてあげるから……」
		ELSE
			PRINTFORMW 「……使えないわね。お仕置きされたいのかしら…？」
		ENDIF
	ENDIF
	RETURN 1
ENDIF

;──────────────────────────────────
;COM41, 消極的やる
;──────────────────────────────────
;・REACT分類:消極的従う
;・REACT派生1は"消極的なのが気に入らない"となりお仕置きポイント追加
;・REACT印象:好印象小
;-------------------------------------------------
;・TFLAG:REACT派生 == 0, 消極的だけどいいか
;・依存度変化(-3, +2)
;-------------------------------------------------
;・TFLAG:REACT派生 == 1, 消極的なのが気に入らない
;・依存度変化(-5, 0)
;-------------------------------------------------
;トリガーする行動や状況…
;ACT分類:0:会話
;	ACT:衣装の変更を要求する
;ACT分類:1:愛撫
;	ACT:
;ACT分類:2:道具
;	ACT:
;ACT分類:3:性交
;	ACT:騎乗位
;ACT分類:4:羞恥
;	ACT:自慰/放尿
;ACT分類:5:奉仕
;	ACT:手で愛撫を強制/口で愛撫を強制/パイズリ/素股/足舐め強制/イラマチオ/胸愛撫強制
;ACT分類:6:加虐
;	ACT:
;ACT分類:7:異常
;	ACT:
;ACT分類:9:休憩
;	ACT:
;ACT分類:10:性交奉仕
;	ACT:正常位させる/後背位させる/対面座位させる/後背位させる/アナルセックスさせる
;-------------------------------------------------
;=================================================
@KOJO_COM_K14_41
;=================================================
;━━━━━━━━━━━━━━━━━━━━━━━━
;COM41_0 & 1, 消極的やる
;━━━━━━━━━━━━━━━━━━━━━━━━
;早い段階から選択可能になる可能性が高く、進展した後は負荷2で選択肢になる場合もあったり
;ある程度注力すべき箇所であるように思う。という訳でちょっと真面目にやってみる
;-------------------------------------------------
IF 口上記入チェック(100) && GROUPMATCH(TFLAG:REACT派生, 0, 1) > 0
	;=================================================
	;ACT分類が性交		騎乗位は弾く
	;=================================================
	IF 口上記入チェック(100) && MENUMATCH(TFLAG:ACT, "性交") && SEX_INITIATIVE(MASTER)
		;-------------------------------------------------
		;無理をした行動
		;-------------------------------------------------
		IF 口上記入チェック(100) && GET_COM_RANK() == "無理をした行動"
			;実はまんざらでもない
			IF COMCOR_POSI()
				SELECTCASE RANDCOM(3, 0)
					CASE 0
						PRINTFORMW 「なによそれ、%RAND_SPLIT("じれったい/もどかしい")%わね。少しは自分で動きなさいよ」
						PRINTFORMW 「ほんと、\@ TFLAG:REACT派生 ? 使えないったらないわね！ # 興ざめもいいとこね \@……」
					CASE 1
						PRINTFORMW 「退屈でツマラナイわね…。%CNAME(MASTER, TARGET)%みたいな人は"dead fish"って言うらしいわ」
						PRINTFORMW 「誰が考えたのか知らないけど、%CNAME(MASTER, TARGET)%にはピッタリの言葉ね…\@ TFLAG:REACT派生 ? ！ # \@」
					CASE 2
						PRINTFORMW 「は？ なんなのよ、それ。%CNAME(MASTER, TARGET)%、全部私にやらせるつもり！？」
						PRINTFORMW 「マグロ相手のセックスって、%RAND_SPLIT("最高にツマラナイわ/ホント退屈/あり得ないくらい最悪")%\@ TFLAG:REACT派生 ? ね！ # だわ… \@」
				ENDSELECT
			;実は嫌
			ELSE
				SELECTCASE RANDCOM(3, 1)
					CASE 0
						PRINTFORMW 「ふぅん。取りあえず、逆らうつもりはないみたいだけど」
						IF TFLAG:REACT負荷
							PRINTFORMW 「そのやる気のない態度、気に入らない%RAND_SPLIT("わ/のよね/わね")%……」
						ELSE
							PRINTFORMW 「単調でつまらないわね、%CNAME(MASTER, TARGET)%……」
						ENDIF
					CASE 1
						PRINTFORMW 「今更悔しそうな顔したって、滑稽なだけなのにね」
						PRINTFORMW 「もう色んな意味で手遅れなんだから、いい加減\@ TFLAG:REACT派生 ? 諦めなさいよ！ # 素直にしたらどうなの？ \@」
					CASE 2
						PRINTFORMW 「逆らうつもりはないけど、積極的に従うつもりも無いってわけ？」
						PRINTFORMW 「ホント退屈ね、%CNAME(MASTER, TARGET)%…。\@ TFLAG:REACT派生 ? 面白くないわ…… # もう少しなんとかならないのかしら \@」
				ENDSELECT
			ENDIF
			RETURN 1
		ENDIF
		
		;---------------------
		;その他
		;---------------------
		;無理のない行動
		IF TFLAG:REACT負荷
			SELECTCASE RANDCOM(2, 2)
				CASE 0
					PRINTFORMW 「う～ん、まぁ…。大人しく、言う通りにするのは結構なんだけど…」
					IF TFLAG:REACT派生
						PRINTFORMW 「%CNAME(MASTER, TARGET)%がそんな様子じゃ、全然愉しくないのよ…」
					ELSE
						PRINTFORMW 「そんなんじゃ、退屈過ぎて欠伸がでるわ。勘弁してくれないかしら」
					ENDIF
				CASE 1
					PRINTFORMW 「気の無い動きねぇ。単調過ぎて嫌になってくるわ」
					PRINTFORMW 「特別なにかを期待してた訳じゃないけど、流石に\@ TFLAG:REACT派生 ? お粗末すぎるわよ！ # \@ちょっと。ねぇ…」
			ENDSELECT
		;自然な行動
		ELSE
			SELECTCASE RANDCOM(2, 3)
				CASE 0
					PRINTFORMW 「逆らえないから仕方なくって感じがアリアリで、ホント白けちゃうわ」
					PRINTFORMW 「『もっと情熱的に』なんて言うつもりはないけど、もう少しなんとかならないの\@ TFLAG:REACT派生 ? ！ # \@？」
				CASE 1
					PRINTFORMW 「取りあえず、私の機嫌を損ねない程度にって感じよね？」
					PRINTFORMW 「半端なことされても、じれったくて…、イライラする\@ TFLAG:REACT派生 ? のよ！ # わ… \@」
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類が羞恥
	;=================================================
	IF 口上記入チェック(100) && MENUMATCH(TFLAG:ACT, "羞恥")
		LOCALS = \@ AFFECTION("溺愛") ? %RAND_SPLIT("ふふ/くすくす/ふふっ")% # %RAND_SPLIT("ウフフ/フフフ/フフ")% \@
		;ACTを決めうちしてしまう
		IF IS_NOWACTNAME("自慰/放尿")
			;FADEの処理を纏めたかったので刻まずに派生処理する
			SELECTCASE GET_COM_RANK()
				CASE "自然な行動"
					;派生：消極的なのが気に入らない
					IF TFLAG:REACT派生
						PRINTFORMW 「大人しく従うのは結構だけど…」
						PRINTFORMW 「%CNAME(MASTER, TARGET)%、消極的すぎるのよ。気に入らないわ…」
					;派生：消極的だけどいいか
					ELSE
						SELECTCASE RANDCOM(3, 10)
							CASE 0
								PRINTFORMW 「悪くない眺めね。その『強要されて仕方なく』って感じもとってもいいわよ」
								PRINTFORMW 「%CNAME(MASTER, TARGET)%には、もっと色んな事させたくなっちゃうわ」
							CASE 1
								PRINTFORMW 「『逆らえないから仕方なく』って感じも悪くないわね」
								PRINTFORMW 「どうせ恥ずかしがったところで手遅れなんだから。もっとよく見えるようにして頂戴」
							CASE 2
								PRINTFORMW 「流石の%CNAME(MASTER, TARGET)%でも、こういうのは気乗りしないのかしら？」
								PRINTFORMW 「ま、そういう事も含めて…。私はじっくり鑑賞させて貰うことにするわ」
						ENDSELECT
					ENDIF
				CASE "無理のない行動"
					;派生：消極的なのが気に入らない
					IF TFLAG:REACT派生
						PRINTFORMW 「ねぇ、言っとくけど…。言う通りにしてれば良いってものじゃないのよ」
						PRINTFORMW 「その、言われた事だけ最低限って感じ…。気に食わないわ」
					;派生：消極的だけどいいか
					ELSE
						SELECTCASE RANDCOM(3, 11)
							CASE 0
								PRINTFORMW 「流石にあまり気乗りはしないみたいね」
								PRINTFORMW 「ま、そうよね。こんな恥ずかしい事、人前ではしたくないわよね？」
							CASE 1
								PRINTFORMW 「どうせなら、もっと堂々とすればいいのに。ってコレ」
								PRINTFORMW 「そういう問題じゃ、ないわよねぇ」
							CASE 2
								PRINTFORMW 「そんなんじゃ駄目よ。もっと私に良く見えるようにして頂戴」
								PRINTFORMW 「だって…、ねぇ？ 折角の機会なんだから」
						ENDSELECT
					ENDIF
				CASE "無理をした行動"
					;実はまんざらでもない
					IF COMCOR_POSI()
						;派生：消極的なのが気に入らない
						IF TFLAG:REACT派生
							PRINTFORMW 「……まったく。駄目ね、%CNAME(MASTER, TARGET)%…」
							PRINTFORMW 「半端なことされても、見てる方は面白くないのよ」
						;派生：消極的だけどいいか
						ELSE
							SELECTCASE RANDCOM(2, 12)
								CASE 0
									PRINTFORMW 「気が進まないようだけど、逆らうまでには至らない。って感じな訳？」
									PRINTFORMW 「でもまぁ…。結局、私に従うなら同じことよね」
								CASE 1
									PRINTFORMW 「あら、いいわねぇ。あり得ないくらい無様で、%CNAME(MASTER, TARGET)%には良くお似合いよ」
									PRINTFORMW 「ま、こんな恥ずかしい事…、積極的にはできないわよね」
							ENDSELECT
						ENDIF
					;実は嫌
					ELSE
						;派生：消極的なのが気に入らない
						IF TFLAG:REACT派生
							PRINTFORMW 「なによ……、悔しそうな顔しちゃって。文句があるなら逆らってみなさいよ」
							PRINTFORMW 「そんな半端なことされても、面白くもなんともないのよ！」
						;派生：消極的だけどいいか
						ELSE
							SELECTCASE RANDCOM(2, 13)
								CASE 0
									PRINTFORMW 「悔しそうな顔したところで、結局は私の言うこと聞くのね？」
									PRINTFORMW 「ふふ…本当に情けない姿。見てる方が恥ずかしくなるわ」
								CASE 1
									PRINTFORMW 「ふふ…、いい表情。こんな事を強要されるのは、さぞかし悔しいでしょうねぇ」
									PRINTFORMW 「こんな恥ずかしい事、本来…、人前ですることじゃないし。ね」
							ENDSELECT
						ENDIF
					ENDIF
			ENDSELECT
			;調教者のBASE:満足が50%以上の場合かつ派生0の場合
			IF TENSION(TARGET, 8) == "高い" && !TFLAG:REACT派生
				;フェードアウト
				CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"「%LOCALS%…」")
				;コンフィグを参照
				SIF ANIME_CONFIG("FADE")
					;真なら行削除する
					CLEARLINE 1
			ENDIF
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類が奉仕
	;=================================================
	IF 口上記入チェック(100) && MENUMATCH(TFLAG:ACT, "奉仕")
		LOCALS = \@ TALENT:MASTER:オトコ ? 犬 # メス犬 \@
		;ACTが口による奉仕である場合		関数による処理を終端で纏めたかったので刻まない。というか刻んで記述したものをあとから統合した
		IF IS_NOWACTNAME("口で愛撫を強制/足舐め強制/イラマチオ") || (IS_NOWACTNAME("胸愛撫強制") && TFLAG:ACT派生 == 2)
			SELECTCASE GET_COM_RANK()
				CASE "自然な行動"
					;派生：消極的なのが気に入らない
					IF TFLAG:REACT派生
						PRINTFORMW 「これまた随分と%RAND_SPLIT("熱のこもってない/おざなりな")%奉仕だこと」
						PRINTFORMW 「%CNAME(MASTER, TARGET)%…、%RAND_SPLIT("私を怒らせたいの？/その程度で誤魔化せると思ったら大間違いよ")%」
					;派生：消極的だけどいいか
					ELSE
						PRINTFORMW 「ふふ、そうそう。大人しく従ってればいいのよ」
						PRINTFORMW 「ご主人様に\@ IS_NOWACTNAME("足舐め強制") ? 舌で # 口を使って \@奉仕できるなんて、%CNAME(MASTER, TARGET)%のような%LOCALS%にとっては、この上ない幸せでしょう？」
						;BASE:8 満足
						PRINTFORMW 「例えどうしようもない駄犬であったとしても、ね\@ TENSION(TARGET, 8) == "高い" ? ぇ # … \@」
					ENDIF
				CASE "無理のない行動"
					PRINTFORMW 「イマイチね、%CNAME(MASTER, TARGET)%…」
					PRINTFORMW 「%LOCALS%の癖に舌使いもなってないし、退屈極まりないわ\@ TFLAG:REACT派生 ? ！ # ね…… \@」
				CASE "無理をした行動"
					;実はまんざらでもない
					IF COMCOR_POSI()
						PRINTFORMW 「飼い犬なら、もっと喜んでご主人様に奉仕すべきじゃないの\@ TFLAG:REACT派生 ? ！ # \@？」
						;派生：消極的なのが気に入らない
						IF TFLAG:REACT派生
							PRINTFORMW 「%CNAME(MASTER, TARGET)%…、%LOCALS%としての自覚が足りないのよ！」
						;派生：消極的だけどいいか
						ELSE
							PRINTFORMW 「もしかして%CNAME(MASTER, TARGET)%…、まだ%LOCALS%としての自覚が足りないのかしら%RAND_SPLIT("。ねぇ？/")%」
						ENDIF
					;実は嫌
					ELSE
						PRINTFORMW 「ん？ 何よ。まだ人並みのプライドが残ってるのかしら」
						PRINTFORMW 「そんなもの…、もはや一匹の%LOCALS%に過ぎない%CNAME(MASTER, TARGET)%には必要ないはずだけど」
						;派生：消極的なのが気に入らない
						IF TFLAG:REACT派生
							PRINTFORMW 「もう一度、%CNAME(MASTER, TARGET)%の立場を思い知らせてあげる必要がありそうね…」
						;派生：消極的だけどいいか
						ELSE
							PRINTFORMW 「…って。ここまで言われても逆らえない時点で、もうどうしようもない%RAND_SPLIT("感じ/わ")%ねぇ？ \@ TENSION(TARGET, 8) == "高い" ? あははっ！ # ウフフ… \@」
						ENDIF
					ENDIF
			ENDSELECT
		;その他の奉仕系ACT
		ELSE
			SELECTCASE GET_COM_RANK()
				CASE "自然な行動"
					SELECTCASE RANDCOM(2, 20)
						CASE 0
							PRINTFORMW 「逆らえないから仕方なくって感じね。%RAND_SPLIT("つまらない/面白くない")%わ」
							;派生：消極的なのが気に入らない
							IF TFLAG:REACT派生
								PRINTFORMW 「一度%CNAME(MASTER, TARGET)%を躾けなおす必要がありそうね……」
							;派生：消極的だけどいいか
							ELSE
								PRINTFORMW 「もう少し愉しませてくれると思ったんだけど」
							ENDIF
						CASE 1
							;BASE:7 苛立ち
							PRINTFORMW 「\@ TENSION(TARGET, 7) == "高い" ? はぁ！？ # \@なによ、随分消極的じゃない」
							;派生：消極的なのが気に入らない
							IF TFLAG:REACT派生
								PRINTFORMW 「%CNAME(MASTER, TARGET)%…、私を怒らせるつもり？」
							;派生：消極的だけどいいか
							ELSE
								PRINTFORMW 「%RAND_SPLIT("特/別")%に期待してたって訳じゃないけど、だからと言ってお粗末過ぎるのも、ねぇ…」
							ENDIF
					ENDSELECT
				CASE "無理のない行動"
					SELECTCASE RANDCOM(2, 21)
						CASE 0
							PRINTFORMW 「言われたことだけ最低限とか…、%RAND_SPLIT("馬鹿/愚か者")%の考えそうなことね」
							;派生：消極的なのが気に入らない
							IF TFLAG:REACT派生
								PRINTFORMW 「私、大嫌いなのよね。そういうの……」
							;派生：消極的だけどいいか
							ELSE
								PRINTFORMW 「退屈なのよ、%CNAME(MASTER, TARGET)%……」
							ENDIF
						CASE 1
							PRINTFORMW 「%CNAME(MASTER, TARGET)%…、言われたことだけやってれば良いとか思ってる？」
							PRINTFORMW 「\@ TFLAG:REACT派生 ? 気に入らない # あまり感心しない \@わね、そういうの……」
					ENDSELECT
				CASE "無理をした行動"
					;実はまんざらでもない
					IF COMCOR_POSI()
						PRINTFORMW 「本当…、じれったいわね。もう少し何とかならないの？」
						PRINTFORMW 「退屈すぎて、\@ TFLAG:REACT派生 ? 頭にくるのよ…！ # 嫌になってくるわ… \@」
					;実は嫌
					ELSE
						PRINTFORMW 「そんな嫌々ながらにされても困る%RAND_SPLIT("のよね/わ/わね")%…」
						;派生：消極的なのが気に入らない
						IF TFLAG:REACT派生
							PRINTFORMW 「――前言撤回。困ると言うより、気に入らないのよね。そういう態度…」
						;派生：消極的だけどいいか
						ELSE
							PRINTFORM 「ま、%CNAME(MASTER, TARGET)%に何かを強要する事自体は、悪くない気分だけど
							CALL PRINT_SYMBOL_K14
						ENDIF
					ENDIF
			ENDSELECT
		ENDIF
		CALL PRINT_HALFHEARTED_K14
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類が性奉
	;=================================================
	;IF 口上記入チェック(0) && MENUMATCH(TFLAG:ACT, "性奉")
	;ENDIF
	
	;-------------------------
	;汎用処理
	;-------------------------
	LOCALS = \@ TFLAG:REACT派生 == 1 || TENSION(TARGET, 7) == "高い" || T_COND("怒り") ? ！ # \@
	;派生：消極的なのが気に入らない（お仕置きポイント加算）
	IF TFLAG:REACT派生
		IF AFFECTION("相思相愛")
			SELECTCASE RANDCOM (2, 30)
				CASE 0
					PRINTFORMW 「なにそれ…。わざわざ無理してまで消極的にする理由な何なのよ\@ T_COND("怒り") ? ！ # \@」
					PRINTFORMW 「面白くないわね、ハッキリ言って…」
				CASE 1
					PRINTFORMW 「やる気のない素振りをしたって…、本当は辛いんじゃないの？」
					PRINTFORMW 「そもそも、そんな事する意味が判らないわね\@ T_COND("怒り") ? ！ # \@」
			ENDSELECT
		ELSE
			;BASE:8 満足派生
			SELECTCASE TENSION(TARGET, 8)
				CASE "高い"
					PRINTFORMW 「なによ、それ…。半端な事されても、じれったいだけなんだけど…」
					PRINTFORMW 「私を怒らせるつもりじゃないなら、もう少し考えた方がいいんじゃない？」
					PRINTFORMW 「尤も…、今更謝ったって、ただで済ますつもりはないんだけど。ね…」
				CASE "一般"
					PRINTFORMW 「そのやる気のなさそうな顔見てるとうんざりするわ…」
					PRINTFORMW 「ホント、もどかしいったらないわね！」
				CASE "低い"
					PRINTFORMW 「中途半端でもどかしいわね…」
					PRINTFORMW 「%CNAME(MASTER, TARGET)%…、私を怒らせたいのかしら」
			ENDSELECT
		ENDIF
	;派生：消極的だけどいいか
	ELSE
		;2/3
		IF RAND:3
			SELECTCASE GET_COM_RANK()
				CASE "自然な行動"
					PRINTFORMW 「ただ従ってれば良いってものじゃないんだけど。%CNAME(MASTER, TARGET)%、判ってる%LOCALS%？」
					PRINTFORMW 「兎も角…、こんなのは不満なの。面白くないのよ%LOCALS%」
				CASE "無理のない行動"
					PRINTFORMW 「…ったく。幾らなんでも消極的すぎない%LOCALS%？」
					PRINTFORMW 「フザケルのも大概にして頂戴%LOCALS%」
				CASE "無理をした行動"
					;実はまんざらでもない
					IF COMCOR_POSI()
						PRINTFORMW 「…まったく。%CNAME(MASTER, TARGET)%…、一々わたしに指図されないと何もできないの？」
						PRINTFORMW 「そういう事じゃ困る\@ AFFECTION("溺愛") ? んですけど？ # のよね… \@」
					;実は嫌
					ELSE
						PRINTFORMW 「逆らわず、私に従うのは結構なことだけど…」
						PRINTFORMW 「半端なことされても、ね……」
					ENDIF
			ENDSELECT
		;1/3
		ELSE
			IF 0
			ELSEIF AFFECTION("相思相愛") && !T_COND("怒り")
				PRINTFORMW 「ねぇ、なによそれ。どうしちゃったのよ…」
				PRINTFORMW 「私、そんなのじゃ嫌よ…」
			ELSEIF AFFECTION("溺愛") && !T_COND("怒り")
				PRINTFORMW 「なによ、どうしたのよ…」
				PRINTFORMW 「ねぇ、もっと真面目にやりなさいよ…」
			ELSEIF AFFECTION("眷愛") && !T_COND("怒り")
				PRINTFORMW 「随分消極的ね。体調でも悪いのかしら…？」
			ELSEIF AFFECTION("嫌悪") || T_COND("怒り") || TENSION(TARGET, 7) == "高い"
				PRINTFORMW 「RAND_SPLIT("ホント/ほんと/本当、")使えない%RAND_SPLIT("駄犬/犬/豚")%ね…%LOCALS%」
			ELSE
				PRINTFORMW 「%RANDTEXT_K14("つまらない", "ツマラナイ", "使えない")%わね、%CNAME(MASTER, TARGET)%…」
			ENDIF
		ENDIF
	ENDIF
	CALL PRINT_HALFHEARTED_K14
	RETURN 1
ENDIF

;──────────────────────────────────
;COM42, 積極的やる
;──────────────────────────────────
;・REACT分類:積極的従う
;・REACT印象:好印象大
;・我慢フラグ解消
;-------------------------------------------------
;トリガーする行動や状況…
;ACT分類:0:会話
;	ACT:衣装の変更を要求する
;ACT分類:1:愛撫
;	ACT:
;ACT分類:2:道具
;	ACT:
;ACT分類:3:性交
;	ACT:騎乗位
;ACT分類:4:羞恥
;	ACT:自慰/放尿
;ACT分類:5:奉仕
;	ACT:手で愛撫を強制/口で愛撫を強制/パイズリ/素股/足舐め強制/イラマチオ/胸愛撫強制
;ACT分類:6:加虐
;	ACT:
;ACT分類:7:異常
;	ACT:
;ACT分類:9:休憩
;	ACT:
;ACT分類:10:性交奉仕
;	ACT:正常位させる/後背位させる/対面座位させる/後背位させる/アナルセックスさせる
;-------------------------------------------------
;・REACT派生なし
;-------------------------------------------------
;=================================================
@KOJO_COM_K14_42
;=================================================
;会話系はREACTで完結させているので枠を設けない
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;COM42, 積極的やる
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100)
	;試験運用中…
	;=================================================
	;無理をした行動
	;=================================================
	IF 口上記入チェック(100) && (GET_COM_RANK() == "無理をした行動" && (!TCVAR:イきそう || PALAM:快Ｖ < 8500 || PALAM:快Ａ < 8500 || PALAM:快Ｂ < 8500))
		SELECTCASE TENSION(TARGET, 8)
			CASE "高い"
				PRINTFORMW 「無理してるのがみえみえよ。まぁでも…」
				PRINTFORM 「その姿勢は評価すべきかしら、ね
				CALL PRINT_SYMBOL_K14
			CASE "一般"
				PRINTFORMW 「あらあら、頑張っちゃって健気なこと。まぁ悪い気はしないけど…」
				PRINTFORMW 「\@ AFFECTION("溺愛") ? ご褒美が欲しいなら # それなりの扱いを受けたいなら \@、もっと頑張る事ね」
				SIF AFFECTION("溺愛")
					PRINTFORMW 「期待しているわ。ふふっ…」
			CASE "低い"
				PRINTFORMW 「必死に尻尾ふっちゃって……、%CNAME(MASTER, TARGET)%にはプライドがないのかしら」
				PRINTFORMW 「ま、私は別に構わないわよ？ それなりの、%CNAME(MASTER, TARGET)%に相応しい扱いをしてあげるから」
				CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), "「フフフ…」")
				CLEARLINE 1
		ENDSELECT
		RETURN 1
	ENDIF
	
	;騎乗位はMENUMATCHで拾う可能性があるので、条件追加して弾く
	;クソ長ったらしい条件は望ましくないけど、枠を新設するのも面倒なので妥協する
	;=================================================
	;奉仕
	;=================================================
	IF 口上記入チェック(100) && MENUMATCH(TFLAG:ACT, "奉仕") && (!IS_NOWACTNAME("騎乗位") || TEQUIP:性交中 != 3) && (SOURCE:快Ｃ + SOURCE:快Ｖ + SOURCE:快Ａ + SOURCE:快Ｂ > 10)
		;書いたものは最後まで機能してもらう構え
		IF AFFECTION("相思相愛") && PERCENT(80)
			SELECTCASE RANDCOM(2, 0)
				CASE 0
					PRINTFORM 「ふぁぁっ
					CALL AUTOHEARTCB_K14
					PRINTFORMW 　や、やだっ！ ソコ、弱いのに……」
					PRINTFORMW 「んっ…、あ…あっ！ こんなに、されたら…ッ！」
				CASE 1
					PRINTFORM 「あんっ
					CALL AUTOHEARTCB_K14
					PRINTFORMW 　んあっ、はぁんっ…、んくっ…！」 
					PRINTFORM 「いいわ……、もっと、もっとっ…あぁ…ッ
					CALL PRINT_SYMBOL2_K14
			ENDSELECT
		ELSEIF AFFECTION("溺愛") && PERCENT(70)
			PRINTFORMW 「あっ、んんっ！ なかなか、良い感じよ……」
			PRINTFORMW 「…んぁっ！ や…、そんな、とこまで……」
		ELSE
			SELECTCASE RANDCOM(2, 1)
				CASE 0
					PRINTFORMW 「んんっ…！ まぁまぁって感じね…」
					PRINTFORMW 「いつもこの調子でお願いしたいものだわ……」
				CASE 1
					PRINTFORMW 「ん…、んんっ！ 悪く、ないわね…」
					PRINTFORMW 「ほら、もっとお願い…」
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;性交の騎乗位は奉仕系反応が出る。枠新設が面倒なんでここに落ちて貰う
	;=================================================
	;性交奉仕＆騎乗位（性交）
	;=================================================
	IF 口上記入チェック(100) && ((MENUMATCH(TFLAG:ACT, "性奉") || IS_NOWACTNAME("騎乗位") || TEQUIP:性交奉仕中 || TEQUIP:性交中 == 3) && (SOURCE:快Ｃ + SOURCE:快Ｖ + SOURCE:快Ａ + SOURCE:快Ｂ > 10))
		IF TEQUIP:性交中 == 3 || IS_NOWACTNAME("騎乗位")
			PRINTFORM 「ふふ、いい感じよ。今の%CNAME(MASTER, TARGET)%、とっても可愛いわ…
			CALL PRINT_SYMBOL_K14
			PRINTFORMW 「ほら、もっといい声を聴かせて頂戴…」
		ELSE
			SELECTCASE RANDCOM(2, 2)
				CASE 0
					PRINTFORM 「うっ…、あぁっ
					CALL AUTOHEARTCB_K14
					PRINTFORMW 　だ、だめぇっ！ もっと、ゆっくり……」
					PRINTFORMW 「ひっ……、あっ！ あぁっ！ んっ…、んくっ！」
				CASE 1
					PRINTFORM 「あんっ
					CALL AUTOHEARTCB_K14
					PRINTFORMW 　んあっ！ はぁんっ……、んっ！」
					PRINTFORM 「いいわ……、もっと、もっとっ！ あぁ…ッ
					CALL PRINT_SYMBOL2_K14
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;-------------------------
	;汎用処理
	;-------------------------
	IF AFFECTION("相思相愛")
		SELECTCASE RANDCOM(2, 3)
			CASE 0
				PRINTFORMW 「ま、そうよね。その位はして貰わないとね」
				PRINTFORMW 「別に褒めてるワケじゃないわよ？ この程度の事、当然だと思うし…ねぇ？」
				CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), "「くすくす…」")
				CLEARLINE 1
			CASE 1
				PRINTFORMW 「すっかり飼いならされちゃって……、って言うのは禁句かしら？ ふふっ」
				PRINTFORMW 「でもそうね。%CNAME(MASTER, TARGET)%が素直なら、私も嬉しいのよ？」
				PRINTFORM 「こう見えたって、ね…
				CALL PRINT_SYMBOL_K14
		ENDSELECT
	ELSE
		SELECTCASE RANDCOM(2, 4)
			CASE 0
				PRINTFORMW 「あら、随分積極的じゃない？」
				PRINTFORMW 「ま、これはこれで…、悪くないわ。%CNAME(MASTER, TARGET)%の本心は知らないけど、ね」
			CASE 1
				PRINTFORMW 「とりあえず、素直に私に従うのはいい傾向と言えるかしら？」
				PRINTFORM 「いつもこんな感じだと、私も楽できるんだけど…、ね
				CALL PRINT_SYMBOL_K14
		ENDSELECT
	ENDIF
	RETURN 1
ENDIF

;──────────────────────────────────
;COM43, 愛情を込めてやる
;──────────────────────────────────
;・一定以上の好感度、調教レベルが必要
;・REACT分類:積極的従う
;・REACT印象:好印象大
;・我慢フラグ解消
;・反発刻印があると弾かれる
;・依存度変化(+6, +10)
;-------------------------------------------------
;トリガーする行動や状況…
;ACT分類:0:会話
;	ACT:
;ACT分類:1:愛撫
;	ACT:
;ACT分類:2:道具
;	ACT:
;ACT分類:3:性交
;	ACT:騎乗位
;ACT分類:4:羞恥
;	ACT:
;ACT分類:5:奉仕
;	ACT:手で愛撫を強制/口で愛撫を強制/パイズリ/素股/足舐め強制/イラマチオ/胸愛撫強制
;ACT分類:6:加虐
;	ACT:
;ACT分類:7:異常
;	ACT:
;ACT分類:9:休憩
;	ACT:
;ACT分類:10:性交奉仕
;	ACT:正常位させる/後背位させる/対面座位させる/後背位させる/アナルセックスさせる
;-------------------------------------------------
;=================================================
@KOJO_COM_K14_43
;=================================================
;━━━━━━━━━━━━━━━━━━━━━━━━
;COM43, 愛情を込めてやる
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && !TFLAG:REACT派生
	;=================================================
	;無理をした行動		性奉時は負荷2以下の選択肢になる事が殆どないので弾いておく。奉仕系ならそうでもないんだけど
	;=================================================
	IF 口上記入チェック(100) && GET_COM_RANK() == "無理をした行動" && !TEQUIP:性交奉仕中
		SELECTCASE RANDCOM(2, 0)
			CASE 0
				PRINTFORMW 「ねぇ%CNAME(MASTER, TARGET)%…、何か無理してない？」
				PRINTFORMW 「そういうのって、微妙に伝わるのよ。ちょっと不愉快だわ」
			CASE 1
				PRINTFORMW 「…ん。ね、悪くないけど…」
				PRINTFORMW 「なんとなく、ちょっと違う気がするのよね。私には判るの」
				PRINTFORMW 「%CNAME(MASTER, TARGET)%、本当は無理してるでしょう…」
		ENDSELECT
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類が性交（騎乗位）
	;=================================================
	IF 口上記入チェック(100) && MENUMATCH(TFLAG:ACT, "性交") && TEQUIP:性交中 == 3
		IF CFLAG:TARGET:依存度 > 4000
			PRINTFORMW 「自分より、私の方を優先してくれるの？ 可愛いわ…」
			PRINTFORMDW %CALLNAME%は%CALLNAME:MASTER%を引き寄せると、優しく抱きしめながらそっと口付けをした…
			PRINTFORM 「ぷは…ぁ…、ふふ、柔らかくって美味し%RAND_SPLIT("い/")%
			CALL PRINT_HEARTCBW_K14(COLOR2_K14("pink"), , , COLOR2_K14("ALICE"), @"　ほら、今度は%CNAME(MASTER, TARGET)%の番よ」")
			PRINTFORMW 「ちゃんと…、私にキス、して……」
			CALL SET_KISS
		ELSE
			PRINTFORMW 「ふふ、私をキモチ良くさせようと頑張っちゃって。健気で可愛い」
			PRINTFORMDW %CALLNAME%は%CALLNAME:MASTER%の手を取るとしっかりと握り締めた…
			PRINTFORMW 「さぁ、いいわよ。%CNAME(MASTER, TARGET)%の可愛いとこ、私に見せて」
			PRINTFORMW 「もっと%CNAME(MASTER, TARGET)%の声、聞かせて……」
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類が奉仕
	;=================================================
	IF 口上記入チェック(100) && MENUMATCH(TFLAG:ACT, "奉仕")
		;-------------------------------------------------
		;ACT足舐め強制
		;-------------------------------------------------
		IF 口上記入チェック(100) && IS_NOWACTNAME("足舐め強制")
			;BASE:8, 満足派生
			SELECTCASE TENSION(TARGET, 8)
				;50%以上
				CASE "高い"
					SELECTCASE RANDCOM(2, 10)
						CASE 0
							PRINTFORM 「%RAND_SPLIT("ッ/ん/あ")%…
							CALL AUTOHEARTCB_K14
							PRINTFORMW 　ずるいわ…、そんなとこまで舐めるなんて」
							PRINTFORM 「%RAND_SPLIT("ひ/ふ")%ぁ
							CALL AUTOHEARTCB_K14
							PRINTFORMW 　もうっ！ だから…、駄目だって、言ってるのに……」
						CASE 1
							PRINTFORMW 「ねぇ、だから…。舐めていいのはふとももまでって言ってるでしょう」
							PRINTFORM 「%RAND_SPLIT("く/ん")%ぁ
							CALL AUTOHEARTCB_K14
							PRINTFORMW 　そんな優しくされたら、怒れなくなるじゃない」
							PRINTFORMW 「ずるいわ、%CNAME(MASTER, TARGET)%…」
					ENDSELECT
				;25%以上
				CASE "一般"
					SELECTCASE RANDCOM(2, 11)
						CASE 0
							PRINTFORMW 「ねぇ、ちょっと。登ってくるのが早すぎない？」
							PRINTFORMW 「舐めていいのはふとももまでだからね。でないと私…」
							PRINTFORMW 「我慢できなくなっちゃうもの。ふふ」
						CASE 1
							PRINTFORMW 「%RAND_SPLIT("んんー/あらぁ")%？ ズルはよくないわよ。許可したのはふとももまでなんだから」
							PRINTFORM 「だって、ねぇ。あまり上まで来られると、私、抑えきれなくなっちゃうもの…
							CALL PRINT_SYMBOL_K14
					ENDSELECT
				;25%未満
				CASE "低い"
					SELECTCASE RANDCOM(3, 12)
						CASE 0
							PRINTFORMW 「あら、随分丁寧に舐めてくれるのね。ふふ」
							PRINTFORM 「こんな風にされるのも、悪くない%RAND_SPLIT("もの/わ")%ね…
							CALL PRINT_SYMBOL_K14
						CASE 1
							PRINTFORMW 「なによ、優しいじゃない。どういう風の吹き回し？」
							PRINTFORMW 「ふふ、%CNAME(MASTER, TARGET)%…。何かご褒美を期待してたり、とか？」
						CASE 2
							PRINTFORMW 「ふふ、上の方が気になる？ でも駄目よ。ふとももまでにしておいて」
							PRINTFORM 「そこから先は、後のお楽しみってね…
							CALL PRINT_SYMBOL_K14
					ENDSELECT
			ENDSELECT
			RETURN 1
		ENDIF
		
		;---------------------
		;その他
		;---------------------
		LOCALS = %RAND_SPLIT("ふ/は/あ/んッ/はぁ/あぁ/ふぁ")%
		;調教者がイきそう
		IF TCVAR:イきそう || PALAM:快Ｖ > 8500 || PALAM:快Ａ > 8500 || PALAM:快Ｂ > 8500
			SELECTCASE RANDCOM(2, 13)
				CASE 0
					PRINTFORM 「%RAND_SPLIT("ふ/は")%……ぁ
					CALL AUTOHEARTCB_K14
					PRINTFORMW 　そんな、優しくされると…わ、私っ！」
					PRINTFORM 「\@ !AFFECTION("相思相愛") ? %CNAME(MASTER, TARGET)%の事…、好きに # 切なくって…、抑えられなく \@なっちゃうっ…
					CALL PRINT_SYMBOL2_K14
				CASE 1
					PRINTFORM 「%LOCALS%…
					CALL AUTOHEARTCB_K14
					PRINTFORMW 　ねぇ…、そんなに優しくしないでよ。私、我慢できなくなっちゃうわ…」
					PRINTFORM 「あ゛っ
					CALL AUTOHEARTCB_K14
					PRINTFORM 　だ、だから！ 駄目だって、言ってるのにぃっ
					CALL PRINT_SYMBOL2_K14
			ENDSELECT
		;その他
		ELSE
			SELECTCASE RANDCOM(2, 14)
				CASE 0
					PRINTFORMW 「な、なによ。随分優しいじゃない」
					PRINTFORMW 「そんな風にされると私…、スイッチ入っちゃう\@ AFFECTION("相思相愛") ? けど、それでもいいの？ # わよ… \@」
				CASE 1
					PRINTFORM 「%LOCALS%…
					CALL AUTOHEARTCB_K14
					PRINTFORMW 　すっかり%CNAME(MASTER, TARGET)%のペースに嵌っちゃって、ちょっと癪ね」
					PRINTFORMW 「でも、こんなに優しくされるなら、それもいいのかも…」
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ACT分類が性奉
	;=================================================
	IF 口上記入チェック(100) && MENUMATCH(TFLAG:ACT, "性奉")
		SELECTCASE GET_ACTNAME(TFLAG:ACT)
			CASE "正常位させる"
				PRINTFORMW 「ん……優しいのね。どういう風の吹き回しか知らないけど。ふふ」
				PRINTFORMW 「ねぇ、キスして。もっと%CNAME(MASTER, TARGET)%の体温、感じ\@ AFFECTION("相思相愛") ? たいの # させて \@……」
				;地の文
				PRINTFORMDW 二人は互いを確かめ合うかのようにしっかりと抱き合い、口付けを交わしながら揺れあった…
				;一行空ける
				PRINTL
				;フェードアウト
				CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"「%KOJO_RAND_K14(("キス"), 1)%」")
				;コンフィグを参照
				IF ANIME_CONFIG("FADE")
					;真なら行削除する
					CLEARLINE 1
				ENDIF
				;相思相愛時80%の確率、もしくは調教者が情欲かつBASE:満足が50%以上の場合に真
				IF (AFFECTION("相思相愛") && PERCENT(80)) || (T_COND("情欲") && TENSION(TARGET, 8) == "高い")
					PRINTFORM 「ん……ぁ
					CALL PRINT_HEARTCBW_K14(COLOR2_K14("pink"), , , COLOR2_K14("ALICE"), @"　すきっ！ 好きなの！ %CNAME(MASTER, TARGET)%のことっ！」")
					
					PRINTFORM 「あんっ、あっ
					CALL AUTOHEARTCB_K14
					PRINTFORM 　お、奥まで…、届いてっ！ %RAND_SPLIT("ん/ふ")%あぁっ…
					CALL PRINT_SYMBOL2_K14
				ELSE
					PRINTFORM 「ん…、ふふ。%CNAME(MASTER, TARGET)%に抱かれながらキスするの、大好き…
					CALL PRINT_SYMBOL_K14
					PRINTFORMW 「ねぇ、ほら。もう一度、キスして……」
				ENDIF
				CALL SET_KISS
			CASE "後背位させる"
				PRINTFORMW 「…っ！ な、なによ…。そんな優しくされたら私、どうにかなっちゃいそうなんだけど…」
				PRINTFORMW 「%CNAME(MASTER, TARGET)%の事…、\@ AFFECTION("相思相愛") ? 好きなキモチが止まらなくなっちゃうわ # 好きになっちゃいそうよ \@…」
				PRINTFORMDW %CALLNAME:MASTER%は快楽を貪るのではなく、さらに優しく、%CALLNAME%を慈しむように抽送を繰り返した…
				;一行空ける
				PRINTL
				;相思相愛時80%の確率、もしくは調教者が情欲かつBASE:満足が50%以上の場合に真
				IF (AFFECTION("相思相愛") && PERCENT(80)) || (T_COND("情欲") && TENSION(TARGET, 8) == "高い")
					CALL QUAKE(4)
					;PS2までで一行
					PRINTFORM 「んっ
					CALL AUTOHEARTCB_K14
					PRINTFORM 　あっ
					CALL AUTOHEARTCB_K14
					PRINTFORM 　あぁぁっ！ ダメっ！ だめぇっ
					CALL PRINT_SYMBOL2_K14
					
					PRINTFORMW 「どんどん%CNAME(MASTER, TARGET)%が……、好きになっちゃうぅっ！！」
				;その他
				ELSE
					PRINTFORM 「ん…
					CALL AUTOHEARTCB_K14
					PRINTFORMW 　いいのよ、もっと激しく……、私を、人形みたいに扱っても」
					
					PRINTFORMW 「私…、%CNAME(MASTER, TARGET)%が相手なら、嫌じゃない、から……」
				ENDIF
			CASE "対面座位させる"
				CALL QUAKE(4)
				;PS2までで一行
				PRINTFORM 「ッ！ ん…
				CALL AUTOHEARTCB_K14
				PRINTFORM 　んっ、あ……あっ
				CALL PRINT_SYMBOL2_K14
				
				;地の文
				PRINTFORMDL %CALLNAME%は%CALLNAME:MASTER%の動きに身を%RAND_SPLIT("委ね/任せ")%ながら、\@ CFLAG:TARGET:依存度 > 4000 ? 揺れる瞳で # \@じっと%CALLNAME:MASTER%を見つめている
				;地の文@カラープリント
				CALL COLORPRINTW(@"__0xC0C0C0__%CALLNAME%は%CALLNAME:MASTER%の頬に両手を這わせると、\@ T_COND("情欲") ? 情熱的に # そっと\@__0x36A6FF__『ちゅ\@ T_COND("情欲") ? ぅっ # っ \@』__0xC0C0C0__口付けをした…")
				;一行空ける
				PRINTL
				;相思相愛、もしくは調教者の状態が情欲かつBASE:満足が50%以上
				IF AFFECTION("相思相愛") && PERCENT(85) || (T_COND("情欲") && TENSION(TARGET, 8) == "高い")
					PRINTFORM 「好き！ すきっ
					CALL AUTOHEARTCB_K14
					PRINTFORM 　好きなの！ 大好き \@ CFLAG:TARGET:依存度 > 4000 ? 大好き！ 大好きっ… # っ…\@
					CALL PRINT_SYMBOL2_K14
					;調教者の依存度5000以上。通常の進展度合いで30日前後での到達を想定
					IF CFLAG:TARGET:依存度 > 4000
						SELECTCASE RANDCOM(2, 20)
							CASE 0
								PRINTFORMW 「好きすぎて……、%RAND_SPLIT("変になっちゃい/頭がおかしくなり")%そうなの…\@ TENSION(TARGET, 8) == "高い" ? っ # \@！」
							CASE 1
								PRINTFORMW 「この私を……、全部\@ TENSION(TARGET, 8) == "高い" ? ！ 全部# \@%CNAME(MASTER, TARGET)%のものにしてっ！」
						ENDSELECT
					ENDIF
					;くどい気もしたので1/2に
					IF !RAND:2
						PRINTFORMDL 普段の澄ました様子からは想像も出来ない、
						PRINTFORMDW ただひたすら\@ CFLAG:TARGET:依存度 > 4000 ? 、狂おしいまでに # \@愛し、愛されたいと願う一人の\@ CFLAG:TARGET:子持ち > 0 ? 女 # 少女 \@の姿が、そこにはあった…
					ENDIF
				;その他
				ELSE
					PRINTFORMW 「ねぇ、私にキスして。もっと強く抱いて\@ T_COND("情欲") ? っ！ # … \@」
					SELECTCASE RANDCOM(2, 21)
						CASE 0
							PRINTFORMW 「もっと激しくったって、私は大丈夫だから…」
						CASE 1
							PRINTFORMW 「あまり優しくされると、不安になるのよ…」
					ENDSELECT
				ENDIF
				CALL SET_KISS
			CASE "背面座位させる"
				CALL QUAKE(4)
				PRINTFORM 「%RAND_SPLIT("ぅ/ん/ふ")%……あ\@ PALAM:快Ｖ > 6000 ? っ # \@
				CALL AUTOHEARTCB_K14
				PRINTFORM 　%RAND_SPLIT("は/あ")%ぅ…、%RAND_SPLIT("ふ/あ/ん")%ぁ
				CALL PRINT_SYMBOL2_K14
				
				PRINTFORMDL %CALLNAME:MASTER%の愛情を込めた動きに感応したのか、%CALLNAME%はいつになく甘えた声で喘いでいる…
				PRINTFORMW 「ねぇもっと！ %RAND_SPLIT("強く……、抱いて/ちゃんと……、ぎゅってして")%\@ TENSION(TARGET, 8) == "高い" ? っ # \@！」
				;一行空ける
				PRINTL
				;地の文2行
				PRINTFORMDL %CALLNAME:MASTER%が背後から抱きしめつつ、身体を密着させながら慈しむような抽送を繰り出すと、
				PRINTFORMDW %CALLNAME%は身体を弓なりに反らしながら、%CALLNAME:MASTER%の動きにその身を任せた…
				IF AFFECTION("相思相愛") && PERCENT(80) || (T_COND("情欲") && TENSION(TARGET, 8) == "高い")
					PRINTFORM 「や、%RAND_SPLIT("嫌/いや/駄目/だめ/ダメ")%っ！ %RAND_SPLIT("おかしく/変に")%なっちゃう！ これ…、怖いくらい……感じちゃうっ
				ELSE
					PRINTFORM 「%RAND_SPLIT("あ/ふ/ん/")%あっ……%RAND_SPLIT("イイ/いい")%っ！ なんで私…、こんなに…感じ、て%RAND_SPLIT("ッ/っ/ぇっ")%
				ENDIF
				CALL PRINT_SYMBOL2_K14
			CASE "アナルセックスさせる"
				;口上の仕様的に、相思相愛以降にしか発生しないので、FAVOR系では分けない
				PRINTFORM 「%RAND_SPLIT("あ/は/ん")%んっ
				CALL AUTOHEARTCB_K14
				PRINTFORMW 　ん……あぁっ！ 随分優しくシてくれるのね…」
				IF ABL:Ａ感覚 >= 4
					PRINTFORMW 「おしりでセックスしてるから、私を気遣ってくれてるの？」
					PRINTFORMW 「私は平気だから、%CNAME(MASTER, TARGET)%が望むだけ激しくしてもいいのよ」
					PRINTFORMW 「%CNAME(MASTER, TARGET)%になら、むしろそうされたいって…、私は思うから……」
				ELSE
					PRINTFORMW 「おしりでするのまだ慣れないけど…。こんな風にされるなら悪くない、かも…」
					PRINTFORMW 「いいわよ、続けて？ 私の全ては、%CNAME(MASTER, TARGET)%のものだから……」
				ENDIF 
			CASE "逆レイプ"
				PRINTFORM 「%RAND_SPLIT("んっ/あっ/はっ/ッ")%…
				CALL AUTOHEARTCB_K14
				PRINTFORM 　私をキモチ良くさせようと\@ CFLAG:MASTER:依存度 > 4500 ? 夢中にな # 頑張 \@っちゃって…、可愛い
				IF AFFECTION("相思相愛") || CFLAG:TARGET:依存度 > 4000
					CALL PRINT_HEARTCBW_K14(COLOR2_K14("pink"), , , COLOR2_K14("ALICE"))
				ELSE
					PRINTFORMW %RAND_SPLIT("わ/わね/")%…」
				ENDIF
				PRINTFORMW 「こんな風にされたら私、\@ AFFECTION("相思相愛") ? %CNAME(MASTER, TARGET)%の事…、もっと好きになっちゃう！ # もう…、たまらなくなっちゃうわ！ \@」
				PRINTFORMD %CALLNAME%は%CALLNAME:MASTER%
				IF AFFECTION("相思相愛") || CFLAG:TARGET:依存度 > 4000
					PRINTFORMDL に覆いかぶさると、
					PRINTFORMDW 溢れ出る感情の赴くまま、%CALLNAME:MASTER%にキスの雨を降らせた…
					PRINTL
					PRINTFORMW 「スキっ！ %CNAME(MASTER, TARGET)%のこと、大好きなの！ \@ CFLAG:TARGET:依存度 > CFLAG:MASTER:依存度 ? だからずっと…、私の傍にいてっ！ # もう絶対に…、離さない！ \@」
					CALL SET_KISS
				ELSE
					PRINTFORMDL の手を取るとしっかりと握り締め、
					PRINTFORMDW 蕩けきった表情で%CALLNAME:MASTER%の動きに応えた…
					CALL FADEIN(8, @"「もっと私を\@ !TEQUIP:アイマスク ? 見て # を感じて \@…！」")
					WAIT
					CALL FADEIN(8, @"「%CNAME(MASTER, TARGET)%の\@ TALENT:MASTER:小柄 || !TALENT:MASTER:オトコ ? 可愛い # \@顔をみせて…！」")
					WAIT
					PRINTFORM 「もっと…！ もっと%RAND_SPLIT("下から/激しく")%突き上げて…
					CALL PRINT_SYMBOL2_K14
				ENDIF
		ENDSELECT
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	;ここまで漏れてこない気はするけど、なにもしない訳にもいかない。"safety valve"は用意しておかないとだ
	IF CFLAG:TARGET:依存度 > CFLAG:MASTER:依存度 || CFLAG:TARGET:依存度 > 4000
		SELECTCASE RANDCOM(2, 30)
			CASE 0
				PRINTFORM 「ひっ…、あ……あんっ
				CALL AUTOHEARTCB_K14
				PRINTFORMW 　んっ、……んんっ！」
				PRINTFORMW 「そ、そんなに…っ！ 優しくされると……、私ッ…！」
			CASE 1
				PRINTFORM 「あんっ
				CALL AUTOHEARTCB_K14
				PRINTFORMW 　んあっ！ はぁんっ……、んくっ！」 
				PRINTFORMW 「だめっ！ そんなに優しく、されたら……抑えられなくなっちゃう…ッ！」
		ENDSELECT
	ELSE
		SELECTCASE RANDCOM(2, 31)
			CASE 0
				PRINTFORMW 「ふふ、優しいのね。でも…」
				PRINTFORMW 「あまり優しくされると私…、不安になるのよ」
			CASE 1
				PRINTFORMW 「うふっ、可愛いわね。私も嬉しくなっちゃうわ」
				PRINTFORMW 「本当…、こんな時間が、何時までも続けばいいのに…」
		ENDSELECT
	ENDIF
	RETURN 1
ENDIF

;────────────────────────────────────
;COM44, 平然とやる
;────────────────────────────────────
;・REACT分類:平然と従う
;・REACT印象:好印象小
;・依存度変化(+2, -2)
;-------------------------------------------------
;トリガーする行動や状況…
;ACT分類:0:会話
;	ACT:衣装の変更を要求する
;ACT分類:1:愛撫
;	ACT:
;ACT分類:2:道具
;	ACT:
;ACT分類:3:性交
;	ACT:騎乗位
;ACT分類:4:羞恥
;	ACT:自慰/放尿
;ACT分類:5:奉仕
;	ACT:手で愛撫を強制/口で愛撫を強制/パイズリ/素股/足舐め強制/イラマチオ/胸愛撫強制
;ACT分類:6:加虐
;	ACT:
;ACT分類:7:異常
;	ACT:
;ACT分類:9:休憩
;	ACT:
;ACT分類:10:性交奉仕
;	ACT:正常位させる/後背位させる/対面座位させる/後背位させる/アナルセックスさせる
;-------------------------------------------------
;・REACT派生なし
;-------------------------------------------------
;=================================================
@KOJO_COM_K14_44
;=================================================
;━━━━━━━━━━━━━━━━━━━━━━━━
;COM44, 平然とやる
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && !TFLAG:REACT派生
	;逆レイプの場合当該COMはトリガーされない。SEX_INITIATIVE(MASTER)で真になる状況のみ
	;=================================================
	;性交奉仕
	;=================================================
	IF 口上記入チェック(100) && MENUMATCH(TFLAG:ACT, "性奉")
		LOCALS = %RAND_SPLIT("ふあ/んん/んぁ/あ/あぁ/はぅ")%
		;-------------------------------------------------
		;イきそう状態
		;-------------------------------------------------
		IF 口上記入チェック(100) && TCVAR:MASTER:イきそう
			;焦らし度派生
			SELECTCASE TCVAR:MASTER:焦らし度
				;焦らし中
				CASE IS > 1
					PRINTFORMW 「%RAND_SPLIT("ハァ、ハァ/んんっ！ くっ/はぁ、はぁ")%…駄目よ。簡単にはイかせないわ」
					PRINTFORMW 「私が満足するまで、もう少し付き合ってもらうから…」
					;実は調教者もイきそう
					SIF TCVAR:イきそう || PALAM:快Ｖ > 8500 || PALAM:快Ａ > 8500 || PALAM:快Ｂ > 8500
						PRINTFORMW （なんて言いながら、実は私がイかされちゃいそうなのは秘密…）
				;その他（焦らし無し）
				CASEELSE
					PRINTFORM 「あ%RANDTEXT_K14("ぅ", "ん", "＿")%っ
					CALL AUTOHEARTCB_K14
					PRINTFORM 　あぁっ…
					CALL AUTOHEARTCB_K14
					PRINTFORMW 　はげしい\@ TENSION(TARGET, 8) == "高い" ? ぃっ！ # わね… \@」
					PRINTFORM 「あぁん
					CALL AUTOHEARTCB_K14
					PRINTFORM 　あっ…、%RAND_SPLIT("ふ//ん")%あぁっ！
					PRINTFORMW \@ TCVAR:イきそう || PALAM:快Ｖ > 8500 || PALAM:快Ａ > 8500 ? 　やだ、コレじゃ私も…イっちゃう、イかされちゃう！ # \@」
			ENDSELECT
			RETURN 1
		ENDIF
		
		;しかしなんとも読み難い
		;-------------------------------
		;イきそう以外はREACT負荷で処理
		;-------------------------------
		;REACT負荷派生
		SELECTCASE GET_COM_RANK()
			CASE "自然な行動"
				SELECTCASE GET_ACTNAME(TFLAG:ACT)
					;逆レイプだと蹴られると思うけど、一応入れておく
					CASE "逆レイプ", "正常位させる", "後背位させる", "背面座位させる"
						PRINTFORM 「%LOCALS%…っ
						CALL AUTOHEARTCB_K14
						PRINTFORMW 　%RAND_SPLIT("やだコレっ！ ゾクゾクしてッ…！/そ、そこ…弱いのに…ッ！")%」
						PRINTFORM 「\@ T_COND("情欲") ? わたし…、頭の中……真っ白にっ！ あぁっ# あっ、ああ…っ！ ふぁあっ \@
						CALL PRINT_SYMBOL2_K14
					CASE "対面座位させる"
						IF RAND:2
							CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"　　　　　%KOJO_RAND_K14(("キス"), 1)%")
						ELSE
							PRINTFORMW 「%KOJO_RAND_K14(("キス"), 1)%」
						ENDIF
						PRINTFORMW 「ねぇもっと\@ TENSION(TARGET, 8) == "高い" ? ！ # … \@　%RAND_SPLIT("もっと、キスして/キスしながら突いて")%…っ！」
						;キスを捏造
						CALL SET_KISS
					CASE "アナルセックスさせる"
						PRINTFORM 「%LOCALS%っ
						CALL AUTOHEARTCB_K14
						PRINTFORMW 　なによ…、随分優しい\@ TENSION(TARGET, 8) == "高い" ? じゃない # のね \@……」
						PRINTFORMW 「でもコレ…\@ ABL:Ａ感覚 < 4 ? まだ違和感あるわね…… # どうしておしりで、こんなに…ッ！ \@」
				ENDSELECT
			CASE "無理のない行動"
				SELECTCASE GET_ACTNAME(TFLAG:ACT)
					CASE "正常位させる", "後背位させる"
						PRINTFORMW 「んんっ！ ごりごり抉ってくれちゃって…\@ TENSION(TARGET, 8) == "高い" ? なまいき…ッ # 生意気 \@」
						PRINTFORM 「でもこれ、奥まできて……%RAND_SPLIT("わたし/すごく")%、キモチ良くって…ッ！%RAND_SPLIT("あん/や")%っ、%RAND_SPLIT("ふ/")%あぁっ
						CALL PRINT_SYMBOL2_K14
					CASE "対面座位させる"
						PRINTFORMW 「ん！？ %KOJO_RAND_K14(("キス"), 1)%」
						PRINTFORMW 「%CNAME(MASTER, TARGET)%からキスしてくるなんて、ちょっと生意気……でも」
						PRINTFORMW 「こういうのも、悪くない、わね……」
						;キスを捏造
						CALL SET_KISS
					CASE "背面座位させる"
						PRINTFORM 「%LOCALS%っ
						CALL AUTOHEARTCB_K14
						PRINTFORMW 　\@ TENSION(TARGET, 8) == "高い" ? やだ…そこ、弄っちゃっ！ # ん…そこ、キモチ良い…… \@」
						PRINTFORM 「\@ TENSION(TARGET, 8) == "高い" ? んはぁっ、あぁっ！ それ…ッ、だめぇっ # んぁ…はぁ……あっ、あぁっ \@
						CALL PRINT_SYMBOL2_K14
					CASE "アナルセックスさせる"
						PRINTFORMW 「%LOCALS%っ！　\@ ABL:Ａ感覚 < 4 ? 激しすぎるわよ # は、はげしいっ \@！」
						PRINTFORM 「こんなにされたら、わ、わたし%RAND_SPLIT("っ/ッ")%！ んあっ、あぁっ
						CALL PRINT_SYMBOL_K14
					CASE "逆レイプ"
						PRINTFORM 「あっ
						CALL AUTOHEARTCB2_K14
						PRINTFORMW 　いいわよ、それ。%CNAME(MASTER, TARGET)%、良く判ってるじゃない…」
						PRINTFORMW 「ほら、もっとよ\@ TENSION(TARGET, 8) == "高い" ? ！ # 。 \@　盛りのついた、犬みたいに…ッ！」
				ENDSELECT
			CASE "無理をした行動"
				SELECTCASE GET_ACTNAME(TFLAG:ACT)
					CASE "正常位させる", "後背位させる", "背面座位させる"
						;実はまんざらでもない
						IF COMCOR_POSI()
							PRINTFORMW 「なにそれ…、ペース%RAND_SPLIT("ダウン/を落とそう")%って訳？」
							PRINTFORMW 「んんっ！ でもその割りに……\@ PALAM:快Ｖ > 5000 ? キモチ良いとこ探り当てられちゃって、ちょっと癪ね # いいとこに当たるわね \@…」
						;実は嫌
						ELSE
							PRINTFORMW 「ちょっと抵抗はあっても、結局は従うのね？」
							PRINTFORMW 「そうやって健気に奉仕する姿、悪くないわよ。可愛いとさえ言えるかしら。ねぇ？」
						ENDIF
					CASE "対面座位させる"
						;実はまんざらでもない
						IF COMCOR_POSI()
							PRINTFORMW 「…ん。キスしながらシてくれるなんて、気前がいいのね？ ふふ…」
							PRINTFORMW 「ほら、今度は私からキスしてあげるわ……」
							;1/2でフェードアウト
							IF RAND:2
								CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"　　　　　%KOJO_RAND_K14(("キス"), 1)%")
								CLEARLINE 1
							;1/2でPRINT
							ELSE
								PRINTFORMW 「%KOJO_RAND_K14(("キス"), 1)%」
							ENDIF
							;キスを捏造
							CALL SET_KISS
						;実は嫌
						ELSE
							PRINTFORMW 「%CNAME(MASTER, TARGET)%が無理してるのが良く判るわ。ただ、そうね？」
							PRINTFORM 「そうやって健気に頑張る姿は、ちょっと可愛いかもだけど
							CALL PRINT_SYMBOL_K14
						ENDIF
					CASE "背面座位させる"
						;実はまんざらでもない
						IF COMCOR_POSI()
							PRINTFORMW 「%LOCALS%…っ！ \@ ABL:MASTER:技巧 > 4 ? 手つきが覚束無いけど、悪くないわよ… # 本当、イヤラシイ手つきね… \@」
							PRINTFORMW 「\@ TCVAR:イきそう || PALAM:快Ｖ > 8500 || PALAM:快Ｂ > 8500 ? これじゃ…、手だけでイかされちゃいそう…… # ただ…、腰の動きが止まってるのがじれったい%RAND_SPLIT("の/わ")%よ… \@」
						;実は嫌
						ELSE
							PRINTFORMW 「強制されるのは嫌みたいだけど…」
							PRINTFORMW 「それでも言う事は聞くのね？ ま、こういうのも悪くないわ…」
						ENDIF
					CASE "アナルセックスさせる", "逆レイプ"
						;実はまんざらでもない
						IF COMCOR_POSI()
							PRINTFORMW 「%LOCALS%…っ！ %CNAME(MASTER, TARGET)%なりに気を使ってくれているのね」
							IF IS_NOWACTNAME("アナルセックスさせる")
								PRINTFORMW 「でも\@ ABL:Ａ感覚 < 4 ? この感じ、まだ慣れないわ # 、ちょっともどかしいのよね。これじゃ \@…」
							ELSE
								PRINTFORMW 「でも\@ ABL:Ｖ感覚 < 4 ? この感じ、まだ慣れないわ # 、ちょっともどかしいのよね。これじゃ \@…」
							ENDIF
						;実は嫌
						ELSE
							PRINTFORMW 「そうよ、%CNAME(MASTER, TARGET)%は私の言いなりになっていればいいの」
							PRINTFORMW 「それがどれほど異常な事であろうとも、ね。%LOCALS%…っ！」
						ENDIF
					CASEELSE
				ENDSELECT ;			書いたあとから刻みたくなったけど、後回しにする構え
		ENDSELECT
		RETURN 1
	ENDIF
	
	;奉仕系、もしくはその他の場合に落ちてくるのを想定。
	;ただ、この構造なら奉仕系の枠を埋めておけばその他に落ちる可能性は無いはず？
	;MENUMATCH(TFLAG:ACT, "奉仕")でやらなかったのは、汎用処理を別個につくるのが面倒だったから
	;何かを表示する為の最後の箇所なんで、まぁ一般分岐はあった方が望ましいよね、という話
	;色々な可能性を完全に把握するのは非常に困難
	;-------------------------
	;その他を処理
	;-------------------------
	SELECTCASE GET_COM_RANK()
		CASE "自然な行動"
			SELECTCASE TENSION(TARGET, 8)
				CASE "高い"
					PRINTFORMW 「…ん、悪くないわ。%CNAME(MASTER, TARGET)%にしては%RAND_SPLIT("上出来/まずます/まあまあ")%かしら？」
					PRINTFORMW 「でもそうね…、その余裕の表情が%RAND_SPLIT("癪に障るわ/ちょっと生意気ね")%」
					PRINTFORMW 「ま、身勝手この上ない話なのは判っているつもりよ。フフ…」
				CASE "一般"
					PRINTFORMW 「まぁ、このくらいは出来て当然よね%RAND_SPLIT("？/")%」
					PRINTFORMW 「というか正直…、今の%CNAME(MASTER, TARGET)%ならもっとできるはずだと思うんだけど」
					SIF !RAND:3
						PRINTFORMW 「その辺どうなのかしら。ねぇ？」
				CASE "低い"
					PRINTFORMW 「%RAND_SPLIT("へぇ/あら/ふーん")%、随分余裕%RAND_SPLIT("がある/")%じゃない」
					PRINTFORMW 「これなら、%CNAME(MASTER, TARGET)%にはもっと色々要求してもよさそうね%RAND_SPLIT("？/")%」
					CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"「\@ AFFECTION("相思相愛") ? クスクス # ウフフ \@…」")
					CLEARLINE 1
			ENDSELECT
		CASE "無理のない行動"
			SELECTCASE TENSION(TARGET, 8)
				CASE "高い"
					PRINTFORM 「そういう%CNAME(MASTER, TARGET)%の姿をみるのは、悪くない気分ね
					CALL PRINT_SYMBOL_K14
					PRINTFORMW 「でも今の%CNAME(MASTER, TARGET)%なら、もっとできるんじゃないかしら。ねぇ？」
				CASE "一般"
					PRINTFORMW 「\@ AFFECTION("溺愛") ? ふふ # フフ \@…、私の歓心を買おうと必死みたいね%RAND_SPLIT("？/")%」
					PRINTFORM 「まあでも……、その姿勢は評価してあげるわ
					CALL PRINT_SYMBOL_K14
				CASE "低い"
					PRINTFORMW 「ま、この程度の事はやって貰わないとね」
					PRINTFORMW 「\@ AFFECTION("溺愛") ? 今の%CNAME(MASTER, TARGET)%ならできて当然だし、ね # 私に気に入られたいのなら尚更。ねぇ？ \@」
			ENDSELECT
		CASE "無理をした行動"
			;実はまんざらでもない
			IF COMCOR_POSI()
				PRINTFORMW 「何でもないような顔してるけど、本当は違うんでしょう%RAND_SPLIT("？/")%」
				PRINTFORM 「そういうのって伝わ%RAND_SPLIT("る/っちゃう")%のよね。\@ TENSION(TARGET, 8) == "高い" ? ザ・ン・ネ・ン # 残念 \@だけど
				CALL PRINT_SYMBOL_K14
				;依存度変化
				CALL ADD_DEPEND(-1, +2)
			;実は嫌
			ELSE
				PRINTFORMW 「平静を装ったって、無理をしてるのは見れば判るわ」
				PRINTFORMW 「その程度%RAND_SPLIT("じゃ私の歓心は買えないわよ/で私のご機嫌を取ろうとしても無駄よ")%、悪いけど」
				;依存度変化
				CALL ADD_DEPEND(-2, +3)
			ENDIF
	ENDSELECT
	RETURN 1
ENDIF