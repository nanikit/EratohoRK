;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	アリス口上 調教開始イベント口上ファイル
;
;	調教開始イベント口上をまとめたファイル
;
;Version		Reason
;1.71@0503		一部加筆等
;1.71			@口上記入チェック仕様に。その他細かな修正
;1.70			GAMEBASE参照を初回時に行うように
;1.60			単体ファイルとして独立
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;========================================================================
;EV11, 調教開始口上
;========================================================================
;・口上主を調教者にして調教開始した場合に表示
;・ここに限らず、原則として上に書いてある分岐が優先される
;・最初からの主人であるか否かはTALENT:ご主人様で分岐を設けるか
;・MS, ASで関数分けして別枠で処理してもいい
;-------------------------------------------------
@KOJO_MS_EVENT_K14_11(ARG)
;-------------------------------------------------
;引数内訳…
;ARG=(0, 通常/1, 調教者の疲労による中止/2, 調教対象の疲労による中止)
;-------------------------------------------------
;ビットを初期化
KFLAG:TARGET:201 = CLEARBITS(KFLAG:TARGET:201, "0/1/2/3/4")
CLEARBIT KFLAG:TARGET:202, 0

[SKIPSTART]
RESULT = 0
CALL KILL_TARGET_CUSTOM_K14
SIF RESULT != 1
	PRINTW 望ましくない改変を検出しました。emueraを終了します…
	QUIT
[SKIPEND]


;────────────────────────────────────
;調教初回・１０回目
;────────────────────────────────────
;・調教初回、及び10回目に表示される口上
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;初回・監禁、通常調教 アリスが主人の場合
;━━━━━━━━━━━━━━━━━━━━━━━━
IF CFLAG:調教回数 == 1
	PRINTL
	PRINTFORMW 「あら、おはよう。夕べは良く眠れたかしら…？」
	PRINTFORMW 「ま、それはいいとして１つ言わせてもらうわね」
	PRINTL
	PRINTFORMW 「良く憶えておいて。今後%CNAME(MASTER, (FLAG:ご主人様))%を生かすも殺すも、私次第だって事…」
	PRINTFORMW 「私を愉しませてくれる限り、%CNAME(MASTER, (FLAG:ご主人様))%の安全は保障するわ」
	PRINTFORMW 「ただ、そうね……もし、%CNAME(MASTER, (FLAG:ご主人様))%が何の役にもたたない木偶なら…」
	;ＢＧＭカットイン プラスチックマインド
	CALL BGM_CUTIN(CSTR:曲２, 1)
	CALL FADEIN(15, @"「%CNAME(MASTER, (FLAG:ご主人様))%にとって……とっても不幸な事になるかも、ね」")
	WAIT
	PRINTL
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;調教１０回目
;━━━━━━━━━━━━━━━━━━━━━━━━
IF CFLAG:調教回数 == 10 && !ARG
	PRINTFORM 「ごきげんよう、私のお人形さん
	CALL PRINT_8THNOTECW_K14(COLOR_K14("white"), , , COLOR2_K14("ALICE"))
	PRINTFORMW 「あら､怖い顔しちゃって…、穏やかじゃないわね？」
	PRINTFORMW 「いいわ、 時間はたっぷりある……」
	PRINTFORMW 「じっくり慣らして、私好みの人形にしてあげる」
	PRINTL
	CALL FADEIN(15, @"「%CNAME(MASTER, (FLAG:ご主人様))%に他の選択肢はないのよ……」")
	CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), "「フフフ…」")
	CLEARLINE 1
	RETURN 1
ENDIF

;────────────────────────────────────
;調教中止 ARG:1 ARG:2
;────────────────────────────────────
;CFLAG:消耗 > 5もしくはCFLAG:MASTER:消耗 > 5を条件として呼び出される箇所
;この条件から漏れた場合は通常の調教開始口上が呼ばれる
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;疲労で調教が中止になる場合
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && ARG > 0
	;疲れているのは口上主
	IF ARG == 1
		PRINTFORMW 「どうも体調がイマイチね……今回はやめておきましょう」
	;疲れているのは調教対象
	ELSE
		PRINTFORMW 「疲れてるみたいね」
		PRINTFORMW 「いいわ。今日はゆっくり休んで」
	ENDIF
	RETURN 1
ENDIF

;30日でEDに到達しない場合、口上側で別枠から呼び出す
;0617安定版だと不要と思われるんでコメントアウト
;*************************************************
;エンディング FIRSTEVENT(0)
;*************************************************
;IF DAY >= 30 && KFLAG:(FLAG:ご主人様):100 == 1 && FIRSTEVENT(0)
;	CALL KOJO_EVENT_K14_11_ENDING
;	RETURN 1
;ENDIF

;────────────────────────────────────
;その他・好感度等による分岐
;────────────────────────────────────
;・現在のACTABLEやCOMABLEの仕様的に、進展度を測るには(事実上)好感度を基準にするしかないと思われる
;・依存度は@COMで代入され参照できるのみで、本体側の挙動には何ら影響を与えていない
;・今後の手入れ如何によっては使い道もあるかも？といったところ
;-------------------------------------------------

;━━━━━━━━━━━━━━━━━━━━━━━━
;相思相愛（好意3000以上・好感度3000以上)
;━━━━━━━━━━━━━━━━━━━━━━━━
;・実はとっくにお互いが好き合っている アリスが主人の場合に限定
;-------------------------------------------------
IF 口上記入チェック(100) && AFFECTION("相思相愛") && CFLAG:調教回数 > 10
	;調教者が処女で無い場合
	IF !TALENT:TARGET:処女
		;アナルセックスさせるを解放
		CFLAG:1099 = 0
		;性器を擦り合うを解放
		;CFLAG:1016 = 0
	ELSE
	ENDIF
	
	;=================================================
	;初回
	;=================================================
	IF 口上記入チェック(100) && KFLAG:(FLAG:ご主人様):50 == 1 && FIRSTEVENT(1)
		PRINTFORMW 「ねぇ、実は%CNAME(MASTER, TARGET)%に言ってなかった事があるのだけど…」
		PRINTL
		PRINTFORMDW アリスはスカートの裾を握りしめ、俯きながら切り出した。
		PRINTL
		PRINTFORMW 「%CNAME(MASTER, (FLAG:ご主人様))%がここに来たのは偶然じゃないし、私の気紛れでもないの」
		PRINTL
		PRINTFORMDW %CALLNAME:MASTER%は言葉の意味が理解出来ず、ポカンとしながらアリスを見やった
		PRINTL
		PRINTFORMW 「本当は…ね……%CNAME(MASTER, (FLAG:ご主人様))%の事は、ずっと前から知っていたのよ…」
		PRINTFORMW 「最初に、ね…里で%CNAME(MASTER, (FLAG:ご主人様))%を見かけたの…それ以来、その…なんか…」
		PRINTFORMW 「気がつけば、目で追うようになってたの…」
		PRINTL
		PRINTFORMDW %CALLNAME:MASTER%はアリスの真意を測りかね、怪訝な顔で次の言葉を待った。
		PRINTL
		CALL BGM_CUTIN("童話の消えた空", 1)
		PRINTFORMW 「わ、私にも良く分からないのよ…」
		PRINTFORMW 「人に干渉するのもされるのも嫌いだったのに…」
		PRINTFORMW 「私は魔女……長い時を生きてきた…でも」
		PRINTFORMW 「こんな気持ちになった事は無かったわ…少なくても、今までは…ね」
		PRINTL
		PRINTFORMW 「最初は、%CNAME(MASTER, (FLAG:ご主人様))%の事もっと知りたいと思った…」
		PRINTFORMW 「そして、私の事知ってほしい、とも思うようになったわ…」
		PRINTFORMW 「そのうちに、自分の気持ちが抑えられなくなってきたの…」
		PRINTFORMW 「触れたい、触れて欲しい、傍にいたい、傍にいて欲しい…」
		PRINTL
		PRINTFORMDW アリスは感情が高ぶり、言葉は熱を帯びてきた…
		PRINTL
		PRINTFORMW 「私だけのものにしたい、私だけを見て欲しい、%CNAME(MASTER, (FLAG:ご主人様))%を独占したい…って」
		PRINTFORMW 「%CNAME(MASTER, (FLAG:ご主人様))%が里の娘と楽しそうに話してるのを見たとき…私の中で何かが弾けたの…」
		PRINTFORMW 「帰宅する%CNAME(MASTER, (FLAG:ご主人様))%を追って、そして……ええ、あとは%CNAME(MASTER, (FLAG:ご主人様))%が知ってる通り…よ」
		PRINTL
		PRINTFORMW 「%CNAME(MASTER, (FLAG:ご主人様))%には酷い事をいっぱいしてきた……でも…でもね…私…」
		PRINTFORMW 「怖かった…怖かったのよ…！ %CNAME(MASTER, (FLAG:ご主人様))%に、拒絶されるのが…！」
		PRINTFORMW 「拒絶されるくらいなら、壊しちゃえばいいって思った…」
		PRINTFORMW 「感情のない人形なら、私を拒絶する事もないだろう…って」
		PRINTL
		PRINTFORMW 「でも…間違ってた。私が欲しいのは…ありのままの%CNAME(MASTER, (FLAG:ご主人様))%だって気がついたの…」
		PRINTFORMW 「拗ねたり、怒ったり、笑ったり、甘えたり…そんな%CNAME(MASTER, (FLAG:ご主人様))%じゃないと駄目なの！」
		PRINTL
		PRINTFORMDW 一気にまくし立てたアリスはちょっと寂しげに俯いた。
		PRINTL
		PRINTFORMW 「こんな女、気持ち悪いでしょう…？ 軽蔑したでしょう…？」
		PRINTFORMW 「そう…悪いのは全部私……自業自得よ…ね」
		PRINTFORMW 「でも…、もし…もしも、%CNAME(MASTER, (FLAG:ご主人様))%が受け入れてくれるなら、私は…！」
		PRINTL
		PRINTFORMDW アリスは涙を浮かべ、縋り付くような視線を%CALLNAME:MASTER%に向ける…
		PRINTFORMDW …つかの間二人は顔を見合わせた。言葉を発する事なく情愛の橋を一瞬にかけて、ただ見つめあった…
		PRINTL
		PRINTFORMDW しばしの沈黙のあと、アリスは肩を竦めながら口を開く。
		PRINTL
		PRINTFORMW 「ふっ…私とした事が…どうかしてたわね…」
		PRINTFORMW 「まったく…%CNAME(MASTER, (FLAG:ご主人様))%といると調子が狂うわ」
		PRINTL
		PRINTFORMDW アリスは、ばつが悪そうな表情で%CALLNAME:MASTER%に向き直った。
		CALL BGM_CUTIN("時間軸上のアリス", 1)
		PRINTL
		PRINTFORMW 「き、気にしないで…！今言った事、全部忘れて頂戴…」
		PRINTFORMW 「らしくないわよね…？そ、そうよ…！ 普通が1番、1番よねっ…！」
		PRINTFORMW 「そ、それじゃ、いつも通りいかせてもらうわね」
		PRINTFORMW 「さぁ…！ひざまづいて、私にご奉仕なさい♪」
		PRINTL
		PRINTFORMDW アリスは満面の笑みを浮かべ、心底楽しそうに言った。
		;相思相愛判定
		KFLAG:(FLAG:ご主人様):100 = 1
		CALL TWAIT3_K14
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	PRINTFORMW 「ふふっ、おまたせ」
	SELECTCASE RANDEVENT(2, 1)
		CASE 0
			PRINTFORM 「さぁ、思いっきり愉しみましょう…
			CALL PRINT_SYMBOL_K14
		CASE 1
			PRINTFORMW 「ほら、ぼんやりしてないで。さっさと始めるわよっ！」
		CASE 2
			PRINTFORMW 「さて、どんな事して愉しもうかしらね？ ふふ…」
	ENDSELECT
	PRINTFORMDW アリスは溢れんばかりの笑顔で、%CALLNAME:MASTER%の手をとった…
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;調教完了（好意2000以上）
;━━━━━━━━━━━━━━━━━━━━━━━━
;・調教対象が口上主にもうメロメロ
;-------------------------------------------------
IF 口上記入チェック(100) && AFFECTION("調教完了") && CFLAG:調教回数 > 10
	PRINTFORMW 「ふふっ…期待してるのね…」
	PRINTFORMW 「%RAND_SPLIT("この/")%%RAND_SPLIT("変態/ヘンタイ")%…」
	PRINTFORM 「いいわ、今日もたっぷり\@ ABL:MASTER:マゾっ気 > 4 ? 虐めて # 可愛がって \@ あげる%RAND_SPLIT("わ/から/")%
	IF RAND:3
		CALL PRINT_8THNOTECW_K14(COLOR_K14("white"), , , COLOR2_K14("ALICE"))
	ELSE
		PRINTW 」
	ENDIF
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;溺愛（好感度2000以上）
;━━━━━━━━━━━━━━━━━━━━━━━━
;・調教者の方が調教対象にはまってしまい、深かったり倒錯した愛を注いでいる
;-------------------------------------------------
IF 口上記入チェック(100) && AFFECTION("溺愛") && CFLAG:調教回数 > 10
	PRINTFORMW 「さぁ、今日も愉しみましょう。私の可愛いお人形さん」
	PRINTFORMW 「あら駄目よ？ そんな顔したって」
	PRINTFORM 「私、楽しみにしてたんだから
	CALL PRINT_8THNOTECW_K14(COLOR_K14("white"), , , COLOR2_K14("ALICE"))
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;調教済み（好意1000以上）
;━━━━━━━━━━━━━━━━━━━━━━━━
;・調教対象が口上主に調教されるのを幸せに感じている
;-------------------------------------------------
IF 口上記入チェック(100) && AFFECTION("調教済み") && CFLAG:調教回数 > 10
	PRINTFORMW 「%RAND_SPLIT("あら/ふ～ん/へぇ")%、大分馴染んできたみたいね%RAND_SPLIT("？/")%」
	PRINTFORMW 「いいわよ…素直にしてればもっと%RAND_SPLIT("可愛がって/気持ちよくして")%あげる…」
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;嫌悪（好意-100未満）
;━━━━━━━━━━━━━━━━━━━━━━━━
;・調教者に対し嫌悪感を抱いている
;-------------------------------------------------
IF 口上記入チェック(100) && AFFECTION("嫌悪") && CFLAG:調教回数 > 10
	PRINTFORMW 「そんなに睨んだって、どうにもなりはしないわ」
	PRINTFORMW 「所詮%CNAME(MASTER, TARGET)%は、ただのか弱い人間」
	PRINTFORMW 「いや、人形かしら…ね」
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;恐怖（好意200未満・トラウマLv3以上）
;━━━━━━━━━━━━━━━━━━━━━━━━
;・心に傷を作られ、ご主人様の姿を見るだけで怯えてしまう
;・トラウマの種は「%ACTSTR(CFLAG:MASTER:トラウマ)%」で表示できる
;-------------------------------------------------
IF 口上記入チェック(25) && AFFECTION("恐怖")
	PRINTFORMW 「怖いのね…？ 不安なのね…？ これから何をされるのか…とか」
	PRINTFORMW 「自分がどうなっちゃうのか…とか」
	PRINTFORMW 「フフフフフ……」
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;反抗（好意0以下・反抗刻印Lv3以上）
;━━━━━━━━━━━━━━━━━━━━━━━━
;・心を開かない奴隷に苛つくご主人様
;-------------------------------------------------
IF 口上記入チェック(100) && AFFECTION("反抗") && CFLAG:調教回数 > 10
	;=================================================
	;初回
	;=================================================
	IF 口上記入チェック(100) && FIRSTEVENT(60)
		PRINTFORMW 「随分頑なね…」
		PRINTFORMW 「私がいつまでも寛容でいると思ったら大間違いよ」
		RETURN 1
	ENDIF
	
	;-------------------------
	;初回以外
	;-------------------------
	PRINTFORMW 「素直に従った方が、%CNAME(MASTER, TARGET)%の為にもなると思うのだけど…」
	PRINTFORMW 「愚鈍ね…」
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;目覚め（好意500以上）
;━━━━━━━━━━━━━━━━━━━━━━━━
;・調教されるのもむしろ好きになってきたかも知れない…
;-------------------------------------------------
IF 口上記入チェック(100) && AFFECTION("目覚め") && CFLAG:調教回数 > 10
	;=================================================
	;初回
	;=================================================
	IF 口上記入チェック(100) && FIRSTEVENT(70)
		PRINTFORMW 「ちょっとは素直になってきたみたいね？」
		PRINTFORMW 「無駄を無駄と悟るのは愚かな事じゃないわ…」
		PRINTFORMW 「そうよ…自分の立場を受け入れなさい」
		RETURN 1
	ENDIF
	
	;-------------------------
	;初回以外
	;-------------------------
	PRINTFORMW 「素直なのはいい事よ…」
	PRINTFORMW 「%CNAME(MASTER, TARGET)%、見込みがありそうね？ フフ…」
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;順応（好意100以上・好感度100以上）
;━━━━━━━━━━━━━━━━━━━━━━━━
;・丁寧に調教を進められて、思ったより嫌じゃないかも…
;-------------------------------------------------
IF 口上記入チェック(100) && AFFECTION("順応") && CFLAG:調教回数 > 10
	;=================================================
	;初回
	;=================================================
	IF 口上記入チェック(100) && FIRSTEVENT(80)
		PRINTFORMW 「少しは慣れてきたかしら？」
		PRINTFORMW 「そう、いいわよ…大人しく従いなさい」
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	PRINTFORMW 「そうよ…大人しくしてれば悪いようにはしないわ」
	PRINTFORMW 「私にご奉仕できるなんて、%CNAME(MASTER, TARGET)%は恵まれてるのよ？」
	SIF !RAND:3
		PRINTFORMW 「ま、%CNAME(MASTER, TARGET)%の立場からしたら…『冗談じゃない』ってとこでしょうけどね」
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;序盤（条件なし）
;━━━━━━━━━━━━━━━━━━━━━━━━
;・要するに、他の条件にヒットしなかった場合に落ちる箇所
;・仮に他の分岐を全て放置した場合、この箇所だけ書いておけば何かは表示されるとか、そんな場所です
;-------------------------------------------------
IF 口上記入チェック(100) && TALENT:TARGET:ご主人様
	PRINTL
	;=================================================
	;初回
	;=================================================
	IF 口上記入チェック(100) && FIRSTEVENT(90)
		PRINTFORMW 「さ、準備はいいかしら？」
		PRINTFORMW 「私に全て委ねなさい。悪いようにはしないから……」
		RETURN 1
	ENDIF
	
	;=================================================
	;回数カウント10以下
	;=================================================
	IF CFLAG:調教回数 < 10
		;調教回数の派生（3~9）
		SELECTCASE CFLAG:調教回数
			CASE 3
				PRINTFORMW 「さてと、今回はどうしてやろうかしら。ちょっと愉しくなってきたわ」
				PRINTFORML 「まぁそれにしても……」
				{
				CALL HTMLPRINTW("「私がこんなことしてるって知ったら、" + 
                               @"%HTMLFONT("<b>『あいつら』</b>", , COLOR2_K14("白"))%は異変だって騒ぐのかしら…」")
				}
			CASE 4
				PRINTFORMW 「私の気紛れに無理やり付き合わされて、%CNAME(MASTER, TARGET)%も気の毒よねぇ」
				PRINTFORMW 「と言ったところで…、すんなり開放してあげる気もないんだけど。今のところは、ね」
			CASE 5
				PRINTFORMW 「なんていうか、人形と違って簡単にはいかないものね」
				PRINTFORMW 「人間相手なんだから、当たり前と言えば当たり前なんだけど。って…」
				PRINTFORMW 「%CNAME(MASTER, TARGET)%に聞かせる話ではなかったかしらね？ ふふ」
			CASE 6
				PRINTFORML 「思ったより元気そうね？」
				PRINTFORMW 「と言われたところで、%CNAME(MASTER, TARGET)%には何の慰めにもならないでしょうけど」
				PRINTFORMW 「まぁ御託はこの辺にして…、そろそろ始めるわよ」
			CASE 7
				PRINTFORMW 「気分はどうかしら？ って聞いたところで、%CNAME(MASTER, TARGET)%が答えるとも思えないけど」
				PRINTFORMW 「ま、早いトコ慣れた方がいいわよ。%CNAME(MASTER, TARGET)%自身の為にも、ね…」
			CASE 8
				PRINTFORMW 「相変わらず冴えない顔してるわねぇ。ま、%CNAME(MASTER, TARGET)%の今の境遇を考えれば無理もないか」
				PRINTFORMW 「ってコレ、別に同情してる訳じゃないから。そもそも…」
				PRINTFORMW 「同情するくらいなら、最初からこんなことしてないし。ね」
			CASE 9
				PRINTFORMW 「さぁ始めるわよ、準備をしなさい。といっても、%CNAME(MASTER, TARGET)%がする事なんて何もない訳なんだけど」
				PRINTFORMW 「うん、そうね。気持ちの準備くらいさせてあげようっていう、私の心遣いと理解して頂戴」
				PRINTFORMW 「ま、今更そんなこと言われても、%CNAME(MASTER, TARGET)%には何のありがたみも無いでしょうけど。ね…」
		ENDSELECT
		PRINTL
		RETURN 1
	ENDIF
	
	;-------------------------
	;回数カウント10以上の汎用処理
	;-------------------------
	PRINTFORMW 「さぁ、愉しませて頂戴」
	PRINTFORMW 「今に、私なしではいられないようになる」
	PRINTFORMW 「じっくり慣らしていってあげるわ……」
	PRINTL
	RETURN 1
ENDIF