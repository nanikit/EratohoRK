;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	アリス口上 調教者行動前イベント口上ファイル
;
;	調教者行動前イベント口上をまとめたファイル
;
;Version		Reason
;1.71			@口上記入チェック仕様に。その他細かな修正
;1.67Rel5		調教中止イベントの構造を若干変更。一部条件の見直し
;1.67			細かな修正
;1.65			一部修正＆加筆
;1.60			単体ファイルとして独立
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;========================================================================
;EV12, 調教者行動前口上 FIRSTEVENT(10～19)
;========================================================================
;・TRAIN_PROCCES.ERB内、@TRAIN_CANCELから呼び出される
;・口上主のパラメータ表示の直後に表示。調教指示は既に決定されている
;・調教中止宣言の差し替えや、調教ターン開始前にご主人様から一言、のように書くことも可能
;・午前、午後の調教開始ではなく、調教ターンの開始前には毎回呼び出される箇所
;・その為もあって、調教中止時と経過時間0以外の箇所は@PERCENTで確率表示制を採用
;・調教中止時にのみ地の文が存在する
;・文章表示後に本体側のDRAWLINE処理あり
;-------------------------------------------------
@KOJO_EVENT_K14_12(ARG)
;-------------------------------------------------
;引数内訳…
;ARG=(0.調教可/1.口上主の疲労で中止/2.調教対象の疲労で中止/3.ご褒美による中止)
;-------------------------------------------------

;────────────────────────────────────
;調教中止 ARG:1 ARG:2 ARG:3 FIRSTEVENT(0)～
;────────────────────────────────────
;・疲労、もしくはご褒美による調教中止の場合の口上
;・記入チェック=1の場合は口上表示後本体側でPRINTLの空行が入る
;・記入チェックを=2にすると、地の文を表示しない
;・というかRESULT > 2で判定されるので2以上なら同じ挙動になりますが一応非推奨ということで
;-------------------------------------------------
;・アリスが主人の場合のみ表示
;-------------------------------------------------
;・KFLAG:(FLAG:ご主人様):50		OP経過判定フラグ
;・KFLAG:(FLAG:ご主人様):102	ED経過判定フラグ
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;ARG3, ご褒美による中止
;RESULT値（1, 口上の後にデフォルト文を表示(非推奨)、2, 口上のみ表示）
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && ARG == 3 && TALENT:TARGET:ご主人様 && KFLAG:(FLAG:ご主人様):50 == 1
	;FAVOR系ではなくEV11における相思相愛初回イベントでのフラグ回収を条件とする
	;=================================================
	;相思相愛
	;=================================================
	IF 口上記入チェック(100) && GET_KFLAG_K14("相思相愛")
		;別ルートでエンディングを達成している場合は弾く
		;FIRSTEVENTの引数のミスで長らく機能してなかった。気付かせてくれたスレ151, >>779の人に感謝
		;-------------------------------------------------
		;フラグ回収イベント
		;-------------------------------------------------
		IF 口上記入チェック(100) && KFLAG:(FLAG:ご主人様):102 != 1 && FIRSTEVENT(1)
			PRINTFORMW 「ま、時にはこういう日があってもいいわよね」
			PRINTFORMW 「私も家に居るから、何かあったら声を掛けてね」
			CALL TWAIT3_K14
			PRINTFORMDW その日%CALLNAME:MASTER%はのんびりと過ごしていた。
			PRINTFORMDW そのうちに飽きてしまい、%CALLNAME:MASTER%は家の中でアリスの姿を探した…
			PRINTFORMDW ある部屋の前に差し掛かると、中から物音が聞こえてくる。
			PRINTL
			CALL BGM_CUTIN("ブクレシュティの人形師")
			PRINTFORMDW 好奇心をそそられ、そっと覗いてみるとアリスが人形づくりをしている姿が目に入った。
			PRINTFORMDW そのあまりに真剣な様子に、一瞬声を掛けようとした%CALLNAME:MASTER%は思いとどまる。
			PRINTL
			PRINTFORMDW 邪魔をしないよう大人しく見ているつもりであったが、アリスの精緻で見事な仕事に
			PRINTFORMDW 思わず感嘆の声を上げてしまう…
			PRINTL
			PRINTFORMW 「なによ、居たのなら一声掛けてくれればよかったのに」
			PRINTL
			PRINTFORMDW アリスは少し驚いたようであったが、%CALLNAME:MASTER%に向ける表情には笑みが浮かんでいる。
			PRINTL
			PRINTFORMW 「もうすっかり元気そうね？ ふふ…」
			PRINTFORMW 「ねぇ、ちょっと待っててもらえるかしら…」
			PRINTFORMW 「今日のうちにもう少し進めておきたいの」
			PRINTFORMDW %CALLNAME:MASTER%は作業を中断させてしまった事を謝り、自分に手伝える事はないかと聞いてみた。
			PRINTL
			PRINTFORMW 「ありがとう。気持ちは凄く嬉しいわ。 本当よ？ でも…」
			PRINTFORMW 「人形づくりだけは一人でやりたいの。ほら、他の人形達にも手伝わせてないでしょう…？」
			PRINTL
			PRINTFORMDW 言われてみれば確かにそうだ。アリスの廻りに人形達の姿は見当たらない。
			PRINTFORMDW 人形づくりに対する彼女流の拘りなのだろう。
			PRINTFORMDW そう理解し納得しかけた時、作りかけの人形が%CALLNAME:MASTER%の注意を引いた…
			PRINTL
			PRINTFORMDW 人形は二体だった。出来上がるまではまだ時間が掛かりそうではあったが、完成すれば
			PRINTFORMDW 他の人形達に劣らない、素晴らしいものになるであろう事は想像に難くない。
			PRINTL
			PRINTFORMW 「ああ、これ？ そうね、たしかにまだ完成までには時間が掛かりそうだけど…」
			PRINTFORMW 「今回はね、ちょっと本気で作ってるの。私の最高傑作にするつもりよ」
			PRINTL
			PRINTFORMDW アリスはそう言ったあと意味深な含み笑いをしたが、この時の%CALLNAME:MASTER%はその意味を知る由も無かった…
			PRINTL
			;エンディング用フラグに代入
			KFLAG:(FLAG:ご主人様):101 = 1
			RETURN 2
		ENDIF
		
		;---------------------
		;初回以外を処理
		;---------------------
		CALL BGM_CUTIN("人形のある風景")
		SELECTCASE RANDEVENT(3, 0)
			CASE 0
				PRINTFORMW 「紅茶を淹れたんだけど、一緒に飲むわよね？」
				PRINTFORMW 「スコーンも焼いてみたの。%CNAME(MASTER, TARGET)%の口に合うといいのだけど…」
				PRINTFORMW 「あらあら、そんなにがっつかなくても。ふふふ…」
				PRINTFORMDW 二人は楽しいひとときを過ごした……
			CASE 1
				PRINTFORMW 「ミルクティーを淹れたんだけど、一緒に飲むわよね？」
				PRINTFORMW 「アップルシフォンも焼いてみたの。%CNAME(MASTER, TARGET)%の口に合うといいのだけど…」
				PRINTFORMW 「ふふ、行儀が悪いわね。でも、喜んでもらえたみたいで嬉しいわ♪」
				PRINTFORMDW 二人は楽しいひとときを過ごした……
			CASE 2
				PRINTFORMW 「ハーブティーを淹れたんだけど、一緒に飲むわよね？」
				PRINTFORMW 「シュークリームもつくってみたの。中身は紅茶クリームよ。気に入ってもらえるかしら…？」
				PRINTFORMW 「ほらほら、ほっぺたについてるわよ？　取ってあげるからじっとしてて…♪」
				PRINTFORMDW 二人は楽しいひとときを過ごした……
		ENDSELECT
		RETURN 2
	ENDIF
	
	;-------------------------
	;相思相愛以外
	;-------------------------
	PRINTFORMW 「今回はゆっくり休んで頂戴」
	PRINTFORM 「じゃ、私
	SELECTCASE RANDEVENT(3, 1)
		CASE 0
			PRINTFORMW ものんびりさせてもらおうかしらね」
		CASE 1
			PRINTFORMW は新しい薬の調合でもしようかしら」
		CASE 2
			PRINTFORMW は庭の手入れでもしようかしらね」
	ENDSELECT
	RETURN 2
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;ARG1, 調教者の疲労による中止
;RESULT値（1, 口上の後にデフォルト文を表示(非推奨)、2, 口上のみ表示）
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && ARG == 1 && TALENT:TARGET:ご主人様 && KFLAG:(FLAG:ご主人様):50 == 1
	PRINTFORMW 「今回はやめておくわ…」
	PRINTFORMW 「ちょっと疲れが残ってるのよね」
	PRINTFORMW 「私、こう見えてもけっこう忙しいのよ…？」
	RETURN 2
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;ARG2, 調教対象の疲労による中止
;RESULT値（1, 口上の後にデフォルト文を表示(非推奨)、2, 口上のみ表示）
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && ARG == 2 && TALENT:TARGET:ご主人様 && KFLAG:(FLAG:ご主人様):50 == 1
	;=================================================
	;相思相愛
	;=================================================
	IF 口上記入チェック(100) && AFFECTION("相思相愛")
		SELECTCASE RANDEVENT(3, 2)
			CASE 0
				PRINTFORMW 「%CNAME(MASTER, TARGET)%酷い顔してるわ… 大丈夫…？」 
				PRINTFORMW 「今日何もしなくていいから、ね？ ゆっくり休んで」
				PRINTL
				PRINTFORMDW 一旦部屋出ようしたアリスだが、急に何か思いついたかのように立ち止まる。
				PRINTL
				PRINTFORMW 「つ、ついでだから、一緒に寝てあげる。感謝しなさいよっ!」
				PRINTFORMW 「これは貸しなんだから……次は今回の分も愉しませてくれないと駄目なんだから…」
				PRINTL
				PRINTFORMDW アリスはちょっと拗ねてみせたあと、ニコニコしながらベッドに潜り込んできた。
				PRINTFORMDW そして二人は何をするでもなく、ただ抱き合い互いの体温を感じ取る…
				PRINTL
				CALL BGM_CUTIN("地上の揺り籠")
				PRINTFORMW 「私、今凄い幸せ。もう怖いくらい…」
				PRINTFORMW 「お願い、離さないで。ずっとそばにいて……」
				PRINTL
				PRINTFORMDW アリスは微かに震えていた。漠然とした不安、恐れといったものが僅かずつではあるが
				PRINTFORMDW 確実に彼女の心を苛み、侵食していっているようだった…
				PRINTL
				PRINTFORMDW そんなアリスの心の揺らぎを朧気に感じ取りつつも、掛けてやるべき言葉は見つからず
				PRINTFORMDW 今はただ、抱きしめてやる事しか出来なかった……
			CASE 1
				PRINTFORMW 「ねぇ、疲れに効くお薬があるんだけど、ちょっと飲んでみる…？」
				PRINTFORMDW アリスはそう言うと奥の部屋から何やら妖気の漂う小瓶を取ってきた…
				PRINTL
				PRINTFORMW 「実は私の母親から送られてきた物なんだけど…」
				PRINTFORMW 「見た目や味は兎も角、効き目は凄いらしいの。ねぇねぇ、試してみて♪」
				PRINTL
				PRINTFORMDW アリスの母親と言えば、確か魔界を創造したという…%CALLNAME:MASTER%は嫌な予感がした…
				PRINTL
				PRINTFORMDW アリスに手渡された小瓶を見ると、蓋の部分に『封』と書かれたものが貼ってある｡
				PRINTFORMDW さらに瓶の中の液体は奇怪に泡立ちその色彩は刻々と変化を続けている…
				PRINTL
				PRINTFORMDW 暫し躊躇した後、意を決した%CALLNAME:MASTER%は腰に手を当て一気に飲み干した……
				CALL TWAIT3_K14
				PRINTFORMDW ！！？？
				PRINTL
				PRINTFORMDW 取り違えて送ってしまった故、絶対に飲まぬようにとの連絡が魔界神から届くのは
				PRINTFORMDW その後しばらくたってからの事であった…
			CASE 2
				CALL BGM_CUTIN("Doll Maker of Bucuresti")
				PRINTFORMDW その日二人は読書をして過ごす事にした。
				PRINTFORMDW アリスは最近紅魔館の図書館から借りてきたという魔術書を
				PRINTFORMDW %CALLNAME:MASTER%は適当に本棚から取り出した本をそれぞれ読み始めた。
				CALL TWAIT1_K14
				PRINTFORMDW 『これらの偉大なる名前と数との内的な意味を悟ろうとする時に、修行者が自分の中に引き入れる
				PRINTFORMDW   力はあまりに強大なので、肉体的な衰弱が』…………　　　　%CALLNAME:MASTER%は混乱した…！
				PRINTL
				PRINTFORMW 「あら、数秘術に興味があるの…？」
				PRINTFORMDW アリスが問う。なるほど、これは数秘術というものなのかと%CALLNAME:MASTER%は理解した。
				PRINTL
				PRINTFORML 「ふふ… ちょっと分かりにくいかしら…？」
				PRINTFORMW 「そうねぇ、簡単に説明すると……」
				CALL HTMLPRINTL(@"%HTMLFONT("<b>『数秘術とは振動の単なる応用研究』</b>", , COLOR2_K14("白"))%で")
				{
				CALL HTMLPRINTL(@"１から９までの数は%HTMLFONT("<b>『振動の完全なサイクルを構成する』</b>", , COLOR2_K14("白"))%。" + 
                                @"%HTMLFONT("<b>『私達は振動の宇宙に住み</b>", , COLOR2_K14("白"))%")
				}
				CALL HTMLPRINTW(@"%HTMLFONT("<b>この世に生を受けた誰しもが、他人とは異なる固有の振動を持っている』</b>", , COLOR2_K14("白"))%ってところなんだけど…」")
				PRINTL
				PRINTFORMW 「やだ、そんな難しい顔しないでよ。ね？ 今度じっくり教えてあげるから♪」
				PRINTFORMW 「そんなことより、そろそろ食事の時間よ。ねぇ、何か食べたいものはある？」
				PRINTL
				PRINTFORMDW そうして時は過ぎていった……
				CALL TWAIT3_K14
		ENDSELECT
		RETURN 2
	ENDIF
	
	;-------------------------
	;その他を処理
	;-------------------------
	PRINTFORM 「今日の夕食は、
	SELECTCASE RANDEVENT(3, 3)
		CASE 0
			PRINTFORM ハナマルハンバーグにしましょう
		CASE 1
			PRINTFORM ウミガメのスープにしましょう
		CASE 2
			PRINTFORM シチューにしましょう
	ENDSELECT
	CALL PRINT_SYMBOL_K14
	PRINTFORMW 「いいのよ……%CNAME(MASTER, TARGET)%はゆっくり休んで頂戴」
	RETURN 2
ENDIF

;-------------------------------------------------
;・以下、ARG:0(調教可)相当
;・TRAIN_PROCCES.ERB内、@BEFORE_ACTから呼び出される
;・この時点でACTはすでに決定しているがACT派生の決定はなされていない
;・NOWEXのリセットはなされていない
;・これらの箇所を抜けた後にACT開始になる
;・分岐の増減は自由にできますが、優先度には注意しましょう
;-------------------------------------------------

;────────────────────────────────────
;調教開始直後
;────────────────────────────────────
;・経過時間が0である場合に表示される
;・その回の調教開始直後に一度きりで、以後その調教中には表示されない
;・この箇所を抜けた後にACT開始になる
;-------------------------------------------------
;　ここでIS_POLICY("今日", "お仕置き")等で今日の方針を喋らせる事も可能だったりしますが
;　EVENT17, 本日の方針口上と被る可能性があります
;-------------------------------------------------
;この辺りはまだまだ手入れする余地が残されている
;やりだすとキリが無いので1.71時点では取りあえず見送るｗ
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;調教開始直後
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && !TFLAG:経過時間
	;=================================================
	;調教者が少し疲れている
	;=================================================
	IF 口上記入チェック(100) && CFLAG:消耗 > 2
		PRINTFORMW 「なんか体が重いわね」
		PRINTFORMW 「でも、やめられないのよね…これ」
		RETURN 1
	ENDIF
	
	;何も表示しない事によって成立する、実動見ないと判らない不思議な分岐
	;=================================================
	;ご褒美による調教中止の場合
	;=================================================
	IF 口上記入チェック(100) && IS_POLICY("今日", "休憩")
		;この分岐に落ちたらRESULTだけ返す
		RETURN 1
	ENDIF
	
	;=================================================
	;調教対象の疲労による中止
	;=================================================
	IF 口上記入チェック(100) && CFLAG:MASTER:消耗 > 5
		;この分岐に落ちたらRESULTだけ返す
		RETURN 1
	ENDIF
	
	;=================================================
	;調教対象が少し疲れている
	;=================================================
	IF 口上記入チェック(100) && CFLAG:MASTER:消耗 > 2
		PRINTFORMW 「ちょっとお疲れ気味？」
		PRINTFORMW 「少しなら大丈夫かしらね」
		RETURN 1
	ENDIF
	
	;=================================================
	;調教対象への好感度1000以上
	;=================================================
	IF 口上記入チェック(100) && AFFECTION("眷愛")
		PRINTFORMW 「さぁ、はじめるわよ」
		PRINTFORM 「ふふ…、今日も愉しませて頂戴
		CALL PRINT_SYMBOL_K14
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	PRINTFORMW 「準備はいいかしら？」
	PRINTFORMW 「じゃ、はじめるわよ」
	RETURN 1
ENDIF

;────────────────────────────────────
;お仕置き FIRSTEVENT(20)～
;────────────────────────────────────
;・TFLAG:お仕置きフラグが立っている場合に表示される箇所
;・お仕置きフラグが立っていても、加虐系以外のACTになる場合がある
;-------------------------------------------------
;加虐系ACT、派生する事により加虐系に含まれる可能性のあるイラマチオ
;衣装の変更を要求する/不気味に笑う/アナルバイブ/後背位/アナルセックス/自慰/野外プレイ/放尿/足舐め強制
;異常系ACT/性交奉仕系ACT
;上記のACT以外はTFLAG:お仕置きフラグによってACT_ABLEで規制される(0203版時点)
;-------------------------------------------------

;━━━━━━━━━━━━━━━━━━━━━━━━
;お仕置き開始 
;━━━━━━━━━━━━━━━━━━━━━━━━
;・ONCEにより一回の調教で一度のみ表示
;-------------------------------------------------
IF 口上記入チェック(100) && TFLAG:お仕置きフラグ && ONCEEVENT(12)
	PRINTFORMW 「%RAND_SPLIT("どうやら/")%お仕置きが必要みたいね」
	PRINTFORMW 「さぁ、覚悟はいいかしら？」
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;お仕置き中 FIRSTEVENT(21)
;━━━━━━━━━━━━━━━━━━━━━━━━
;100%で落として内部で確率変化
;------------------------------------------------
IF 口上記入チェック(100) && TFLAG:お仕置きフラグ
	;=================================================
	;前回ACTがお仕置き宣言かつ今回ACTが加虐系
	;=================================================
	IF 口上記入チェック(100) && MENUMATCH(TFLAG:ACT, "加虐") && IS_PREVACTNAME("お仕置きと宣言する")
		PRINTFORMW 「言葉には責任が伴う、ってことで…」
		PRINTFORMW 「さあ、覚悟してもらうわよ。精々頑張ってみせて頂戴」
		RETURN 1
	ENDIF
	
	;=================================================
	;その他
	;=================================================
	IF 口上記入チェック(25)
		PRINTFORMW 「どう？ 少しは%RAND_SPLIT("反省した/堪えた")%かしら」
		SIF ABL:MASTER:マゾっ気 > 4
			PRINTFORMW 「…って。%CNAME(MASTER, TARGET)%にはただのご褒美だったかもだけど」
		RETURN 1
	ENDIF
ENDIF

;────────────────────────────────────
;膣内、腸内射精直後 FIRSTEVENT(30)～
;────────────────────────────────────
;NOWEX:射精ないしNOWEX:MASTER:射精で射精種の参照も可能だったりしますが
;EVENT20, 絶頂口上に同種の分岐がると被る可能性もあります
;-------------------------------------------------
;関連変数…
;
;TFLAG:前回中出し(1=Ｖ性交/2=Ｖ性交奉仕/3=Ｖ性交(空射)/4=Ｖ性交奉仕(空射)/5=Ａ性交/6=Ａ性交奉仕/7=Ａ性交(空射)/8=Ａ性交奉仕(空射)
;NOWEX:射精(1=空射精/2=射精/3=大量射精/4=射精(早漏)/5=A責射精)
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;膣内射精直後(調教対象→調教者)
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && GROUPMATCH(TFLAG:前回中出し, 2, 4) > 0
	;=================================================
	;性交奉仕系ACTに続く場合
	;=================================================
	IF 口上記入チェック(100) && TFLAG:抜かず
		;今日の方針が子作り
		IF IS_POLICY("今日", "子作り")
			;調教対象射精種派生
			SELECTCASE TCVAR:MASTER:前回ターン射精
				;空射精
				CASE 1
					PRINTFORMW 「私を孕ませたいとか言っておきながらこのザマとか…、あり得ないわね」
				;大量射精
				CASE 3
					PRINTFORMW 「いっぱい注ぎ込んでくれちゃって…」
					PRINTFORMW 「%CNAME(MASTER, TARGET)%…、そんなにこの私を孕ませたいの？ ふふ」
				;早漏
				CASE 4
					PRINTFORMW 「早漏とか許し難いんですけど？ まぁ…、子作りって目的には適ってるけど」
				;前立腺射精
				CASE 5
					PRINTFORMW 「おしりが良くなってイっちゃうとか…。ましてや子作りの日に。ねぇ？」
				;その他
				CASEELSE
					PRINTFORMW 「なによ、この程度しか出ないの？」
					PRINTFORMW 「孕ませたいとか言った割には、ちょっと不甲斐ないんじゃない？」
			ENDSELECT
			PRINTFORMW 「ん…、まだしっかり硬いし、もう少しいけるわよね？ 私だって…」
			;ここから
			PRINTFORM 「%CNAME(MASTER, TARGET)%に『
			CALL PRINTFONTSTYLE(COLOR_K14("white"), "種付けしたい", 1)
			PRINTFORMW 』って言われて%RAND_SPLIT("その気になってる/スイッチ入っちゃった/まんざらじゃない/火が付いちゃった")%んだから…」
			;ここまで
			PRINTFORMW 「そのくらいは察し%RAND_SPLIT("なさいよ…/てよね")%」
		;子作りでない場合
		ELSE
			;調教対象射精種派生
			SELECTCASE TCVAR:MASTER:前回ターン射精
				;空射精
				CASE 1
					PRINTFORMW 「何も出ないけど、まだ硬いし……いけそうよね」
				;大量射精
				CASE 3
					PRINTFORMW 「こんなにいっぱい出るなら、まだまだいけそうよね」
				;早漏
				CASE 4
					;調教者が前回ターンに絶頂している場合
					IF TCVAR:前回ターンＣ絶頂 || TCVAR:前回ターンＶ絶頂 || TCVAR:前回ターンＡ絶頂 || TCVAR:前回ターンＢ絶頂
						PRINTFORML 「なによ、随分早いじゃない…って」
						PRINTFORMW 「そんな%CNAME(MASTER, TARGET)%にイかされちゃった、私が言えた義理じゃないけど\@ TENSION(TARGET, 8) == "高い" ? 。ふふ… # …… \@」
					;その他
					ELSE
						PRINTFORMW 「自分だけさっさとイっちゃって……、それで済むと思ったら大間違いよ」
					ENDIF
				;前立腺射精
				CASE 5
					PRINTFORMW 「おしり弄られてイっちゃうような不甲斐ない子には、お仕置きが必要よね」
				;その他
				CASEELSE
					PRINTFORMW 「まだ硬いのね……。これはもう少しいけそうかしら」
			ENDSELECT
			PRINTFORMW 「ほら、ぼさっとしてないで続けるわよ！」
			PRINTFORM 「限界まで搾り取ってあげるから、精々覚悟しておくのね…
			CALL PRINT_SYMBOL_K14
			;1/3で真
			SIF !RAND:3
				PRINTFORMW 「%CNAME(MASTER, TARGET)%だって、それが望みなんでしょう？ \@ AFFECTION("相思相愛") ? ふふ # フフ \@…」
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;空射精
	;=================================================
	IF 口上記入チェック(100) && TFLAG:前回中出し == 4
		SELECTCASE RANDEVENT(2, 30)
			CASE 0
				PRINTFORMW 「あら…？ もう出ないみたいね」
				PRINTFORMW 「搾り取り過ぎちゃったかしら」
			CASE 1
				PRINTFORMW 「この程度で打ち止めとか、不甲斐ないわね」
		ENDSELECT
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	;調教対象射精種派生
	SELECTCASE NOWEX:MASTER:射精
		;大量射精
		CASE 3
			SELECTCASE RANDEVENT(2, 33)
				CASE 0
					PRINTFORMW 「私の中にいっぱい注ぎこんでくれたお礼に、何か考えないと。ねぇ？ ふふ」
				CASE 1
					PRINTFORMW 「いくら私の中が良かったとは言え、限度ってものがあると思うけど。まぁ…」
					PRINTFORMW 「この調子なら、今日はたっぷり絞り取れそうよねぇ？ フフ…」
			ENDSELECT
		;射精(早漏)
		CASE 4
			SELECTCASE RANDEVENT(2, 34)
				CASE 0
					PRINTFORMW 「勝手に一人でイっちゃうような不出来な代物は…」
					PRINTFORMW 「一から躾けなおす必要がありそうね」
				CASE 1
					PRINTFORMW 「私とのセックスの最中に許可なく勝手に射精とか、あり得ないわね」
					PRINTFORMW 「いいわよ。堪え性のない早漏ちんぽは、この私が鍛えなおしてやるから…」
			ENDSELECT
		;A責射精
		CASE 5
			PRINTFORMW 「おしり弄られた程度で射精しちゃうとか…」
			PRINTFORMW 「ある意味、セックスしてる相手に対しての侮辱よね」
		;その他
		CASEELSE
			PRINTFORMW 「%CNAME(MASTER, TARGET)%、まだいけそうよねぇ？」
			PRINTFORMW 「次はどうやって搾り取ってやろうかしら。ふふ」
	ENDSELECT
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;腸内射精直後(調教対象→調教者)
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(25) && GROUPMATCH(TFLAG:前回中出し, 6, 8) > 0
	;=================================================
	;空射精
	;=================================================
	IF 口上記入チェック(100) && TFLAG:前回中出し == 8
		PRINTFORMW 「あら、打ち止めかしら…？」
		PRINTFORMW 「なんか物足りないわね」
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	PRINTFORMW 「んっ…お腹の中…あったかい…」
	PRINTFORMW 「ふふ、お尻で妊娠させるつもりかしら」
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;膣内射精直後(調教者→調教対象)
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(25) && GROUPMATCH(TFLAG:前回中出し, 1, 3) > 0
	;=================================================
	;空射精
	;=================================================
	IF 口上記入チェック(100) && TFLAG:前回中出し == 3
		PRINTFORMW 「んくっ…はぁ、はぁ……」
		PRINTFORMW 「ふふっ…もう打ち止めみたい」
		RETURN 1
	ENDIF
	
	;-------------------------
	;派生を処理(調教者射精)
	;-------------------------
	SELECTCASE NOWEX:射精
		;大量射精
		CASE 3
			PRINTFORMW 「ふふ、溢れてきちゃってるわね」
			PRINTFORMW 「ほら、指で掻き回してあげる」
		;その他
		CASEELSE
			SELECTCASE RANDEVENT(2, 34)
				CASE 0
					PRINTFORMW 「この、射精の瞬間の感覚…病みつきになっちゃいそうね……」
				CASE 1
					PRINTFORM 「ふふっ…、膣内に出しちゃったわ
					CALL PRINT_SYMBOL_K14
					PRINTFORMW 「んっ…まだ出てるわ……全部注ぎ込んであげる」
			ENDSELECT
	ENDSELECT
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;腸内射精直後(調教者→調教対象)
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(25) && GROUPMATCH(TFLAG:前回中出し, 5, 7) > 0
	;=================================================
	;空射精
	;=================================================
	IF 口上記入チェック(100) && TFLAG:前回中出し == 7
		PRINTFORMW 「んくっ…はぁ、はぁ……」
		PRINTFORMW 「やだ…もう打ち止めみたいね」
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	PRINTFORMW 「ふふっ…お尻にの中出しちゃったわ」
	PRINTFORM 「これ、%CNAME(MASTER, TARGET)%を征服した感じがして最高ね
	CALL PRINT_SYMBOL_K14
	RETURN 1
ENDIF

;────────────────────────────────────
;性交中・性交奉仕中 FIRSTEVENT(40)～
;────────────────────────────────────
;関連変数…
;TEQUIP:性交中(調教者が調教対象に挿入中) (1=正常位/2=後背位/3=騎乗位/4=対面座位/5=背面座位/6=アナルセックス)
;TEQUIP:性交奉仕中(調教対象が調教者に挿入中) 内容は上記のものと同じ
;
;関連関数…
;V_SEX (=TARGET, TEQUIP:性交中 < 6/=MASTER, TEQUIP:性交奉仕中 < 6)と同義
;A_SEX (=TARGET, TEQUIP:性交中 == 6/MASTER, TEQUIP:性交奉仕中 == 6)と同義
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;性交中(調教者→調教対象)
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(30) && TEQUIP:性交中 && MENUMATCH(TFLAG:ACT, "性交")
	;=================================================
	;ペニスバンド使用
	;=================================================
	IF 口上記入チェック(100) && !PENIS(TARGET) && TEQUIP:調教者ペニスバンド
		;Ａ性交中
		IF A_SEX()
			PRINTFORMW 「はっ…はぁ…」
			PRINTFORMW 「%CNAME(MASTER, TARGET)%のおしり…もっと、もっと突いてあげるわ…」
		;その他
		ELSE
			PRINTFORMW 「はっ…んんっ…」
			PRINTFORMW 「%CNAME(MASTER, TARGET)%……、こんなおもちゃで突かれても、よくなっちゃうのかしら」
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;Ａ性交中
	;=================================================
	IF 口上記入チェック(100) && A_SEX()
		SELECTCASE RANDEVENT(2, 41)
			CASE 0
				PRINTFORMW 「はんっ…んんっ…」
				PRINTFORMW 「お尻の穴、もっと突いてあげるわ…」
			CASE 1
				PRINTFORMW 「お尻の穴で、根元までくわえ込んじゃって…」
				PRINTFORMW 「ふふっ…もっと犯してあげるわ」
		ENDSELECT
		RETURN 1
	ENDIF
	
	;=================================================
	;Ｖ性交中
	;=================================================
	IF 口上記入チェック(100) && V_SEX()
		SELECTCASE RANDEVENT(2, 42)
			CASE 0
				PRINTFORMW 「んくっ…%CNAME(MASTER, TARGET)%の膣内キツくてとってもいいわよ」
				PRINTFORMW 「ほら、もっと気持ちよくなって…」
			CASE 1
				PRINTFORMW 「ほら、どう…？　いいのよ、イっちゃっても…」
				PRINTFORM 「フフ…壊れちゃうくらい突きまくってあげるわ
				CALL PRINT_SYMBOL_K14
		ENDSELECT
		RETURN 1
	ENDIF
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;性交奉仕中(調教対象→調教者)
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(30) && TEQUIP:性交奉仕中 && MENUMATCH(TFLAG:ACT, "性奉")
	;=================================================
	;Ａ性交奉仕中
	;=================================================
	IF 口上記入チェック(100) && A_SEX(MASTER)
		SELECTCASE RANDEVENT(2, 43)
			CASE 0
				PRINTFORMW 「んんっ……あぅっ……やっ、あぁ…ッ」
				PRINTFORMW 「お尻の穴､良すぎてっ…　バカになっちゃう…ッ」
			CASE 1
				PRINTFORMW 「っふあ…！…ああぁっ……！」
				PRINTFORMW 「突いてッ､もっと……壊れちゃうくらい…ッ」
			CASE 2
				PRINTFORMW 「やんっ…あぁっ、んあっ……！」
				PRINTFORMW 「も、もう…変になる…おかしくなっちゃうぅ…ッ」
			CASE 3
				PRINTFORMW 「んっ…んんっ……」
				PRINTFORMW 「お尻の穴、気持ちいい…」
		ENDSELECT
		RETURN 1
	ENDIF
	
	;=================================================
	;Ｖ性交奉仕中
	;=================================================
	IF 口上記入チェック(100) && V_SEX(MASTER)
		SELECTCASE RANDEVENT(4, 44)
			CASE 0
				PRINTFORMW 「くっ…あっ…あッ……ああぁっ……」
				PRINTFORMW 「突いてっ、奥までっ……めちゃめちゃに犯してっ……」
			CASE 1
				PRINTFORMW 「っふあ……あぁっ…！…あんっ……」
				PRINTFORMW 「突いて…もっと……壊れちゃうくらい」
			CASE 2
				PRINTFORMW 「やぁっ……あっ……っふあ……あぅっ…ッ」
				PRINTFORMW 「中が擦れてっ…ゾクゾクしちゃう…ッ」
			CASE 3
				PRINTFORMW 「んっ…私の中はどうかしら？」
				PRINTFORMW 「ねぇ…もっと動いて」
		ENDSELECT
		RETURN 1
	ENDIF
ENDIF

;────────────────────────────────────
;絶頂した直後 FIRSTEVENT(50)～
;────────────────────────────────────
;・調教者、調教対象が絶頂した場合に表示される口上
;・個別の部位別絶頂を拾いたいなら、IF 口上記入チェック(100) && NOWEX:Ｃ絶頂等の分岐を追加してください
;・射精種はNOWEX:射精/NOWEX:MASTER:射精で参照する事が可能です
;-------------------------------------------------
;関連変数…
;NOWEX:射精(1=空射精/2=射精/3=大量射精/4=射精(早漏)/5=A責射精)
;
;関連関数…
;TRUECHECK 真になるものがいくつあるかを返す
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;調教者が絶頂した直後
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TRUECHECK(NOWEX:Ｃ絶頂, NOWEX:Ｖ絶頂, NOWEX:Ａ絶頂, NOWEX:Ｂ絶頂) > 0
	LOCALS = %RANDTEXT_K14("はぁ……はぁ……はぁ", "ハァ、ハァ……、ハァ")%
	;=================================================
	;初回
	;=================================================
	IF 口上記入チェック(100) && FIRSTEVENT(50)
		PRINTFORML 「%LOCALS%……」
		PRINTFORMW 「知らなかったわ、こんなの…」
		PRINTFORMDW %CALLNAME%は初めての絶頂に戸惑いながらも、その余韻に浸っている…
	ENDIF
	
	;=================================================
	;深くイった
	;=================================================
	IF 口上記入チェック(100) && (NOWEX:Ｃ絶頂 + NOWEX:Ｖ絶頂 + NOWEX:Ａ絶頂 + NOWEX:Ｂ絶頂) > 1
		;多重絶頂
		IF TRUECHECK(NOWEX:Ｃ絶頂, NOWEX:Ｖ絶頂, NOWEX:Ａ絶頂, NOWEX:Ｂ絶頂) > 1
			PRINTFORMW 「%LOCALS%……こんな、こんなのって…」
			PRINTFORMW 「こんなの、繰り返してたら…頭の悪い女になっちゃいそう……」
		;強絶頂
		ELSE
			PRINTFORMW 「%LOCALS%……これ、ちょっと凄すぎるわね……」
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;条件なし
	;=================================================
	IF 口上記入チェック(30)
		PRINTFORMW 「%LOCALS%……」
		PRINTFORMDW アリスは\@ NOWEX:射精 ? 射精 # 絶頂 \@の余韻に浸りながら、呼吸を整えている…
		RETURN 1
	ENDIF
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;調教対象が絶頂した直後
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(30) && TRUECHECK(NOWEX:MASTER:Ｃ絶頂, NOWEX:MASTER:Ｖ絶頂, NOWEX:MASTER:Ａ絶頂, NOWEX:MASTER:Ｂ絶頂) > 0
	;=================================================
	;深くイかせた
	;=================================================
	IF 口上記入チェック(100) && (NOWEX:MASTER:Ｃ絶頂 + NOWEX:MASTER:Ｖ絶頂 + NOWEX:MASTER:Ａ絶頂 + NOWEX:MASTER:Ｂ絶頂) > 1
		;多重絶頂
		IF TRUECHECK(NOWEX:MASTER:Ｃ絶頂, NOWEX:MASTER:Ｖ絶頂, NOWEX:MASTER:Ａ絶頂, NOWEX:MASTER:Ｂ絶頂) > 1
			PRINTFORMW 「すごいわね…%CNAME(MASTER, TARGET)%……最高よ…」
			PRINTFORM 「ホント、なんて可愛いのかしら…
			CALL PRINT_SYMBOL_K14
		;強絶頂
		ELSE
			PRINTFORMW 「とってもいいイき顔よ…」
			PRINTFORMW 「まだ、ビクビクしてる……痙攣が止まらないわね？ ふふっ…」
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;射精させた後
	;=================================================
	IF 口上記入チェック(100) && NOWEX:MASTER:射精
		IF AFFECTION("溺愛") && TENSION(TARGET, 8) == "高い"
			PRINTFORM 「あらあら、満足そうな顔しちゃって…
			CALL PRINT_SYMBOL_K14
		ELSE
			SELECTCASE RANDEVENT(2, 55)
				CASE 0
					PRINTFORMW 「ふん、他愛無いわね。所詮快楽には抗えないのよ」
				CASE 1
					PRINTFORMW 「ま、こんなものよね…」
					PRINTFORMW 「私にイかされて%CNAME(MASTER, TARGET)%は嬉しいのかしら、それとも悔しいのかしら？」
					PRINTFORM 「別にどっちでもいいんだけど、ね
					CALL PRINT_SYMBOL_K14
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	PRINTFORMW 「\@ AFFECTION("溺愛") ? ふふっ # フフ \@、イっ%RAND_SPLIT("ちゃっ/")%たのね…」
	PRINTFORM 「\@ TALENT:MASTER:オトコ ? 他愛ない # 可愛らしい \@わ
	CALL PRINT_SYMBOL_K14
	RETURN 1
ENDIF

;────────────────────────────────────
;調教方針変更 FIRSTEVENT(60)
;────────────────────────────────────
;・調教方針が変わったときに表示する
;・あくまでも方針なので行動を細かく決定するものではない
;-------------------------------------------------
;関連変数…
;TFLAG:ターン方針		このターンの調教方針(1=休憩/2=ソフト/3=ノーマル/4=ハード/5=異常)
;TFLAG:前ターン方針		前ターンの調教方針(内容はターン方針と同じ)
;
;関連関数…
;IS_POLICY	調教方針名称と合致すれば真を返す
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;調教方針変更
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(25) && (TFLAG:ターン方針 != TFLAG:前ターン方針)
	;=================================================
	;ターン方針1:休憩（確実に体力回復行動になる）
	;=================================================
	IF 口上記入チェック(100) && IS_POLICY("今", "休憩")
		SELECTCASE RANDEVENT(2, 60)
			CASE 0
				PRINTFORMW 「ちょっと疲れたわね…、休憩にしましょう」
				PRINTFORM 「大丈夫…、時間はまだあるわ
				CALL PRINT_SYMBOL_K14
			CASE 1
				PRINTFORMW 「ちょっと休憩にしようかしら？」
		ENDSELECT
		RETURN 1
	ENDIF
	
	;=================================================
	;ターン方針2:ソフト
	;=================================================
	IF 口上記入チェック(100) && IS_POLICY("今", "ソフト")
		PRINTFORM 「さ、可愛がってあげるわ。こっちにいらっしゃい
		CALL PRINT_SYMBOL_K14
		PRINTFORMDW アリスは慈愛に満ちた表情で%CALLNAME:MASTER%を見つめている…
		RETURN 1
	ENDIF
	
	;=================================================
	;ターン方針3:ノーマル
	;=================================================
	IF 口上記入チェック(100) && IS_POLICY("今", "ノーマル")
		PRINTFORMW 「普通が１番かしらね…」
		RETURN 1
	ENDIF
	
	;=================================================
	;ターン方針4:ハード
	;=================================================
	IF 口上記入チェック(100) && IS_POLICY("今", "ハード")
		PRINTFORMW 「さぁ、激しくいくわよ！」
		PRINTFORMW 「覚悟なさい」
		RETURN 1
	ENDIF
	
	;=================================================
	;ターン方針5:異常
	;=================================================
	IF 口上記入チェック(100) && IS_POLICY("今", "異常")
		PRINTFORMW 「…………」
		PRINTFORMDW アリスは瞳に妖しい光を湛えながら、%CALLNAME:MASTER%ににじり寄ってきた…
		RETURN 1
	ENDIF
ENDIF

;────────────────────────────────────
;条件なし FIRSTEVENT(70)～
;────────────────────────────────────
;・ここまでの条件にヒットしなかった場合に表示される箇所
;・BASEで小分岐を設けてありますが、任意の条件を追加する事もできます
;------------------------------------------------
;関連変数…
;BASE		(0, 体力/1, 気力/2, 射精/3, 母乳/4, 尿意/5, 理性/6, 興味/7, 苛立ち/8, 満足)
;
;関連関数…
;TENSION	引数で指定したBASE値を表す(50%以上は高い、25%以上は一般、25%未満は低い)
;------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;条件なし
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(25)
	;=================================================
	;満足50％以上
	;=================================================
	IF 口上記入チェック(100) && TENSION(TARGET, 8) == "高い" ;BASERATIO(TARGET, 8, 80)
		PRINTFORMW 「いいわ…なかなかいい感じよ」
		PRINTFORM 「さぁ、続けるわよ
		CALL PRINT_SYMBOL_K14
		RETURN 1
	ENDIF
	
	;=================================================
	;理性25％以下
	;=================================================
	IF 口上記入チェック(100) && TENSION(TARGET, 5) == "低い" ;!BASERATIO(TARGET, 5, 20)
		PRINTFORMW 「くっ…私…も、もう……」
		RETURN 1
	ENDIF
	
	;=================================================
	;興味25％以下
	;=================================================
	IF 口上記入チェック(100) && TENSION(TARGET, 6) == "低い" ;!BASERATIO(TARGET, 6, 20)
		PRINTFORMW 「なんていうか…%RANDTEXT_K14("興ざめ、ね", "飽きてきたわね")%」
		RETURN 1
	ENDIF
	
	;=================================================
	;苛立ち50％以上
	;=================================================
	IF 口上記入チェック(100) && TENSION(TARGET, 7) == "高い" ;BASERATIO(TARGET, 7, 50)
		PRINTFORMW 「そんなに私を怒らせたいのかしらね？」
		PRINTFORMW 「いいわ、思い知らせてあげる……」
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	IF GET_KFLAG_K14("相思相愛") && !RAND:3
		PRINTFORM 「ふふ……楽しいわね
		CALL PRINT_SYMBOL_K14
	ELSE
		PRINTFORMW 「さて、%RAND_SPLIT("じゃぁお次は/次はどうしようかしらね")%……」
	ENDIF
	RETURN 1
ENDIF