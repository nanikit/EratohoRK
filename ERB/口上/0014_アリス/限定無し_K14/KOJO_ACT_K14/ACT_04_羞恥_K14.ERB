;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	アリス口上 羞恥系アクション口上ファイル
;
;	羞恥系アクション口上をまとめたファイル
;
;Version		Reason
;1.71			@口上記入チェック仕様に。その他細かな修正
;1.67			一部修正
;1.62			全体的に手入れ。自慰、自慰見せつけに加筆
;1.60			ACT分類単体ファイルとして独立
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;========================================================================
;羞恥系 ACT40～45
;========================================================================
;・TFLAG:ACT(40=自慰/41=秘貝開帳/42=自慰みせつけ/43=羞恥プレイ/44=野外プレイ/45=放尿/46=コスプレ(未実装))
;・TFLAG:ACT分類 == 4に該当
;・汎用リアクションにおいてはMENUMATCH(TFLAG:ACT, "羞恥")で拾う事が可能
;・ACTによっては拒否が可能であったりなかったり
;-------------------------------------------------

;────────────────────────────────────
;ACT40, 自慰
;────────────────────────────────────
;・オナニーしてみせろと命令する
;・この段階で命令に従うか否かは未定
;・REACT分類が消極的従う/平然と従う/積極的従う/受け入れる以外の場合は、全てREACT分類"拒否"扱い
;・ターン方針が"異常"だと発生しない
;・押し倒し中である場合は発生しない
;・すでに自慰中である場合は発生しない
;・TEQUIP(縄/三角木馬)装着中には発生しない
;・性交中/性交奉仕中には発生しない
;-------------------------------------------------
@KOJO_ACT_K14_40
;-------------------------------------------------
;・ACT派生なし
;-------------------------------------------------
;・地の文に分岐はなし
;・@TARGET_DESCRIPTIONで状況描写が入る場合あり
;・調教対象のペニスの有無、ペニス無しの場合はペニスバンドの有無による書き分けが考えられる
;・調教対象がオトコであれば快Ｂは発生しない
;-------------------------------------------------

;性交中、性交奉仕中でなければ特定のTEQUIPは解除する
IF TRUECHECK(!TEQUIP:性交中, !TEQUIP:性交奉仕中) > 0
	TEQUIP:調教対象Ｃ使用 = 0
	TEQUIP:顔面騎乗 = 0
	TEQUIP:顔面騎乗アナル = 0
ENDIF
LOCALS = \@ AFFECTION("相思相愛") ? クスクス # フフフ \@

;━━━━━━━━━━━━━━━━━━━━━━━━
;お仕置きモード RANDACT(30)
;━━━━━━━━━━━━━━━━━━━━━━━━
;PENISの有無では分けずに上層でまとめてしまう。確率を絞る
;------------------------------------------------
IF 口上記入チェック(40) && TFLAG:お仕置きフラグ
	SELECTCASE RANDACT(3, 30)
		CASE 0
			PRINTFORMW 「さぁ、自慰をしなさい」
			PRINTFORM 「無様に自分を慰めるところ、じっくり見てあげるわ
			CALL PRINT_SYMBOL_K14
		CASE 1
			PRINTFORMW 「じゃ、%CNAME(MASTER, TARGET)%にはオナニーしてみせて貰おうかしら」
			PRINTFORMW 「悪いけど、恥ずかしいとか私の知った事ではないの」
			PRINTFORMW 「さっさと始めなさい……」
		CASE 2
			PRINTFORMW 「%CNAME(MASTER, TARGET)%にオナニーしろって命じるのが、お仕置きになるのか疑問だけど…」
			PRINTFORMW 「取りあえず、私の言うことには従ってもらうわよ。ほら、さっさと始めて頂戴…」
	ENDSELECT
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;ACT40_0, 調教対象にペニスがない場合
;━━━━━━━━━━━━━━━━━━━━━━━━
;TEQUIP:ペニスバンドとか考えるのも面倒なので忘れることにする
;-------------------------------------------------
IF 口上記入チェック(100) && !PENIS(MASTER)
	;=================================================
	;初回
	;=================================================
	IF 口上記入チェック(100) && FIRSTACT(0)
		PRINTFORMW 「さて、自分でするところを見せて貰おうかしら」
		;自慰経験あり
		IF EXP:MASTER:自慰経験 > 0 || EXP:MASTER:調教自慰経験 > 0
			PRINTFORMW 「ふふ、とぼけても無駄よ。別に初めてって訳じゃないでしょう？」
			PRINTFORMW 「普段シてるように、やってみせてくれればそれでいいの」
			PRINTFORMW 「これは勘なんだけど…、%CNAME(MASTER, TARGET)%ってこういうの好きだと思うわ」
			PRINTFORM 「…恥ずかしい程興奮しちゃう。きっとそういう類の人だって、ね
			CALL PRINT_SYMBOL_K14
		;自慰経験無し
		ELSE
			PRINTFORMW 「何も特別な事をしろって言ってる訳じゃないのよ」
			PRINTFORMW 「自分の身体のキモチ良いところを、自分がキモチ良いように弄ればそれでいいの」
			PRINTFORMW 「ただ、そうね？ 初めてのオナニーが人前で、っていうのもなんていうか…」
			PRINTFORMW 「ちょっとアレな感じね？ フフ…」
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;調教対象がイキそう
	;=================================================
	IF 口上記入チェック(100) && TCVAR:MASTER:イきそう
		SELECTCASE RANDACT(3, 1)
			CASE 0
				PRINTFORMW 「ぷっくり膨らませちゃって…。すぐにでもイっちゃいそうね？」
				PRINTFORMW 「まぁでも…、%CNAME(MASTER, TARGET)%が自慰で果てる様を見られるのは悪くないわ」
				PRINTFORM 「私が手を煩わせる事もないし、ね…
				CALL PRINT_SYMBOL_K14
				CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), "「ふふっ…」")
				CLEARLINE 1
			CASE 1
				PRINTFORMW 「へぇ、%CNAME(MASTER, TARGET)%…もうそろそろイきそうじゃない」
				PRINTFORMW 「いい機会だから、自分でイクまでオナニーして貰おうかしら」
				PRINTFORMW 「\@ !TEQUIP:アイマスク ? 私から目を逸らさ # 顔を背け \@ず、私の\@ !TEQUIP:アイマスク ? 目を見 # 方を向き \@ながらするのよ…」
				SIF AFFECTION("相思相愛") && !RAND:2
					PRINTFORMW 「なんだったら、私の名前を呼びながらシてもいいわ。ふふ」
			CASE 2
				PRINTFORMW 「言わなくてもいいわ。%CNAME(MASTER, TARGET)%、私にオナニーしなさいって命じて欲しいんでしょう？」
				PRINTFORMW 「背中%RAND_SPLIT("仰け反らして/丸めて")%\@ TALENT:MASTER:小柄 ? 子猫 # メス豚 \@みたいに鳴きながら、イク瞬間を見て欲しいんでしょう？」
				PRINTFORMW 「止める理由はないから、存分にやるといいわ」
		ENDSELECT
		RETURN 1
	ENDIF
	
	;=================================================
	;調教者も自慰中
	;=================================================
	IF 口上記入チェック(100) && TRUECHECK(GETBIT(TEQUIP:自慰中, 1), TCVAR:イきそう) > 0
		;相思相愛の場合1/2の確率。もしくは調教者がイきそう
		IF (AFFECTION("相思相愛") && !RAND:2) || TCVAR:イきそう
			PRINTFORMW 「んっ、やだ…気持ちいい……指が止まらない…ッ」
			PRINTFORMW 「ねぇ、%CNAME(MASTER, TARGET)%もシて見せて。\@ !TEQUIP:アイマスク ? 私を見ながら気持ちよくなって # %CNAME(MASTER, TARGET)%の声を聞かせて \@…」
		ELSE
			SELECTCASE RANDACT(2, 2)
				CASE 0
					PRINTFORMW 「…良く見て頂戴。私の気持ちいいところ」
					PRINTFORMW 「ほら、%CNAME(MASTER, TARGET)%もシて見せて。何処がキモチ良いのか、私に良く見せて頂戴…」
				CASE 1
					PRINTFORMW 「ほら、%CNAME(MASTER, TARGET)%も\@ !TEQUIP:アイマスク ? 見てる # じっとしてる \@だけじゃ退屈でしょう」
					PRINTFORMW 「いいわよ？ 私\@ !TEQUIP:アイマスク ? がオナニーする姿を見ながらシても # の声を聞きながらオナニーしても \@」
					PRINTFORMW 「%CNAME(MASTER, TARGET)%のイヤラシイ姿、私に見せて頂戴…」
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;反抗刻印あり
	;=================================================
	IF 口上記入チェック(100) && MARK:反抗刻印 > 0
		;この箇所初回のみ表示
		IF FIRSTEVENT(3)
			PRINTFORMW （こういう反抗的な手合いには、屈辱を与えるのが一番かも知れないわね…）
			PRINTL
		ENDIF
		
		IF AFFECTION("溺愛")
			LOCALS = %RAND_SPLIT("クスクス…/ふふふ…")%
		ELSEIF MARK:反抗刻印 > 5
			LOCALS = %RAND_SPLIT("あははっ！/ふん、いい気味だわ…")%
		ELSE
			LOCALS = %RAND_SPLIT("ウフフ…/フフフ…/フフ…")%
		ENDIF
		
		SELECTCASE RANDACT(3, 4)
			CASE 0
				PRINTFORMW 「さて、%CNAME(MASTER, TARGET)%にはここで自慰をしてもらうわ」
				PRINTFORMW 「言ってる事が判らない？ オナニーしろって言ってるのよ！」
			CASE 1
				PRINT 「
				CALL PRINTFONTSTYLE(COLOR2_K14("ALICE"), "オナニーしてみせなさい", 1)
				PRINTW 」
				PRINTFORMW 「……聞こえなかったかしら。今ここで、%CNAME(MASTER, TARGET)%が普段してるようにしてみせなさいって言ってるの」
				PRINTFORMW 「あら、なによその目は。悔しい？ ま…、そうでしょうねぇ」
				;フェードアウト
				CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"「%LOCALS%」")
				;コンフィグを参照
				SIF ANIME_CONFIG("FADE")
					;FADEがONなら行削除
					CLEARLINE 1
			CASE 2
				PRINT 「
				CALL PRINTFONTSTYLE(COLOR2_K14("ALICE"), "ここでオナニーしなさい", 1)
				PRINTW 」
				PRINTFORMW 「ん？ なによ。別に難しい話じゃないと思うけど」
				PRINTFORMW 「普段通りにすればいいのよ。メス豚みたいに浅ましい声を上げながらね。実に簡単なお話よ」
				PRINTFORMW 「あらあら、睨んじゃってどうしたのかしら。まぁ…、悔しいでしょうねぇ」
				;フェードアウト
				CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"「%LOCALS%」")
				;コンフィグを参照
				SIF ANIME_CONFIG("FADE")
					;FADEがONなら行削除
					CLEARLINE 1
		ENDSELECT
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	;調教者満足度派生
	SELECTCASE TENSION(TARGET, 8)
		CASE "高い"
			PRINTFORMW 「さぁて…、%CNAME(MASTER, TARGET)%のオナニー見せて貰おうかしら」
			PRINTFORMW 「この私に見られてる事を意識しながら、思う存分シてちょうだい」
			PRINTFORM 「同じ女同士なんだし、色々と参考にさせて貰うわ
			CALL PRINT_SYMBOL_K14
			;フェードアウト
			CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"「%LOCALS%…」")
			;行削除
			CLEARLINE 1
		CASE "一般"
			PRINTFORMW 「グズグズしないで、さっさと始めてもらおうかしら」
			PRINTFORMW 「恥ずかしがったところで、もう色々と手遅れなんだから、遠慮なくすればいいと思うわよ？」
			PRINTFORMW 「%LOCALS%…」
		CASE "低い"
			PRINTFORMW 「さぁ、オナニーしてみせて頂戴」
			PRINTFORMW 「ほら、%RAND_SPLIT("早く/さっさと")%始めなさいよ。いつもみたいに…」
	ENDSELECT
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;ACT40_1, 調教対象にペニスがある場合
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && PENIS(MASTER)
	;=================================================
	;初回
	;=================================================
	IF 口上記入チェック(100) && FIRSTACT(10)
		PRINTFORMW 「ほら、こっちに来なさい」
		PRINTFORMDL アリスの言葉に進み出た%CALLNAME:MASTER%は、
		PRINTFORMDW その冷ややかな、それでいて何処か悪戯っぽい視線を感じ、心ならずも屹立させてしまう…
		PRINTFORMW 「…手は後ろに。隠さず、キチンと私に見せなさい」
		PRINTL
		;自慰経験あり
		IF EXP:MASTER:自慰経験 > 0 || EXP:MASTER:調教自慰経験 > 0
			PRINTFORMW 「…ここでオナニーをしなさい」
			PRINTFORMW 「いつもしてるようにすればいいのよ。何も難しい事は無いと思うけど？」
			SIF !PENIS(TARGET)
				PRINTFORMW 「なんてったって、私には付いてない代物だからね。どういう風にするのか興味あるのよ」
			PRINTFORMW 「兎に角、私に言われた通りにすればいいの。それが分かったのなら…」
			PRINTFORMW 「グズグズしないで、さっさと始めて頂戴」
		ELSE
			PRINTFORMW 「じゃ、%CNAME(MASTER, TARGET)%が自分でするところを見せて貰おうかしら」
			PRINTFORMW 「ん、初めて？ それ、本気で言ってる？」
			PRINTFORM 「フフ、そうなの。それは、なんていうか、その……ちょっと興奮しちゃうわね
			CALL PRINT_SYMBOL_K14
			PRINTFORMW 「さ、お喋りはここまでよ。そろそろ始めて頂戴」
			PRINTFORMW 「%CNAME(MASTER, TARGET)%が初めてのオナニーをどんな風にするのか、じっくりと見させて貰うから…」
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;前COMがイかせて！
	;=================================================
	IF 口上記入チェック(100) && IS_PREVCOMNAME("イかせて！")
		PRINTFORMDW アリスはイかせて！とせがむ%CALLNAME:MASTER%に、自慰をするよう命じた…
		SELECTCASE TENSION(TARGET, 8)
			CASE "高い"
				PRINTFORMW 「いいわよ？ 勝手にイきなさいよ」
				PRINTFORMW 「とりあえず、私は何もしてあげるつもりはないから…」
				CALL FADEIN(13, "「自分でなんとかする事ね？」")
				WAIT
			CASE "一般"
				PRINTFORMW 「そんなにイきたいなら、好きにすれば？」
				PRINTFORMW 「私はここで鑑賞させてもらうから」
			CASE "低い"
				PRINTFORMW 「いいわ。好きなようにイきなさいよ。ただし…自分で、ね？」
		ENDSELECT
		CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"「%LOCALS%…」")
		CLEARLINE 1
		RETURN 1
	ENDIF
	
	;=================================================
	;前COMが気持ち良くして
	;=================================================
	IF 口上記入チェック(100) && IS_PREVCOMNAME("気持ち良くして")
		PRINTFORMDW アリスは気持ち良くしてとせがむ%CALLNAME:MASTER%に、自慰をするよう命じた…
		SELECTCASE RANDACT(2, 12)
			CASE 0
				PRINTFORMW 「…気持ち良くして、ねぇ。まぁいいけど」
				PRINTFORMW 「とりあえず、自分でなんとかして頂戴。ふふ…」
			CASE 1
				PRINTFORMW 「どうぞご自由に。自分の事は自分で面倒みることね」
				PRINTFORMW 「なによ、判らないの？ 勝手にオナニーでもなんでもすればって言ってるのよ」
				PRINTFORMW 「私は、高みの見物と洒落込ませて貰うから…」
		ENDSELECT
		RETURN 1
	ENDIF
	
	;=================================================
	;調教対象がイキそう
	;=================================================
	IF 口上記入チェック(100) && TCVAR:MASTER:イきそう
		SELECTCASE RANDACT(3, 13)
			CASE 0
				PRINTFORMW 「…ビクビクさせちゃて。扱いたらすぐにイっちゃいそうね？」
				PRINTFORMW 「まぁでも…、%CNAME(MASTER, TARGET)%が自慰で果てる様を見られるのは悪くないわ」
				PRINTFORM 「私が手を煩わせる事もないし、ね…
				CALL PRINT_SYMBOL_K14
				CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), "「ふふっ…」")
				CLEARLINE 1
			CASE 1
				PRINTFORMW 「へぇ、%CNAME(MASTER, TARGET)%…もうそろそろ出ちゃいそうじゃない」
				PRINTFORMW 「いい機会だから、自分でイクまでオナニーして貰おうかしら」
				PRINTFORMW 「\@ !TEQUIP:アイマスク ? 私から目を逸らさ # 顔を背け \@ず、私の\@ !TEQUIP:アイマスク ? 目を見 # 方を向き \@ながらするのよ…」
				SIF AFFECTION("相思相愛") && !RAND:2
					PRINTFORMW 「なんだったら、私の名前を呼びながらシてもいいわ。ふふ」
			CASE 2
				PRINTFORMW 「言わなくてもいいわ。%CNAME(MASTER, TARGET)%、私にオナニーしなさいって命じて欲しいんでしょう？」
				PRINTFORM 「背中%RAND_SPLIT("仰け反らして/丸めて")%
				;調教対象がオトコ
				IF TALENT:MASTER:オトコ
					PRINTFORM \@ TALENT:MASTER:小柄 ? 女の子 # 豚 \@
				;調教対象がふたなり
				ELSE
					PRINTFORM メス豚
				ENDIF
				PRINTFORMW みたいに鳴きながら、精液ぶちまけるとこ見て欲しいんでしょう？」
				PRINTFORMW 「止める理由はないから、存分にやるといいわ」
		ENDSELECT
		RETURN 1
	ENDIF
	
	;=================================================
	;調教者も自慰中
	;=================================================
	IF 口上記入チェック(100) && GETBIT(TEQUIP:自慰中, 1)
		IF (AFFECTION("相思相愛") && !RAND:2) || TCVAR:イきそう
			PRINTFORMW 「…んっ……やだ…気持ちいい……どうして……こんなに…ッ」
			PRINTFORMW 「…ねぇ、%CNAME(MASTER, TARGET)%もシて見せて。私を見ながら気持ちよくなって……」
		ELSE
			SELECTCASE RANDACT(2, 15)
				CASE 0
					PRINTFORMW 「…ほら、良く見て。私の気持ちいいところ…」
					PRINTFORMW 「%CNAME(MASTER, TARGET)%も見せて。どんな風にするのか、私に良く見せて頂戴…」
				CASE 1
					PRINTFORMW 「ほら、%CNAME(MASTER, TARGET)%も\@ !TEQUIP:アイマスク ? 見てる # ぼんやりしてる \@だけじゃ退屈でしょう」
					PRINTFORMW 「いいわよ？ 私が自分でシてる\@ !TEQUIP:アイマスク ? とこ # 声 \@、オカズにしても」
					PRINTFORMW 「%CNAME(MASTER, TARGET)%の情けない姿、私に見せて頂戴…」
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;オナホール装着中
	;=================================================
	IF 口上記入チェック(100) && TEQUIP:オナホール
		SELECTCASE RANDACT(3, 16)
			CASE 0
				PRINTFORMW 「さぁ、オナホで自分のをシゴいてみせなさい」
				PRINTFORMW 「%CNAME(MASTER, TARGET)%の無様な姿、ちゃんと見ててあげるから…」
			CASE 1
				PRINTFORMW 「いい機会ね。ソレを使って自分でシごいてみせなさい」
				PRINTFORMW 「回りを汚す心配もないし、心置きなくできるでしょう？」
				PRINTFORMW 「%LOCALS%…」
			CASE 2
				PRINTFORMW 「そうねぇ、折角オナホを付けてるんだから…そのままオナニーして貰おうかしら」
				PRINTFORMW 「正直、画的に見栄えがしないにも程があるけど。%CNAME(MASTER, TARGET)%にはお似合いの姿だと思うわよ？」
				CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"「%LOCALS%…」")
				CLEARLINE 1
		ENDSELECT
		RETURN 1
	ENDIF
	
	;=================================================
	;反抗刻印あり
	;=================================================
	IF 口上記入チェック(100) && MARK:反抗刻印 > 0
		;この箇所初回のみ表示
		IF FIRSTEVENT(17)
			PRINTFORMW （こういう反抗的な手合いには、屈辱を与えるのが一番かも知れないわね…）
			PRINTL
		ENDIF
		
		IF AFFECTION("溺愛")
			LOCALS = %RAND_SPLIT("クスクス…/ふふふ…")%
		ELSEIF MARK:反抗刻印 > 5
			LOCALS = %RAND_SPLIT("あははっ！/ふん、いい気味だわ…")%
		ELSE
			LOCALS = %RAND_SPLIT("ウフフ…/フフフ…/フフ…")%
		ENDIF
		
		SELECTCASE RANDACT(3, 17)
			CASE 0
				PRINTFORMW 「さて、%CNAME(MASTER, TARGET)%にはここで自慰をしてもらうわ」
				PRINTFORMW 「言ってる事が判らない？ オナニーしろって言ってるのよ！」
			CASE 1
				PRINT 「
				CALL PRINTFONTSTYLE(COLOR2_K14("ALICE"), "オナニーしてみせなさい", 1)
				PRINTW 」
				PRINTFORMW 「……聞こえなかったかしら。そこにぶら下げてるモノを扱いてみせなさいと言ってるの」
				PRINTFORMW 「あら、なによその目は。悔しい？ ま…、そうでしょうねぇ」
				;フェードアウト
				CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"「%LOCALS%」")
				;コンフィグを参照
				SIF ANIME_CONFIG("FADE")
					;FADEがONなら行削除
					CLEARLINE 1
			CASE 2
				PRINT 「
				CALL PRINTFONTSTYLE(COLOR2_K14("ALICE"), "ここでオナニーしなさい", 1)
				PRINTW 」
				PRINTFORMW 「ん？ なによ。別に難しい話じゃないと思うけど」
				PRINTFORMW 「精液ぶちまけるまで、その粗末なモノをシコシコすれば済むだけの…、実に簡単なお話よ」
				PRINTFORMW 「あらあら、睨んじゃってどうしたのかしら。まぁ…、悔しいでしょうねぇ」
				;フェードアウト
				CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"「%LOCALS%」")
				;コンフィグを参照
				SIF ANIME_CONFIG("FADE")
					;FADEがONなら行削除
					CLEARLINE 1
		ENDSELECT
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	;調教者満足度派生
	SELECTCASE TENSION(TARGET, 8)
		CASE "高い"
			PRINTFORMW 「ま、そういう訳だから…、%CNAME(MASTER, TARGET)%にはここでオナニーをして貰うわ」
			PRINTFORMW 「何がそういう訳とか、空気の読めない発言は控えるのね」
			PRINTFORMW 「ほら、グズグズしないで…、さっさと始めなさい」
			SIF !TEQUIP:アイマスク
				PRINTFORMW 「私から目を逸らさず、私の目を見ながらするのよ…」
		CASE "一般"
			PRINTFORMW 「じゃ、自分でするところを見せて貰おうかしら」
			PRINTFORMW 「%CNAME(MASTER, TARGET)%、私に見られながらするの好きでしょう？」
			CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"「%LOCALS%…」")
			CLEARLINE 1
		CASE "低い"
			PRINTFORMW 「さぁ、オナニーしてみせなさい」
			PRINTFORMW 「恥ずかしがらず、私に良く見えるように…」
	ENDSELECT
	RETURN 1
ENDIF

;────────────────────────────────────
;ACT41, 秘貝開帳
;────────────────────────────────────
;・調教者が調教対象の秘貝を開帳する。所謂「くぱぁ」
;・調教対象がオトコの場合は発生しない
;・TFLAG:お仕置きフラグが立っていると発生しない
;・ターン方針が"異常"の場合は発生しない
;・TEQUIP:三角木馬装着中は発生しない
;・押し倒し中は発生しない
;・性交中/性交奉仕中は発生しない
;・このACTに派生はない
;・現在リアクション後の@ACTION_APPLY2に処理なし
;-------------------------------------------------
@KOJO_ACT_K14_41
;-------------------------------------------------
;・顔面騎乗、自慰中は解除される
;・下半身全部強制脱衣の処理あり
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;ACT41, 秘貝開帳
;━━━━━━━━━━━━━━━━━━━━━━━━
;これはやっぱりアレだ。PENIS(MASTER)の派生が欲しいなｗ
;「ちっちゃーい」だの「お粗末ね…」だの言われたい感
;まぁそれはいいとしても、ACTが特定のTALENT限定というのは無駄が多いし認めたくない類の代物
;現状、ACTの偏りが避けられない以上、可能性は多い方が良い
;------------------------------------------------
IF 口上記入チェック(100)
	;=================================================
	;初回
	;=================================================
	IF 口上記入チェック(100) && FIRSTACT(0)
		IF TALENT:MASTER:小柄
			PRINTFORMW 「当たり前だけど、やっぱり子供って感じよね」
			PRINTFORMW 「なんていうかコレ……、凄くイケナイことしてる気がする」
		ELSE
			PRINTFORMW 「ふ～ん。興味深いわね……」
			PRINTFORMW 「同じ女とはいえ、他人のをこうやって見る機会なんてないものね」
		ENDIF
		PRINTFORMW 「ま、それはいいとして……折角だから、もう少し観察させてもらうわ」
		RETURN 1
	ENDIF
	
	;=================================================
	;調教対象が処女
	;=================================================
	IF 口上記入チェック(100) && TALENT:MASTER:処女
		;潤滑十分
		IF PALAM:MASTER:潤滑 >= 500
			PRINTFORMW 「もうグチョグチョって感じかしら」
			PRINTFORMW 「処女の癖に随分浅ましいのね……誘ってるのかしら？」
		;潤滑不足
		ELSE
			PRINTFORMW 「…まだお湿りが足りないみたいだけど」
			PRINTFORMW 「だんだん濡れてきたみたいね…。見られて興奮しちゃったのかしら？」
		ENDIF
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	;潤滑十分
	IF PALAM:MASTER:潤滑 >= 500
		PRINTFORMW 「すっかり濡れちゃって、準備OKってとこかしらね？」
		PRINTFORMW 「さぁ、もっと開いて。私に、良く見えるように……」
	;潤滑不足
	ELSE
		PRINTFORMW 「ちょっと濡れ方が足りないかしら」
		PRINTFORMW 「でも%CNAME(MASTER, TARGET)%…、見られてると溢れてきちゃうのよね？ ふふふ…」
	ENDIF
	RETURN 1
ENDIF

;────────────────────────────────────
;ACT42, 自慰見せつけ
;────────────────────────────────────
;・調教者が見せ付けるように自慰を始める
;・TFLAG:お仕置きフラグが立っていると発生しない
;・押し倒し中には発生しない
;・調教者がすでに自慰中である場合は発生しない
;・性交中/性交奉仕中には発生しない
;-------------------------------------------------
@KOJO_ACT_K14_42
;-------------------------------------------------
;調教者欲望、技巧、調教レベル、調教者技巧、器用な指先素質に応じて快Ｃ/快ＢSOURCE増加
;バイブ所持の場合の処理は0616版時点では封印されている
;-------------------------------------------------

;━━━━━━━━━━━━━━━━━━━━━━━━
;アイマスク装着中
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TEQUIP:アイマスク
	;調教対象も自慰中
	IF GETBIT(TEQUIP:自慰中, 0)
		CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"　　%KOJO_RAND_K14(("自慰"), 1)%")
		CLEARLINE 1
		PRINTFORMW 「んっ！ これ、凄い倒錯的な感じがするわね……」
		PRINTFORMW 「どう？ 見えない分、音には敏感にならない？」
		PRINTFORMW 「見せてあげられないのは残念だけど、私の声と音で楽しんで頂戴。フフ…」
	;通常
	ELSE
		CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"　　%KOJO_RAND_K14(("自慰"), 1)%")
		CLEARLINE 1
		PRINTFORMW 「私が今何してるか、分かるかしら…？」
		PRINTFORMW 「駄目よ、動かないで。そこにじっとしてなさい」
		PRINTFORMW 「そして想像してみて。私が何処をどう弄ってよくなっちゃってるのか…」
	ENDIF
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;調教者にペニスがない場合
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && !PENIS(TARGET)
	;=================================================
	;調教対象も自慰中
	;=================================================
	IF 口上記入チェック(100) && GETBIT(TEQUIP:自慰中, 0)
		;調教者理性派生
		SELECTCASE TENSION(TARGET, 5)
			;25%以下
			CASE "低い"
				PRINTFORM 「あっ
				CALL AUTOHEARTCB_K14
				PRINTFORMW 　やだコレ…、凄く興奮しちゃうっ！」
				PRINTFORMW 「%CNAME(MASTER, TARGET)%とシてるみたいな気分になっちゃって…」
				PRINTFORMW 「一人でするより、ずっと……んんっ！」
			;25%以上
			CASE "一般"
				PRINTFORMW 「これ、凄くイヤラシイ…。一人でするより興奮しちゃう。んっ！」
				PRINTFORMW 「%CNAME(MASTER, TARGET)%が夢中でシてるの見たら、なんか、私も……」
			;50%以上
			CASE "高い"
				PRINTFORMW 「冷静に考えると馬鹿みたいだけど…」
				PRINTFORMW 「でも、なんか興奮しちゃうわね。コレ…」
		ENDSELECT
		RETURN 1
	ENDIF
	
	;=================================================
	;拘束中
	;=================================================
	IF 口上記入チェック(100) && TEQUIP:縄
		PRINTFORMW 「さぁ、目を離さないで……しっかり見てなさい」
		PRINTFORM 「思いっきりいやらしく、オナニーしてるところを見せ付けてあげる…
		CALL PRINT_SYMBOL_K14
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	;調教者理性派生
	SELECTCASE TENSION(TARGET, 5)
		CASE "低い"
			PRINTFORM 「ん…あっ
			CALL AUTOHEARTCB_K14
			PRINTFORMW 　見られてると興奮しちゃって…、おかしくなりそう……」
			PRINTFORMW 「あ…ああ……、こんな。もう、もうわたしっ！ あぁぁっ！！」
		CASE "一般"
			PRINTFORMW 「…ん。こうやって、見られながらオナニーするのも悪くないわ」
			PRINTFORMW 「我ながら度し難いとは思うけど…、止まらなくなっちゃうのよ、これ…」
		CASE "高い"
			PRINTFORMW 「ほら、目を背けずによく見てて…」
			PRINTFORMW 「私が何処をどう弄るのか、どこでよくなっちゃうのか」
			PRINTFORMW 「%CNAME(MASTER, TARGET)%に見せてあげるわ……」
	ENDSELECT
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;調教者にペニスがある場合
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && PENIS(TARGET)
	;=================================================
	;調教対象も自慰中
	;=================================================
	IF 口上記入チェック(100) && GETBIT(TEQUIP:自慰中, 0)
		PRINTFORMW 「んっ…私がしてるとこ見て、%CNAME(MASTER, TARGET)%も興奮してるみたいね…」
		PRINTFORMW 「ほら、もっと良く見えるようにしてあげるわ…」
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	PRINTFORM 「%CNAME(MASTER, TARGET)%は動い\@ AFFECTION("相思相愛") ? ちゃ # ては \@駄目よ\@ AFFECTION("相思相愛") ? ？ # 。 \@ 自分で弄るのもダメ…
	CALL PRINT_SYMBOL_K14
	PRINTFORMW 「ほら、見て？ 私のいやらしい姿……」
	RETURN 1
ENDIF

;────────────────────────────────────
;ACT43, 羞恥プレイ
;────────────────────────────────────
;・調教対象を大鏡の前に連れて行く
;・TFLAG:お仕置きフラグが立っていると発生しない
;・ターン方針が"異常"だと発生しない
;・押し倒し中には発生しない
;・TEQUIP:三角木馬装着中には発生しない
;・性交中/性交奉仕中には発生しない
;・既に羞恥プレイ中なら、羞恥プレイ終了になる
;-------------------------------------------------
@KOJO_ACT_K14_43
;-------------------------------------------------
;・TFLAG:ACT派生(0=開始/1=終了)
;-------------------------------------------------
;・現状本体側では露出SOURCE増加の処理があるのみ
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;ACT43_0, 羞恥プレイ開始
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && !TFLAG:ACT派生
	;=================================================
	;初回
	;=================================================
	IF 口上記入チェック(100) && FIRSTACT(0)
		PRINTFORMW 「改めて見ても中々良い鏡よね、コレ」
		PRINTFORMW 「こんな事に使うのが勿体無いくらいだわ。というか正直…」
		PRINTFORMW 「鏡の前って、私もちょっと落ち着かないのよね……」
		RETURN 1
	ENDIF
	
	SELECTCASE RANDACT(4, 0)
		CASE 0
			IF 0
			ELSEIF !TEQUIP:アイマスク
				PRINTFORMW 「ちゃんと鏡を見て…。目を逸らしては駄目よ？」
				PRINTFORMW 「自分のはしたない姿、よく見ておきなさい…」
			ELSE
				PRINTFORMW 「目隠しをされていても、今、自分がどういう状況かは判るわよね？」
				PRINTFORMW 「ほら、想像してみて。鏡の中の%CNAME(MASTER, TARGET)%がどんな姿をしているのか」
				PRINTFORMW 「そして、私にどんな事されちゃうのか……」
			ENDIF
		CASE 1
			PRINTFORMW 「こういう事に鏡を使うのって、誰が考え出したのかしらね？」
			PRINTFORMW 「魔法使いと鏡とかって、割りとありがちな話だけど…」
			PRINTFORMW 「私自身…、鏡にこんな使い方があるなんて考えもしなかったわ」
		CASE 2
			PRINTFORMW 「鏡を通り抜けて異世界へって話、%CNAME(MASTER, TARGET)%は聞いた事あるかしら？」
			PRINTFORMW 「『鏡の国のアリス』って本なんだけど」
			PRINTFORMW 「なんていうか…、読んだ時に凄く落ち着かない気分になったのよねぇ」
			PRINTFORMW 「ま、だからどうだって訳じゃないんだけど」
		CASE 3 ;
			;-------------------------------------
			;『鏡の国のアリス』からジャバウォックの詩
			;TESのワバジャックの元ネタでもある。Jabberwock→Wabbajackとなっており、まぁアナグラムと言って良いかと
			;-------------------------------------
			;フォントをセット
			CALL SETFONT_K14
			;あぶりの刻、ねばらかなるトーヴ
			CALL FADEIN(10, "　　夕火の刻、粘滑なるトーヴ")
			TWAIT 1500, 0
			PRINTL
			;はるばにありてまわりふるまいきりうがつ
			CALL FADEIN(10, "　　　　遥場にありて回儀い錐穿つ")
			TWAIT 1500, 0
			PRINTL
			;総て弱ぼらしきはボロゴーヴ
			CALL FADEIN(10, "　　　　　　総て弱ぼらしきはボロゴーヴ")
			TWAIT 1500, 0
			PRINTL
			;かくてさととおとしラースのうずめき叫ばん
			CALL FADEIN(10, "　　　　　　　　かくて郷遠しラースのうずめき叫ばん")
			WAIT
			;フォントを戻す
			CALL RESETFONT_K14
			;この手の代物はTWAITじゃないと許せないという人なので最後だけWAITであとはTWAIT。自然に流れた方がより美しいと思う次第
			PRINTL
			PRINTFORMW 「あら、ご免なさい。なぜか急に口ずさんでみたくなったのよね」
			PRINTFORMW 「時間があれば説明してあげてもいいけど。流石に、今はちょっとアレよね。ふふ…」
	ENDSELECT
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;ACT43_1, 羞恥プレイ終了
;━━━━━━━━━━━━━━━━━━━━━━━━
;IF 口上記入チェック(0) && TFLAG:ACT派生 == 1
;ENDIF

;────────────────────────────────────
;ACT44, 野外プレイ
;────────────────────────────────────
;・調教対象に首輪をつけて外に連れ出す
;・ターン方針が"ソフト", "ノーマル"だと発生しない
;・押し倒し中には発生しない
;・TEQUIP(縄/三角木馬)装着中には発生しない
;・性交中/性交奉仕中には発生しない
;・既に野外プレイ中なら、野外プレイ終了になる
;-------------------------------------------------
@KOJO_ACT_K14_44
;-------------------------------------------------
;・TFLAG:ACT派生(0=開始/1=終了)
;-------------------------------------------------
;TCVAR:今日の満足ボーナス(0=いつもどおり/1=A開発/2=お仕置き/3=ハード/4=イチャイチャ/5=異常/6=野外/7=子作り/8=休憩)
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;ACT44_0, 野外プレイ開始
;━━━━━━━━━━━━━━━━━━━━━━━━
;既存の調教バリアントから同じようなコマンドをただ持ってきても面白くもなんともないという良い例
;調教者のパラメータが少なすぎる現状、色々とアレな感じ
;"TCVAR:野外プレイ中"のようなものがあっても良い。ABLE関連やMESSAGE関連及び口上側で様々な利用法があるように思う
;-------------------------------------------------
IF 口上記入チェック(100) && !TFLAG:ACT派生
	;=================================================
	;満足ボーナスによる外出
	;=================================================
	;TCVAR:今日の満足ボーナスのみで参照可能だけどまぁPOLICY絡めると間違いはないとかそういう
	;今日の満足ボーナスの関数化が一番いいんだろうけど。
	IF 口上記入チェック(100) && TCVAR:今日の満足ボーナス == 6 && IS_POLICY("今日", "野外")
		SELECTCASE RANDACT(3, 0)
			CASE 0
				PRINTFORMW 「まぁでも、自分から外に出たいとか%CNAME(MASTER, TARGET)%も大概よね？」
				PRINTFORMW 「誰かに見られたら、とか考えない訳？」
			CASE 1
				PRINTFORMW 「連れ出すのは構わないけど、誰かと会った時の言い訳が…、ね」
				PRINTFORMW 「ま、心配するほどの事でもないけれど…。ちょっと面倒なのよね」
			CASE 2
				PRINTFORMW 「やっぱり何？ 外の空気が吸いたいとか、そういう事な訳？」
				PRINTFORMW 「まぁ…外出自体は構わないけど。あまり派手なことする訳にもいかないのよね」
		ENDSELECT
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	SELECTCASE RANDACT(3, 1)
		CASE 0
			PRINTFORMW 「さぁ出掛けるわよ。準備をしなさい」
			PRINTFORMW 「と言ったところで、%CNAME(MASTER, TARGET)%がする事なんて殆どないけど。\@ AFFECTION("相思相愛") ? ふふ # フフ \@」
		CASE 1
			PRINTFORMW 「外に出ると言っても、あまり目立つのもアレなのよねぇ」
			PRINTFORMW 「面倒なやつに会いたくないし…」
		CASE 2
			PRINTFORMW 「ああ、そうそう。別に四つん這いになって歩けとか言わないから…」
			CALL FADEIN(10, @"「安心していいわよ\@ AFFECTION("相思相愛") ? ？ # \@」")
			WAIT
			CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"「\@ AFFECTION("相思相愛") ? クスクス # フフフ \@…」")
			SIF ANIME_CONFIG("FADE")
				CLEARLINE 1
	ENDSELECT
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;ACT44_1, 野外プレイ終了
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:ACT派生 == 1
	IF AFFECTION("相思相愛")
		PRINTFORMW 「さて、そろそろ戻りましょうか」
		PRINTFORM 「帰ってからも、たっぷり愉しませてね…
	ELSE
		PRINTFORMW 「じゃ、そろそろ戻るわよ。%CNAME(MASTER, TARGET)%は楽しめたかしら…？」
		PRINTFORM 「ふふ……、そんな顔しても駄目。また連れ出してあげるわ
	ENDIF
	CALL PRINT_SYMBOL_K14
	RETURN 1
ENDIF

;────────────────────────────────────
;ACT45, 放尿
;────────────────────────────────────
;・おしっこしてみせろと命令する
;・この段階では命令に従うかどうか確定していない
;・REACT分類が消極的従う/平然と従う/積極的従う/受け入れる以外の場合は、全てREACT分類"拒否"扱い
;・ターン方針が"ソフト"だと発生しない
;・TEQUIP(三角木馬/ペニスバンド)装着中には発生しない
;・押し倒し中には発生しない
;・性交中/性交奉仕中には発生しない
;・尿意(BASE:MASTER:尿意)が5000以下だと発生しない
;-------------------------------------------------
@KOJO_ACT_K14_45
;-------------------------------------------------
;・ACT派生なし
;-------------------------------------------------

LOCALS = \@ AFFECTION("相思相愛") ? クスクス # フフフ \@

;━━━━━━━━━━━━━━━━━━━━━━━━
;初回
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && FIRSTACT(0)
	PRINTFORMW 「さて、ここに容器を用意した訳だけど…」
	PRINTFORMW 「一々説明しなくても、何をすればいいか判るわよね？」
	PRINTFORMW 「とぼけたって駄目よ。私の言うことには従ってもらうわ」
	PRINTL
	
	IF 0
	ELSEIF TALENT:MASTER:オトコ
		PRINTFORML 「男がどんな風にするのか、私によく見せて頂戴」
		PRINTFORMW 「ちなみに…緊張して出ないとか、情けない言い訳は聞かないから」
	ELSEIF TALENT:MASTER:ふたなり
		PRINTFORML 「%CNAME(MASTER, TARGET)%のような娘がどんな風にするのか、何処から出るのか…」
		PRINTFORMW 「私に良く見せて頂戴…」
	ELSE
		PRINTFORML 「同じ女同士、恥ずかしいって事もないでしょう？」
		PRINTFORMW 「私の事は気にせず、存分にやるといいわ」
	ENDIF
	
	PRINTL
	PRINTFORMW 「でもまぁ…。正直コレ、あまり上品な見世物とは言えないわよねぇ」
	CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"「%LOCALS%…」")
	SIF ANIME_CONFIG("FADE")
		CLEARLINE 1
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;お仕置きモード
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:お仕置きフラグ
	PRINTFORMW 「%CNAME(MASTER, TARGET)%のような人は、辱めるのが一番なのかもねぇ」
	PRINTFORMW 「まぁそういう訳だから、ここでオシッコをしてもらうわ」
	PRINTFORMW 「格好は、そうねぇ…%CNAME(MASTER, TARGET)%の好きにすればいいと思うけど」
	PRINTFORMW 「する時は『\@ TALENT:MASTER:オトコ ? 僕 # 私 \@のオシッコする姿見て下さい』って言いなさい」
	PRINTFORMW 「私に聞こえるように、大きな声でね……」
	CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"「%LOCALS%…」")
	SIF ANIME_CONFIG("FADE")
		CLEARLINE 1
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;汎用処理
;━━━━━━━━━━━━━━━━━━━━━━━━
;TALENT毎の枠を粛清！
;------------------------------------------------
IF 口上記入チェック(100)
	;2/3
	IF RAND:3
		PRINTFORM 「そうねぇ…。今回は
		SELECTCASE RANDACT(3, 10)
			CASE 0
				PRINTFORMW 、しゃがんだ格好でしてもらおうかしら」
				PRINTFORMW 「別に難しい話じゃないわよねぇ？」
			CASE 1
				PRINTFORMW 、立った状態でしてもらおうかしら」
				PRINTFORMW 「\@ PENIS(MASTER) ? %CNAME(MASTER, TARGET)%ならどうって事ないでしょう？ # 上手くやらないと大変な事になるかもだけど。ふふ \@」
			CASE 2
				PRINTFORMW 四つん這いになって、犬みたいな格好でしてもらおうかしら？」
				PRINTFORMW 「%CNAME(MASTER, TARGET)%にはピッタリだと思うわよ」
		ENDSELECT
	;1/3
	ELSE
		PRINTFORMW 「さて、ここでしてみせて%RAND_SPLIT("貰おうかしら/頂戴")%」
		PRINTFORMW 「何をすればいいかって？ %CNAME(MASTER, TARGET)%には判ってるはずよ」
		CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"「%LOCALS%…」")
		SIF ANIME_CONFIG("FADE")
			CLEARLINE 1
	ENDIF
	RETURN 1
ENDIF
