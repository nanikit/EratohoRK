;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	アリス口上 性交系アクション口上ファイル
;
;	性交系アクション口上をまとめたファイル
;
;Version		Reason
;1.71			@口上記入チェック仕様に。その他細かな修正
;1.61			TFLAG:ACT派生分岐を各派生毎に分割。一部口上の関数化。その他加筆修正
;1.60			ACT分類単体ファイルとして独立
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;========================================================================
;性交系 ACT30～35
;========================================================================
;・TFLAG:ACT(30=正常位/31=後背位/32=騎乗位/33=対面座位/34=背面座位/35=アナルセックス)
;・TFLAG:ACT分類 == 3に該当
;・汎用リアクションにおいてはMENUMATCH(TFLAG:ACT, "性交")で拾う事が可能
;・原則として調教者→調教対象の挿入となる
;・入れようとしているところにあたる
;・媚薬・ローションを使用するケースがある
;・体位の連携によってTFLAG:ACT派生で派生する
;・各ACTに於ける主導権は@SEX_INITIATIVEにて定義される
;-------------------------------------------------
;関連フラグ…
;TFLAG:ACT派生
;(=0, 通常、1, 同一体位連続、2, 体位変更、3, Ａ⇔Ｖの挿し換え、4, 通常⇔逆レイプの交代、5, 通常⇔逆アナルの交代）
;TFLAG:前回中出し
;(1=Ｖ性交/2=Ｖ性交奉仕/3=Ｖ性交(空射)/4=Ｖ性交奉仕(空射)/5=Ａ性交/6=Ａ性交奉仕/7=Ａ性交(空射)/8=Ａ性交奉仕(空射))
;TFLAG:106 抜かず
;-------------------------------------------------
;関連関数…
;FIRSTACT
;初回ACT判定関数
;TFLAG:ACT（TFLAG:90）を自動で参照、現在実行されているACTごとに番号が独立して保存される
;
;ONCEACT
;調教内未読判定関数　調教前に初期化される
;その他の仕様はほぼFIRSTACTと同様
;
;RANDACT
;ランダムイベントの表示順整理関数
;全種見るまで同じ数字は返さない。全て表示すると初期化
;@KOJO_ACT_KX1_Y1毎に管理される
;-------------------------------------------------

;────────────────────────────────────
;ACT30, 正常位
;────────────────────────────────────
;・正常位・調教者が調教対象に挿入する
;・イニシアティブはTARGET(SEX_INITIATIVE(TARGET))
;・調教対象がオトコの場合は発生しない
;・調教者のふたなり化、若しくはペニスバンド装着によって発生する
;・TEQUIP(三角木馬/ペニスバンド/バイブ)装着中には発生しない
;・TFLAG:お仕置きフラグが立っていると発生しない
;・ターン方針が"ハード", "異常"だと発生しない
;・TEQUIP:性交中 == 5(背面座位)からは発生しない
;・TFLAG:ACT派生は前ターンからの状態、及び体位の参照に充てられる
;-------------------------------------------------
@KOJO_ACT_K14_30
;-------------------------------------------------
;・TFLAG:ACT派生(0=通常/1=連続実行/2=他のＶ性交から/3=Ａ性交から/4=Ｖ性交奉仕から/5=Ａ性交奉仕から)
;-------------------------------------------------
;・REACT分類が暴れるだった場合はキャンセルされる
;・受け入れた場合、MASTERが処女なら処女喪失、TARGETが童貞なら童貞喪失が起こる
;・v1.010現在、拒絶した場合のSOURCEリセット処理がない
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;ACT30, 正常位
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100)
	;=================================================
	;調教対象が処女
	;=================================================
	IF 口上記入チェック(100) && TALENT:MASTER:処女
		PRINTFORMW 「さぁ、いくわよ。 準備はいいかしら…」
		PRINTFORMW 「ほら、力を抜いて…」
		RETURN 1
	ENDIF
	
	;=================================================
	;調教者が童貞
	;=================================================
	IF 口上記入チェック(100) && TALENT:TARGET:童貞
		PRINTFORMW 「…上手く出来るといいのだけれど」
		PRINTFORMW 「痛かったら言って頂戴…」
		RETURN 1
	ENDIF
	
	;Ａセックス以外は関数で汎用処理
	;=================================================
	;前回中出し
	;=================================================
	IF 口上記入チェック(100) && TFLAG:前回中出し && TFLAG:前ターン性交
		;汎用処理
		CALL PRINTTEXT_CATEGORY_3_ACT_1_K14
		RETURN 1
	ENDIF
	
	;=================================================
	;派生１：連続(正常位から)
	;=================================================
	IF 口上記入チェック(100) && TFLAG:ACT派生 == 1
		;-------------------------------------------------
		;調教者がイキそう
		;-------------------------------------------------
		IF 口上記入チェック(100) && TCVAR:TARGET:イきそう
			;汎用処理
			CALL PRINTTEXT_CATEGORY_3_ACT_2_K14
			RETURN 1
		ENDIF
		
		;関数で汎用処理。関数内でACT毎に分岐させる
		;-------------------------------------------------
		;調教者が情欲
		;-------------------------------------------------
		IF 口上記入チェック(100) && T_COND("情欲")
			;汎用処理
			CALL PRINTTEXT_CATEGORY_3_ACT_3_K14
			RETURN 1
		ENDIF
		
		;---------------------
		;漏れたものはここで処理
		;---------------------
		SELECTCASE RANDACT(2, 0)
			CASE 0
				PRINTFORMW 「んくっ！ ねぇ、奥に当たってるの分かるかしら…」
				PRINTFORMW 「あら、もう答える余裕は無さそうね？ ふふっ」
			CASE 1
				PRINTFORMW 「ふふ、足を絡ませてきちゃって。可愛い」
				PRINTFORM 「いいわ……もっと激しく突いてあげる
				CALL PRINT_SYMBOL_K14
		ENDSELECT
		RETURN 1
	ENDIF
	
	;背面座位からは派生しない
	;=================================================
	;派生２：体位変更(Ｖ性交から)
	;=================================================
	IF 口上記入チェック(100) && TFLAG:ACT派生 == 2
		SELECTCASE RANDACT(2, 1)
			CASE 0
				PRINTFORMW 「ほら、当たるとこ変わっていいでしょ？」
				PRINTFORM 「ふふ、もっと突きまくってあげるわ
				CALL PRINT_SYMBOL_K14
			CASE 1
				PRINTFORMW 「恥ずかしがらないで……、もっと%CNAME(MASTER, TARGET)%の顔を見せて頂戴」
				PRINTFORMW 「キスしながら可愛がってあげるから……」
				;フェードアウト
				CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), KOJO_RAND_K14(("キス"), 1))
				CLEARLINE 1
				PRINTFORMDW %CALLNAME:TARGET%は羞恥に身悶える%CALLNAME:MASTER%の腕を押さえつけ、キスをしながら腰を動かし始めた…
		ENDSELECT
		RETURN 1
	ENDIF
	
	;=================================================
	;派生３：Ａ性交から
	;=================================================
	IF 口上記入チェック(100) && TFLAG:ACT派生 == 3
		;No.2
		SELECTCASE RANDACT(2, 2)
			CASE 0
				PRINTFORMW 「腸液と愛液が混ざり合って、凄い事になってるわね…」
				PRINTFORM 「でも、ふふ……%CNAME(MASTER, TARGET)%の膣内、とってもいいわよ
				CALL PRINT_SYMBOL_K14
			CASE 1
				PRINTFORM 「ふふ、顔が近いと恥ずかしいのかしら？ 可愛らしいわ
				CALL PRINT_SYMBOL_K14
				PRINTFORMW 「ほら、いくわよ。いい声で鳴いて頂戴……」
		ENDSELECT
		RETURN 1
	ENDIF
	
	;基本的に射精するまでは他の行動になる事はないので
	;ペニスバンドによる性交奉仕からの派生、といった按配
	;TFLAG:ACT派生 == 4は00616版時点で、ほぼ無意味な状態と思われ
	;=================================================
	;派生４/派生５：Ｖ性交奉仕/Ａ性交奉仕から
	;=================================================
	IF 口上記入チェック(100) && GROUPMATCH(TFLAG:ACT派生, 4, 5) > 0
		;調教者が苛立ってる
		IF TENSION(TARGET, 7) == "高い"
			;汎用処理
			CALL PRINTTEXT_CATEGORY_3_ACT_4_K14
		ELSE
			SELECTCASE RANDACT(2, 3)
				CASE 0
					PRINTFORMW 「……攻守交替ってとこかしら」
					PRINTFORM 「よがり狂わせてあげるから、覚悟して頂戴
					CALL PRINT_SYMBOL_K14
				CASE 1
					PRINTFORMW 「さぁ、今度は私の番よ」
					PRINTFORM 「ふふ…、思いっきり突いてあげるから愉しんで頂戴
					CALL PRINT_SYMBOL_K14
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ペニスバンド使用時
	;=================================================
	IF 口上記入チェック(100) && TRUECHECK(!PENIS(TARGET), TEQUIP:調教者ペニスバンド) > 0
		PRINTFORMW 「さぁ、準備はいいかしら？ こんな無粋なものだけど…」
		PRINTFORM 「%CNAME(MASTER, TARGET)%ならきっと愉しんでもらえると思うわ…
		CALL PRINT_SYMBOL_K14
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	PRINTFORMW 「さぁ、私のモノで貫いてあげる」
	PRINTFORMW 「コレが欲しかったんでしょう？ ふふ…、正直に言いなさい」
	RETURN 1
ENDIF

;────────────────────────────────────
;ACT31, 後背位
;────────────────────────────────────
;・後背位・調教者が調教対象に挿入する
;・イニシアティブはTARGET(SEX_INITIATIVE(TARGET))
;・調教対象がオトコの場合は発生しない
;・調教者のふたなり化、若しくはペニスバンド装着によって発生する
;・TEQUIP(三角木馬/ペニスバンド/バイブ)装着中には発生しない
;・TFLAG:お仕置きフラグが立っていると発生しない
;・ターン方針が"ソフト", "異常"だと発生しない
;・TEQUIP:性交中 == 4(対面座位)からは発生しない
;・TFLAG:ACT派生は前ターンからの状態、及び体位の参照に充てられる
;-------------------------------------------------
@KOJO_ACT_K14_31
;-------------------------------------------------
;・TFLAG:ACT派生(0=通常/1=連続実行/2=他のＶ性交から/3=Ａ性交から/4=Ｖ性交奉仕から/5=Ａ性交奉仕から)
;-------------------------------------------------
;・REACT分類が暴れるだった場合はキャンセルされる
;・受け入れた場合、MASTERが処女なら処女喪失、TARGETが童貞なら童貞喪失が起こる
;・v1.010現在、拒絶した場合のSOURCEリセット処理がない
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;ACT31, 後背位
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100)
	;派生に絡まないものは上層に切り分ける
	;=================================================
	;調教対象が処女
	;=================================================
	IF 口上記入チェック(100) && TALENT:MASTER:処女
		PRINTFORMW 「初めてが後ろからなんて、倒錯的だと思わない？ ふふ」
		PRINTFORMW 「さぁ、力を抜いて頂戴……じっくり慣らしてあげるから…」
		RETURN 1
	ENDIF
	
	;=================================================
	;調教者が童貞
	;=================================================
	IF 口上記入チェック(100) && TALENT:TARGET:童貞
		PRINTFORMW 「ふふ……いよいよ、ね」
		PRINTFORMW 「上手くできるといいのだけれど……大人しくしてて頂戴」
		RETURN 1
	ENDIF
	
	;=================================================
	;前回中出し
	;=================================================
	IF 口上記入チェック(100) && TFLAG:前回中出し && TFLAG:前ターン性交
		;汎用処理
		CALL PRINTTEXT_CATEGORY_3_ACT_1_K14
		RETURN 1
	ENDIF
	
	;=================================================
	;派生１：連続(後背位から)
	;=================================================
	IF 口上記入チェック(100) && TFLAG:ACT派生 == 1
		;-------------------------------------------------
		;調教者がイキそう
		;-------------------------------------------------
		IF 口上記入チェック(100) && TCVAR:TARGET:イきそう
			;2/3の確率で表示
			IF RAND:3
				PRINTFORMW 「後ろからだと刺激が強いのよね……」
				PRINTFORMW 「んっ！ も、もう私……イっちゃうかも」
			;1/3の確率で汎用処理
			ELSE
				CALL PRINTTEXT_CATEGORY_3_ACT_2_K14
			ENDIF
			RETURN 1
		ENDIF
		
		;-------------------------------------------------
		;調教者が情欲
		;-------------------------------------------------
		IF 口上記入チェック(100) && T_COND("情欲")
			;汎用処理
			CALL PRINTTEXT_CATEGORY_3_ACT_3_K14
			RETURN 1
		ENDIF
		
		;-----------------------------------------
		;漏れたものはここで処理
		;-----------------------------------------
		SELECTCASE RANDACT(2, 0)
			CASE 0
				PRINTFORMW 「おしりの穴が丸見えでイヤラシイわね、これ……」
				PRINTFORMW 「ねぇ、犬みたいな格好で犯されるのはどんな気分かしら？」
				IF !RAND:3
					CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), "「フフフ…」")
					CLEARLINE 1
				ENDIF
			CASE 1
				PRINTFORM 「目の前でおしりの穴がヒクヒクしてるわ……
				CALL PRINT_SYMBOL_K14
				PRINTFORM 「この格好……、%CNAME(MASTER, TARGET)%をレイプしてるみたいで興奮するのよね
				CALL PRINT_SYMBOL_K14
		ENDSELECT
		RETURN 1
	ENDIF
	
	;=================================================
	;派生２：体位変更(Ｖ性交から)
	;=================================================
	IF 口上記入チェック(100) && TFLAG:ACT派生 == 2
		PRINTFORMW 「ふふ、可愛らしいおしりね？ 後ろからたっぷり犯してあげる」
		PRINTFORMW 「擦れるとこが違って、きっと愉しんで貰えると思うわよ」
		IF !RAND:3
			CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), "「フフフ…」")
			CLEARLINE 1
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;派生３：Ａ性交から
	;=================================================
	IF 口上記入チェック(100) && TFLAG:ACT派生 == 3
		SELECTCASE RANDACT(2, 1)
			CASE 0
				PRINTFORMW 「ほら、こっちの穴が寂しかったでしょう？」
				PRINTFORMW 「ふふ……腸液と愛液で凄いヌルヌルね」
			CASE 1
				PRINTFORMW 「ん、凄い濡れてるわ。こっちに欲しかったなら言えば良かったのにね？」
				PRINTFORMW 「ま、それはそれとして……、思いっきりいくから、いい声で鳴いて頂戴……」
		ENDSELECT
		RETURN 1
	ENDIF
	
	;=================================================
	;派生４/派生５：Ｖ性交奉仕/Ａ性交奉仕から
	;=================================================
	IF 口上記入チェック(100) && GROUPMATCH(TFLAG:ACT派生, 4, 5) > 0
		;調教者が苛立ってる
		IF TENSION(TARGET, 7) == "高い"
			;汎用処理
			CALL PRINTTEXT_CATEGORY_3_ACT_4_K14
		;Ｖ性交奉仕から
		ELSEIF TFLAG:ACT派生 == 4
			PRINTFORMW 「今度は私がしてあげるわ。ふふ、じゃあおしりをこっちに向けて？」
			PRINTFORMW 「ほら、手を付いて……もっとおしりを高く上げなさい」
		;Ａ性交奉仕から
		ELSEIF TFLAG:ACT派生 == 5
			PRINTFORMW 「さ、私のおしりを可愛がってくれたお礼よ」
			PRINTFORM 「ただし……私は前で愉しませてもらう事にするけど、ね
			CALL PRINT_SYMBOL_K14
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ペニスバンド使用時
	;=================================================
	IF 口上記入チェック(100) && TRUECHECK(!PENIS(TARGET), TEQUIP:調教者ペニスバンド) > 0
		PRINTFORMW 「ねぇ、%CNAME(MASTER, TARGET)%が今感じてるのは不安？ それとも期待してるのかしら？」
		PRINTFORMW 「ま、いずれにせよ……この紛い物で思いっきり鳴かせてあげる」
		PRINTFORMW 「壊れちゃうくらい突いてあげるから……」
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	SELECTCASE RANDACT(2, 2)
		CASE 0
			PRINTFORMW 「さぁ、四つん這いになってお尻をこっちに向けて頂戴…」
			PRINTFORM 「ふふ……、良い眺めね。思いっきり犯してあげるわ
		CASE 1
			PRINTFORMW 「目の前でお尻の穴がヒクヒクしてるわ」
			PRINTFORM 「ふふっ、この格好……%CNAME(MASTER, TARGET)%をレイプしてるみたいで興奮するのよね
	ENDSELECT
	CALL PRINT_SYMBOL_K14
	RETURN 1
ENDIF

;────────────────────────────────────
;ACT32, 騎乗位
;────────────────────────────────────
;・騎乗位・調教者が調教対象に挿入する
;・イニシアティブはMASTER(SEX_INITIATIVE(MASTER))
;・調教対象がオトコの場合は発生しない
;・調教者のふたなり化、若しくはペニスバンド装着によって発生する
;・TEQUIP(三角木馬/ペニスバンド/バイブ)装着中には発生しない
;・TFLAG:お仕置きフラグが立っていると発生しない
;・ターン方針が"ハード", "異常"だと発生しない
;・TEQUIP:押し倒し中には発生しない
;・TFLAG:ACT派生は前ターンからの状態、及び体位の参照に充てられる
;-------------------------------------------------
@KOJO_ACT_K14_32
;-------------------------------------------------
;・TFLAG:ACT派生(0=通常/1=連続実行/2=他のＶ性交から/3=Ａ性交から/4=Ｖ性交奉仕から/5=Ａ性交奉仕から)
;-------------------------------------------------
;・REACT分類が暴れるだった場合はキャンセルされる
;・受け入れた場合、MASTERが処女なら処女喪失、TARGETが童貞なら童貞喪失が起こる
;・拒絶した場合、SOURCE:MASTER:快ＶとSOURCE:快Ｃはリセットされる
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;ACT32, 騎乗位
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100)
	;=================================================
	;調教対象が処女
	;=================================================
	IF 口上記入チェック(100) && TALENT:MASTER:処女
		PRINTFORMW 「さぁ、自分から捧げるのよ」
		PRINTFORMW 「慌てなくていいから……、ゆっくり、ゆっくりね」
		RETURN 1
	ENDIF
	
	;=================================================
	;調教者が童貞
	;=================================================
	IF 口上記入チェック(100) && TALENT:TARGET:童貞
		PRINTFORMW 「私の初めてはこの格好にするわ」
		PRINTFORMW 「じゃ、よろしくお願いするわね」 
		RETURN 1
	ENDIF
	
	;=================================================
	;前回中出し
	;=================================================
	IF 口上記入チェック(100) && TFLAG:前回中出し && TFLAG:前ターン性交
		;汎用処理
		CALL PRINTTEXT_CATEGORY_3_ACT_1_K14
		RETURN 1
	ENDIF
	
	;=================================================
	;派生１：連続(騎乗位から)
	;=================================================
	IF 口上記入チェック(100) && TFLAG:ACT派生 == 1
		;-------------------------------------------------
		;調教者がイキそう
		;-------------------------------------------------
		IF 口上記入チェック(100) && TCVAR:TARGET:イきそう
			IF RAND:3
				SELECTCASE RANDACT(2, 0)
					CASE 0
						PRINTFORMW 「んんっ！ そんな腰使いされたら……」
						PRINTFORMW 「私……もう、イっちゃうかも」
					CASE 1
						PRINTFORMW 「んっ！ %CNAME(MASTER, TARGET)%の腰使いがいやらしくて、そろそろ限界みたい…」
						PRINTFORMW 「いいわよ……もっと動いて。私ももっと突き上げてあげるわ…」
				ENDSELECT
			ELSE
				;汎用処理
				CALL PRINTTEXT_CATEGORY_3_ACT_2_K14
			ENDIF
			RETURN 1
		ENDIF
		
		;-------------------------------------------------
		;調教者が情欲
		;-------------------------------------------------
		IF 口上記入チェック(100) && T_COND("情欲")
			;汎用処理
			CALL PRINTTEXT_CATEGORY_3_ACT_3_K14
			RETURN 1
		ENDIF
		
		;---------------------
		;その他
		;---------------------
		PRINTFORMW 「ほら、もっと頑張って動いて。ふふ」
		PRINTFORMW 「私も突き上げてあげるから……」
		RETURN 1
	ENDIF
	
	;=================================================
	;派生：２体位変更(Ｖ性交から)
	;=================================================
	IF 口上記入チェック(100) && TFLAG:ACT派生 == 2
		PRINTFORMW 「さぁ、今度は上に乗って頂戴」
		PRINTFORMW 「ほら、こうやって……腰をぐりぐりさせるのと、上下に動くのを組み合わせるのよ」
		PRINTFORM 「ね？ こうすればお互い気持ち良くなれるわ
		CALL AUTOHEARTCB2_K14
		PRINTFORMW 　ふふ……」
		RETURN 1
	ENDIF
	
	;=================================================
	;派生３：Ａ性交から
	;=================================================
	IF 口上記入チェック(100) && TFLAG:ACT派生 == 3
		PRINTFORMW 「さぁ、今度は騎乗位よ」
		PRINTFORMW 「……待って。そっちじゃなくて私は前でシたいの」
		PRINTFORMW 「そう、それでいいわよ……じゃ、ゆっくり腰を下ろして」
		RETURN 1
	ENDIF
	
	;=================================================
	;派生４/派生５：Ｖ性交奉仕/Ａ性交奉仕から
	;=================================================
	IF 口上記入チェック(100) && GROUPMATCH(TFLAG:ACT派生, 4, 5) > 0
		;調教者が苛立ってる
		IF TENSION(TARGET, 7) == "高い"
			;汎用処理
			CALL PRINTTEXT_CATEGORY_3_ACT_4_K14
		;その他
		ELSE
			SELECTCASE RANDACT(3, 1)
				CASE 0
					PRINTFORMW 「今度は私がシてあげるわ。さ、上に乗って頂戴」
					PRINTFORMW 「そう、いいわよ。そのまま腰を下ろして……」
				CASE 1
					PRINTFORMW 「さぁ、今度は私の番……」
					PRINTFORMW 「そうよ……、私の上に乗って」
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ペニスバンド使用時
	;=================================================
	IF 口上記入チェック(100) && TRUECHECK(!PENIS(TARGET), TEQUIP:調教者ペニスバンド) > 0
		PRINTFORMW 「こんな無粋な代物でも、%CNAME(MASTER, TARGET)%が乱れるを眺められるのは悪くないわよね」
		PRINTFORM 「さぁ…、来て。遠慮なく乱れて頂戴…
		CALL PRINT_SYMBOL_K14
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	PRINTFORMW 「さぁ、私の上に跨って頂戴」
	PRINTFORM 「%CNAME(MASTER, TARGET)%がどんな乱れ方するのか、愉しみね
	CALL PRINT_SYMBOL_K14
	RETURN 1
ENDIF

;────────────────────────────────────
;ACT33, 対面座位
;────────────────────────────────────
;・対面座位・調教者が調教対象に挿入する
;・イニシアティブはTARGET(SEX_INITIATIVE(TARGET))
;・調教対象がオトコの場合は発生しない
;・調教者のふたなり化、若しくはペニスバンド装着によって発生する
;・TEQUIP(三角木馬/ペニスバンド/バイブ)装着中には発生しない
;・TFLAG:お仕置きフラグが立っていると発生しない
;・後背位、対面座位からは発生しない
;・ターン方針が"異常"だと発生しない
;・TFLAG:ACT派生は前ターンからの状態、及び体位の参照に充てられる
;-------------------------------------------------
@KOJO_ACT_K14_33
;-------------------------------------------------
;TFLAG:ACT派生(0=通常/1=連続実行/2=他のＶ性交から/3=Ａ性交から/4=Ｖ性交奉仕から/5=Ａ性交奉仕から)
;-------------------------------------------------
;・REACT分類が暴れるだった場合はキャンセルされる
;・受け入れた場合、MASTERが処女なら処女喪失、TARGETが童貞なら童貞喪失が起こる
;・0203版現在、拒絶した場合のSOURCEリセット処理がない
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;ACT33, 対面座位
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100)
	;=================================================
	;調教対象が処女
	;=================================================
	IF 口上記入チェック(100) && TALENT:MASTER:処女
		PRINTFORMW 「%CNAME(MASTER, TARGET)%のドキドキが伝わってくるわ。ほら、緊張しないで？」
		PRINTFORMW 「そう……、私の首に手を回して。大丈夫だから……」
		RETURN 1
	ENDIF
	
	;=================================================
	;調教者が童貞
	;=================================================
	IF 口上記入チェック(100) && TALENT:TARGET:童貞
		PRINTFORMW 「私の初めてはこの格好にするわ」
		PRINTFORM 「じゃ、よろしくお願いするわね
		CALL PRINT_SYMBOL_K14
		RETURN 1
	ENDIF
	
	;=================================================
	;前回中出し
	;=================================================
	IF 口上記入チェック(100) && TFLAG:前回中出し && TFLAG:前ターン性交
		;汎用処理
		CALL PRINTTEXT_CATEGORY_3_ACT_1_K14
		RETURN 1
	ENDIF
	
	;=================================================
	;調教者がイキそう
	;=================================================
	IF 口上記入チェック(100) && TCVAR:TARGET:イきそう
		;汎用処理
		CALL PRINTTEXT_CATEGORY_3_ACT_2_K14
		RETURN 1
	ENDIF
	
	;=================================================
	;派生１：連続(対面座位から)
	;=================================================
	IF 口上記入チェック(100) && TFLAG:ACT派生 == 1
		;-------------------------------------------------
		;調教者がイキそう
		;-------------------------------------------------
		IF 口上記入チェック(100) && TCVAR:TARGET:イきそう
			;2/3の確率で表示
			IF RAND:3
				PRINTFORMW 「はっ、ハァ……んんっ！ %KOJO_RAND_K14(("キス"), 1)%」
				PRINTFORMDW 自らの限界を感じ取った%CALLNAME:TARGET%は、キスをしながらより激しく腰を揺すりたてた…
			;1/3の確率で汎用処理を呼ぶ
			ELSE
				CALL PRINTTEXT_CATEGORY_3_ACT_2_K14
			ENDIF
			RETURN 1
		ENDIF
		
		;-------------------------------------------------
		;調教者が情欲
		;-------------------------------------------------
		IF 口上記入チェック(100) && T_COND("情欲")
			;汎用処理
			CALL PRINTTEXT_CATEGORY_3_ACT_3_K14
			RETURN 1
		ENDIF
		
		;---------------------
		;その他
		;---------------------
		SELECTCASE RANDACT(2, 0)
			CASE 0
				PRINTFORM 「ふふ、しっかりしがみついちゃって。可愛い…
				CALL PRINT_SYMBOL_K14
				PRINTFORMW 「ほら、キスしてあげる。もっと口を開けて……」
				;フェードアウト
				CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), KOJO_RAND_K14(("キス"), 1))
				CLEARLINE 1
			CASE 1
				PRINTFORMW 「%KOJO_RAND_K14(("キス"), 1)%」
				PRINTFORMW 「……可愛いわ。このまま壊しちゃいたいくらい……」
		ENDSELECT
		RETURN 1
	ENDIF
	
	;=================================================
	;派生２/派生３：体位変更(Ｖ性交/Ａ性交から)
	;=================================================
	IF 口上記入チェック(100) && GROUPMATCH(TFLAG:ACT派生, 2, 3) > 0
		;No.1
		SELECTCASE RANDACT(2, 1)
			CASE 0
				PRINTFORMW 「抱き合いながらするの、気持ち良いでしょう？」
				PRINTFORMW 「ほら、もっと強く抱きしめてあげるから…%CNAME(MASTER, TARGET)%も……」
			CASE 1
				PRINTFORMW 「さぁ、抱き合いながらするわよ」
				PRINTFORMW 「んっ、んちゅっ……そう、もっと舌を絡めて……」
		ENDSELECT
		RETURN 1
	ENDIF
	
	;=================================================
	;派生４/派生５：Ｖ性交奉仕/Ａ性交奉仕から
	;=================================================
	IF 口上記入チェック(100) && GROUPMATCH(TFLAG:ACT派生, 4, 5) > 0
		;調教者が苛立ってる
		IF TENSION(TARGET, 7) == "高い"
			;汎用処理
			CALL PRINTTEXT_CATEGORY_3_ACT_4_K14
		;その他
		ELSE
			SELECTCASE RANDACT(2, 2)
				CASE 0
					PRINTFORM 「はい、攻守交替
					CALL PRINT_SYMBOL_K14
					PRINTFORMW 「抱っこしながら突いてあげるわ。ふふふ……」
				CASE 1
					PRINTFORMW 「今度は私がしてあげるわ」
					PRINTFORMW 「ほら、抱き合ってキスしながら…」
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ペニスバンド使用時
	;=================================================
	IF 口上記入チェック(100) && TRUECHECK(!PENIS(TARGET), TEQUIP:調教者ペニスバンド) > 0
		PRINTFORMW 「こんな玩具でも%CNAME(MASTER, TARGET)%の中を感じとる事はできるわ」
		PRINTFORMW 「さぁ、来て……抱きしめながら、男みたいに突いてあげるから…」
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	PRINTFORMW 「抱っこしながら突いてあげるわ」
	PRINTFORM 「さぁ、いらっしゃい。可愛がってあげる…
	CALL PRINT_SYMBOL_K14
	RETURN 1
ENDIF

;────────────────────────────────────
;ACT34, 背面座位
;────────────────────────────────────
;・背面座位・調教者が調教対象に挿入する
;・イニシアティブはTARGET(SEX_INITIATIVE(TARGET))
;・調教対象がオトコの場合は発生しない
;・調教者のふたなり化、若しくはペニスバンド装着によって発生する
;・TEQUIP(三角木馬/ペニスバンド/バイブ)装着中には発生しない
;・TFLAG:お仕置きフラグが立っていると発生しない
;・後背位、対面座位からは発生しない
;・ターン方針が"ソフト", "異常"だと発生しない
;・正常位、対面座位からは発生しない
;・TFLAG:ACT派生は前ターンからの状態、及び体位の参照に充てられる
;-------------------------------------------------
@KOJO_ACT_K14_34
;-------------------------------------------------
;TFLAG:ACT派生(0=通常/1=連続実行/2=他のＶ性交から/3=Ａ性交から/4=Ｖ性交奉仕から/5=Ａ性交奉仕から)
;-------------------------------------------------
;・REACT分類が暴れるだった場合はキャンセルされる
;・受け入れた場合、MASTERが処女なら処女喪失、TARGETが童貞なら童貞喪失が起こる
;・v1.010現在、拒絶した場合のSOURCEリセット処理がない
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;ACT34, 背面座位
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100)
	;=================================================
	;調教対象が処女
	;=================================================
	IF 口上記入チェック(100) && TALENT:MASTER:処女
		PRINTFORMW 「ほら、体を預けて。そう、それでいいわ……」
		PRINTFORMW 「心配しないで……、私に任せて頂戴」
		RETURN 1
	ENDIF
	
	;=================================================
	;調教者が童貞
	;=================================================
	IF 口上記入チェック(100) && TALENT:童貞
		PRINTFORMW 「私の初めてはこの格好にするわ」
		PRINTFORMW 「じゃ、よろしくお願いするわね」
		RETURN 1
	ENDIF
	
	;=================================================
	;前回中出し
	;=================================================
	IF 口上記入チェック(100) && TFLAG:前回中出し && TFLAG:前ターン性交
		;汎用処理
		CALL PRINTTEXT_CATEGORY_3_ACT_1_K14
		RETURN 1
	ENDIF
	
	;=================================================
	;派生１：連続(対面座位から)
	;=================================================
	IF 口上記入チェック(100) && TFLAG:ACT派生 == 1
		;-------------------------------------------------
		;調教者がイキそう
		;-------------------------------------------------
		IF 口上記入チェック(100) && TCVAR:TARGET:イきそう
			;2/3の確率で表示（RANDACTでいいような気もする）
			IF RAND:3
				PRINTFORMW 「おっぱい弄る度に中がキュンキュンして可愛らしいわね…」
				PRINTFORMW 「んんっ！ これじゃ私……、もうイっちゃいそう」
			;1/3の確率で汎用処理
			ELSE
				CALL PRINTTEXT_CATEGORY_3_ACT_2_K14
			ENDIF
			RETURN 1
		ENDIF
		
		;-------------------------------------------------
		;調教者が情欲
		;-------------------------------------------------
		IF 口上記入チェック(100) && T_COND("情欲")
			;汎用処理
			CALL PRINTTEXT_CATEGORY_3_ACT_3_K14
			RETURN 1
		ENDIF
		
		;---------------------
		;その他
		;---------------------
		PRINTFORMW 「繋がったまま、いっぱい弄ってあげるわ」
		PRINTFORMW 「ほら、耳も可愛がってあげる……」
		;フェードアウト
		CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), KOJO_RAND_K14(("キス"), 1))
		CLEARLINE 1
		PRINTFORMDW %CALLNAME:TARGET%は%CALLNAME:MASTER%を背後から貫いたまま、全身を愛撫している…
		RETURN 1
	ENDIF
	
	;=================================================
	;派生２：体位変更(Ｖ性交から)
	;=================================================
	IF 口上記入チェック(100) && TFLAG:ACT派生 == 2
		PRINTFORMW 「さぁ、体を預けて。後ろからシてあげるから」
		PRINTFORM 「%CNAME(MASTER, TARGET)%のキモチイイとこ、全部弄ってあげるわ
		CALL PRINT_SYMBOL_K14
		RETURN 1
	ENDIF
	
	;=================================================
	;派生３：体位変更(Ａ性交から)
	;=================================================
	IF 口上記入チェック(100) && TFLAG:ACT派生 == 3
		PRINTFORM 「今度はこの体勢よ。と言っても……、入れるのは前だけど、ね
		CALL PRINT_SYMBOL_K14
		PRINTFORMW 「ほら、乳首も摘まんであげるわ…」
		RETURN 1
	ENDIF
	
	;=================================================
	;派生４/派生５：Ｖ性交奉仕/Ａ性交奉仕から
	;=================================================
	IF 口上記入チェック(100) && GROUPMATCH(TFLAG:ACT派生, 4, 5) > 0
		;調教者が苛立ってる
		IF TENSION(TARGET, 7) == "高い"
			;汎用処理
			CALL PRINTTEXT_CATEGORY_3_ACT_4_K14
		;その他
		ELSE
			SELECTCASE RANDACT(2, 0)
				CASE 0
					PRINTFORMW 「%CNAME(MASTER, TARGET)%見てたら……、私からシたくなっちゃったわ。ふふ」
					PRINTFORM 「さぁ、後ろから抱きしめながら突いてあげる
					CALL PRINT_SYMBOL_K14
				CASE 1
					PRINTFORMW 「今度は私からシてあげる……」
					PRINTFORMW 「さぁ、こっちに体を預けて。そう、いいわよ……ふふ」
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;=================================================
	;ペニスバンド使用時
	;=================================================
	IF 口上記入チェック(100) && TRUECHECK(!PENIS(TARGET), TEQUIP:調教者ペニスバンド) > 0
		PRINTFORMW 「こんな玩具でも%CNAME(MASTER, TARGET)%の中を感じとる事はできるわ」
		PRINTFORM 「後ろから抱きしめながら可愛がってあげる……
		CALL PRINT_SYMBOL_K14
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	SELECTCASE RANDACT(2, 1)
		CASE 0
			PRINTFORMW 「%CNAME(MASTER, TARGET)%の髪、良い香りね……」
			PRINTFORMW 「ふふ、キモチいいとこ全部弄ってあげるわ…」
		CASE 1
			PRINTFORMW 「後ろから抱きしめながらシてあげるわ」
			PRINTFORMW 「そうよ、そのまま……、私に背中を預けて」
	ENDSELECT
	RETURN 1
ENDIF

;────────────────────────────────────
;ACT35, アナルセックス
;────────────────────────────────────
;・調教対象の性別に関わらず調教者のふたなり化、ペニスバンド装着によって発生する
;・アナルセックス・調教者が調教対象のＡに挿入する
;・イニシアティブはTARGET(SEX_INITIATIVE(TARGET))
;・調教対象の性別に関わらず調教者のふたなり化、ペニスバンド装着によって発生する
;・TEQUIP(三角木馬/ペニスバンド/バイブ/浣腸器＋プラグ)装着中には発生しない
;・TFLAG:お仕置きフラグが立っていると発生しない
;・後背位、対面座位からは発生しない
;・ターン方針が"ソフト", "異常"だと発生しない
;・調教対象のＡ経験が2未満かつ、技巧2未満だと発生しない
;・TFLAG:ACT派生は前ターンからの状態、及び体位の参照に充てられる
;-------------------------------------------------
@KOJO_ACT_K14_35
;-------------------------------------------------
;・TFLAG:ACT派生(0=通常/1=連続実行/3=Ｖ性交から/4=Ｖ性交奉仕から/5=Ａ性交奉仕から)
;・アナルセックスの場合TFLAG:ACT派生 == 2は定義されていない
;-------------------------------------------------
;・アナルセックスで童貞を捨てた扱いにはならない
;・TEQUIP(アナルバイブ/アナルビーズ)はACTION_APPLYで解除される
;・REACT分類が暴れるだった場合はキャンセルされる
;・v1.010現在、拒絶した場合のSOURCEリセット処理がない
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;ACT35, アナルセックス
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100)
	;TALENT:小柄はお子様扱いにする方針
	;ごちゃごちゃするのが嫌なんでオトコとそれ以外では小分岐を分けつつFIRSTACTMESSAGEとFIRSTACTを共用する
	;=================================================
	;初回且つ調教対象がオトコ
	;=================================================
	IF 口上記入チェック(100) && (TALENT:MASTER:オトコ && FLAG:地の文制御 == 2 && FIRSTACT(0))
		;調教対象が小柄体型
		IF TALENT:MASTER:小柄
			PRINTFORMW 「\@ TALENT:MASTER:童貞 ? こういう事 # おしり \@は初めて？ ……って、聞くまでもないわよね。ふふ」
			PRINTFORMW 「別に心配しなくていいわ。痛めつけようって訳じゃないから」
			PRINTFORMW 「と言っても……、キミみたいな小さな子には刺激が強すぎるかもだけど、ね」
		;その他
		ELSE
			PRINTFORMW 「女にこんな事されるの、男としては最高に惨めだと思うけど…」
			PRINTFORMW 「%CNAME(MASTER, TARGET)%の場合はどうなのかしら、ね？ フフ」
		ENDIF
		;地の文捏造
		PRINTFORMDL %CALLNAME:TARGET%はそう言うと%CALLNAME:MASTER%のアヌスに\@ TEQUIP:調教者ペニスバンド ? %DHARNESS_DESCRIPTION(TARGET)% # 自身のペニス \@をあてがい、
		PRINTFORMDW 一気に刺し貫こうとした…
		;空行
		PRINTL
		;TALENTによって分ける
		IF TALENT:MASTER:小柄
			PRINTFORMW 「ほら、もっと力を抜くのよ。痛くないようにしてあげるから」
			PRINTFORMW 「さぁ…、女の子みたいに鳴かせてあげる。いい声で鳴いて頂戴……」
		ELSE
			PRINTFORMW 「ほら、もっと力を抜きなさい。痛い思いはしたくないでしょう？」
			PRINTFORMW 「さぁ…、女みたいに鳴かせてあげるから、覚悟して……」
		ENDIF
		RETURN 1
	ENDIF
	
	;初回時は本体の地の文を殺してFIRSTACTMESSAGEを使う。戻り値は2
	;KOJO_TRAIN_MESSAGEは口上側で捏造する場合戻り値を1
	;その他の場合は戻り値を2で統一し、関数内で分岐させる方針
	;=================================================
	;初回且つ調教対象がオトコ以外
	;=================================================
	IF 口上記入チェック(100) && (!TALENT:MASTER:オトコ && FLAG:地の文制御 == 2 && FIRSTACT(0))
		;調教対象が小柄体型
		IF TALENT:MASTER:小柄
			PRINTFORMW 「そういえばおしりでするのは初めてよね？ ……って、当たり前過ぎたかしら。ふふ」
			PRINTFORMW 「大丈夫、怖がる事ないわ。楽にしてれば自然に受け入れられるから」
			PRINTFORMW 「ま、そんな事いっても……%CNAME(MASTER, TARGET)%みたいな小さな子には何の慰めにもならないかもだけど、ね」
		;その他
		ELSE
			;調教対象が処女
			IF TALENT:MASTER:処女
				PRINTFORMW 「初めてがおしりってちょっとどうなのって気はするけど…」
				PRINTFORMW 「%CNAME(MASTER, TARGET)%は興味津々ってところじゃない？ 違うかしら？ ふふ」
			;その他
			ELSE
				PRINTFORMW 「ん、そうね……\@ EXP:性交経験 < 1 ? %CNAME(MASTER, TARGET)%との初めてが # \@何もおしりじゃなくてもいい気はするけど」
				PRINTFORMW 「なぜか今…ふふ。そういう気分なのよね\@ TENSION(TARGET, 8) == "高い" ? ぇ # \@。ま、そういう訳だから……大人しく諦めて頂戴」
			ENDIF
		ENDIF
		;地の文捏造
		PRINTFORMDL %CALLNAME:TARGET%はそう言うと%CALLNAME:MASTER%のアヌスに\@ TEQUIP:調教者ペニスバンド ? %DHARNESS_DESCRIPTION(TARGET)% # 自身のペニス \@をあてがい、
		PRINTFORMDW 一気に刺し貫こうとした…
		;空行
		PRINTL
		;TALENTによって分ける
		IF TALENT:MASTER:小柄
			PRINTFORMW 「ほら、息を吐いて力を抜くのよ。少しずつ拡げていってあげるから」
			PRINTFORMW 「さぁ…、可愛がってあげる。いい声で鳴いて頂戴……」
		ELSE
			PRINTFORMW 「ほら、緊張しないで楽にして。こわばらなければ自然に受け入れられるわ」
			PRINTFORMW 「さぁ…、メス豚みたいに鳴かせてあげるから、覚悟して……」
		ENDIF
		RETURN 1
	ENDIF
	
	;実際問題として現状は調教者が性交で射精したあと、そのまま性交を継続する事はまずない感じ
	;とはいえ挙動が変わる可能性や、枠を作っておけば口上側で捻じ曲げる事もできるんでまぁやるしかってところ
	;=================================================
	;前回中出し
	;=================================================
	IF 口上記入チェック(100) && TFLAG:前回中出し && TFLAG:前ターン性交
		SELECTCASE TFLAG:前回中出し
			;前回Ｖ中出し
			CASE 1
				;前回COM派生
				SELECTCASE TRAINNAME:PREVCOM
					CASE "中に出して！"
						PRINTFORMW 「さて、今度はこっちに注ぎ込んであげようかしら？ ふふ」
						PRINTFORMW 「ほら、いくわよ……そう、力を抜いて」
					CASE "中に出さないで！"
						PRINTFORMW 「ねぇ、おしりの場合でも中に出されるのは嫌？」
						PRINTFORMW 「な～んて、今聞くのは野暮かしらね？ どうせすぐに判るもの…」
						;フェードアウト
						CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), "「フフフ……」")
						CLEARLINE 1
					CASEELSE
						PRINTFORMW 「ほら、大人しくして。今度はおしりに注ぎ込んであげるから」
						PRINTFORM 「%CNAME(MASTER, TARGET)%がそれを望むかどうかは知らないけど、ね
						CALL PRINT_SYMBOL_K14
				ENDSELECT
			;前回空射精
			CASE 3, 7
				PRINTFORMW 「流石に私も打ち止めみたいだけど……、まだ%CNAME(MASTER, TARGET)%の中を愉しむ事はできそうね」
				PRINTFORMW 「さぁ、続けるわよ。いい声で鳴いて頂戴……」
			;前回Ａ中出し
			CASE 5
				;前回COM派生
				SELECTCASE TRAINNAME:PREVCOM
					CASE "中に出して！"
						PRINTFORMW 「%CNAME(MASTER, TARGET)%のおしりの中、ヌルヌルして凄い事になってるわ。ふふ」
						PRINTFORMW 「このままグチャグチャに掻き混ぜてあげるから……」
					CASE "中に出さないで！"
						PRINTFORMW 「どうしておしりの中で出されるのが嫌なのか判らないけど……」
						PRINTFORMW 「何か言ったところで無駄なのはそろそろ理解してもいいんじゃないかしら？」
						PRINTFORMW 「ま、そんな訳で……、まだまだ愉しませて貰うわ。悪いけど、ね」
					CASEELSE
						PRINTFORMW 「ほら、まだ続けるわよ。今の私、こんな程度じゃ満足できないの」
						PRINTFORMW 「溢れちゃうくらい注ぎ込んであげるから覚悟して……」
				ENDSELECT
		ENDSELECT
		RETURN 1
	ENDIF
	
	;ACT派生は可能な限り刻む
	;本当はTALENT等でもっと細かく分けたいけど、手間を考えるとある程度妥協せざるを得ない
	;まぁいずれ余裕ができたらってところ
	;=================================================
	;派生１：連続(Ａセックスから)
	;=================================================
	IF 口上記入チェック(100) && TFLAG:ACT派生 == 1
		;-------------------------------------------------
		;調教者がイキそう
		;-------------------------------------------------
		IF 口上記入チェック(100) && TCVAR:TARGET:イきそう
			;2/3の確率で表示
			IF RAND:3
				PRINTFORMW 「……とってもきつくて気持良いわ。%CNAME(MASTER, TARGET)%もキモチイイでしょ？ …って」
				PRINTFORMW 「正直、そんな事言ってる余裕は、もう無いのよね……んんっ！」
			;1/3の確率で汎用処理
			ELSE
				;汎用処理
				CALL PRINTTEXT_CATEGORY_3_ACT_2_K14
			ENDIF
			RETURN 1
		ENDIF
		
		;-------------------------------------------------
		;調教者が情欲
		;-------------------------------------------------
		IF 口上記入チェック(100) && T_COND("情欲")
			PRINTFORMW 「……根元まで飲み込んじゃってイヤラシイわ。ほら、どうなの？ おしりキモチイイの？」
			PRINTFORMW 「ま、%CNAME(MASTER, TARGET)%みたいなマゾ豚にはお似合いの姿よ。壊れちゃうくらい突いてあげるから、存分に鳴いて頂戴」
			CALL FADEIN(10, "「豚みたいに、ね……」")
			;満足度50%以上で表示
			IF TENSION(TARGET, 8) == "高い"
				WAIT
				;フェードアウト
				CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), "「クスクス」")
				CLEARLINE 1
			;TENSIONから漏れたらWAIT
			ELSE
				WAIT
			ENDIF
			RETURN 1
		ENDIF
		
		;---------------------
		;その他
		;---------------------
		SELECTCASE RANDACT(2, 0)
			CASE 0
				PRINTFORMW 「ほら、判る？ %CNAME(MASTER, TARGET)%のおしりの中が\@ TEQUIP:調教者ペニスバンド ? この玩具の # 私の \@形になってるの…」
				PRINTFORM 「ふふ……、さぁ、もっといい声で鳴いて頂戴。お漏らししちゃうくらい、突きまくってあげるから
				CALL PRINT_SYMBOL_K14
			CASE 1
				PRINTFORMW 「フフ、\@ TALENT:MASTER:オトコ ? 女みたいな # 可愛らしい \@声出しちゃって……」
				PRINTFORM 「そんなにいいなら、もっと激しくシてあげるわ…
				CALL PRINT_SYMBOL_K14
		ENDSELECT
		RETURN 1
	ENDIF
	
	;=================================================
	;派生３：Ｖ性交から
	;=================================================
	IF 口上記入チェック(100) && TFLAG:ACT派生 == 3
		PRINTFORMW 「今度はおしりで愉しませて頂戴」
		PRINTFORMW 「ふふ……可愛らしいおちょぼ口ね。さ、息を吐いて力を抜きなさい…」
		RETURN 1
	ENDIF
	
	;=================================================
	;派生４/派生５：Ｖ性交奉仕/Ａ性交奉仕から
	;=================================================
	IF 口上記入チェック(100) && GROUPMATCH(TFLAG:ACT派生, 4, 5) > 0
		;調教者が苛立ってる
		IF TENSION(TARGET, 7) == "高い"
			;汎用処理
			CALL PRINTTEXT_CATEGORY_3_ACT_4_K14
		;その他
		ELSE
			;No.1
			SELECTCASE RANDACT(2, 1)
				CASE 0
					PRINTFORMW 「%WARD_R_K14()%……随分激しくシてくれたじゃない…」
					PRINTFORMW 「ほら、おしりをこっちに向けなさい。たっぷりとお返ししてあげるわ。ふふっ」
				CASE 1
					PRINTFORMW 「ふふ……攻守交替ってとこかしら」
					PRINTFORM 「さぁ、いくわよ。覚悟して頂戴…
					CALL PRINT_SYMBOL_K14
			ENDSELECT
		ENDIF
		RETURN 1
	ENDIF
	
	;本体側の地の文の記述に合わせてABLで3段階に分ける
	;-------------------------
	;その他
	;-------------------------
	LOCALS = \@ TEQUIP:調教者ペニスバンド ? この玩具 # 私の形 \@
	SELECTCASE ABL:MASTER:Ａ感覚
		;A開発済
		CASE IS > 3
			PRINTFORM 「ふふ、
			SIF TEQUIP:調教者ペニスバンド
				PRINTFORM こんな紛い物でも
			PRINTFORMW %CNAME(MASTER, TARGET)%のおしりがトロトロになってるのが良く判るわ」
			PRINTFORMW 「あっさり奥まで飲み込んじゃって、ホントいやらしい……」
			PRINTFORMW 「\@ TALENT:MASTER:オトコ ? 男としては情けない限りね？ クスっ # まぁでも……、中々可愛らしいわ。ふふ \@」
			PRINTFORMW 「さぁ、いくわよ。%CNAME(MASTER, TARGET)%のおしり…、めちゃめちゃにしてあげる」
			PRINTFORM 「形が変わっちゃう位、突きまくってあげるから…
			CALL PRINT_SYMBOL_K14
		;A開発途中
		CASE IS > 1
			PRINTFORMW 「……大分すんなり入るようになったわ。すっかり%LOCALS%に馴染んだようで何よりね？ ふふ」
			PRINTFORMW 「ほら、息を吐いて楽にして……いい子にしてれば可愛がってあげる」
			PRINTFORMW 「尤も……%CNAME(MASTER, TARGET)%は虐められた方が嬉しいのかしら、ねぇ？」
			;フェードアウト
			CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), "「クスクス」")
			CLEARLINE 1
		;A未開発
		CASEELSE
			PRINTFORMW 「……流石にまだきつい感じね。ちょっと苦しいかしら？ フフ」
			PRINTFORMW 「でも大丈夫よ。%CNAME(MASTER, TARGET)%のおしり……、少しずつ%LOCALS%に慣らしていってあげるわ」
	ENDSELECT
	RETURN 1
ENDIF

;*************************************************
;汎用関数置き場
;*************************************************

;-------------------------------------------------
;前回中出し
;Ａセックスは当該ACTで別個に処理する
;-------------------------------------------------
@PRINTTEXT_CATEGORY_3_ACT_1_K14
SELECTCASE TFLAG:前回中出し
	;前回Ｖ膣内出し
	CASE 1
		;前回COM派生
		SELECTCASE TRAINNAME:PREVCOM
			CASE "中に出して！"
				PRINTFORMW 「%CNAME(MASTER, TARGET)%の中、ヌルヌルして凄くイヤラシイわ。ふふ」
				PRINTFORMW 「ほら、まだ続けるわよ。お望み通り、たっぷりと注ぎ込んであげるから…」
			CASE "中に出さないで！"
				PRINTFORMW 「%CNAME(MASTER, TARGET)%にはまだ判らないのかしら？」
				PRINTFORMW 「わたし、中に出さないでなんて言われると余計にやりたくなっちゃうの」
				PRINTFORMW 「ま、何を言うのも%CNAME(MASTER, TARGET)%の勝手だけど、それでどうこうなる訳じゃないから…」
			CASEELSE
				PRINTFORMW 「折角膣内に出したんだからちゃんと掻き混ぜないと、ってね」
				PRINTFORMW 「%CNAME(MASTER, TARGET)%だって、私にこうされるのを望んでいるんでしょう？」
				PRINTFORMW 「正直に言えたら、もっと可愛がってあげるわ…」
		ENDSELECT
	;前回空射精
	CASE 3, 7
		PRINTFORMW 「流石に私も打ち止めみたいだけど……、まだ%CNAME(MASTER, TARGET)%の中を愉しむ事はできそうね」
		PRINTFORMW 「さぁ、続けるわよ。いい声で鳴いて頂戴……」
	;前回Ａ中出し
	CASE 5
		PRINTFORMW 「ほら、今度はこっちに注ぎ込んであげるわ。おしりだけじゃ気の毒ですものね？ ふふ」
		PRINTFORMW 「さぁ、いくわよ。準備はいいかしら……」
ENDSELECT

;-------------------------------------------------
;調教者がイキそう
;当該小分岐でTCVAR:イきそうを条件にしているので関数では省く
;-------------------------------------------------
@PRINTTEXT_CATEGORY_3_ACT_2_K14
;調教対象もイきそう
IF (IS_NOWACTNAME("正常位/後背位/騎乗位/対面座位/背面座位") && PALAM:MASTER:快Ｖ >= 8500) || (IS_NOWACTNAME("アナルセックス") && PALAM:MASTER:快Ａ >= 8500) || TCVAR:MASTER:イきそう
	PRINTFORMW 「%WARD_R_K14()%……私、もう限界だけど…、%CNAME(MASTER, TARGET)%も同じみたいね」
	PRINTFORMW 「一緒にイければいいけれど、後は%CNAME(MASTER, TARGET)%次第かしら、ね」
;快Ｖもしくは快Ａが5000以上
ELSEIF (IS_NOWACTNAME("正常位/後背位/騎乗位/対面座位/背面座位") && PALAM:MASTER:快Ｖ >= 5000) || (IS_NOWACTNAME("アナルセックス") && PALAM:MASTER:快Ａ >= 5000)
	PRINTFORMW 「%WARD_R_K14()%……そろそろ、限界、ね…」
	PRINTFORMW 「悪いけどこのままイかせてもらうわ……」
;その他
ELSE
	PRINTFORMW 「んっ、くッ！ わたし、もう限界みたい…」
	PRINTFORMW 「ねぇ、中と外……、どちらをお望みかしら？ フフ」
ENDIF

;-------------------------------------------------
;調教者が情欲。Ａセックスは汎用処理しない
;-------------------------------------------------
@PRINTTEXT_CATEGORY_3_ACT_3_K14
;2/3の確率で表示
IF RAND:3
	PRINTFORM 「%RANDTEXT_K14("ハッ、はぁ", "はぁ、はぁ", "ハァ、ハァ", "はっ、ハァ")%……可愛い
	CALL HEARTCB(COLOR2_K14("pink"), 1, "ＭＳ Ｐゴシック")
	PRINTFORMW 　私、止まらなくなっちゃう%RANDTEXT_K14("わ", "＿")%…」
	;フェードアウト
	CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), KOJO_RAND_K14(("キス"), 1))
	CLEARLINE 1
	PRINTFORMD %CALLNAME:TARGET%は%CALLNAME:MASTER%
	;騎乗位は弾く
	IF IS_NOWACTNAME("正常位/後背位/対面座位/背面座位")
		PRINTFORMD の顔を
	ENDIF
	;正常位、対面座位
	IF IS_NOWACTNAME("正常位/対面座位")
		PRINTFORMD 引き寄せ、
	;後背位、背面座位
	ELSEIF IS_NOWACTNAME("後背位/背面座位")
		PRINTFORMD 振り向かせ、
	;騎乗位
	ELSEIF IS_NOWACTNAME("騎乗位")
		PRINTFORMD を抱き寄せ、
	ENDIF
	;調教者満足度50%以上
	SIF TENSION(TARGET, 8) == "高い"
		PRINTFORMD 情熱的な
	PRINTFORMDW キスをしながら腰を激しく\@ IS_NOWACTNAME("騎乗位") ? 突き上げた # 揺すりたてた \@…
;1/3の確率で表示
ELSE
	PRINTFORMW 「ねぇ、キモチイイ？ キモチイイわよねぇ？」
	PRINTFORMW 「んッ！ フフ……%CNAME(MASTER, TARGET)%、とってもいいわよ。ほら、もっと声を聴かせて…」
ENDIF

;-------------------------------------------------
;Ｖ性交奉仕/Ａ性交奉仕から且つ調教者が苛立っている
;-------------------------------------------------
@PRINTTEXT_CATEGORY_3_ACT_4_K14
PRINTL
PRINTFORMDW %CALLNAME:TARGET%は%CALLNAME:MASTER%の消極的な姿勢に苛立っているようだ…
SELECTCASE AUTORAND(2, 130)
	CASE 0
		PRINTFORMW 「もうダメ、こんなの……もどかしくってやってられないわ！」
		PRINTFORMW 「さぁ、覚悟はいいかしら？ 私が手本を見せてあげるから……」
	CASE 1
		PRINTFORMW 「冗談じゃないわよ、こんなの……もうっ、本当にイライラするわ！」
		PRINTFORMW 「さぁ覚悟して。どうすればいいのか、その身体に直接教えてあげるから……」
ENDSELECT
IF IS_NOWACTNAME("アナルセックス")
	CALL FADEIN(8, "「ただし……おしりで、ね。フフフ…」")
	WAIT
ENDIF
