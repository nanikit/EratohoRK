;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	アリス口上 追加アクション口上ファイル
;
;	調教者が調教指示以外に行う行動のファイル
;
;Version		Reason
;1.71			@口上記入チェック仕様に
;1.67			脱衣系を全面的に改修
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;========================================================================
;調教アクション・コマンド口上
;========================================================================

;────────────────────────────────────
;脱衣アクション口上
;────────────────────────────────────
;・調教者アクションの前に強制脱衣や自主脱衣が発生したときに表示
;・この口上の時点ではまだTEQUIPは更新されていない
;・初回分岐は「初めて見た/見せた」とは限らない。
;　ショップメニューで脱がせたことがあるかもしれないし、調教の流れで完全に脱がせずに露出だけしている場合もある。
;　もしかすると助手だったときに見たことがあるかもしれない。そういう意味では簡易的なものである。
;　厳密な意味での「初見」フラグを用意するかどうかは要望次第ということで。
;-------------------------------------------------
@KOJO_DATUI_K14
;-------------------------------------------------
;関連変数…
;強制脱衣：TFLAG:102　自主脱衣：TFLAG:103
;=(-1, 強制脱衣に抵抗、1, 上着、2, 下半身上着、3, 上衣、4, 上半身全部、5, 下半身全部、
;　6, アクセサリー、7, アクセサリー以外全て、8, 全裸まで、9, 下着だけ)
;-------------------------------------------------
;見直すついでにちょっと梃入れしてみる
;考えてみると、調教内では毎度必ず出てくる箇所なんで相応に注力すべきなのかも知れない
;-------------------------------------------------
;単に@KOJO_DATUI_KXで呼び出すとか、システム側が投げやりすぎる感がある
;ARGで分けるなり、関数分けするなり、本体側が枠組みをきちんと整えるべきなのかも知れない
;とかいいながら、中々手が付かないのが現状な訳なんですが
;せめてフラグ類の関数化、GET_DATUI(TFLAG:自主脱衣) == "まんまんうｐ"みたいなのはやるべきなのかもｗ
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;強制脱衣に抵抗された FIRSTDATUI(0)
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:強制脱衣 < 0
	;=================================================
	;初回
	;=================================================
	IF 口上記入チェック(100) && FIRSTDATUI(0)
		PRINTFORMW 「なによ、逆らうつもり？ まぁ%RAND_SPLIT("いいわ/でも")%…」
		PRINTFORMW 「抵抗するなら無理やり脱がせるまでよ」
		RETURN 1
	ENDIF
	
	;=================================================
	;自分は脱いだ
	;=================================================
	IF 口上記入チェック(100) && TFLAG:自主脱衣
		SELECTCASE RANDDATUI(3, 0)
			CASE 0
				PRINTFORMW 「仕様の無い人ね…」
			CASE 1
				PRINTFORMW 「やれやれ……。面倒なひとね」
			CASE 2
				PRINTFORMW 「へぇ、そう。勝手にすれば？」
		ENDSELECT
		PRINTFORMW 「ま、私は脱がさせてもらうけど」
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	;調教者BASE:苛立ち派生
	SELECTCASE TENSION(TARGET, 7)
		CASE "高い"
			PRINTFORMW 「またそうやって逆らうのね」
			PRINTFORMW 「%CNAME(MASTER, TARGET)%…、私を苛立たせる才能があるわ……」
		CASE "一般"
			PRINTFORMW 「……面倒な人ね」
			PRINTFORMW 「私に無理やり裸に剥かれるのがお望み？」
		CASE "低い"
			PRINTFORMW 「%CNAME(MASTER, TARGET)%、もしかすると…」
			PRINTFORMW 「着たままするのが、好きだったりするのかしら…？」
	ENDSELECT
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;二人とも何か脱いだ FIRSTDATUI(1)
;------------------------------------------------
;・主にセックス系行動の予備動作で発生する
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:強制脱衣 > 0 && TFLAG:自主脱衣 && !IS_NOWASSIACTNAME(, "脱衣")
	;=================================================
	;調教者が[情欲]かつ全裸になった
	;=================================================
	IF 口上記入チェック(100) && T_COND("情欲") && ZENRACHECK(0)
		PRINTFORMW 「こんなもの、もう必要ないわ！」
		PRINTFORM 「さぁ、生まれたままの姿で愉しみましょう…
		CALL PRINT_SYMBOL_K14
		RETURN 1
	ENDIF
	
	[SKIPSTART]
	;=================================================
	;調教者が調教対象を溺愛
	;=================================================
	IF 口上記入チェック(0) && AFFECTION("溺愛")
		PRINTFORMW 「さぁ、脱いで脱いで」
		PRINTFORM 「ふふ…、思いっきり愉しみましょう…
		CALL PRINT_SYMBOL_K14
		RETURN 1
	ENDIF
	[SKIPEND]
	
	;-------------------------
	;その他
	;-------------------------
	;調教者BASE:満足派生
	SELECTCASE TENSION(TARGET, 8)
		CASE "高い"
			PRINTFORMW 「やっぱりこうでなくちゃね」
			PRINTFORMW 「%CNAME(MASTER, TARGET)%の体温を感じたいし。ま、私の身体は…」
			PRINTFORM 「ちょっとひんやりするかもだけど。ね…
			CALL PRINT_SYMBOL_K14
		CASE "一般"
			PRINTFORMW 「着たままするのも悪くないけど…」
			PRINTFORMW 「やっぱり邪魔よね？ ふふ…」
		CASE "低い"
			PRINTFORMW 「準備はいいかしら…？」
			PRINTFORM 「じゃぁ、早速始め\@ AFFECTION("溺愛") ? ましょう # るわよ \@
			IF AFFECTION("溺愛")
				CALL PRINT_SYMBOL_K14
			ELSE
				PRINTW 」
			ENDIF
	ENDSELECT
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;自主脱衣・アクセサリーのみ FIRSTDATUI(2)
;------------------------------------------------
;・主に靴下。他には手袋や袖など
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:自主脱衣 == 6 && !IS_NOWASSIACTNAME(, "脱衣") && !TEQUIP:アイマスク
	;=================================================
	;全裸になった（脱ぐ前は靴下以外裸とかそういう感じ）
	;=================================================
	IF 口上記入チェック(100) && TFLAG:自主脱衣 && ZENRACHECK(0)
		SELECTCASE RANDDATUI(2, 2)
			CASE 0
				PRINTFORMW 「ねぇ、見て頂戴。私の裸、どうかしら…？」
			CASE 1
				PRINTFORMW 「裸に靴下とかの方が\@ AFFECTION("相思相愛") ? 良かった？ # %CNAME(MASTER, TARGET)%の好みだったかしら \@」
				PRINTFORM 「まぁでも…、もう脱いじゃったものは仕方無いわよね
				CALL PRINT_SYMBOL_K14
		ENDSELECT
		RETURN 1
	ENDIF
	
	;その他
	PRINTFORMW 「もう%RAND_SPLIT("余計/こん")%なものは要らないわね」
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;自主脱衣・まんまんうｐ FIRSTDATUI(3)
;------------------------------------------------
;・今回の脱衣で性器が見えるようになった場合に表示
;・ぱんつ見せを経由せず一気に脱ぐ場合あり
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:自主脱衣 && P_DATUICHECK(0) == 3 && !IS_NOWASSIACTNAME(, "脱衣") && !TEQUIP:アイマスク
	;=================================================
	;初回
	;=================================================
	IF 口上記入チェック(100) && TALENT:TARGET:ご主人様 && FIRSTDATUI(3)
		PRINTFORMW 「どう？ 私の誰にも見せた事のない部分よ」
		PRINTFORMW 「その意味に気づかない程、%CNAME(MASTER, TARGET)%が馬鹿じゃなければ良いんだけど、ね」
		RETURN 1
	ENDIF
	
	;=================================================
	;ちんこうｐ
	;=================================================
	IF 口上記入チェック(100) && PENIS(TARGET)
		SELECTCASE RANDDATUI(2, 3)
			CASE 0
				PRINTFORMW 「ほら、見て頂戴……」
				PRINTFORMW 「これで%CNAME(MASTER, TARGET)%を虐めたくて、もうこんなになっちゃってるの」
			CASE 1
				PRINTFORMW 「さぁ、良く見ておきなさい」
				PRINTFORM 「コレで%CNAME(MASTER, TARGET)%がどうされちゃうのか、想像しながら。ね…
				CALL PRINT_SYMBOL_K14
		ENDSELECT
		;フェードアウト
		CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"「\@ AFFECTION("相思相愛") ? ふふふ # フフフ \@…」")
		CLEARLINE 1
		RETURN 1
	ENDIF
	
	;=================================================
	;すでに胸が見えてる
	;=================================================
	IF 口上記入チェック(100) && B_DATUICHECK(0) == 1
		PRINTFORMW 「そんな目で見られたら…」
		PRINTFORM 「こっちも見せない訳にはいかないわね…
		CALL PRINT_SYMBOL_K14
		RETURN 1
	ENDIF
	
	;その他
	PRINTFORMW 「ほら、遠慮しないで」
	PRINTFORM 「もっと良く見ていいのよ…
	CALL PRINT_SYMBOL_K14
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;自主脱衣・おっぱいうｐ FIRSTDATUI(4)
;------------------------------------------------
;・今回の脱衣でおっぱいが見えるようになった場合に表示
;・ブラ見せを経由せず一気に脱ぐ場合あり
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:自主脱衣 && B_DATUICHECK(0) == 3 && !IS_NOWASSIACTNAME(, "脱衣") && !TEQUIP:アイマスク
	;=================================================
	;初回
	;=================================================
	IF 口上記入チェック(100) && TALENT:TARGET:ご主人様 && !TALENT:巨乳 && FIRSTDATUI(4)
		PRINTFORMW 「大きいってほどじゃないけど、形には自身があるのよ…？」
		PRINTFORMW 「あら、遠慮する事ないわ。もっとよく見ていいわよ」
		PRINTFORMW 「私のこんな姿を見られる事に、感謝しながらねっ！」
		PRINTFORMDW %CALLNAME%は得意そうに、そう言い放った…
		RETURN 1
	ENDIF
	
	;=================================================
	;すでに性器が見えてる
	;=================================================
	IF 口上記入チェック(50) && P_DATUICHECK(0) == 1
		PRINTFORM 「さぁ、お待ちかねのおっぱいよ…
		CALL PRINT_SYMBOL_K14
		PRINTFORMW 「\@ TALENT:MASTER:オトコ ? ホント、男っておっぱいが好きよね # ねぇ、私の胸…、どうかしら \@？」
		RETURN 1
	ENDIF
	
	;=================================================
	;調教内初回
	;=================================================
	IF 口上記入チェック(100) && ONCEDATUI(4)
		IF 0
		ELSEIF TALENT:巨乳
			PRINTFORM 「\@ TALENT:MASTER:小柄 ? 目を輝かせちゃって可愛らしいわ # 目つきがイヤラシイわ \@…
			CALL PRINT_SYMBOL_K14
			PRINTFORMW 「大きなおっぱいが好きな人って、子供っぽいところあるわよねぇ」
			SIF TALENT:MASTER:小柄
				PRINTFORMW 「って%CNAME(MASTER, TARGET)%…、まだ子供だったわよね。ふふ…」
		ELSEIF TALENT:貧乳
			PRINTFORMW 「小さな胸が好きとか、ホント%CNAME(MASTER, TARGET)%も変わり者よね？」
			PRINTFORMW 「ほら、これが見たかったんでしょう？」
		ELSE
			PRINTFORMW 「もじもじしてないで、どうせならもっと堂々と見れば？」
			PRINTFORMW 「%CNAME(MASTER, TARGET)%が何を考えてるかなんて、とっくにお見通しよ」
			SIF !RAND:3
				PRINTFORMW 「見られたからって減るものじゃないし、ね」
		ENDIF
		RETURN 1
	ENDIF
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;自主脱衣・ぱんつ見せ FIRSTDATUI(5)
;------------------------------------------------
;・今回の脱衣でパンティ（キャラによってドロワーズ・トランクス）が見えるようになった場合に表示
;・下半身下着の無いキャラは書く必要なし
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:自主脱衣 && P_DATUICHECK(0) == 2 && !IS_NOWASSIACTNAME(, "脱衣") && !TEQUIP:アイマスク
	;=================================================
	;初回
	;=================================================
	IF 口上記入チェック(100) && TALENT:TARGET:ご主人様 && FIRSTDATUI(5)
		PRINTFORMW 「どう？ これ\@ KFLAG:(FLAG:ご主人様):300 == 2 ? も # 、 \@私のお手製なのよ」
		PRINTFORMW 「人によっては下着姿の方が好きだったりするらしいけど…」
		PRINTFORMW 「%CNAME(MASTER, TARGET)%の場合はどうなのかしら。ねぇ？」
		KFLAG:(FLAG:ご主人様):300 = 1
		RETURN 1
	ENDIF
	
	;=================================================
	;調教内初回
	;=================================================
	IF 口上記入チェック(100) && ONCEDATUI(5)
		;2/3で真
		IF RAND:3
			PRINTFORMW 「ねぇ、どうかしら？」
			PRINTFORM 「今回は、
			CALL WHICH_LINGERIE(COLOR2_K14("ALICE"))
			PRINTFORMW にしてみたんだけど」
			PRINTFORMW 「ほら、どうなの！？ 何か%RAND_SPLIT("気の利いたほめ言葉/感想")%のひとつくらい、%RAND_SPLIT("言いなさいよっ！/言えない訳？")%」
		;1/3
		ELSE
			PRINTFORMW 「ねぇ、良く見て…」
			PRINTFORMW 「もう%RANDTEXT_K14("滲みになってる", "少し濡れちゃってる")%の、分かるかしら？」
			;1/3で真
			IF !RAND:3
				PRINTFORM 「%CNAME(MASTER, TARGET)%を虐める事考えると、こうなっちゃうのよね…
				CALL PRINT_SYMBOL_K14
				CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), "「フフ…」")
				CLEARLINE 1
			ENDIF
		ENDIF
		RETURN 1
	ENDIF
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;自主脱衣・ブラ見せ FIRSTDATUI(6)
;------------------------------------------------
;・今回の脱衣でブラ（キャラによってスポーツブラ・サラシ）が見えるようになった場合に表示
;・上半身下着の無いキャラは書く必要なし
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:自主脱衣 && B_DATUICHECK(0) == 2 && !IS_NOWASSIACTNAME(, "脱衣") && !TEQUIP:アイマスク
	;=================================================
	;初回
	;=================================================
	IF 口上記入チェック(100) && TALENT:TARGET:ご主人様 && FIRSTDATUI(6)
		PRINTFORMW 「ほら、なかなか可愛いブラでしょ？ これ\@ KFLAG:(FLAG:ご主人様):300 == 1 ? も # 、 \@私の手作りなの」
		IF TALENT:MASTER:オトコ
			PRINTFORMW 「男から見て、こういう可愛らしい感じのってどうなのかしら？」
		ELSE
			PRINTFORMW 「そのうち%CNAME(MASTER, TARGET)%にもお揃いのを作ってあげてもいいわ」
			SIF TALENT:MASTER:小柄
				PRINTFORMW 「でも、%CNAME(MASTER, TARGET)%にはまだちょっと早いかしらね？」
		ENDIF
		KFLAG:(FLAG:ご主人様):300 = 2
		RETURN 1
	ENDIF
	
	;=================================================
	;調教内初回
	;=================================================
	IF 口上記入チェック(100) && ONCEDATUI(6)
		;昼夜あるから今日はってのは厳密に言うとアレ
		PRINTFORM 「どう？ \@ DAY ? 今夜 # 今日 \@は
		CALL WHICH_LINGERIE(COLOR2_K14("ALICE"))
		PRINTFORMW ブラにしてみたの」
		SELECTCASE TENSION(TARGET, 8)
			CASE "高い"
				PRINTFORMW 「これ、割と自信作なんだけど…って」
				PRINTFORMW 「ほら、何ぼけーっとしてるのよ？ 褒めるなら今のうちよ！」
			CASE "一般"
				PRINTFORMW 「気に入って貰えたかしら。っていうか…」
				PRINTFORMW 「黙ってないで、なにか言いなさいよ」
			CASE "低い"
				PRINTFORMW 「…何黙ってるのよ。何か言うこと、あるでしょう？」
		ENDSELECT
		RETURN 1
	ENDIF
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;自主脱衣・全裸に FIRSTDATUI(7)・RANDDATUI(8) ～ (11)
;------------------------------------------------
;・「全裸まで脱いだ」だけではなく、「今脱いだので全裸になった」という意味
;・「上着しか残っていない状態で上着を脱いだ」なども含まれる
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:自主脱衣 && ZENRACHECK(0) && !IS_NOWASSIACTNAME(, "脱衣")
	;相思相愛
	IF AFFECTION("相思相愛") && !TEQUIP:アイマスク
		PRINTFORMW 「ねぇ、私を見て…私の裸、綺麗かしら…？」
	ELSE
		PRINTFORMW 「さぁ、準備はOKってとこかしらね」
	RETURN 1
	ENDIF
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;強制脱衣・アクセサリーのみ FIRSTDATUI(8)
;------------------------------------------------
;・主に靴下。他には手袋や袖など
;・脱がしたうちに入らない！となりがちなのでこれだけは部位別テンプレを用意
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:強制脱衣 == 6
	;=================================================
	;全裸になった
	;=================================================
	IF 口上記入チェック(100) && ZENRACHECK(1)
		PRINTFORMW 「さてと、これでもう隠すものはないわね」
		PRINTFORMW 「ほら、隠さないで。手を後ろにして、私によく見せ%RAND_SPLIT("なさい/るのよ")%」
		PRINTL
		;調教対象がオトコ
		IF TALENT:MASTER:オトコ
			IF TALENT:MASTER:小柄
				SELECTCASE RANDDATUI(2, 8)
					CASE 0
						PRINTFORMW 「こうしてみると流石に幼いけれど…%CNAME(MASTER, TARGET)%、悪くないわ…」
						PRINTFORMW 「これは将来が楽しみね？ うふっ…」
					CASE 1
						PRINTFORMW 「どう見ても男っていうより、まだ男の子って感じよねぇ」
						PRINTFORMW 「でもまぁ…、%CNAME(MASTER, TARGET)%、そう悪くないわよ？」
				ENDSELECT
			ELSE
				SELECTCASE RANDDATUI(2, 9)
					CASE 0
						PRINTFORM 「ま、女に裸に剥かれた男の心境は判らないんだけどね…
						CALL PRINT_SYMBOL_K14
					CASE 1
						PRINTFORMW 「大の男が裸に剥かれて、恥ずかしそうにモジモジし%RAND_SPLIT("ちゃって/てる姿")%…」
						PRINTFORMW 「ちょっと堪らないわね？ フフフ…」
				ENDSELECT
			ENDIF
		;その他
		ELSE
			IF TALENT:MASTER:小柄
				SELECTCASE RANDDATUI(2, 10)
					CASE 0
						PRINTFORMW 「ふふ…、ホント可愛らしいわね。%CNAME(MASTER, TARGET)%…」
						PRINTFORM 「ま、正直…小さな女の子相手に私、何してるのかって思わないでもないけれど
						CALL PRINT_SYMBOL_K14
					CASE 1
						PRINTFORMW 「そうねぇ…、腰回りとか、まだ少女とも呼べない感じよねぇ」
						PRINTFORMW 「私の小さい頃に、少し似てる気がしないでもないわ」
						PRINTFORMW 「将来が楽しみね？ ふふ…」
				ENDSELECT
			ELSE
				SELECTCASE RANDDATUI(2, 11)
					CASE 0
						PRINTFORMW 「恥ずかしがらず、背筋を伸ばしなさい。その方が見栄えがするわ」
						PRINTFORMW 「私程ではないけど%CNAME(MASTER, TARGET)%の裸…、なかなか綺麗だしね？」
					CASE 1
						PRINTFORMW 「そうやって恥ずかしがってる%CNAME(MASTER, TARGET)%の姿見ると、ちょっとゾクゾクしちゃうわね」
						;TALENT:サドよりALIで見た方がより効率的な気はするので
						PRINTFORMW 「思いっきり\@ ALI(0) ? 可愛がってあげたく # 虐めたく \@なっちゃうわ」
						CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"「\@ ALI(0) ? うふふ # ウフフ \@…」")
						CLEARLINE 1
				ENDSELECT
			ENDIF
		ENDIF
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	PRINTFORMW 「さぁ、%RAND_SPLIT("余計/邪魔")%なものは外して頂戴」
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;強制脱衣・ちんこ見せろ FIRSTDATUI(9)
;------------------------------------------------
;・今回の脱衣で男性器が見えるようになった場合に表示
;・ぱんつ見せろを経由せず一気に脱がせる場合あり
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:強制脱衣 && P_DATUICHECK(1) == 3 && PENIS(MASTER)
	;-----------------------------------
	;RETURN挟まず、終端まで一連の処理
	;-----------------------------------
	;優しく脱がす（好感度1000以上・アライメント正）
	IF AFFECTION("眷愛") && ALI(0)
		PRINTFORM 「ほら、脱がせてあげるから、%RAND_SPLIT("大人しく/じっと")%してて%RAND_SPLIT("頂戴/")%
		CALL PRINT_SYMBOL_K14
		PRINTL
	ENDIF
	PRINTFORM 「あら、
	;調教対象勃起度派生
	SELECTCASE TCVAR:MASTER:勃起度
		CASE IS > 1500
			PRINTFORMW もう%RAND_SPLIT("ギンギン/はちきれそう")%になってるわね…」
		CASE IS > 1000
			PRINTFORMW もうこんなに%RAND_SPLIT("膨らませ/しちゃっ")%て…」
		CASEELSE
			PRINTFORMW 縮こまっちゃって%RAND_SPLIT("元気が/見栄えがし")%ないわね…？」
	ENDSELECT
	;調教対象の勃起度が一定以上
	IF TCVAR:MASTER:勃起度 >= 1000
		SELECTCASE RANDDATUI(2, 9)
			CASE 0
				PRINTFORM 「私に脱がされて、興奮しちゃったのかしら…
				CALL PRINT_SYMBOL_K14
			CASE 1
				PRINTFORMW 「ちょっと気が早いんじゃない？」
		ENDSELECT
	;その他
	ELSE
		PRINTFORMW 「ま、それはそれで中々可愛らしいわよ。\@ TALENT:MASTER:オトコ ? 男としては傷付くかもだけど # 別に貶している訳じゃないからね？ \@」
	ENDIF
	;フェードアウト
	CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"「\@ AFFECTION("眷愛") ? ふふふ # フフフ \@…」")
	CLEARLINE 1
	;終端でのみRETURN
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;強制脱衣・まんまん見せろ FIRSTDATUI(10)
;------------------------------------------------
;・今回の脱衣で女性器が見えるようになった場合に表示
;・ぱんつ見せろを経由せず一気に脱がせる場合あり
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:強制脱衣 && P_DATUICHECK(1) == 3 && !TALENT:MASTER:オトコ
	;-----------------------------------
	;RETURN挟まず、終端まで一連の処理
	;-----------------------------------
	;優しく脱がす（好感度1000以上・アライメント正）
	IF AFFECTION("眷愛") && ALI(0)
		PRINTFORMW 「ほら、脱がせてあげるから%RAND_SPLIT("大人しく/じっと")%してなさい」
		PRINTFORMW 「足を上げて貰えるかしら？ そう、いいわよ…」
		PRINTL
	ENDIF
	PRINTFORM 「さて、ご開帳…
	CALL PRINT_SYMBOL_K14
	SELECTCASE MOIST_K14(MASTER)
		CASE "高い"
			PRINTFORMW 「なによ、もうこんなに濡らしちゃって。%RAND_SPLIT("はしたない/イヤラシイ")%%RAND_SPLIT("娘/わ")%ね？」
		CASE "一般"
			PRINTFORMW 「ふふ、%RAND_SPLIT("もう/少し")%濡れてきてる%RAND_SPLIT("みたい/わ")%ね」
		CASE "低い"
			PRINTFORMW 「あら、まだお湿りが足りないようね？」
	ENDSELECT
	PRINTFORMW 「さ%RAND_SPLIT("ぁ/て")%、今回はどうやって\@ ALI(0) ? 可愛がって # 虐めて \@あげようかしら」
	;フェードイン
	CALL FADEIN(13, "「…愉しみ、ね」")
	;フェードアウト
	CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"「%RAND_SPLIT("ウフフ/うふふ")%…」")
	CLEARLINE 1
	;終端でRETURN
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;強制脱衣・おっぱい見せろ FIRSTDATUI(11)
;------------------------------------------------
;・今回の脱衣でおっぱいが見えるようになった場合に表示
;・ブラ見せろを経由せず一気に脱がせる場合あり
;・調教対象がオトコだと表示しないので、オトコ限定口上では書く必要が無い
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:強制脱衣 && B_DATUICHECK(1) == 3 && !TALENT:MASTER:オトコ
	LOCALS = \@ FIRSTDATUI(11) ? こうしてみると、ホント # いつ見ても本当に \@
	PRINTFORM 「さぁ、%CNAME(MASTER, TARGET)%のおっぱい見せて頂戴…
	CALL PRINT_SYMBOL_K14
	IF 0
	;調教対象が巨乳
	ELSEIF TALENT:MASTER:巨乳
		PRINTFORML 「%LOCALS%大きいわ%RAND_SPLIT("ね？/…")%」
		IF 0
		;調教者が巨乳
		ELSEIF TALENT:巨乳
			PRINTFORMW 「%RAND_SPLIT("ま、大きさじゃ私も負けてないけど/とは言っても、私と同じくらいかしらね")%」
			SIF !RAND:3
				PRINTFORMW 「でも、ここまでになると肩がこるのよねぇ」
		;調教者が貧乳
		ELSEIF TALENT:貧乳
			PRINTFORMW 「%RAND_SPLIT("あら、なによ。勝ち誇ったような顔しちゃって…/別にそれを羨みはしないけど。言っとくけど、これは負け惜しみじゃないから…")%」
		;並
		ELSE
			PRINTFORMW 「%RAND_SPLIT("私も魔力で大きくしてみようかしら？/でも、大切なのは形と感度よ。別に僻んでる訳じゃないけど")%」
		ENDIF
	;調教対象が貧乳
	ELSEIF TALENT:MASTER:貧乳
		PRINTFORML 「%LOCALS%可愛らしいわ%RAND_SPLIT("ね/")%」
		IF 0
		;調教者が巨乳
		ELSEIF TALENT:巨乳
			PRINTFORMW 「%RAND_SPLIT("あ、別に他意はないわよ？ 素直に可愛らしいと思ってね/あら、嫌味に聞こえたのなら謝るわ")%」
			CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), "「ふふふ…」")
			CLEARLINE 1
		;調教者が貧乳
		ELSEIF TALENT:貧乳
			PRINTFORMW 「%RAND_SPLIT("このくらいだと楽でいいわよね？ 色々と/二人でお揃いって感じよね。ふふ…")%」
		;並
		ELSE
			PRINTFORMW 「%RAND_SPLIT(@"無駄に大きければ良いってものでもないし。%CNAME(MASTER, TARGET)%には良く似合ってると覆うわよ？/ま、可愛らしさじゃ私も負けてないけど。それにちょっとだけ%CNAME(MASTER, TARGET)%のより大きいし、ね。ふふ…")%」
		ENDIF
	;並
	ELSE
		PRINTFORML 「%LOCALS%綺麗な%RAND_SPLIT("形/胸")%ね」
		IF 0
		;調教者が巨乳
		ELSEIF TALENT:巨乳
			PRINTFORMW 「%RAND_SPLIT("ま、私のくらい大きいともっと良かったけどね？/大き過ぎると感度が悪くなるし、私、元に戻そうかしら")%」
		;調教者が貧乳
		ELSEIF TALENT:貧乳
			PRINTFORMW 「%RAND_SPLIT("それに私のより大きいし生意気…あ、これは冗談よ？ 元に戻せば済む話だし、ね/まぁでも、感度の良さじゃ負けてないつもりだけどね！")%」
		;並
		ELSE
			PRINTFORMW 「%RAND_SPLIT("と言っても…私ほどではないけど/やっぱりこのくらいが一番いいわよね")%。ふふ…」
		ENDIF
	ENDIF
	;おっぱい系を真面目にやると、ホント面倒すぎる
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;強制脱衣・ぱんつ見せろ FIRSTDATUI(12)
;------------------------------------------------
;・今回の脱衣で下半身下着が見えるようになった場合に表示
;・調教対象が下半身下着の無いキャラ限定の口上であれば書く必要がない
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:強制脱衣 && P_DATUICHECK(1) == 2
	;共通で処理したい部分があったんで、枠を分けずにまとめてしまったという。分けた方が見た目スッキリするんだけど
	PRINTFORMW 「%RAND_SPLIT("ふふ/フフ")%…良い格好。ま、他人には見せられないわよねぇ？ こんな\@ TALENT:MASTER:オトコ ? %RAND_SPLIT("情けない/無様な")% # %RAND_SPLIT("はしたない/ふしだらな")% \@下着姿」
	;調教対象がオトコ
	IF TALENT:MASTER:オトコ
		PRINTFORMW 「ほら！ もじもじするのは止めて、男らしく堂々としていなさい。みっともない」
	;その他
	ELSE
		PRINTFORM 「恥ずかしそうに俯いてる姿も%RAND_SPLIT("可愛いわね？/堪らないわね…")%　\@ ALI(0) ? ちょっと # もっと \@意地悪したくなっちゃうわ
		CALL PRINT_SYMBOL_K14
	ENDIF
	;調教対象にペニスあり
	IF PENIS(MASTER)
		;調教対象勃起度派生
		SELECTCASE TCVAR:MASTER:勃起度
			CASE IS > 1500
				PRINTFORMW 「って%CNAME(MASTER, TARGET)%…、もうそんなに大きくしちゃって……」
				PRINTFORMW 「一体何を期待してそんなにしちゃってる訳？ ほら、正直に言ってみなさいよ\@ TENSION(TARGET, 8) == "高い" ? っ！ # \@」
			CASE IS > 1000
				PRINTFORMW 「あら？ %CNAME(MASTER, TARGET)%、そこに何を隠しているのかしら。そして…」
				PRINTFORMW 「その中身を、この私にどうして欲しいの？ ほら、ちゃんと答えなさいよ\@ TENSION(TARGET, 8) == "高い" ? ！ # \@」
			CASEELSE
				PRINTFORMW 「何よ、元気ないわね？ お粗末過ぎて%RAND_SPLIT("泣けてく/涙が出")%るわ」
				PRINTFORMW 「%RAND_SPLIT("これは少し、躾けなおす必要があるかしらね？/まぁいいわ。これからしっかり仕込んであげるから…")%」
		ENDSELECT
	;調教対象にペニス無し
	ELSE
		;調教対象潤滑派生
		SELECTCASE MOIST_K14(MASTER)
			CASE "高い"
				PRINTFORMW 「…って。あらあら、お漏らししたみたいになってるじゃない」
				PRINTFORMW 「疼いちゃって辛いんでしょう？ 私に%RAND_SPLIT("メチャメチャにし/虐め/かき回し")%て欲しいんでしょう？」
				PRINTFORMW 「さぁ、正直に言ってごらんなさい…」
			CASE "一般"
				PRINTFORMW 「ん？ %CNAME(MASTER, TARGET)%…、もう濡れちゃってる%RAND_SPLIT("訳/のね")%？」
				PRINTFORMW 「これから私にされる事%RAND_SPLIT("考え/期待し")%て、そんなにしちゃってるんでしょう」
				PRINTFORMW 「ねぇ、そうなんでしょう？ そうなのよね？ 私の目を見て、ちゃんと答えなさい…」
			CASE "低い"
				PRINTFORMW 「あら？ まだ濡れてないのね。%RAND_SPLIT("ツマラナイ/つまらない/面白くない")%わ」
				PRINTFORMW 「%RAND_SPLIT(@"もしかして%CNAME(MASTER, TARGET)%…、濡れにくかったりするのかしら？/この程度じゃ%CNAME(MASTER, TARGET)%…、まだ興奮しないのかしら？ 面倒な事ね")%」
		ENDSELECT
	ENDIF
	;1/3の確率で表示する
	IF !RAND:3
		;フェードアウト
		CALL FADE(20, COLOR2_K14("ALICE"), COLOR_K14("black"), @"「%RAND_SPLIT("クスクス/くすくす/フフフ/ウフフ")%…」")
		;行削除
		CLEARLINE 1
	ENDIF
	;終端でのみRETURN
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;強制脱衣・ブラ見せろ FIRSTDATUI(13)
;------------------------------------------------
;・今回の脱衣でブラ上半身下着が見えるようになった場合に表示
;・調教対象がオトコだと表示しないので、オトコ限定口上では書く必要が無い
;・調教対象が上半身下着の無いキャラ限定の口上であれば書く必要がない
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:強制脱衣 && B_DATUICHECK(1) == 2 && !TALENT:MASTER:オトコ
	;ここの構造ならAUTOORDERに統合していいのかも知れない
	;=================================================
	;初回
	;=================================================
	IF 口上記入チェック(100) && TALENT:TARGET:ご主人様 && FIRSTDATUI(13)
		PRINTFORMW 「あら、可愛らしい下着ね？ %CNAME(MASTER, TARGET)%、良い趣味してるわ」
		PRINTFORMW 「ただそうね…、もう少し\@ TALENT:MASTER:小柄 ? 可愛い # 大人っぽい \@感じの方が似合うかも」
		PRINTFORMW 「そのうち、私が誂えてあげてもいいわ。%CNAME(MASTER, TARGET)%がいい子にしてたら、ね」
		KFLAG:(FLAG:ご主人様):310 = 1
		RETURN 1
	ENDIF
	
	;-------------------------
	;その他
	;-------------------------
	;AUTOORDER使い切ったら汎用に
	SELECTCASE ORDERDATUI(13)
		CASE 0
			IF 0
			ELSEIF TALENT:MASTER:巨乳
				PRINTFORMW 「%RAND_SPLIT("ちょっと/いつ見ても")%窮屈そうね？」
				PRINTFORMW 「もう少しゆとりのあるサイズにしてもいいかも知れないわ」
			ELSEIF TALENT:MASTER:貧乳
				PRINTFORMW 「カップもアンダーもちょっと緩い感じね。それだと形が崩れちゃうかも」
				PRINTFORMW 「大きさに関わらず、サイズの合ったものにしないとね？」
			ELSE
				PRINTFORMW 「ふふ、良く似合ってるわ。ただ、そうね…」
				PRINTFORMW 「サイズは変化していくから、それに合わせていかないとね」
				PRINTFORMW 「それが美しさを保つ秘訣よ。良く覚えておいて」
			ENDIF
		CASE 1
			PRINTFORMW 「うん、悪くないわね。ただ、やっぱり%CNAME(MASTER, TARGET)%の場合、\@ TALENT:MASTER:巨乳 ? フル # %RANDTEXT_K14("1/2", "3/4")% \@カップの方がいいのかも」
			PRINTFORMW 「ちょっと考えておいてあげるわ」
		CASE 2
			PRINTFORMW 「あら、なかなか良い感じね？ サイズ的にもぴったりみたいだし」
			PRINTFORMW 「あとはそうね。間違ったつけ方はバストラインを崩すから、着用方法も大切よ」
			PRINTFORMW 「例えばまず…、上体を90度に倒して、胸をカップの中に収めてて、それからそれから…」
			PRINTFORMDW 唐突に始まった%CALLNAME%による着用講座は、その後暫くの間続いた…
		;汎用処理
		CASEELSE
			SELECTCASE RANDDATUI(2, 13)
				CASE 0
					PRINTFORMW 「私の言いつけを守って、しっかり付けてるようね？ %RAND_SPLIT("良い事/何より")%だわ」
					PRINTFORM 「そうね、今度
					CALL WHICH_LINGERIE(COLOR2_K14("ALICE"))
					PRINTFORMW やつ試してみる？」
				CASE 1
					PRINTFORMW 「\@ ALI(0) ? ふふ # フフ \@、可愛い。とっても良く似合ってるわ」
					PRINTFORM 「脱が%RAND_SPLIT("す/しちゃう")%のが勿体無いくらいに、ね…
					CALL PRINT_SYMBOL_K14
			ENDSELECT
	ENDSELECT
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;強制脱衣・全裸に FIRSTDATUI(14)
;------------------------------------------------
;・「全裸まで剥いだ」だけではなく、「今剥いだので全裸になった」という意味
;・「上着しか残っていない状態で上着を剥いだ」なども含まれる
;・「おっぱい見せろ」～「ぱんつ見せろ」を書いた場合は記入する必要はない（上層の分岐に食われてここまで落ちてこない為）
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && TFLAG:強制脱衣 && ZENRACHECK(1)
	PRINTFORMW 「…%RAND_SPLIT("さて、/")%これで全部ね。\@ ALI(0) ? ふふ # フフ \@、%RAND_SPLIT("いい/悪くない")%眺めだわ」
	PRINTFORMW 「さぁ、今回も思いっきり\@ ALI(0) ? 虐めて # 可愛がって \@あげる」
	PRINTFORM 「楽しみにしてて…
	CALL PRINT_SYMBOL_K14
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;自主脱衣・何か脱いだ FIRSTDATUI(15)
;------------------------------------------------
;・自慰・奉仕系の予備動作になりやすい
;━━━━━━━━━━━━━━━━━━━━━━━━
;ここまでの調教者の自主脱衣条件にヒットしない場合、この箇所が補完する
;-------------------------------------------------
IF 口上記入チェック(100) && TFLAG:自主脱衣 && !IS_NOWASSIACTNAME(, "脱衣") && !TEQUIP:アイマスク
	PRINTFORMW 「\@ T_COND("情欲") ? 邪魔ね、こんなの # ふふん、見たければ見てもいいわよ。ほら… \@」
	PRINTFORMDW %CALLNAME%はそう言うと、\@ T_COND("情欲") ? 勢いよく着衣を脱ぎ捨てた # 挑発するかのように、ゆっくりと扇情的に着衣を脱いでいく \@…
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;強制脱衣・何か剥いだ FIRSTDATUI(16)
;------------------------------------------------
;・愛撫系の予備動作になりやすい
;━━━━━━━━━━━━━━━━━━━━━━━━
;ここまでの強制脱衣条件にヒットしない場合、この箇所が補完する
;-------------------------------------------------
IF 口上記入チェック(100) && TFLAG:強制脱衣
	PRINTFORMW 「さぁ、余計なものは脱いでもらうわよ」
	PRINTFORMW 「大人しくして%RAND_SPLIT("なさい/頂戴")%……」
	RETURN 1
ENDIF


;========================================================================
;追加アクション口上
;========================================================================
;・調教者が追加行動を取ったときに汎用的に表示
;・書く場合、アクション口上の後に続けて違和感のない形で書かれることが望ましい
;-------------------------------------------------
;いずれ手入れしないと、な箇所
;-------------------------------------------------

;────────────────────────────────────
;EXTRA1, Ｃ愛撫
;────────────────────────────────────
;・追加で愛撫または手淫を行う
;・調教対象にペニスがない場合は愛撫、ある場合は手淫
;-------------------------------------------------
@KOJO_ACT_EXTRA_K14_1
;-------------------------------------------------

;━━━━━━━━━━━━━━━━━━━━━━━━
;EXTRA1_0, 愛撫する
;-------------------------------------------------
;・調教対象にペニスがない場合
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && !PENIS(MASTER)
	SELECTCASE RANDACTEX(2, 0)
		CASE 0
			PRINTFORMW 「直接触れないで、周りをクニクニされるのも良いでしょ…？」
			PRINTFORMW 「焦らされて嬲られるのが、癖になっちゃうかもね…？ フフフ…」
		CASEELSE
			PRINTFORMW 「こんなに膨らませちゃって、弄って欲しかったのかしら」
			PRINTFORMW 「ほら、皮を剥かないで嬲られるの、気持ちいいでしょう？」
	ENDSELECT
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;EXTRA1_1, 手淫する
;-------------------------------------------------
;・調教対象にペニスがある場合
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && PENIS(MASTER)
	SELECTCASE RANDACTEX(2, 1)
		CASE 0
			PRINTFORMW 「フフ…手の中でどんどん熱くなってくる…」
			PRINTFORM 「雁首の後ろもグニグニしてあげるわ…
			CALL PRINT_SYMBOL_K14
		CASEELSE
			PRINTFORMW 「ほら、シゴいて欲しかったんでしょう？　ねぇ、どうなの？」
			PRINTFORMW 「正直に言わないと止めるわよ？　ウフフ…」
	ENDSELECT
	RETURN 1
ENDIF

;────────────────────────────────────
;EXTRA2, Ｖ愛撫
;────────────────────────────────────
;・追加でヴァッジ愛撫を行う
;・調教対象がオトコの場合発生しない
;・調教対象が処女の場合は発生しない
;-------------------------------------------------
@KOJO_ACT_EXTRA_K14_2
;-------------------------------------------------
IF 口上記入チェック(100)
	SELECTCASE RANDACTEX(2, 0)
		CASE 0
			PRINTFORMW 「フフ…指が締め付けられるわね」
			PRINTFORM 「ほら、天井のザラザラも擦ってあげるわ…
		CASEELSE
			PRINTFORMW 「ほら、ココ？　それともこうかしら？」
			PRINTFORM 「ウフフ、健気に指を締め付けてきて可愛らしいわね
	ENDSELECT
	CALL PRINT_SYMBOL_K14
	RETURN 1
ENDIF

;────────────────────────────────────
;EXTRA3, Ａ愛撫
;────────────────────────────────────
;・追加でアナル愛撫を行う
;-------------------------------------------------
@KOJO_ACT_EXTRA_K14_3
;-------------------------------------------------
IF 口上記入チェック(100)
	SELECTCASE RANDACTEX(2, 0)
		CASE 0
			PRINTFORMW 「ほら、指が2本入ったわよ」
			PRINTFORM 「ぐいぐい締め付けちゃって… 貪欲なのね
			CALL PRINT_SYMBOL_K14
		CASEELSE
			PRINTFORMW 「弄る度にビクビクしちゃって…」
			PRINTFORMW 「随分敏感なおしりね？　ウフフ…」
	ENDSELECT
	RETURN 1
ENDIF

;────────────────────────────────────
;EXTRA4, Ｂ愛撫
;────────────────────────────────────
;・追加で乳首責めを行う
;-------------------------------------------------
@KOJO_ACT_EXTRA_K14_4
;-------------------------------------------------
IF 口上記入チェック(100)
	SELECTCASE RANDACTEX(2, 0)
		CASE 0
			PRINTFORMW 「ほら、乳首の周りを嬲られると、もどかしくて堪らないでしょう…？」
		CASEELSE
			PRINTFORMW 「もう乳首が立ってるわね？　期待してるの？」
			PRINTFORMW 「じゃ、お望み通り乳首も虐めてあげるわ。ウフフ…」
	ENDSELECT
	RETURN 1
ENDIF

;────────────────────────────────────
;媚薬等使用口上
;────────────────────────────────────
;・調教者が媚薬やローションを使用したときに汎用的に表示
;・「アクション口上」で記入チェックを=2とした場合は表示されない
;・書く場合、アクション口上の後に続けて違和感のない形で書かれることが望ましい
;-------------------------------------------------
@KOJO_USE_K14
;-------------------------------------------------

;━━━━━━━━━━━━━━━━━━━━━━━━
;ローション/媚薬同時使用
;------------------------------------------------
;・主に性交系コマンドから発生する
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && GETBIT(TFLAG:媚薬等使用口上, 0) && GETBIT(TFLAG:媚薬等使用口上, 1)
	PRINTFORMW 「目が潤んで、顔は上気しちゃって…」
	PRINTFORMW 「発情しちゃったかしら…？」
	PRINTFORMW 「ローションもヌルヌルして良い感じでしょ…？」
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;ローション使用
;------------------------------------------------
;・玩具・性交系コマンド等から発生する
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && GETBIT(TFLAG:媚薬等使用口上, 0)
	PRINTFORMW 「このヌルヌルが癖になるのよね…」
	PRINTFORM 「さぁ、思いっきり愉しむわよ
	CALL PRINT_SYMBOL_K14
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;媚薬使用
;------------------------------------------------
;・愛撫系コマンド等から発生する
;・キスで発生した場合は口移しとなるため、そのケースはキスのアクション口上内で処理するほうがよい
;━━━━━━━━━━━━━━━━━━━━━━━━
;IF 口上記入チェック(100) && GETBIT(TFLAG:媚薬等使用口上, 1)
;ENDIF