;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	アリス口上 ショップイベント口上ファイル
;
;	ショップから呼び出される口上をまとめたファイル
;
;version		Reason
;1.71@0503		EV110のMS化。EV111に加筆
;1.71			@口上記入チェック仕様に
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;========================================================================
;ショップイベント口上
;========================================================================

;────────────────────────────────────
;EV101, 衣装変更
;────────────────────────────────────
;・ショップから衣装変更を行った場合に表示
;-------------------------------------------------
@KOJO_EVENT_K14_101(ARG)
;-------------------------------------------------
;引数内訳…
;ARG=
;　一の位…（0, もう着てる、1, 着替えた、2, 恥ずかしくて着られない、3, 嫌がって着ない、9, 持ってない）
;　十の位…（0, 通常、10, 半裸、20, 全裸、30, 下着無し）
;　百の位…コスプレのために保留
;　千の位…（0, 調教者, 1000, 調教対象）
;　例（調教対象が恥ずかしがって全裸にできなかった＝1022）
;-------------------------------------------------

;━━━━━━━━━━━━━━━━━━━━━━━━
;調教者が着替えた
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && ARG < 1000 && (ARG % 10) == 1
	SELECTCASE ARG
		;通常になった
		CASE 1
			PRINTFORMW 「これが１番私らしいかしら､ね…？」
			RETURN 1
		;半裸になった
		CASE 11
			PRINTFORMW 「%CNAME(MASTER, TARGET)%、こういう格好が趣味なのかしら…？」
			RETURN 1
		;全裸になった
		CASE 21
			IF TALENT:TARGET:妊娠 && TALENT:TARGET:ご主人様
				PRINTFORML 「家の中とは言っても……」
				PRINTFORMW 「妊婦が全裸でうろうろするのって、ちょっとどうかって気もするわ」
			ELSE
				PRINTFORMW 「…変態」
			ENDIF
			RETURN 1
		;……すーすーする          現在のR#こ_Lでは実装されていない。将来的には復活する可能性は割りとある
		;CASE 31
		;	PRINTFORMW 「私にスカートのたくし上げをして欲しいのかしら…？」
		;	RETURN 1
		;その他
		CASEELSE
			RETURN 0
	ENDSELECT
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;調教者が恥ずかしがって着替えられない
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && ARG < 1000 && (ARG % 10) == 2
	SELECTCASE ARG
		;半裸だって恥ずかしい
		CASE 12
			PRINTFORMW 「%RAND_SPLIT(@"%CNAME(MASTER, TARGET)%…、バカなの？/嫌よ")%」
			RETURN 1
		;普段から全裸はちょっと……
		CASE 22
			IF TALENT:TARGET:妊娠 && TALENT:TARGET:ご主人様
				PRINTFORMW 「妊娠中の私に、普段から裸でいてくれなんて。そんな…」
				PRINTFORMW 「ねぇ%CNAME(MASTER, TARGET)%…、いっぺん死んでみる？」
			;この条件を成立させるのは難しい
			ELSEIF CFLAG:TARGET:依存度 > CFLAG:MASTER:依存度
				PRINTFORMW 「私そんな変態じゃ、ない…」
			ELSE
				PRINTFORMW 「凄く馬鹿っぽいわ。お断りよ」
			ENDIF
			RETURN 1
		;パンツが無いから恥ずかしいもん！    現在は実装されていない
		;CASE 32
		;	PRINTFORMW 「却下よ」
		;	RETURN 1
		;その他
		CASEELSE
			RETURN 0
	ENDSELECT
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;調教者が嫌がって着替えられない
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && ARG < 1000 && (ARG % 10) == 3
	SELECTCASE ARG
		;半裸は嫌
		CASE 13
			PRINTFORMW 「%RAND_SPLIT("くだらないわね。お断りよ/嫌っ！")%」
			RETURN 1
		;普段から全裸なんて嫌
		CASE 23
			IF TALENT:TARGET:妊娠 && TALENT:TARGET:ご主人様
				PRINTFORML 「は？ %CNAME(MASTER, TARGET)%…、ふざけてるの？」
				PRINTFORMW 「私、妊娠してるのよ！？ お腹の中にいるのは%CNAME(MASTER, TARGET)%の子なのよ！？」
				PRINTFORMW 「あまり馬鹿なこと言ってると、張り倒してやるから…」
			ELSEIF CFLAG:TARGET:依存度 > CFLAG:MASTER:依存度
				PRINTFORMW 「家に誰か来た時どうするのよ…」
				PRINTFORMW 「絶対に嫌っ！」
			ELSE
				PRINTFORMW 「%RAND_SPLIT("バカみたいなこと言ってないで、さっさと死んでくれる？/嫌よ。あり得ないわ")%」
			ENDIF
			RETURN 1
		;すーすーするのは嫌						現在のRevでは実装されていない
		;CASE 33
		;	PRINTFORMW 「嫌よ、そんな馬鹿っぽい格好…」
		;	RETURN 1
		;その他
		CASEELSE
			RETURN 0
	ENDSELECT
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;調教者が着替えるのをやめた	FIRSTEVENT(30)
;━━━━━━━━━━━━━━━━━━━━━━━━
;%CALLNAME:TARGET%の衣装はすでに[　　]に設定されています
;------------------------------------------------
IF 口上記入チェック(100) && ARG < 1000 && (ARG % 10) == 0
	SELECTCASE ARG
		;通常のまま
		CASE 0
			PRINTFORMW 「別に%RAND_SPLIT("着替える必要ないわね/このままでいいわね")%」
			RETURN 1
		;半裸のまま
		CASE 10
			PRINTFORMW 「ま、このままでいいかしら…」
			PRINTFORMW 「%CNAME(MASTER, TARGET)%もその方が嬉しいんでしょう？ この変態…」
			RETURN 1
		;全裸のまま
		CASE 20
			IF TALENT:TARGET:妊娠 && TALENT:TARGET:ご主人様
				PRINTFORMW 「正直、お腹の子に良くない気がするわ。これ…」
			ELSE
				PRINTFORMW 「今更%CNAME(MASTER, TARGET)%に対して隠すものなんてないし」
				PRINTFORMW 「別に裸のままだって構わないわ。ただ…」
				PRINTFORM 「冷静に考えると、凄く馬鹿っぽいけど。ね
				CALL PRINT_SYMBOL_K14
			ENDIF
			RETURN 1
		CASEELSE
			RETURN 0
	ENDSELECT
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;調教対象を着替えさせた
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && ARG >= 1000 && (ARG % 10) == 1
	SELECTCASE ARG
		;通常になった
		CASE 1001
			PRINTFORMW 「それが１番%CNAME(MASTER, TARGET)%らしいわね」
			RETURN 1
		;半裸になった
		CASE 1011
			PRINTFORMW 「こういうのも悪くない、かも…」
			RETURN 1
		;全裸になった
		CASE 1021
			PRINTFORM 「ふふっ…、この変態
			CALL PRINT_SYMBOL_K14
			RETURN 1
		;……すーすーする
		;CASE 1031
		;	PRINTFORMW 「これはコレで有り、かも…」
		;	RETURN 1
		;その他
		CASEELSE
			RETURN 0
	ENDSELECT
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;調教対象が恥ずかしがって着替えられない
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && ARG >= 1000 && (ARG & 10) == 2
	SELECTCASE ARG
		;半裸だって恥ずかしい
		CASE 1012
			IF CFLAG:TARGET:依存度 > CFLAG:MASTER:依存度
				PRINTFORMW 「そう、仕方ないわね…」
			ELSE
				PRINTFORMW 「恥ずかしがったところで、今更どうにもならないのにね？」
				PRINTFORMW 「ま、今回は見逃してあげるわ」
			ENDIF
			RETURN 1
		;普段から全裸なんて恥ずかしい
		CASE 1022
			IF CFLAG:TARGET:依存度 > CFLAG:MASTER:依存度
				PRINTFORMW 「そう、残念ね…」
			ELSE
				PRINTFORMW 「%CNAME(MASTER, TARGET)%にまだ羞恥心が残ってるなんて…」
				CALL FADEIN(8, "「ちょっと驚きね？」")
				WAIT
			ENDIF
			RETURN 1
		;パンツがないから恥ずかしいもん！
		;CASE 1032
		;	PRINTFORMW 「いいじゃない、どうせ減るものじゃあるまいし…」
		;	RETURN 1
		CASEELSE
			RETURN 0
	ENDSELECT
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;調教対象が嫌がって着替えられない
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && ARG >= 1000 && (ARG & 10) == 3
	SELECTCASE ARG
		;半裸は嫌
		CASE ARG == 1013
			PRINTFORMW 「そんなに嫌なの…？」
			RETURN 1
		;普段から全裸なんて嫌
		CASE 1023
			PRINTFORMW 「どうしても駄目なの…？」
			RETURN 1
		;すーすーするのは嫌
		;CASE 1033
		;	PRINTFORMW 「%CNAME(MASTER, TARGET)%も頑固ね…」
		;	RETURN 1
		CASEELSE
			RETURN 0
	ENDSELECT
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;調教対象を着替えさせるのをやめた FIRSTEVENT(70)
;━━━━━━━━━━━━━━━━━━━━━━━━
;%CALLNAME:MASTER%の衣装はすでに[　　]に設定されています
;------------------------------------------------
IF 口上記入チェック(100) && ARG >= 1000 && (ARG % 10) == 0
	SELECTCASE ARG
		;通常のまま
		CASE 1000
			PRINTFORMW 「今のところ、着替えて貰う必要はないかしらね」
			RETURN 1
		;半裸のまま
		CASE 1010
			PRINTFORMW 「そうね。とりあえず、そのままの格好でいて貰おうかしら」
			RETURN 1
		;全裸のまま
		CASE 1020
			PRINTFORMW 「裸のままでも平気みたいだし、%CNAME(MASTER, TARGET)%はそのままでいいわ」
			SELECTCASE CFLAG:MASTER:依存度
				CASE IS > 5000
					PRINTFORM 「むしろ裸の方が良いんでしょう？ この変態…
					CALL PRINT_SYMBOL_K14
				CASE IS > 3000
					PRINTFORM 「よく似合ってると思うし。ね
					CALL PRINT_SYMBOL_K14
				CASEELSE
					PRINTFORMW 「下僕としては、それが相応しい姿だと思うし。ねぇ？」
			ENDSELECT
			RETURN 1
		;CASE 1030
		;	RETURN 0
		CASEELSE
			RETURN 0
	ENDSELECT
ENDIF

;────────────────────────────────────
;EV102, 調教者変更
;────────────────────────────────────
;・調教者を交代させたときに表示
;-------------------------------------------------
@KOJO_EVENT_K14_102(ARG)
;-------------------------------------------------
;引数内訳…
;ARG=(0, 口上主が他のキャラに任せた、1, 口上主が調教者になった)
;旧調教者は「%CALLNAME:SHOP_OLDTARGET%」、新調教者は「%CALLNAME:SHOP_NEWTARGET%」で参照できる
;口上主はTARGETで参照
;-------------------------------------------------

;一行空ける
PRINTL

;━━━━━━━━━━━━━━━━━━━━━━━━
;他の調教者に交代
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && ARG == 0
	;アリスがご主人様
	IF TALENT:TARGET:ご主人様
		;相思相愛
		IF AFFECTION("相思相愛")
			PRINTFORMW 「気分転換って訳？」
			PRINTFORMW 「まぁいいわ。 %RAND_SPLIT(@"精々%CNAME(SHOP_NEWTARGET, TARGET)%に可愛がって貰うことね！ /少しくらいの我侭は許してあげる。")%でも…」
			PRINTFORMW 「私をいつまでも待たせたらどうなるか、 良く考えておく事ね！！」
			PRINTFORMD アリスはそう言い放つと
				SELECTCASE RANDEVENT(3, 0)
					CASE 0
						PRINTFORMDW %CALLNAME:MASTER%に藁人形を見せつけた……
					CASE 1
						PRINTFORMDW %CALLNAME:MASTER%に極太アナルバイブを見せつけた……
					CASE 2
						PRINTFORMDW グリモワールを取り出し、呪詛の呪文を唱えている……
				ENDSELECT
		;その他
		ELSE
			PRINTFORMW 「なによ、私じゃ不満だって言うの？」
			PRINTFORMW 「まぁいいけど。 私も少し考えなくてはいけないようね！」
		ENDIF
	;普通の助手
	ELSE
		PRINTFORMW 「%RAND_SPLIT("ふーん/あら、そう")%。別にいいわよ？」
		PRINTFORMW 「じゃ、後は%CNAME(SHOP_NEWTARGET, TARGET)%に任せて…私はのんびりさせてもらおうかしらね」
	ENDIF
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;アリスさまが調教者になった
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && ARG == 1
	;アリスがご主人様
	IF TALENT:TARGET:ご主人様
		;相思相愛
		IF AFFECTION("相思相愛")
			SELECTCASE RANDEVENT(2, 10)
				CASE 0
					PRINTFORMW 「ま、結局こうなるのは分かってたけど！」
					PRINTFORMDW アリスは少し拗ねてみせたあと、%CALLNAME:MASTER%に体を摺り寄せてきた… 
					PRINTFORMW 「私に寂しい思いをさせた償い、きっちりしてもらうからね？ ウフフ…」
				CASE 1
					PRINTFORMW 「あら、私が恋しくなったのかしら？」
					PRINTFORMW 「ほら、どうしたの？ もうっ、素直に言いなさいよ！」
					PRINTFORMW 「ちゃんと言えたなら、今回の事は水に流してあげてもいいわよ？」
			ENDSELECT
		;その他
		ELSE
			PRINTFORMW 「フフ… 離れてみて分かる『アリス様』の良さってとこかしら？」
			PRINTFORMW 「さぁいらっしゃい。今日はたっぷり可愛がってあげる」
			PRINTFORM 「色々な意味で、ね
			CALL PRINT_SYMBOL_K14
		ENDIF
	;普通の助手
	ELSE
		PRINTFORMW 「ちょっと%CNAME(SHOP_OLDTARGET, TARGET)%。私に厄介ごとを押し付けるつもり？」
		PRINTFORMW 「まぁいいわ。どうせなら、精々愉しませてもらうから」
		PRINTFORMW 「壊しちゃっても知らないけど、ね♪」
	ENDIF
	RETURN 1
ENDIF

;────────────────────────────────────
;EV110, 魔力使用
;────────────────────────────────────
;・魔力を消費して何らかの特典を購入したときに表示
;-------------------------------------------------
@KOJO_MS_EVENT_K14_110(ARG)
;-------------------------------------------------
;引数内訳…
;ARG=(0, 魔力使用前、1, 調教技巧、2, 知識を得る前、3, 調合知識取得、4, 魅惑取得、5, 禁断の知識取得、
;     6, 道具使い取得、7, 技術を得る前、8, 舌使い取得、9, 器用な指先取得、10, 縛り上手、
;     11, サド消去/サド取得、12, 汚れ無視取得、 38, 着衣プレイ好き、39, 腰使い)
;
;イベント口上：アイテム
;引数内訳…
;ARG=(13, アイテム練成前、14, ローター練成、15,バイブ練成、16, アナルビーズ練成、 17, ペニスバンド練成、
;     18, アナルバイブ練成、19, クリキャップ練成、 20, ニプルキャップ練成、21, オナホール練成)
;
;イベント口上：責め具錬成
;引数内訳…
;ARG=(22, 責め具練成前、23, アイマスク練成、24, 鞭練成、 25, 縄練成、 26, ボールギャグ練成、27, 針練成、
;     28, 浣腸セット練成、29, 三角木馬練成、 30, 大鏡練成、 31, 首輪練成、 32, 道具をカスタマイズした)
;
;その他イベント
;引数内訳…
;ARG=(33, ふたなり化/戻す、34, 巨乳化/戻す、35, 貧乳化/戻す、36, 精力の限界値上昇)
;-------------------------------------------------
;ご主人様の立場でない場合、微妙になる箇所がでてくる気がしたので口上関数をMSにしてしまうことに
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;アリスさま魔力使用前
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && ARG == 0
	;=================================================
	;初回
	;=================================================
	IF 口上記入チェック(100) && FIRSTEVENT(0)
		PRINTFORMW 「同じ魔力といっても、これにはあまり馴染みが無いのよね」
		PRINTFORMW 「まぁ、使い方は把握できたし…」
		PRINTFORMW 「とりあえず、試さない手は無いわね」
		RETURN 1
	ENDIF
	
	;-------------------------
	;初回以外の処理
	;-------------------------
	;なぜかこういう事になった。というか、こんな感じだと自然に整合性がとれる気がしたｗ
	SELECTCASE ORDEREVENT(0)
		CASE 0
			PRINTFORMW 「さりげなく、カタログめいたものが手元にあったりして」
			PRINTFORMW 「というかコレ、いつの間に家に紛れ込んだのかしら。不思議だわ…」
		CASE 1
			PRINTFORMW 「どうみても、外の世界由来のアイテム類。そして、何時の間にか家の中にあったカタログらしきもの」
			PRINTFORMW 「この手の事をやってのける人物っていうと、一人思い当たるけど…」
		CASEELSE
			SELECTCASE RANDEVENT(2, 0)
				CASE 0
					PRINTFORMW 「正直言って、魔力で練成してるって実感ないのよね。これ」
					PRINTFORMW 「何か注文したら、品物が届くって感じで…」
				CASE 1
					PRINTFORMW 「もしも私の推測通りなら、あいつが何か関わってるはず…」
					PRINTFORMW 「でもそうなると、これまでの事、全部知られてるってことになるのよね」
					PRINTFORMW 「考えただけで、頭痛くなってきたわ……」
				CASE 2
					PRINTFORMW 「どうせなら、他にももっと色々取り寄せられたらいいのに」
					PRINTFORMW 「ちょっと紫に相談…、って訳にもいかないわよねぇ。建前上……」
			ENDSELECT
	ENDSELECT
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;アリスさまの調教技巧がアップ
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && ARG == 1
	IF FIRSTEVENT(1)
		PRINTFORMW 「こんな簡単に何かを得られちゃうのって、ちょっと疑問よね」
		PRINTFORMW 「ある意味、魔術に対する冒涜…、って気もするわ」
	;アライメント正
	ELSEIF ALI(0)
		PRINTFORMW 「私もちょっとは上達したかしら…？」
		PRINTFORMW 「ねぇ、期待していいわよ？ \@ AFFECTION("相思相愛") ? ふふっ # ウフフ \@…」
	;アライメント負
	ELSE
		PRINTFORMW 「私も少しは上達したかしら…？」
		PRINTFORM 「もっと虐めてあげるから、期待して頂戴…
		CALL PRINT_SYMBOL_K14
	ENDIF
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;アリスさま魅惑取得
;━━━━━━━━━━━━━━━━━━━━━━━━
;別に他意はないよ アリス様の心の叫びって事でよろしく
IF 口上記入チェック(100) && ARG == 4
	PRINTFORMW 「まぁ、こんなものかしら」
	PRINTFORMW 「というか、私が初期設定からコレ持ってないのっておかしくない？」
	PRINTFORMW 「ちょっと考えれば分かりそうなものだけど…」
	PRINTFORMW 「製作者って、揃いも揃ってボンクラばかりなのかしらね？」
	RETURN 1
ENDIF

;対象限定側で作ったのをとりあえずもってきた
;━━━━━━━━━━━━━━━━━━━━━━━━
;アリスさま禁断の知識取得
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && ARG == 5
	PRINTFORMW 「な、なによこれ。知識が、私の中に流れ込んでくるけど……」
	PRINTFORMW 「こんな術式…、見た事も聞いた事もないわ」
	PRINTFORMW 「正直、これがどんな影響を及ぼすのかも判らないけど」
	PRINTFORMW 「%RAND_SPLIT("パチュリー/魔理沙")%に聞けば、何か知っているかしら？」
	RETURN 1
ENDIF

;道具封印パッチとか死ねばいい
;━━━━━━━━━━━━━━━━━━━━━━━━
;アリスさまアイテム練成前
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && ARG == 13
	IF FIRSTEVENT(13)
		PRINTFORMW 「さて、何を練成しようかしら」
		PRINTFORMW 「ねぇ、%CNAME(MASTER, TARGET)%ならどれを選ぶ？」
		PRINTFORMW 「あぁ、そうそう。練成したものは消す事が出来ないから、良く考えた方がいいわよ？」
		PRINTFORM 「後悔しないように、ね
		CALL PRINT_SYMBOL_K14
		RETURN 1
	ENDIF
	
	SELECTCASE RANDEVENT(3, 13)
		CASE 0
			PRINTFORMW 「正直、実際に練成してみないと私にも良く判らないのよね」
			PRINTFORMW 「こういった物に対する知識って、そうある訳じゃないし……」
		CASE 1
			PRINTFORMW 「さてと、何にしようかしら」
			PRINTFORMW 「実際にどんなものが出来上がるのか、見てのお楽しみってね」
		CASE 2
			PRINTFORMW 「コレ、何でもありな能力に見えるけど、結構制約があるのよね」
			PRINTFORMW 「尤も…、制約があるからこその、無茶な能力って訳なんでしょうけど」
	ENDSELECT
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;アリスさまアイテム練成
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && INRANGE(ARG, 14, 21)
	SELECTCASE ARG
		;ローター
		CASE 14
			PRINTFORMW 「こうして見ただけじゃピンとこないわね。どう使う物なのかしら？」
		;バイブ
		CASE 15
			PRINTFORMW 「あぁ、なるほど。流石にコレは、見れば判るわね…」
		;アナルビーズ
		CASE 16
			PRINTFORML 「う～ん、これは」
			PRINTFORMW 「想像力を働かせればなんとなく判るけど、判りたくない気もするわね…」
		;ペニスバンド
		CASE 17
			PRINTFORMW 「流石にこれは説明不要ね。試すのが愉しみ、フフ。でも…」
			PRINTFORMW 「双頭になってるとは思わなかったわ……」
		;アナルバイブ
		CASE 18
			PRINTFORMW 「まぁ…、見ればなんとなく判るけど。でも、えげつない代物ね……」
		;クリキャップ
		CASE 19
			PRINTFORMW 「あら、これはちょっと可愛らしいわね」
		;ニプルキャップ
		CASE 20
			PRINTFORMW 「ふ～ん、なるほど。クリップ式になってて挟めばいいって訳」
			PRINTFORMW 「よく考えるものねぇ……」
		;オナホール
		CASE 21
			PRINTFORMW 「ああ、これは判った。判ったけど…、判りたくない気がする」
			PRINTFORMW 「頭痛くなってきたわ……」
	ENDSELECT
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;アリスさまＳＭグッズ練成前
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && ARG == 22
	SELECTCASE ORDEREVENT(22)
		CASE 0
			PRINTFORMW 「見たことあるような物から、そうでない物まで…」
			PRINTFORMW 「責め具といっても、実に色々なものがあるのね。やれやれだわ」
			PRINTFORMW 「って良く見ると、道具毎の解説が書かれているのね。ふむふむ……」
		CASE 1
			PRINTFORMW 「魔力で練成と言っても、私自身、この原理は良く判ってないのよね」
			PRINTFORMW 「見たところ、どれも外の世界の物みたいだし。ってやっぱり、紫が何か関わってるのかしら…」
		CASEELSE
			SELECTCASE RANDEVENT(3, 22)
				CASE 0
					PRINTFORMW 「さて、今回はどうしようかしら」
					PRINTFORMW 「散らかるから、あまり余計なものは練成したくないのよねぇ」
				CASE 1
					PRINTFORMW 「結局、何でも自由に練成できるって訳じゃないのね。ちょっと残念」
					PRINTFORMW 「外の世界の人形とか手に入れられたら、最高なんだけど…」
				CASE 2
					PRINTFORMW 「こんな代物を生み出す外の世界って、いったいどうなっているのかしら」
					PRINTFORMW 「ちょっと見てみたい気もするし…、関わりたくない気もするわね」
			ENDSELECT
	ENDSELECT
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;アリスさまＳＭグッズ錬成
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && INRANGE(ARG, 23, 32)            ;道具カスタマイズも含める
	;派生を処理
	SELECTCASE ARG
		;アイマスク
		CASE 23
			PRINTFORMW 「ふむ、目隠しねぇ。ま、視界を奪われた%CNAME(MASTER, TARGET)%を虐めるのも、一興ではあるわね」
		;鞭
		CASE 24
			PRINTFORMW 「見るからに凶悪ね。これで、%CNAME(MASTER, TARGET)%はどんな声を聞かせてくれるのかしら？」
			PRINTFORMW 「\@ TALENT:TARGET:サド || !ALI(0) ? フフ、もう待ちきれないわ。ゾクゾクしちゃう # 今から楽しみ、ね\@…」
		;縄
		CASE 25
			PRINTFORMW 「縄で拘束、ねぇ。ま、私の場合…魔力の糸を使ってもいいんだけど」
			PRINTFORMW 「でもこういうのって、まずは形からって言うじゃない？ フフ…」
		;ボールギャグ
		CASE 26
			PRINTFORMW 「なるほどねぇ。コレ、うるさい\@ TALENT:MASTER:オトコ ? マゾ # メス \@豚を黙らせるにはぴったりね」
		;針
		CASE 27
			PRINTFORMW 「針とは言っても、こういうこと専用の物みたい。凄く細いのね」
			PRINTFORMW 「目隠しと組み合わせると効果的って話だけど。さて、どんなものかしら…」
		;浣腸セット
		CASE 28
			PRINTFORMW 「要するに、腸内洗浄の為のものみたいだけど…」
			PRINTFORMW 「責め具用として最適化されてるとか。まったく、呆れた話ね」
		;三角木馬
		CASE 29
			PRINTFORMW 「拷問具として存在してる事は、知識としてはあったけど…」
			PRINTFORMW 「ってこれ、背中に生えてるじゃない！？」
		;大鏡
		CASE 30
			PRINTFORMW 「羞恥心を煽る道具としてのこういう物の存在は、割りと納得はできる話ではあるわね」
			PRINTFORMW 「というかこれ、普通に鏡としても使えるし…」
		;首輪
		CASE 31
			PRINTFORMW 「ペットを外に連れ出すにはこれが欠かせない、とか。ふむふむ」
			PRINTFORMW 「正直、あまり趣味ではないけれど。首輪をはめられた、%CNAME(MASTER, TARGET)%の姿を想像すると…」
			PRINTFORMW 「ちょっとゾクゾクしちゃうわね……」
		;道具をカスタマイズ		ORDEREVENT使いたい感が。まぁ後で考えることに
		CASE 32
			PRINTFORMW 「道具をカスタマイズする事で、より効率的な調教云々…とか」
			PRINTFORMW 「まったく…、呆れるくらい至れり尽くせりなのね」
		;既存部分は汎用処理に。
		CASEELSE
			SELECTCASE RANDEVENT(3, 1)
			CASE 0
				PRINTFORMW 「こんなもの誰が考えるのかしらね…？」
				PRINTFORM 「まぁ、これはコレで愉しめそうだからいいけれど…ね
				CALL PRINT_SYMBOL_K14
			CASE 1
				PRINTFORMW 「これは中々凶悪な代物ね…」
				PRINTFORM 「%CNAME(MASTER, TARGET)%に試すのが愉しみだわ…
				CALL PRINT_SYMBOL_K14
			CASE 2
				PRINTFORMW 「この魔力って、本当に何でもアリなのね」
				PRINTFORMW 「ちょっと無茶過ぎる気もするけど…」
			ENDSELECT
	ENDSELECT
	RETURN 1
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;アリスさまふたなり化orふたなり消去
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && ARG == 33 && FLAG:モード != 1
	;ふたなり化
	IF TALENT:TARGET:ふたなり
		;調教対象がオトコ
		IF TALENT:MASTER:オトコ
			PRINTFORM 「これで%CNAME(MASTER, TARGET)%のお尻を突いてあげられるわね
			CALL PRINT_SYMBOL_K14
			PRINTFORMW 「\@ TALENT:MASTER:小柄 ? 女の子 # 女 \@みたいに鳴かせてあげるから、愉しみにしてて……」
		;調教対象が処女
		ELSEIF TALENT:MASTER:処女
			PRINTFORMW 「そういえば%CNAME(MASTER, TARGET)%ってその、まだ…、だったわよね？」
			IF CFLAG:合意
				PRINTFORMW 「ま、%CNAME(MASTER, TARGET)%のＯＫはもう貰ってる訳だし…」
				PRINTFORM 「あとはいつ試すか……、ってとこよね？ ふふ
				CALL PRINT_SYMBOL_K14
			ELSE
				PRINTFORMW 「ま、%CNAME(MASTER, TARGET)%からのＯＫは貰えてない訳なんだけど…」
				PRINTFORMW 「その日が来るのを愉しみにしてるわ。フフ……」
			ENDIF
		;条件なし
		ELSE
			PRINTFORMW 「%CNAME(MASTER, TARGET)%、こういうのが好きな訳…？」
			PRINTFORM 「まぁ、折角だから私も愉しませてもらう事にするわ
			CALL PRINT_SYMBOL_K14
		ENDIF
	;ふたなり消去
	ELSE
		;調教対象がオトコ
		IF TALENT:MASTER:オトコ
			PRINTFORMW 「女の私が自分より大きなモノをぶら下げてるのは耐えられなかったかしら？」
			PRINTFORMW 「ほら、どうなの？ はっきり言ってみなさいよ！」
			PRINTFORMW 「な～んて。冗談よ、冗談。クスクス」
		;調教対象が処女
		ELSEIF TALENT:MASTER:処女
			PRINTFORMW 「結局、%CNAME(MASTER, TARGET)%に試す機会がなかった訳だけど…」
			PRINTFORMW 「それが結果的に良かったのか、そうではないのか。ま、なんとも言えないところねぇ」
		;条件なし
		ELSE
			PRINTFORMW 「やっぱり元の私の方がいいかしら？ っていうか…」
			PRINTFORMW 「急にこう…、すっきりしちゃうと。なんか、逆に落ち着かないわ」
			PRINTFORMW 「これが普通のはずなのにね？ ふふ」
		ENDIF
	ENDIF
	RETURN 2
ENDIF

;━━━━━━━━━━━━━━━━━━━━━━━━
;アリスさま巨乳化or通常に戻す
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && ARG == 34 && FLAG:モード != 1
	;巨乳化
	IF TALENT:TARGET:巨乳
		;調教対象がオトコ
		IF TALENT:MASTER:オトコ
			PRINTFORMW 「男ってやっぱり胸が大きい方が好きなのかしら？」
			PRINTFORMW 「ちょっと、視線がいやらしいわよ。この変態…」
		;調教対象が巨乳
		ELSEIF TALENT:MASTER:巨乳
			PRINTFORMW 「うふっ、これでお揃いって訳ね？」
		;条件なし
		ELSE
			PRINTFORMW 「大きい方がお好み？」
		ENDIF
	;元に戻す
	ELSE
		;調教対象がオトコ
		IF TALENT:MASTER:オトコ
			PRINTFORMW 「元の私が1番って訳ね？」
		;その他
		ELSE
			PRINTFORMW 「やっぱりこの方が私らしいでしょ？」
		ENDIF
	ENDIF
	RETURN 2
ENDIF	

;━━━━━━━━━━━━━━━━━━━━━━━━
;アリスさま貧乳化or通常に戻す
;━━━━━━━━━━━━━━━━━━━━━━━━
IF 口上記入チェック(100) && ARG == 35 && FLAG:モード != 1
	;貧乳化
	IF TALENT:TARGET:貧乳
		;調教対象がオトコ
		IF TALENT:MASTER:オトコ
			PRINTFORMW 「男は大きい方が好きってよく聞くけど…」
			PRINTFORMW 「%CNAME(MASTER, TARGET)%は違うみたいね？」
		;調教対象が巨乳
		ELSEIF TALENT:MASTER:巨乳
			PRINTFORMW 「その勝ち誇ったような顔…」
			PRINTFORMW 「どういうつもりなのか、聞かせてもらう必要がありそうね！」
		;調教対象が貧乳
		ELSEIF TALENT:MASTER:貧乳
			PRINTFORMW 「自分より大きいのが妬ましいとか？」
		;その他
		ELSE
			PRINTFORMW 「小さい方が好きなの？」
			PRINTFORMW 「変わってるのね…」
		ENDIF
	;元に戻す
	ELSE
		;調教対象がオトコ
		IF TALENT:MASTER:オトコ
			PRINTFORMW 「元の私が一番って訳ね？」
		;胸を小さく
		ELSE
			PRINTFORMW 「肩が凝るからこの方がいいわね」
		ENDIF
	ENDIF
	RETURN 2
ENDIF

;────────────────────────────────────
;EV111, ご主人様として新しい調教者を雇った
;────────────────────────────────────
@KOJO_EVENT_K14_111(ARG)
;-------------------------------------------------
;・新しい調教者がTARGETになっているため、口上主はTARGETではないので注意
;・口上主を参照したい場合はFLAG:ご主人様で
;-------------------------------------------------
;戻り値（1,表示、2,表示して加入したキャラの加入口上をスキップ）
IF 口上記入チェック(100)
	IF 0
	;サニー/ルナチャ/スターの場合
	;妖精風情が、とか言ってたけど一度面識のある場合は妖精さんとか呼びそう。でもあくまで個としては認識してはいないという感じで
	ELSEIF GROUPMATCH(NO:TARGET, 45, 46, 47)
		CSTR:(FLAG:ご主人様):呼び方リスト = %CALLNAME:TARGET%_妖精さん/%CSTR:(FLAG:ご主人様):呼び方リスト%
	;メディスンの場合。なんとなくこう呼びそうな気がするという。儚月抄では一緒にいる場面があったし、まぁ多少はね？
	ELSEIF NO:TARGET == 33 && CALLNAME:TARGET == "メディスン"
		CSTR:(FLAG:2):呼び方リスト = メディスン_メディ/%CSTR:(FLAG:2):呼び方リスト%
	ELSE
		;その他召喚可能な相手（霊夢/魔理沙/パチュリー/咲夜/レミリア/紫/幽香/雛/早苗）は呼び捨て
	ENDIF
	;CSTR:(FLAG:2):呼び方リスト = 元の名前_呼ばせたい名前/%CSTR:(FLAG:2):呼び方リスト%
	IF FAVOR(3000, 3000, (FLAG:ご主人様))
		SELECTCASE NO:TARGET
			;スキマ妖怪		紫口上に対応させておく
			CASE 22
				PRINTFORMW 「ま、あんたのことだから……そういう登場の仕方するとは思ってたけど…」
				PRINTFORMW 「人の家に来るときくらい、普通にできないの！？」
				PRINTFORMW 「ていうか私…、なんで紫を呼んじゃったのかしら……」
			;その他
			CASEELSE
				PRINTFORML 「あら、%CNAME(TARGET, (FLAG:ご主人様))%｡  急にどうしたの？」
				PRINTFORMW 「家に呼んだ覚えはないけど…」
				PRINTFORMW 「ってコラ！　勝手に家の中入らないでよ！」
				CALL TWAIT3_K14
				PRINTFORMW 「まぁ、ばれてしまったものは仕方無いわね…」
				PRINTFORMW 「でも良く覚えておきなさいよ？　人のものに勝手に手を出したら、許さないからねっ！」
		ENDSELECT
	;調教対象に反抗刻印あり
	ELSEIF MARK:(FLAG:ご主人様):反抗刻印 >= 1
		PRINTFORMW 「よく来てくれたわね、%CNAME(TARGET, (FLAG:ご主人様))%。取りあえず、秘密厳守でお願いするわ」
		PRINTFORMW 「まぁ情けない話なんだけど、ちょっと苦労してるのよね」
		PRINTFORMW 「%CNAME(TARGET, (FLAG:ご主人様))%が手伝ってくれると助かるんだけど…」
	;その他
	ELSE
		SELECTCASE NO:TARGET
			;魔理沙           結構酷いかも知れないｗ
			CASE 2
				PRINTFORMW 「いらっしゃい魔理沙。早かったわね」
				PRINTFORMW 「先に言っておくけど、べつに食事に招待したって訳じゃないからね？」
				PRINTFORMW 「まぁその……なんて言うか。魔理沙も魔法使いなら、こういう事も知っておくべきだと思って」
				PRINTFORMW 「正直、こんな機会はそうないと思うわよ？ クスクス」
			;パチュリー
			CASE 8
				PRINTFORMW 「よく来てくれたわね。ん、急にどうしたのかって？」
				PRINTFORMW 「そうね、本を読んでばかりいるのにも飽きたんじゃないかと思って」
				PRINTFORMW 「ってまぁ、パチュリーに限ってそんな事はなさそうだけど。ふふ」
			;スキマ妖怪       紫口上に対応させておく
			CASE 22
				PRINTFORMW 「あら、いらっしゃい紫、って……アンタ」
				PRINTFORMW 「そういう登場の仕方やめてくれる？ 普通に玄関から入ってきなさいよ、玄関から！」
			;その他
			CASEELSE
				;アライメント正
				IF ALI(0, (FLAG:ご主人様))		;TARGETで参照できないこの箇所で使う為に@ALIを改変。まぁ他でも使い道はなにかしらありそう？
					PRINTFORMW 「いらっしゃい、%CNAME(TARGET, (FLAG:ご主人様))%｡  よく来てくれたわね」
					PRINTFORMW 「そうよ、これが例の。手伝ってくれると色々と助かるわ」
					PRINTFORMW 「じゃ、これから宜しく頼むわね」
				;アライメント負
				ELSE
					PRINTFORMW 「あら、思ったより早かったわね。%CNAME(TARGET, (FLAG:ご主人様))%」
					PRINTFORMW 「急に呼びつけてどうしたのかって？ それはまぁ、うん……、そうね」
					PRINTFORMW 「とりあえず、玄関先で話せることじゃないのよ。ま、すぐにわかるわ……」
				ENDIF
		ENDSELECT
	ENDIF
	
	;---------------------------------------------------------------------------
	;現在TALENT:ご主人様以外は助手を呼べないらしいけど、予期せぬ改変に備えて保険の意味で召喚可能助手を代入しておく
	;というか、仮に召喚した対象の口上内で設定されていた場合はアレな感じ。EV11の頭でもやるとか、あまり考えたくないな！
	;---------------------------------------------------------------------------
	;霊夢
	SIF NO:TARGET == 1
		CSTR:召喚可能助手 = 魔理沙/咲夜/レミリア/紫/萃香/てゐ/鈴仙/文/幽香/小町/にとり/サニーミルク/ルナチャイルド/スターサファイア/天子/華扇/小鈴/針妙丸/
	;魔理沙
	SIF NO:TARGET == 2
		CSTR:召喚可能助手 = 霊夢/パチュリー/咲夜/レミリア/紫/萃香/にとり/華扇/小鈴/
	;パチュリー
	SIF NO:TARGET == 8
		CSTR:召喚可能助手 = 魔理沙/小悪魔/咲夜/レミリア/
	;咲夜
	SIF NO:TARGET == 9
		CSTR:召喚可能助手 = 霊夢/魔理沙/美鈴/レミリア/妖夢/
	;レミリア
	SIF NO:TARGET == 10
		CSTR:召喚可能助手 = 霊夢/魔理沙/パチュリー/美鈴/咲夜/
	;紫
	SIF NO:TARGET == 22
		CSTR:召喚可能助手 = 霊夢/魔理沙/妖夢/幽々子/藍/萃香/
	;メディスン
	SIF NO:TARGET == 33
		CSTR:召喚可能助手 = 幽香/永琳/
	;幽香
	SIF NO:TARGET == 34
		CSTR:召喚可能助手 = リグル/メディスン/
	;雛
	SIF NO:TARGET == 39
		CSTR:召喚可能助手 = にとり/パルスィ/
	;早苗
	SIF NO:TARGET == 42
		CSTR:召喚可能助手 = 霊夢/魔理沙/諏訪子/小傘/
	;サニーミルク
	SIF NO:TARGET == 45
		CSTR:召喚可能助手 = チルノ/リリーホワイト/ルナチャイルド/スターサファイア/
	;ルナチャイルド
	SIF NO:TARGET == 46
		CSTR:召喚可能助手 = チルノ/リリーホワイト/サニーミルク/スターサファイア/
	;スターサファイア
	SIF NO:TARGET == 47
		CSTR:召喚可能助手 = チルノ/リリーホワイト/サニーミルク/ルナチャイルド/
	RETURN 1
ENDIF

;────────────────────────────────────
;EV112, アリスさまが調教者として加入した
;────────────────────────────────────
@KOJO_EVENT_K14_112(ARG)
;-------------------------------------------------
;・ご主人様の名前は「FLAG:ご主人様」で参照できる
;・ご主人様が雇用口上で戻り値2を返していると表示されない
;・口上主はTARGET
;-------------------------------------------------
IF 口上記入チェック(100)
	;調教対象の反抗刻印Lv2以上
	IF MARK:(FLAG:ご主人様):反抗刻印 >= 2
		PRINTFORMW 「%CNAME((FLAG:ご主人様), TARGET)%が苦労してるって言われて来てみたけど…」
		PRINTFORMW 「これは確かに、中々の難物みたいね…」
		PRINTFORMW 「まぁ、この際仕方無いから、%CNAME((FLAG:ご主人様), TARGET)%には躾の仕方ってものを教えてあげるわ」
		PRINTFORMW 「私の手並み、良く見ていて頂戴。ウフフ…」
	;調教対象の反抗刻印Lv2以下
	ELSE
		PRINTFORMW 「何事かと思って来てみれば、中々楽しそうな事になってるみたいね？」
		PRINTFORMW 「%CNAME((FLAG:ご主人様), TARGET)%にこんな趣味があるなんて知らなかったけど…」
		PRINTFORMW 「まぁ折角の機会だから、精々愉しませてもらう事にするわ。ウフフ…」
	ENDIF
	RETURN 1
ENDIF