;이런 화면이라면 설정하기 쉬운 것이 아니야? 그렇다고 하는 전파를 수신했으므로, 해 본 결과가 이것이야!!!
;화면의 정보량이 생각하고 있던 이상으로 너무 많아 팍 보고, (뜻)이유가 모른다. 이건 실패입니다.
;개량의 여지는… 있을까?
@MASTER_CUSTOM2
;-----------------------------------------------------------
;표시부분
;-----------------------------------------------------------
PRINTFORML %CALLNAME:MASTER%의 스테이터스를 설정 할 수 있습니다.
PRINTL 변경하고 싶은 항목을 선택해 주세요.
PRINTW (극단적인 설정에 의해 난이도가 크게 바뀌는 경우도 있기 때문에 주의해 주세요)

WHILE 1
	DRAWLINE
	LOCAL:1 = 0
	LOCAL:2 = GETCOLOR()
	PRINT 이름: 
	PRINTBUTTON @"[{LOCAL:1,2}] %CALLNAME:MASTER%", LOCAL:1++
	PRINTL 
	PRINTL □ 소질 □-----------------------------------------------------------------------
	PRINT 성별: 
	SETCOLOR !TALENT:MASTER:후타나리 && !TALENT:MASTER:남자 ? 0x00FF00 # 0xA0A0A0
	PRINTBUTTON @"[{LOCAL:1,2}] 여자 아이      　", LOCAL:1++
	SETCOLOR TALENT:MASTER:후타나리 ? 0x00FF00 # 0xA0A0A0
	PRINTFORM [{LOCAL:1++,2}] %TALENTNAME:121,12,LEFT%　
	SETCOLOR TALENT:MASTER:남자 ? 0x00FF00 # 0xA0A0A0
	PRINTFORML [{LOCAL:1++,2}] %TALENTNAME:122,12,LEFT%
	SETCOLOR LOCAL:2

	LOCAL:3 = 4
	PRINT 성격: 
	FOR LOCAL, 10, 94
		IF LOCAL == 10
			;처음은 PRINTBUTTON를 사용하지 않으면, 『성격:』이라고 들러붙는다
			LOCAL:3--
			SETCOLOR TALENT:MASTER:LOCAL ? 0x00FF00 # 0xA0A0A0
			PRINTBUTTON @"[{LOCAL:1,2}] %TALENTNAME:LOCAL,12,LEFT%", LOCAL:1++
			CONTINUE
		ELSEIF LOCAL:3
			;공백
			PRINT 　
		ELSE
			;개행
			PRINTL 
			PRINT 　　　 
			LOCAL:3 = 4
		ENDIF
		LOCAL:3--
		SETCOLOR TALENT:MASTER:LOCAL ? 0x00FF00 # 0xA0A0A0
		PRINTFORM [{LOCAL:1++,2}] %TALENTNAME:LOCAL,12,LEFT%
		;성격으로 분류되지 않는 소질을 날린다
		SELECTCASE LOCAL
			CASE 17
				LOCAL += 2
			CASE 37
				LOCAL += 25
			CASE 63, 88, 90
				LOCAL += 1
			CASE 65
				LOCAL += 14
		ENDSELECT
	NEXT
	PRINTL 
	SETCOLOR LOCAL:2

	LOCAL:3 = 4
	LOCAL:4 = GETSTYLE()
	PRINT 체질: 
	FOR LOCAL, 40, 127
		IF LOCAL == 40
			LOCAL:3--
			SETCOLOR TALENT:MASTER:LOCAL ? 0x00FF00 # 0xA0A0A0
			PRINTBUTTON @"[{LOCAL:1,2}] %TALENTNAME:LOCAL,12,LEFT%", LOCAL:1++
			CONTINUE
		ELSEIF LOCAL:3
			PRINT 　
		ELSE
			PRINTL 
			PRINT 　　　 
			LOCAL:3 = 4
		ENDIF
		LOCAL:3--
		;여자 아이/남자로 선택 할 수 없는 소질에 취소해 선을 넣어 본다
		SELECTCASE LOCAL
			CASE 102, 103, 108, 109
				SIF TALENT:MASTER:남자
					FONTSTYLE 4
			CASE 123, 124
				SIF !TALENT:MASTER:후타나리 && !TALENT:MASTER:남자
					FONTSTYLE 4
		ENDSELECT
		SETCOLOR TALENT:MASTER:LOCAL ? 0x00FF00 # 0xA0A0A0
		PRINTFORM [{LOCAL:1++,2}] %TALENTNAME:LOCAL,12,LEFT%
		FONTSTYLE LOCAL:4
		SELECTCASE LOCAL
			CASE 43
				LOCAL += 16
			CASE 62
				LOCAL += 7
			CASE 71
				LOCAL += 28
			CASE 113
				LOCAL += 2
			CASE 116
				LOCAL += 6
		ENDSELECT
	NEXT
	PRINTL 
	SETCOLOR LOCAL:2

	PRINT 기능: 
	SETCOLOR TALENT:MASTER:습득빠름 ? 0x00FF00 # 0xA0A0A0
	PRINTBUTTON @"[{LOCAL:1,2}] %TALENTNAME:50,12,LEFT%　", LOCAL:1++
	SETCOLOR TALENT:MASTER:습득느림 ? 0x00FF00 # 0xA0A0A0
	PRINTFORML [{LOCAL:1++,2}] %TALENTNAME:51,12,LEFT%
	SETCOLOR LOCAL:2

	PRINTL □ 경험 □-----------------------------------------------------------------------
	LOCAL:1 = 100
	PRINT 성 경험: 
	SETCOLOR !EXP:MASTER:성교경험 ? 0x00FF00 # 0xA0A0A0
	PRINTBUTTON @"[{LOCAL:1,3}] 없음　　", LOCAL:1++
	SETCOLOR EXP:MASTER:성교경험 == 1 ? 0x00FF00 # 0xA0A0A0
	PRINTFORM [{LOCAL:1++,3}] 조금　　
	SETCOLOR EXP:MASTER:성교경험 > 1 ? 0x00FF00 # 0xA0A0A0
	PRINTFORML [{LOCAL:1++,3}] 많은
	SETCOLOR LOCAL:2

	PRINT 성지식: 
	SETCOLOR !MASTER_CUSTOMV(-1) ? 0x00FF00 # 0xA0A0A0
	PRINTBUTTON @"[{LOCAL:1,3}] 전무　　", LOCAL:1++
	SETCOLOR MASTER_CUSTOMV(-1) == 1 ? 0x00FF00 # 0xA0A0A0
	PRINTFORM [{LOCAL:1++,3}] 조금　　
	SETCOLOR MASTER_CUSTOMV(-1) > 1 ? 0x00FF00 # 0xA0A0A0
	PRINTFORML [{LOCAL:1++,3}] 풍부
	SETCOLOR LOCAL:2

	DRAWLINE
	ALIGNMENT RIGHT
	PRINT [997] - 설정 모드 변경　　
	PRINT [998] - 리셋트　　
	PRINTL  [999] - 설정 완료　
	ALIGNMENT LEFT

;-----------------------------------------------------------
;입력 처리 부분
;-----------------------------------------------------------
	$INPUT_LOOP
	INPUT
	SELECTCASE RESULT
		;이름의 변경
		CASE 0
			PRINTL 이름을 입력해 주세요.
			INPUTS
			CALLNAME:MASTER = %RESULTS%
;-----------------------------------------------------------
		;여자 아이→후타나리/남자/조루/지루를 OFF
		CASE 1
			TALENT:MASTER:후타나리 = 0
			TALENT:MASTER:남자 = 0
			TALENT:MASTER:조루 = 0
			TALENT:MASTER:지루 = 0
		;후타나리→후타나리를 ON. 남자를 OFF
		CASE 2
			TALENT:MASTER:후타나리 = 1
			TALENT:MASTER:남자 = 0
		;남자→남자를 ON. V민감/V둔감/빈유/거유/후타나리를 OFF
		CASE 3
			TALENT:MASTER:V민감 = 0
			TALENT:MASTER:V둔감 = 0
			TALENT:MASTER:빈유 = 0
			TALENT:MASTER:거유 = 0
			TALENT:MASTER:후타나리 = 0
			TALENT:MASTER:남자 = 1
;-----------------------------------------------------------
		;겁쟁이→겁쟁이를 반전. 기장을 OFF
		CASE 4
			TALENT:MASTER:겁쟁이 = !TALENT:MASTER:겁쟁이
			TALENT:MASTER:기장 = 0
		;반항적→반항적을 반전. 순수를 OFF
		CASE 5
			TALENT:MASTER:반항적 = !TALENT:MASTER:반항적
			TALENT:MASTER:순수 = 0
		;기장→기장을 반전. 겁쟁이를 OFF
		CASE 6
			TALENT:MASTER:겁쟁이 = 0
			TALENT:MASTER:기장 = !TALENT:MASTER:기장
		;순수→순수를 반전. 반항적을 OFF
		CASE 7
			TALENT:MASTER:반항적 = 0
			TALENT:MASTER:순수 = !TALENT:MASTER:순수
		;얌전한→얌전한을 반전. 거만함을 OFF
		CASE 8
			TALENT:MASTER:얌전함 = !TALENT:MASTER:얌전함
			TALENT:MASTER:거만함 = 0
		;프라이드높음→프라이드높음을 반전. 프라이드낮음을 OFF
		CASE 9
			TALENT:MASTER:프라이드높음 = !TALENT:MASTER:프라이드높음
			TALENT:MASTER:프라이드낮음 = 0
		;거만함→거만함을 반전. 순수를 OFF
		CASE 10
			TALENT:MASTER:얌전함 = 0
			TALENT:MASTER:거만함 = !TALENT:MASTER:거만함
		;프라이드낮음→프라이드낮음을 반전. 프라이드높음을 OFF
		CASE 11
			TALENT:MASTER:프라이드높음 = 0
			TALENT:MASTER:프라이드낮음 = !TALENT:MASTER:프라이드낮음
		;자제적→자제적을 반전. 충동적을 OFF
		CASE 12
			TALENT:MASTER:자제적 = !TALENT:MASTER:자제적
			TALENT:MASTER:충동적 = 0
		;충동적→충동적을 반전. 자제적을 OFF
		CASE 13
			TALENT:MASTER:자제적 = 0
			TALENT:MASTER:충동적 = !TALENT:MASTER:충동적
		;무관심→무관심을 반전. 호기심을 OFF
		CASE 14
			TALENT:MASTER:무관심 = !TALENT:MASTER:무관심
			TALENT:MASTER:호기심 = 0
		;호기심→호기심을 반전. 무관심을 OFF
		CASE 15
			TALENT:MASTER:무관심 = 0
			TALENT:MASTER:호기심 = !TALENT:MASTER:호기심
		;감정결핍→감정결핍을 반전. 감수성을 OFF
		CASE 16
			TALENT:MASTER:감정결핍 = !TALENT:MASTER:감정결핍
			TALENT:MASTER:감수성 = 0
		;감수성→감수성을 반전. 감정결핍을 OFF
		CASE 17
			TALENT:MASTER:감정결핍 = 0
			TALENT:MASTER:감수성 = !TALENT:MASTER:감수성
		;낙관적→낙관적을 반전. 비관적을 OFF
		CASE 18
			TALENT:MASTER:낙관적 = !TALENT:MASTER:낙관적
			TALENT:MASTER:비관적 = 0
		;비관적→비관적을 반전. 낙관적을 OFF
		CASE 19
			TALENT:MASTER:낙관적 = 0
			TALENT:MASTER:비관적 = !TALENT:MASTER:비관적
		;선을그음→선을그음을 반전
		CASE 20
			TALENT:MASTER:선을그음 = !TALENT:MASTER:선을그음
		;튀고싶어함→튀고싶어함를 반전
		CASE 21
			TALENT:MASTER:튀고싶어함 = !TALENT:MASTER:튀고싶어함
		;정조관념→정조관념을 반전. 정조관념없음을 OFF
		CASE 22
			TALENT:MASTER:정조관념 = !TALENT:MASTER:정조관념
			TALENT:MASTER:정조관념없음 = 0
		;정조관념없음→정조관념없음을 반전. 정조관념을 OFF
		CASE 23
			TALENT:MASTER:정조관념 = 0
			TALENT:MASTER:정조관념없음 = !TALENT:MASTER:정조관념없음
		;억압→억압을 반전. 해방을 OFF
		CASE 24
			TALENT:MASTER:억압 = !TALENT:MASTER:억압
			TALENT:MASTER:해방 = 0
		;해방→해방을 반전. 억압을 OFF
		CASE 25
			TALENT:MASTER:억압 = 0
			TALENT:MASTER:해방 = !TALENT:MASTER:해방
		;수줍음→수줍음을 반전. 수치심옅음을 OFF
		CASE 26
			TALENT:MASTER:수줍음 = !TALENT:MASTER:수줍음
			TALENT:MASTER:수치심옅음 = 0
		;수치심옅음→수치심옅음을 반전. 수줍음을 OFF
		CASE 27
			TALENT:MASTER:수줍음 = 0
			TALENT:MASTER:수치심옅음 = !TALENT:MASTER:수치심옅음
		;호색→호색을 반전. 청초를 OFF
		CASE 28
			TALENT:MASTER:호색 = !TALENT:MASTER:호색
			TALENT:MASTER:청초 = 0
		;청초→청초를 반전. 호색을 OFF
		CASE 29
			TALENT:MASTER:호색 = 0
			TALENT:MASTER:청초 = !TALENT:MASTER:청초
		;헌신적→헌신적을 반전. 수동을 OFF
		CASE 30
			TALENT:MASTER:헌신적 = !TALENT:MASTER:헌신적
			TALENT:MASTER:수동 = 0
		;수동→수동을 반전. 헌신적을 OFF
		CASE 31
			TALENT:MASTER:헌신적 = 0
			TALENT:MASTER:수동 = !TALENT:MASTER:수동
		;도착적→도착적을 반전
		CASE 32
			TALENT:MASTER:도착적 = !TALENT:MASTER:도착적
		;양도→양도를 반전. 남성혐오를 OFF
		CASE 33
			TALENT:MASTER:양도 = !TALENT:MASTER:양도
			TALENT:MASTER:남성혐오 = 0
		;남성혐오→남성혐오를 반전. 양도를 OFF
		CASE 34
			TALENT:MASTER:양도 = 0
			TALENT:MASTER:남성혐오 = !TALENT:MASTER:남성혐오
		;새디즘→새디즘을 반전
		CASE 35
			TALENT:MASTER:새디즘 = !TALENT:MASTER:새디즘
		;신중→신중을 반전. 조급함을 OFF
		CASE 36
			TALENT:MASTER:신중 = !TALENT:MASTER:신중
			TALENT:MASTER:조급함 = 0
		;조급함→조급함을 반전. 신중을 OFF
		CASE 37
			TALENT:MASTER:신중 = 0
			TALENT:MASTER:조급함 = !TALENT:MASTER:조급함
		;심술쟁이→심술쟁이를 반전. 상냥함을 OFF
		CASE 38
			TALENT:MASTER:심술쟁이 = !TALENT:MASTER:심술쟁이
			TALENT:MASTER:상냥함 = 0
		;상냥함→상냥함을 반전. 심술쟁이를 OFF
		CASE 39
			TALENT:MASTER:심술쟁이 = 0
			TALENT:MASTER:상냥함 = !TALENT:MASTER:상냥함
		;유치→유치를 반전
		CASE 40
			TALENT:MASTER:유치 = !TALENT:MASTER:유치
		;위압감→위압감을 반전
		CASE 41
			TALENT:MASTER:위압감 = !TALENT:MASTER:위압감
		;사교적→사교적을 반전. 대인기피를 OFF
		CASE 42
			TALENT:MASTER:사교적 = !TALENT:MASTER:사교적
			TALENT:MASTER:대인기피 = 0
		;대인기피→대인기피를 반전. 사교적을 OFF
		CASE 43
			TALENT:MASTER:사교적 = 0
			TALENT:MASTER:대인기피 = !TALENT:MASTER:대인기피
;-----------------------------------------------------------
		;아픔에약함→아픔에약함을 반전. 아픔에강함을 OFF
		CASE 44
			TALENT:MASTER:아픔에약함 = !TALENT:MASTER:아픔에약함
			TALENT:MASTER:아픔에강함 = 0
		;아픔에강함→아픔에강함을 반전. 아픔에약함을 OFF
		CASE 45
			TALENT:MASTER:아픔에약함 = 0
			TALENT:MASTER:아픔에강함 = !TALENT:MASTER:아픔에강함
		;젖기쉬움→젖기쉬움을 반전. 젖기어려움을 OFF
		CASE 46
			TALENT:MASTER:젖기쉬움 = !TALENT:MASTER:젖기쉬움
			TALENT:MASTER:젖기어려움 = 0
		;젖기어려움→젖기어려움을 반전. 젖기쉬움을 OFF
		CASE 47
			TALENT:MASTER:젖기쉬움 = 0
			TALENT:MASTER:젖기어려움 = !TALENT:MASTER:젖기어려움
		;자위쉬움→자위쉬움을 반전.
		CASE 48
			TALENT:MASTER:자위쉬움 = !TALENT:MASTER:자위쉬움
		;악취둔감→악취둔감을 반전. 악취민감을 OFF
		CASE 49
			TALENT:MASTER:악취둔감 = !TALENT:MASTER:악취둔감
			TALENT:MASTER:악취민감 = 0
		;악취민감→악취민감을 반전. 악취둔감을 OFF
		CASE 50
			TALENT:MASTER:악취둔감 = 0
			TALENT:MASTER:악취민감 = !TALENT:MASTER:악취민감
		;쾌감에순수→쾌감에순수를 반전. 쾌감을부정을 OFF
		CASE 51
			TALENT:MASTER:쾌감에순수 = !TALENT:MASTER:쾌감에순수
			TALENT:MASTER:쾌감을부정 = 0
		;쾌감을부정→쾌감을부정을 반전. 쾌감에순수를 OFF
		CASE 52
			TALENT:MASTER:쾌감에순수 = 0
			TALENT:MASTER:쾌감을부정 = !TALENT:MASTER:쾌감을부정
		;C민감→C민감을 반전. C둔감을 OFF
		CASE 53
			TALENT:MASTER:C민감 = !TALENT:MASTER:C민감
			TALENT:MASTER:C둔감 = 0
		;C둔감→C둔감을 반전. C민감을 OFF
		CASE 54
			TALENT:MASTER:C민감 = 0
			TALENT:MASTER:C둔감 = !TALENT:MASTER:C둔감
		;V민감→남자의 경우는 입력 에러. V민감을 반전. V둔감을 OFF
		CASE 55
			IF TALENT:MASTER:남자
				CLEARLINE 1
				REUSELASTLINE 남자군요?
				GOTO INPUT_LOOP
			ENDIF
			TALENT:MASTER:V민감 = !TALENT:MASTER:V민감
			TALENT:MASTER:V둔감 = 0
		;V둔감→남자의 경우는 입력 에러. V둔감을 반전. V민감을 OFF
		CASE 56
			IF TALENT:MASTER:남자
				CLEARLINE 1
				REUSELASTLINE 남자군요?
				GOTO INPUT_LOOP
			ENDIF
			TALENT:MASTER:V민감 = 0
			TALENT:MASTER:V둔감 = !TALENT:MASTER:V둔감
		;A민감→A민감을 반전. A둔감을 OFF
		CASE 57
			TALENT:MASTER:A민감 = !TALENT:MASTER:A민감
			TALENT:MASTER:A둔감 = 0
		;A둔감→A둔감을 반전. A민감을 OFF
		CASE 58
			TALENT:MASTER:A민감 = 0
			TALENT:MASTER:A둔감 = !TALENT:MASTER:A둔감
		;B민감→B민감을 반전. B둔감을 OFF
		CASE 59
			TALENT:MASTER:B민감 = !TALENT:MASTER:B민감
			TALENT:MASTER:B둔감 = 0
		;B둔감→B둔감을 반전. B민감을 OFF
		CASE 60
			TALENT:MASTER:B민감 = 0
			TALENT:MASTER:B둔감 = !TALENT:MASTER:B둔감
		;빈유→남자의 경우는 입력 에러. 빈유를 반전. 거유를 OFF
		CASE 61
			IF TALENT:MASTER:남자
				CLEARLINE 1
				REUSELASTLINE 남자군요?
				GOTO INPUT_LOOP
			ENDIF
			TALENT:MASTER:빈유 = !TALENT:MASTER:빈유
			TALENT:MASTER:거유 = 0
		;거유→남자의 경우는 입력 에러. 거유를 반전. 빈유를 OFF
		CASE 62
			IF TALENT:MASTER:남자
				CLEARLINE 1
				REUSELASTLINE 남자군요?
				GOTO INPUT_LOOP
			ENDIF
			TALENT:MASTER:빈유 = 0
			TALENT:MASTER:거유 = !TALENT:MASTER:거유
		;작은체형 체형→작은체형 체형을 반전. 장신을 OFF
		CASE 63
			TALENT:MASTER:작은체형 = !TALENT:MASTER:작은체형
			TALENT:MASTER:장신 = 0
		;장신→장신을 반전. 작은체형 체형을 OFF
		CASE 64
			TALENT:MASTER:작은체형 = 0
			TALENT:MASTER:장신 = !TALENT:MASTER:장신
		;회복빠름→회복빠름을 반전. 회복느림을 OFF
		CASE 65
			TALENT:MASTER:회복빠름 = !TALENT:MASTER:회복빠름
			TALENT:MASTER:회복느림 = 0
		;회복느림→회복느림을 반전. 회복빠름을 OFF
		CASE 66
			TALENT:MASTER:회복빠름 = 0
			TALENT:MASTER:회복느림 = !TALENT:MASTER:회복느림
		;소변버릇→소변버릇을 반전
		CASE 67
			TALENT:MASTER:소변버릇 = !TALENT:MASTER:소변버릇
		;조루→여자 아이의 경우는 입력 에러. 조루를 반전. 지루를 OFF
		CASE 68
			IF !TALENT:MASTER:후타나리 && !TALENT:MASTER:남자
				CLEARLINE 1
				REUSELASTLINE 여자 아이군요?
				GOTO INPUT_LOOP
			ENDIF
			TALENT:MASTER:조루 = !TALENT:MASTER:조루
			TALENT:MASTER:지루 = 0
		;지루→여자 아이의 경우는 입력 에러. 지루를 반전. 조루를 OFF
		CASE 69
			IF !TALENT:MASTER:후타나리 && !TALENT:MASTER:남자
				CLEARLINE 1
				REUSELASTLINE 여자 아이군요?
				GOTO INPUT_LOOP
			ENDIF
			TALENT:MASTER:조루 = 0
			TALENT:MASTER:지루 = !TALENT:MASTER:지루
		;정력절륜→정력절륜을 반전. 정력박약을 OFF
		CASE 70
			TALENT:MASTER:정력절륜 = !TALENT:MASTER:정력절륜
			TALENT:MASTER:정력박약 = 0
		;정력박약→정력박약을 반전. 정력절륜을 OFF
		CASE 71
			TALENT:MASTER:정력절륜 = 0
			TALENT:MASTER:정력박약 = !TALENT:MASTER:정력박약
		;습득빠름→습득빠름을 반전. 습득느림을 OFF
		CASE 72
			TALENT:MASTER:습득빠름 = !TALENT:MASTER:습득빠름
			TALENT:MASTER:습득느림 = 0
		;습득느림→습득느림을 반전. 습득빠름을 OFF
		CASE 73
			TALENT:MASTER:습득빠름 = 0
			TALENT:MASTER:습득느림 = !TALENT:MASTER:습득느림
;-----------------------------------------------------------
		;성 경험 없음
		CASE 100
			EXP:MASTER:성교경험 = 0
		;성 경험 조금
		CASE 101
			EXP:MASTER:성교경험 = 1
		;성 경험 많은
		CASE 102
			EXP:MASTER:성교경험 = 3
		;성지식 전무
		CASE 103
			CALLF MASTER_CUSTOMV(0)
		;성지식 조금
		CASE 104
			CALLF MASTER_CUSTOMV(1)
		;성지식 풍부
		CASE 105
			CALLF MASTER_CUSTOMV(2)
;-----------------------------------------------------------
		CASE 997
			JUMP MASTER_CUSTOM
		CASE 998
			REPEAT 300
				TALENT:MASTER:COUNT = 0
			REND
			REPEAT 100
				EXP:MASTER:COUNT = 0
			REND
			CALLF MASTER_CUSTOMV(0)
			TALENT:MASTER:남자 = 1
		;설정 완료
		CASE 999
			BREAK
		CASEELSE
			CLEARLINE 1
			GOTO INPUT_LOOP
	ENDSELECT
WEND

;-----------------------------------------------------------
;후 처리 부분
;-----------------------------------------------------------
;성 경험에 의한 후 처리
SELECTCASE EXP:MASTER:성교경험
	CASE 0
		;성 경험 없음
		TALENT:MASTER:처녀 = !TALENT:MASTER:남자
		TALENT:MASTER:동정 = TALENT:MASTER:후타나리 || TALENT:MASTER:남자
		EXP:MASTER:사정경험 = (TALENT:MASTER:후타나리 || TALENT:MASTER:남자) ? TALENT:MASTER:자위쉬움 # 0
		EXP:MASTER:성지식 = MASTER_CUSTOMV(-1) * 5
	CASE 1
		;성 경험 조금
		TALENT:MASTER:동정 = 0
		EXP:MASTER:C경험 = 4
		EXP:MASTER:V경험 = TALENT:MASTER:남자 ? 0 # 2
		EXP:MASTER:B경험 = TALENT:MASTER:남자 ? 0 # 3
		EXP:MASTER:사정경험 = (TALENT:MASTER:후타나리 || TALENT:MASTER:남자) ? 3 + TALENT:MASTER:자위쉬움 # 0
		EXP:MASTER:성지식 = 5 + MASTER_CUSTOMV(-1) * 5
	CASE IS > 1
		;성 경험 많은
		TALENT:MASTER:동정 = 0
		ABL:MASTER:욕망 = 1
		EXP:MASTER:C경험 = 10
		EXP:MASTER:V경험 = TALENT:MASTER:남자 ? 0 # 10
		EXP:MASTER:B경험 = TALENT:MASTER:남자 ? 0 # 10
		EXP:MASTER:사정경험 = (TALENT:MASTER:후타나리 || TALENT:MASTER:남자) ? 12 + TALENT:MASTER:자위쉬움 # 0
		EXP:MASTER:성지식 = 10 + MASTER_CUSTOMV(-1) * 5
ENDSELECT

;자위경험. 자위쉬움 경우,+10
EXP:MASTER:자위경험 = TALENT:MASTER:자위쉬움 * 10

;-----------------------------------------------------------
;MASTER_CUSTOM로 이용하는 변수(MASTER 성지식의 정도)를 보유
;-----------------------------------------------------------
@MASTER_CUSTOMV(ARG)
#FUNCTION
;ARG를 보유 해, ARG가-1이면 그 값을 돌려준다
SIF ARG != -1
	LOCAL = ARG
RETURNF LOCAL

