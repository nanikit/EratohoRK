;캐릭터 메이킹  by.
;우선 유카리씨의 질문에 답해 성별과 체격을 결정.
;그 후, 랜덤으로 선택된 10명으로부터의 질문 코너가 시작합니다
;대답에 응해 소질이 정해집니다.
@CHARA_MAKER
#LOCALSIZE 200
#DIM LCOUNT, 2
#DIM REROOL, 1
#DIM LITTLE_CHECK, 1
;지휘자로서 유카리를 추가. 조수로 지정（이미지적인 것입니다）
ADDCHARA 22
ASSI = CHARANUM-1

;세이브된 캐릭터가 있으면 읽는다
IF MATCH(GLOBALS, "", 0, 5) < 5
	$MAKE_OR_LORD
	PRINTFORMDL [0] 캐릭터의 작성
	PRINTFORMDL [1] 캐릭터의 로드
	CALL INPUTINT(0, 1)
	IF RESULT == 1
		CALL CHARAMAKE_DATA_MENU
		SIF !RESULT
			GOTO MAKE_OR_LORD
		CALL KOJO_COLOR(ASSI)
		PRINTFORMW 「어서 오세요\@ CALLNAME:MASTER != "당신" ? 、%CALLNAME:MASTER% # \@」
		PRINTL 
		CALL CHARA_CUTIN(CSTR:ASSI:10, CSTR:ASSI:11, CSTR:ASSI:로마자)
		PRINTL 
		PRINTFORMW 「또 헤매러 와 버리다니 구할 길이 없어요」
		PRINTFORMW 「자 선택하는거야. 당신의――주인님을」
		PRINTL 
		RESETCOLOR
		DELCHARA ASSI
		ASSI = -1
		RETURN 1
	ENDIF
ENDIF

CALL KOJO_COLOR(ASSI)
PRINTFORMW 「후후, 처음 뵙겠습니다」
PRINTL 
CALL CHARA_CUTIN(CSTR:ASSI:10, CSTR:ASSI:11, CSTR:ASSI:로마자)
PRINTL 
PRINTFORMW 「나의 이름은 아시는 바라고 생각하기 때문에 생략해요」
PRINTFORMW 「이 앞은 환상이라고 부르려면 조금 저속하고 몽매한 세계」
PRINTFORMW 「자, 준비는 좋을까?」
PRINTL 
CALL BGM_CUTIN("2개의 세계")
PRINTFORMW 「-환상에 빠지는 준비를. 자」

;성별의 결정
PRINTL 
PRINTFORML 「우선은 내가 물읍시다. 이 세계에서의 당신의 성별은?」
PRINTFORMW 「정기에 흘러넘치는 남성?   용모도 아름다운 여성?   그렇지 않으면……」
PRINTL 
PRINTFORMDL [0] 남성
PRINTFORMDL [1] 여성
PRINTFORMDL [2] 『그렇지 않으면……?』
CALL INPUTINTD(0, 1, 2)
SELECTCASE RESULT
	CASE 0
		PRINTFORMW 「남성이네. 알았어요… 기다려지게 하셔」
		TALENT:MASTER:남자 = 1
	CASE 1
		PRINTFORMW 「여성이, 네. … 후훗, 좋은거야. 그것도 또, 일흥이군요」
	CASE 2
		PRINTFORML 「선택할 수 없는거네?   후후, 멋대로이고 욕심쟁이인 당신에서도, 여기는 상냥하게 받아들여요」
		PRINTFORMW 「자, 이제 돌아올 수 없어요. 괜찮아, 성의 경계는 원래 있어 없는 것 같은 것」
		TALENT:MASTER:후타나리 = 1
ENDSELECT

;체격의 결정
PRINTFORMW 「그러면, 다음에 갈까요」
PRINTL 
PRINTFORMW 「당신의 성은 정해졌어요. 뒤는, 당신은 어떤 체격을 하고 있는 거야?」
IF TALENT:MASTER:남자
	PRINTFORML 「매우 보통 남성?   그렇지 않으면 장신의 미남일까」
	PRINTFORMW 「혹시…… 원복도 아직도의 꼬마님, 일지도 모르네요?」
ELSE
	PRINTFORML 「평범한 아가씨도 좋아요. 그렇지 않으면, 남자 압도하는 장신의 미녀?」
	PRINTFORMW 「세상도 모르는 어린 소녀… 도 좋지만, 상냥하게 해 줄 수 있다고는 생각하지 않는 것이군요」
ENDIF
PRINTL 
PRINTFORMDL [0] 보통\@ TALENT:MASTER:남자 ? 청년 # 여성 \@정도
PRINTFORMDL [1] 이른바\@ TALENT:MASTER:남자 ? 소년 # 소녀 \@정도
PRINTFORMDL [2] 키는 큰 편이라고 생각한다
PRINTFORMDL [3] 어떻게 봐도 아이
CALL INPUTINTD(0, 1, 2, 3)
SELECTCASE RESULT
	CASE 0
		PRINTFORML 「특징이 없는 사람은 아첨하게 된다고 말해요」
		PRINTFORMW 「당신은 어떻게 되어 버릴, 까요」
	CASE 1
		PRINTFORML 「젊고 신선한 신체… 우후훗」
		PRINTFORMW 「더럽혀져도 매력을 잃지 않는, 이상한 것이군요」
		;다음에 참조하기 위해서, 작은체형을=2로 한다
		;이 경우, 능력이나 소질의 판정은 어린 취급으로 행해지지만, 작은체형 자체는 마지막에 사라진다
		TALENT:MASTER:작은체형 = 2
		LITTLE_CHECK = 1
	CASE 2
		PRINTFORML 「그래, 장신인 것…… 불쌍하게. 풍족해, 자각 없게 잘난체한 그 마음」
		PRINTFORMW 「앞으로의 당신을 탓하는 독이 된다. 풍족한 탓이군요」
		TALENT:MASTER:장신 = 1
	CASE 3
		PRINTFORML 「후후… 아하핫」
		PRINTFORMW 「아이가 이런 곳에 와서는 안 되는데. 즐겁네요」
		TALENT:MASTER:작은체형 = 1
		TALENT:MASTER:유치 = 1
		LITTLE_CHECK = 2
ENDSELECT


;남자가 아니면, 젖가슴의 결정
IF !TALENT:MASTER:남자
	PRINTL 
	PRINTFORML 「그렇게 말하면, 당신의 여성의 상징… 에에, 그렇게. 유방」
	PRINTFORMW 「요컨데, 젖가슴의 크기야. 어느 정도일까?」
	PRINTL 
	PRINTFORMDL [0] 작고 나쁩니까?
	PRINTFORMDL [1] 남들 수준 정도입니다
	PRINTFORMDL [2] 크다고 말해집니다
	CALL INPUTINTD(0, 1, 2)
	SELECTCASE RESULT
		CASE 0
			IF TALENT:MASTER:작은체형
				PRINTFORMW 「그 체격에서는 작고 당연합니다. 신경쓰는 것은 아니네요」
			ELSE
				PRINTFORMW 「작은거네. 그것이 당신이라면, 어쩔 수 없는 것이야」
			ENDIF
			TALENT:MASTER:빈유 = 1
		CASE 1
			PRINTFORMW 「보통이라고 하는 일이군요. 누구에게라도 귀여워해 받을 수 있을 것 같구나, 후후후」
		CASE 2
			IF TALENT:MASTER:작은체형
				PRINTFORML 「그 체격으로 큽니다 라고?   … 남 좋아하는 하는, 약삭빠른 신체」
				PRINTFORMW 「오스를 위해서(때문에) 출생한 것 같다, 천한 신체… 후후, 감정을 해쳤을까?」
			ELSE
				PRINTFORMW 「풍족한 거네. 원수가 되지 않으면, 좋네요. 후후」
			ENDIF
			TALENT:MASTER:거유 = 1
	ENDSELECT
ENDIF

;이름의 결정
PRINTL 
PRINTFORMW 「대단히, 당신의 일을 알 수 있었습니다. 하나 더, 소중한 일을 들려주어 받습니다」
PRINTL 
PRINTFORML 「큰 일이지만 간단한 일이야. 당신의 일은, 뭐라고 부릅시다?」
PRINTFORMW 「이름이라도 좋고… 자칭해 주지 않다면, 단지 당신이라고 부르면 좋네요」
PRINTL 
PRINTFORMDL [0] 이름을 자칭한다
PRINTFORMDL [1] 자칭하지 않는다
CALL INPUTINTD(0, 1)
IF !RESULT
	PRINTFORMW 「당신의 이름은…?」
	;캐릭터 라인 입력인 것으로 칼라를 되돌린다
	RESETCOLOR
	REUSELASTLINE ※이름을 입력해 주세요
	$NAME_ENTRY
	INPUTS
	IF STRLENS(RESULTS) > 16
		CLEARLINE 1
		REUSELASTLINE ※이름이 너무 깁니다!
		GOTO NAME_ENTRY
	ELSEIF RESULTS == ""
		CLEARLINE 1
		REUSELASTLINE ※이름을 입력해 주세요
		GOTO NAME_ENTRY
	ELSE
		PRINTFORML 「%RESULTS%」로 좋습니까?
		PRINTL 
		PRINTL [0] 네
		PRINTL [1] 아니오
		PRINTL [2] 자칭하는 것을 그만둔다
		CALL INPUTINT(0, 1, 2)
		IF RESULT == 1
			REUSELASTLINE ※이름을 입력해 주세요
			GOTO NAME_ENTRY
		ELSEIF RESULT == 0
			NAME:MASTER = %RESULTS%
			CALL KOJO_COLOR(ASSI)
			PRINTFORMW 「%NAME:MASTER%씨 라는 것이군요. 알았습니다」
			PRINTFORMW 「부를 때도, 이름 그대로 『%NAME:MASTER%』, 그리고 좋을까?」
			PRINTL 
			PRINTDL [0] 좋습니다
			PRINTDL [1] 좋지 않다
			CALL INPUTINTD(0, 1)
			IF RESULT == 1
				PRINTFORMW 「좋지 않다면, 뭐라고 부르면 좋은가 가르쳐 주세요」
				;캐릭터 라인 입력인 것으로 칼라를 되돌린다
				RESETCOLOR
				REUSELASTLINE ※통칭을 입력해 주세요
				$CALLNAME_ENTRY
				INPUTS
				IF STRLENS(RESULTS) > 12
					CLEARLINE 1
					REUSELASTLINE ※통칭이 너무 깁니다!
					GOTO CALLNAME_ENTRY
				ELSEIF RESULTS == ""
					CLEARLINE 1
					REUSELASTLINE ※통칭을 입력해 주세요
					GOTO CALLNAME_ENTRY
				ELSE
					PRINTFORML 「%RESULTS%」로 좋습니까?
					PRINTL 
					PRINTL [0] 네
					PRINTL [1] 아니오
					CALL INPUTINTD(0, 1)
					IF RESULT == 1
						REUSELASTLINE ※통칭을 입력해 주세요
						GOTO CALLNAME_ENTRY
					ENDIF
				ENDIF
				CALLNAME:MASTER = %RESULTS%
				CALL KOJO_COLOR(ASSI)
				PRINTFORMW 「%CALLNAME:MASTER%… 후훗, 알았습니다. 이렇게 부르는 일에 합시다」
			ELSE
				CALLNAME:MASTER = %NAME:MASTER%
				PRINTFORMW 「그러면,%CALLNAME:MASTER%라고 부르게 해 받아요」
			ENDIF
		ELSE
			PRINTFORML 「아라, 역시 자칭하지 않는다는?」
			PRINTFORMW 「그래. 현명해 소심, 애완동물에는 의외로 많은 기질이군요」
		ENDIF
	ENDIF
ELSE
	PRINTFORML 「… 자칭할 생각은 없는 것 같네. 알았어요」
	PRINTFORMW 「후후, 경계하고 있을까?   그렇지 않으면, 부끄러운거야?」
ENDIF
PRINTFORMW 「인연이 있을까는 모르지만」
PRINTFORMW 「잘 부탁해요, 『%CALLNAME:MASTER%』」

;성격 결정은 전캐릭터로부터 10명을 선택해 질문받는다
PRINTL 
PRINTFORML 「그럼. 슬슬 휴가의 시간이군요」
PRINTFORMW 「에에, 꿈을 꾸어 받는거야. 그 꿈으로 당신은, 10의 방을 지나겠지요」
PRINTFORMW 「자주(잘) 생각하가 되어. 거기서의 문답으로, 우리들은%CALLNAME:MASTER%의 일을 알니까」
PRINTFORMW 「후후, 기대하세요. 그러면, 잘 자요」
PRINTL 
PRINTFORMW 「-깨는 무렵에는, 환상이에요」
PRINTL 
RESETCOLOR
PRINTW ………
PRINTW ……
PRINTW …
PRINTL 
$CHARAMAKE_REROLL
;수치를 리셋트
VARSET LOCAL
CALLF CM_PROF("리셋트")
;10인 돌려 부른다. FLAG:8은 당신 이외의 캐릭터 총수
;LOCAL:캐릭터 번호가 진이라고 벌써 불린 취급
FOR LCOUNT, 0, 10
	$CHARAMAKE_LOOP
	ADDCHARA RAND:(FLAG:Cha총수+1)
	TARGET = CHARANUM-1
	IF LOCAL:(NO:TARGET)
		DELCHARA TARGET
		GOTO CHARAMAKE_LOOP
	ENDIF
	LOCAL:(NO:TARGET) = 1
	CALL KOJO_COLOR(TARGET)
	TRYCCALLFORM CHARAMAKE_K{NO:TARGET}(LCOUNT)
	CATCH
		DELCHARA TARGET
		GOTO CHARAMAKE_LOOP
	ENDCATCH
	PRINTL 
	PRINTDW ――。
	PRINTL 
	DELCHARA TARGET
NEXT
;소질 결정
FOR LCOUNT, 0, 150
	TRYCALLFORM CM_SET_TALENT_{LCOUNT}
NEXT
;경험 결정
FOR LCOUNT, 0, 100
	TRYCALLFORM CM_SET_EXP_{LCOUNT}
NEXT
;능력 결정
FOR LCOUNT, 0, 30
	TRYCALLFORM CM_SET_ABL_{LCOUNT}
NEXT
;소녀 판정에 사용한 작은체형의 삭제
TALENT:MASTER:작은체형 %= 2
RESETCOLOR
PRINTL 
PRINTDW ………
PRINTDW ……
PRINTDW …
PRINTL 

;주인공의 게이지를 셋업, MASTERCUSTOM.ERB에 격납
CALL BASE_MASTER_SETUP


CALL PRINTSTATE_CHARAMAKE(MASTER)
CALL PRINT_DIALOG("이 캐릭터를 보존합니까?", 1, 1, 1)
PRINTL [0] 네
PRINTL [1] 아니오
CALL INPUTINT(0, 1)
IF !RESULT
	CALL CHARAMAKE_DATA_MENU, 1
ENDIF

CALL KOJO_COLOR(ASSI)
PRINTFORMW 「후후, 좋은 꿈은 볼 수 있어서?」
PRINTL 
CALL CHARA_CUTIN(CSTR:ASSI:10, CSTR:ASSI:11, CSTR:ASSI:로마자)
PRINTL 
PRINTFORMW 「헤에,%CALLNAME:MASTER%는 이런 인물인 거네. 맞고 있어?」
PRINTL 
PRINTDL [0] 이것으로 좋다
PRINTDL [1] 뭔가의 실수
CALL INPUTINTD(0, 1)
IF RESULT == 1
	PRINTFORMW 「아라?   그렇게 말한다면, 한번 더 꿈을 꿀까요」
	PRINTFORMW 「손질하는 것은 보기 흉한 것이지만」
	PRINTFORMW 「후후후, 어차피 희롱해진다면, 이상의 자신이 좋은 것도 아는 것」
	PRINTL 
	PRINTFORMW 「-잘 자요, 멋대로인%CALLNAME:MASTER%」
	PRINTL 
	RESETCOLOR
	PRINTW ………
	PRINTW ……
	PRINTW …
	PRINTL 
	;새롭게 당신을 추가한다
	ADDCHARA 0
	TARGET = CHARANUM-1
	;결정 끝난 요소를 카피
	NAME = %NAME:MASTER%
	CALLNAME = %CALLNAME:MASTER%
	TALENT:남자 = TALENT:MASTER:남자
	TALENT:후타나리 = TALENT:MASTER:후타나리
	TALENT:빈유 = TALENT:MASTER:빈유
	TALENT:거유 = TALENT:MASTER:거유
	TALENT:장신 = TALENT:MASTER:장신
	IF LITTLE_CHECK == 2
		TALENT:작은체형 = 1
		TALENT:유치 = 1
	ELSEIF LITTLE_CHECK == 1
		TALENT:작은체형 = 2
	ENDIF
	SWAPCHARA MASTER, TARGET
	MASTER = 0
	DELCHARA CHARANUM-1
	GOTO CHARAMAKE_REROLL
ENDIF
PRINTFORMW 「그러면, 나부터 마지막 선택입니다」
PRINTFORMW 「이 세계에서의 당신의 운명을 잡는다――주인님은, 어떤 분일까?」
PRINTL 
RESETCOLOR
DELCHARA ASSI
ASSI = -1

;관련 함수. 성격치의 받침접시 따위
;인수 0은 가산, 인수 1은 대입（생략 하면 대입하지 않는다）

;성격치 셋타
@CM_PROF(ARGS, ARG, ARG:1 = -100)
#FUNCTION
#LOCALSIZE 100
#DIM PROFNUM, 1
;PROFNUM세트
SELECTCASE ARGS
	CASE "공격"
		PROFNUM = 0
	CASE "피학"
		PROFNUM = 1
	CASE "솔직함"
		PROFNUM = 2
	CASE "프라이드"
		PROFNUM = 3
	CASE "충동"
		PROFNUM = 4
	CASE "호기심"
		PROFNUM = 5
	CASE "수치"
		PROFNUM = 6
	CASE "낙관"
		PROFNUM = 7
	CASE "지성"
		PROFNUM = 8
	CASE "도착"
		PROFNUM = 9
	CASE "동성애"
		PROFNUM = 10
	CASE "결벽"
		PROFNUM = 11
	CASE "모랄"
		PROFNUM = 12
	CASE "신경질"
		PROFNUM = 13
	CASE "Ｃ를 좋아하는 사람"
		PROFNUM = 50
	CASE "Ｖ를 좋아하는 사람"
		PROFNUM = 51
	CASE "Ａ를 좋아하는 사람"
		PROFNUM = 52
	CASE "Ｂ를 좋아하는 사람"
		PROFNUM = 53
	CASE "리셋트"
		VARSET LOCAL
		RETURNF 0
	CASEELSE
		RETURNF 0
ENDSELECT

;대입
IF ARG:1 != -100
	LOCAL:PROFNUM = ARG:1
;가산
ELSE
	LOCAL:PROFNUM += ARG
ENDIF
RETURNF LOCAL:PROFNUM

;성격치로부터 소질 판정치를 꺼내는 함수
;성격치에 편향을 갖게하고 싶은 경우, 두 번 지정하면 된다
@CM_TALENT_MAKE(ARGS, ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5, ARGS:6, ARGS:7, ARGS:8, ARGS:9, ARGS:10, ARGS:11, ARGS:12, ARGS:13, ARGS:14, ARGS:15, ARGS:16, ARGS:17, ARGS:18, ARGS:19)
#FUNCTION
#DIM LCOUNT, 1
VARSET LOCAL
FOR LCOUNT, 0, 20
	;분모 가산
	LOCAL:1++
	;역  라고 하는 글자가 들어가 있으면 성격치의 할인구
	IF STRFIND(ARGS:LCOUNT, "역") >= 0
		LOCAL -= CM_PROF(REPLACE(ARGS:LCOUNT, "역",""))
	ELSEIF ARGS:LCOUNT != ""
		LOCAL += CM_PROF(ARGS:LCOUNT)
	ELSE
		;인수가 하늘이라면 분모를 되돌린다
		LOCAL:1--
	ENDIF
NEXT
RETURNF LOCAL/(LOCAL:1)


