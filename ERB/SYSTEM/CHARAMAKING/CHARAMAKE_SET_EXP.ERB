;EXP 생성

;C경험
@CM_SET_EXP_0
;우선 경험 자체의 유무를 판정
;기본 판정치 50  상당히 결벽하지 않으면 C경험은 있다
LOCAL = 50
;소질 판정치를 이용
LOCAL += CM_TALENT_MAKE("프라이드역", "피학", "충동", "수치역", "모랄역", "결벽역", "도착역", "C를 좋아하는 사람", "C를 좋아하는 사람", "호기심", "호기심")
;고추 있다고 판정치+50
SIF PENIS(MASTER)
	LOCAL += 50
;작은체형이라면 판정치-40
SIF TALENT:MASTER:작은체형
	LOCAL -= 40
;경험이 있다고 했을 경우, 양의 처리
IF PERCENT(LOCAL)
	;기본치는 판정치(부가 될 수 없다)의 1/4에 RAND:판정치를 더한다
	EXP:MASTER:C경험 = LOCAL/4+RAND:LOCAL
	;작은체형이라면 0.75배
	SIF TALENT:MASTER:작은체형
		TIMES EXP:MASTER:C경험, 0.75
	;C 민감하다면 1.5배
	SIF TALENT:MASTER:C민감
		TIMES EXP:MASTER:C경험, 1.50
	;페니스가 있어 동정이 아닌 경우, 허리사용이라면 1.5배
	SIF PENIS(MASTER) && !TALENT:MASTER:동정 && TALENT:MASTER:허리사용
		TIMES EXP:MASTER:C경험, 1.50
	;C 둔하다면 0.75배
	SIF TALENT:MASTER:C둔감
		TIMES EXP:MASTER:C경험, 0.75
	;자위쉬움이라면 2배
	SIF TALENT:MASTER:자위쉬움
		TIMES EXP:MASTER:C경험, 2.00
ENDIF

;고추 있는 비동정의 경우, 만약 0 이하가 되어 있으면, 1+RAND:3으로 갈아넣는다
SIF PENIS(MASTER) && !TALENT:MASTER:동정 && EXP:MASTER:C경험 <= 0
	EXP:MASTER:C경험 = 1+RAND:3


;V경험
@CM_SET_EXP_1
;남자 거절
SIF TALENT:MASTER:남자
	RETURN 0
;처녀 거절
SIF TALENT:MASTER:처녀
	RETURN 0
;기본 판정치 30  처녀는 아니기 때문에 반드시 경험 있어
LOCAL = 30
;소질 판정치를 이용
LOCAL += CM_TALENT_MAKE("프라이드역", "지성역", "솔직함", "수치역", "모랄역", "결벽역", "결벽역", "V를 좋아하는 사람", "V를 좋아하는 사람", "호기심", "호기심")
;고추 있다고 판정치-30
SIF PENIS(MASTER)
	LOCAL -= 30
;작은체형이라면 판정치-20
SIF TALENT:MASTER:작은체형
	LOCAL -= 20

;판정치가 10 이하라면, 1+RAND:20으로 갈아넣는다
SIF LOCAL <= 10
	LOCAL = 1+RAND:20

;↑를 기본치로서 양의 처리
;기본치는 판정치(부가 될 수 없다)의 1/4에 RAND:판정치를 더한다
EXP:MASTER:V경험 = LOCAL/4+RAND:LOCAL
;작은체형이라면 0.75배
SIF TALENT:MASTER:작은체형
	TIMES EXP:MASTER:V경험, 0.75
;V 민감하다면 1.5배
SIF TALENT:MASTER:V민감
	TIMES EXP:MASTER:V경험, 1.50;
;허리사용이라면 1.5배
SIF TALENT:MASTER:허리사용
	TIMES EXP:MASTER:V경험, 1.50
;V 둔하다면 0.75배
SIF TALENT:MASTER:V둔감
	TIMES EXP:MASTER:V경험, 0.75
;자위쉬움이라면 1.2배
SIF TALENT:MASTER:자위쉬움
	TIMES EXP:MASTER:V경험, 1.20
;마지막에 3으로 나눈다
EXP:MASTER:V경험 /= 3

;만약 0 이하가 되어 있으면, 1+RAND:3으로 갈아넣는다
SIF EXP:MASTER:V경험 <= 0
	EXP:MASTER:V경험 = 1+RAND:3

;A경험
@CM_SET_EXP_2
;우선 경험 자체의 유무를 판정
;기본 판정치 0  상당히가 아니면 A경험은 가지고 있지 않다
LOCAL = 0
;소질 판정치를 이용
LOCAL += CM_TALENT_MAKE("프라이드역", "피학", "지성", "수치역", "도착", "결벽역", "결벽역", "A를 좋아하는 사람", "A를 좋아하는 사람", "호기심", "호기심")
;작은체형이라면 판정치-60
SIF TALENT:MASTER:작은체형
	LOCAL -= 60
;경험이 있다고 했을 경우, 양의 처리
IF PERCENT(LOCAL)
	;기본치는 판정치(부가 될 수 없다)의 1/4에 RAND:판정치를 더한다
	EXP:MASTER:A경험 = LOCAL/4+RAND:LOCAL
	;작은체형이라면 0.75배
	SIF TALENT:MASTER:작은체형
		TIMES EXP:MASTER:A경험, 0.75
	;민감하다면 1.5배
	SIF TALENT:MASTER:A민감
		TIMES EXP:MASTER:A경험, 1.50
	;둔하다면 0.75배
	SIF TALENT:MASTER:A둔감
		TIMES EXP:MASTER:A경험, 0.75
	;자위쉬움이라면 2배
	SIF TALENT:MASTER:자위쉬움
		TIMES EXP:MASTER:A경험, 2.00
	;마지막에 4로 나눈다
	EXP:MASTER:A경험 /= 4
ENDIF

;B경험
@CM_SET_EXP_3
;우선 경험 자체의 유무를 판정
;기본 판정치 50  비교적 B경험은 있다
LOCAL = 50
;소질 판정치를 이용
LOCAL += CM_TALENT_MAKE("프라이드역", "피학", "솔직함", "수치역", "모랄역", "결벽역", "결벽역", "B를 좋아하는 사람", "B를 좋아하는 사람", "호기심", "호기심")
;작은체형이라면 판정치-30
SIF TALENT:MASTER:작은체형
	LOCAL -= 30
;남자라면 판정치-30
SIF TALENT:MASTER:남자
	LOCAL -= 30
;경험이 있다고 했을 경우, 양의 처리
IF PERCENT(LOCAL)
	;기본치는 판정치(부가 될 수 없다)의 1/4에 RAND:판정치를 더한다
	EXP:MASTER:B경험 = LOCAL/4+RAND:LOCAL
	;작은체형이라면 0.75배
	SIF TALENT:MASTER:작은체형
		TIMES EXP:MASTER:B경험, 0.75
	;민감하다면 1.5배
	SIF TALENT:MASTER:B민감
		TIMES EXP:MASTER:B경험, 1.50
	;둔하다면 0.75배
	SIF TALENT:MASTER:B둔감
		TIMES EXP:MASTER:B경험, 0.75
	;자위쉬움이라면 2배
	SIF TALENT:MASTER:자위쉬움
		TIMES EXP:MASTER:B경험, 2.00
	;마지막에 남자라면 6, 그렇지 않으면 2로 나눈다
	EXP:MASTER:B경험 /= 2+(TALENT:MASTER:남자*4)
ENDIF

;절정 경험
@CM_SET_EXP_4
;남자
IF TALENT:MASTER:남자
	;부위 경험의 화를 70~100배로 해, 100으로 나누지만, C경험을 넘지 않는다
	EXP:MASTER:절정경험 = MIN(SUMARRAY(EXP:MASTER:0, 0, 4)*(70+RAND:31)/100, EXP:MASTER:C경험)
;후타나리
ELSEIF TALENT:MASTER:남자
	;부위 경험의 화를 70~100배로 해, 100으로 나눈다
	EXP:MASTER:절정경험 = SUMARRAY(EXP:MASTER:0, 0, 4)*(70+RAND:31)/100
;여자 아이
ELSE
	;부위 경험의 화를 40~80배로 해, 100으로 나눈다
	EXP:MASTER:절정경험 = SUMARRAY(EXP:MASTER:0, 0, 4)*(40+RAND:41)/100
ENDIF

;사정 경험
@CM_SET_EXP_5
;페니스가 없으면 0, 페니스가 있으면 절정 경험과 C경험의 쳐 적은 편과 같음
EXP:MASTER:사정경험 = PENIS(MASTER) ? MIN(EXP:MASTER:절정경험, EXP:MASTER:C경험) # 0
;다만 비처녀로 후타나리의 경우, 경험의 차이에 응해 절정수부터 사정수가 적게 되거나
SIF TALENT:MASTER:후타나리 && !TALENT:MASTER:처녀
	EXP:MASTER:사정경험 = EXP:MASTER:사정경험*(EXP:MASTER:C경험)/(EXP:MASTER:C경험+EXP:MASTER:V경험)

;분유경험
@CM_SET_EXP_6
;생성되지 않는다

;성교 경험
@CM_SET_EXP_7
;남자·후타나리 처녀
IF TALENT:MASTER:남자 || (TALENT:MASTER:후타나리 && TALENT:MASTER:처녀)
	;동정 거절이니까^^;
	IF !TALENT:MASTER:동정
		;C경험을 0~100배로 해, 200으로 나누지만, 사정 경험을 넘지 않는다
		EXP:MASTER:성교경험 = MIN(EXP:MASTER:C경험*(RAND:100)/200, EXP:MASTER:사정경험)
		;만약 0 이하가 되어 있으면, 1+RAND:2로 갈아넣는다
		SIF EXP:MASTER:성교경험 <= 0
			EXP:MASTER:성교경험 = 1+RAND:2
	ENDIF
;여자 아이·후타나리 동정
ELSEIF !TALENT:MASTER:남자 || (TALENT:MASTER:후타나리 && TALENT:MASTER:동정)
	;처녀라든지 귀찮은 것뿐이겠지
	IF !TALENT:MASTER:처녀
		;V경험을 80~100배로 해, 100으로 나눈다
		EXP:MASTER:성교경험 = EXP:MASTER:V경험*(80+RAND:21)/100
		;만약 0 이하가 되어 있으면, 1+RAND:2로 갈아넣는다
		SIF EXP:MASTER:성교경험 <= 0
			EXP:MASTER:성교경험 = 1+RAND:2
	ENDIF
;양쪽 모두 사용이 끝난 후타나리
ELSE
	;C경험을 10~100배, V경험을 80~100배로 해, 200으로 나눈다
	EXP:MASTER:성교경험 = ((EXP:MASTER:C경험*(10+RAND:91))+(EXP:MASTER:V경험*(80+RAND:21)))/200
	;만약 0 이하가 되어 있으면, 2+RAND:3으로 갈아넣는다
	SIF EXP:MASTER:성교경험 <= 0
		EXP:MASTER:성교경험 = 2+RAND:3
ENDIF
;자위쉬움이라고 1/3가 된다
SIF TALENT:MASTER:자위쉬움
	EXP:MASTER:성교경험 /= 3


;성 지식
@CM_SET_EXP_8
;부위 경험의 화에 성교 경험의 3배를 더해, 10으로 나눈다
EXP:MASTER:성지식 = (SUMARRAY(EXP:MASTER:0, 0, 4)+(EXP:MASTER:성교경험*3))/10

;자위 경험
@CM_SET_EXP_10
;남자·후타나리 처녀
IF TALENT:MASTER:남자 || (TALENT:MASTER:후타나리 && TALENT:MASTER:처녀)
	;부위 경험의 화와 사정 경험 가운데, 적은 (분)편으로부터 성교 경험을 당긴 것
	EXP:MASTER:자위경험 = MIN(SUMARRAY(EXP:MASTER:0, 0, 4), EXP:MASTER:사정경험)-EXP:MASTER:성교경험
ELSE
	;부위 경험의 화로부터 성교 경험을 당긴 것
	EXP:MASTER:자위경험 = SUMARRAY(EXP:MASTER:0, 0, 4)-EXP:MASTER:성교경험
ENDIF


;조교 자위 경험
@CM_SET_EXP_11
;조교 요소인 것으로 생성되지 않는다

;방뇨 경험
@CM_SET_EXP_12
;조교 요소인 것으로 생성되지 않는다

;피사경효험
@CM_SET_EXP_13
;조교 요소인 것으로 생성되지 않는다

;정액 경험
@CM_SET_EXP_20
;처녀도 남자도 아닌 경우
IF !TALENT:MASTER:남자 && !TALENT:MASTER:처녀
	;성교 경험을 100~200배로 해, 100으로 나눈다.
	EXP:MASTER:정액경험 = EXP:MASTER:성교경험*(100+RAND:101)/100
	;혀사용이라면 1.2배
	SIF TALENT:MASTER:혀사용
		TIMES EXP:MASTER:정액경험, 1.20
	;기묘한손가락라면 1.2배
	SIF TALENT:MASTER:기묘한손가락
		TIMES EXP:MASTER:정액경험, 1.20
	;헌신적이다면 1.2배
	SIF TALENT:MASTER:헌신적
		TIMES EXP:MASTER:정액경험, 1.20
	;비동정 후타나리의 경우 반으로 한다
	EXP:MASTER:정액경험 = TALENT:MASTER:후타나리 && !TALENT:MASTER:동정 ? EXP:MASTER:정액경험/2 # EXP:MASTER:정액경험
;남자는 아니다=처녀의 경우
ELSEIF !TALENT:MASTER:남자
	;우선 경험 자체의 유무를 판정
	;기본 판정치-20  처녀 봉사는 그다지 발생하지 않는다
	LOCAL = -20
	;소질 판정치를 이용
	LOCAL += CM_TALENT_MAKE("프라이드역", "피학역", "솔직함", "수치역", "공격역", "결벽역", "결벽역", "도착", "도착", "호기심", "호기심")
	;작은체형이라면 판정치-60
	SIF TALENT:MASTER:작은체형
		LOCAL -= 60
	;경험이 있다고 했을 경우, 양의 처리
	IF PERCENT(LOCAL)
		;기본치는 판정치(부가 될 수 없다)의 1/4에 RAND:판정치를 더한다
		EXP:MASTER:정액경험 = LOCAL/4+RAND:LOCAL
		;혀사용이라면 1.5배
		SIF TALENT:MASTER:혀사용
			TIMES EXP:MASTER:정액경험, 1.50
		;기묘한손가락라면 1.5배
		SIF TALENT:MASTER:기묘한손가락
			TIMES EXP:MASTER:정액경험, 1.50
		;헌신적이다면 1.5배
		SIF TALENT:MASTER:헌신적
			TIMES EXP:MASTER:정액경험, 1.50
		;수동이라면 0.75배
		SIF TALENT:MASTER:수동
			TIMES EXP:MASTER:정액경험, 0.75
		;악취민감이라면 0.75배
		SIF TALENT:MASTER:악취민감
			TIMES EXP:MASTER:정액경험, 0.75
		;마지막에 20으로 나눈다
		EXP:MASTER:정액경험 /= 20
		;만약 0 이하가 되어 있으면, 1+RAND:2로 갈아넣는다
		SIF EXP:MASTER:정액경험 <= 0
			EXP:MASTER:정액경험 = 1+RAND:2
	ENDIF
ENDIF

;봉사 쾌락 경험
@CM_SET_EXP_21
;조교 요소인 것으로 생성되지 않는다

;구음경험
@CM_SET_EXP_22
;정액 경험으로부터 성교 경험을 당긴 것을 0~200배로 해,150+기묘한손가락*50-혀사용*50으로 나눈다
EXP:MASTER:구음경험 = (EXP:MASTER:정액경험 - EXP:MASTER:성교경험)*RAND:201/(150+(TALENT:MASTER:기묘한손가락*50)-(TALENT:MASTER:혀사용*50))
;만약 0 이하가 되어 있으면, 0으로 갈아넣는다
SIF EXP:MASTER:구음경험 <= 0
	EXP:MASTER:구음경험 = 0

;수음 경험
@CM_SET_EXP_23
;정액 경험으로부터 성교 경험과 구음경험의 8할을 당긴 것을 100~150배로 해, 100으로 나눈다
EXP:MASTER:수음경험 = (EXP:MASTER:정액경험 - EXP:MASTER:성교경험 - (EXP:MASTER:구음경험*8/10))*(100+RAND:51)/100
;만약 0 이하가 되어 있으면, 0으로 갈아넣는다
SIF EXP:MASTER:수음경험 <= 0
	EXP:MASTER:수음경험 = 0

;도구 사용 경험
@CM_SET_EXP_24
;생성율은 일률 20%. 다만 도구사용을 가지고 있는 경우는 100%
IF !RAND:5 || TALENT:MASTER:도구사용
	;생성량은 성교 경험+수음 경험+구음경험의 화를, 남자는 2배, 여자 아이는 반으로 해, 그 1/10(도구사용은 1/4)
	EXP:MASTER:도구사용경험 = (EXP:MASTER:성교경험+EXP:MASTER:구음경험+EXP:MASTER:수음경험*(TALENT:MASTER:남자+1))/(!PENIS(MASTER)+1)/(10-(TALENT:MASTER:도구사용*6))
ENDIF

;키스 경험
@CM_SET_EXP_25
;성교 경험이 있다
IF EXP:MASTER:성교경험
	;성교 경험*2과 10의 쳐 큰 (분)편까지로 랜덤
	EXP:MASTER:키스경험 = RAND:(MAX(EXP:MASTER:성교경험*2, 10))
;성교 경험이 없다
ELSE
	;기초 확률 50%, 작은체형이라면 20%
	LOCAL = TALENT:MASTER:작은체형 ? 20 # 50
	LOCAL += TALENT:MASTER:사교적-TALENT:MASTER:대인기피*30
	SIF PERCENT(LOCAL)
		EXP:MASTER:키스경험 = RAND:(3*(TALENT:MASTER:사교적-TALENT:MASTER:대인기피)+5)
ENDIF

;회화 경험
@CM_SET_EXP_26
;조교 요소인 것으로 생성되지 않는다

;고통 쾌락 경험
@CM_SET_EXP_30
;조교 요소인 것으로 생성되지 않는다

;치욕 쾌락 경험
@CM_SET_EXP_31
;조교 요소인 것으로 생성되지 않는다
;이따금 생성되어도 재미있지만

;애정 경험
@CM_SET_EXP_32
;조교 요소인 것으로 생성되지 않는다

;백합 경험
@CM_SET_EXP_40
;후타나리의 경우
IF TALENT:MASTER:후타나리
	;성교 경험으로부터 정액 경험을 당긴 것인가, 사정 경험으로부터 자위 경험을 당긴 것 가운데, 많은 (분)편
	EXP:MASTER:백합경험 = MAX(EXP:MASTER:성교경험-EXP:MASTER:정액경험, EXP:MASTER:사정경험-EXP:MASTER:자위경험, 0)
;여자 아이로, 남성혐오가 있는 경우
ELSEIF TALENT:MASTER:남성혐오 && !TALENT:MASTER:남자
	;성교 경험과 정액 경험을 백합 경험으로 이동한다
	EXP:MASTER:백합경험 = (EXP:MASTER:성교경험+EXP:MASTER:정액경험)/2
	;처녀라면 함께 0에. 처녀가 아니면 RAND:3으로 보충한다
	EXP:MASTER:성교경험 = TALENT:MASTER:처녀 ? 0 # RAND:3
	EXP:MASTER:정액경험 = EXP:MASTER:성교경험
;여자 아이로, 양도가 있는 경우
ELSEIF TALENT:MASTER:양도 && !TALENT:MASTER:남자
	LOCAL = -20
	;소질 판정치를 이용
	LOCAL += CM_TALENT_MAKE("동성애", "동성애", "B를 좋아하는 사람", "도착", "호기심", "결벽역")
	SIF PERCENT(LOCAL)
		EXP:MASTER:백합경험 = (RAND:LOCAL)/5
ENDIF

;BL경험
@CM_SET_EXP_41
;조교 요소인 것으로 생성되지 않는다

;이상 경험
@CM_SET_EXP_50
;완전하게 랜덤(2%)으로 1개있어 온다
SIF !RAND:50
	EXP:MASTER:이상경험 = 1

;긴박 경험
@CM_SET_EXP_51
;조교 요소인 것으로 생성되지 않는다

;V확장 경험
@CM_SET_EXP_52
;조교 요소인 것으로 생성되지 않는다

;A확장 경험
@CM_SET_EXP_53
;조교 요소인 것으로 생성되지 않는다

;촉수 경험
@CM_SET_EXP_55
;조교 요소인 것으로 생성되지 않는다

;굴복 경험
@CM_SET_EXP_60
;조교 요소인 것으로 생성되지 않는다

;공포 경험
@CM_SET_EXP_61
;조교 요소인 것으로 생성되지 않는다

;노출 경험
@CM_SET_EXP_62
;기본치는 자위 경험을 반으로 해 50을 뺀 것
LOCAL = EXP:MASTER:자위경험/2-50
;소질 판정지는 반으로 해 사용한다
LOCAL += CM_TALENT_MAKE("프라이드역", "피학", "수치", "수치", "도착", "도착", "결벽역", "충동", "호기심")/2
;작은체형이라면 판정치-30
SIF TALENT:MASTER:작은체형
	LOCAL -= 30
;남자라면 판정치-30
SIF TALENT:MASTER:남자
	LOCAL -= 30
;도착적이다면 판정치+50
SIF TALENT:MASTER:도착적
	LOCAL += 30
;자제적이다면 판정치-50
SIF TALENT:MASTER:자제적
	LOCAL -= 30
;경험이 있다고 했을 경우, 양의 처리
IF PERCENT(LOCAL)
	;기본치는 판정치(부가 될 수 없다)의 1/4에 RAND:판정치를 더한다
	EXP:MASTER:노출경험 = LOCAL/4+RAND:LOCAL
	;신중하다면 0.5배
	SIF TALENT:MASTER:신중
		TIMES EXP:MASTER:노출경험, 0.50
	;도착적이다면 1.5배
	SIF TALENT:MASTER:도착적
		TIMES EXP:MASTER:노출경험, 1.50
	;수줍음이라면 0.75배
	SIF TALENT:MASTER:수줍음
		TIMES EXP:MASTER:노출경험, 0.75
	;수치심옅음이라면 1.5배
	SIF TALENT:MASTER:수치심옅음
		TIMES EXP:MASTER:노출경험, 1.50
	;마지막에 남자라면 10, 그렇지 않으면 4로 나눈다
	EXP:MASTER:노출경험 /= 4+(TALENT:MASTER:남자*6)
ENDIF

;가게 한 경험
@CM_SET_EXP_63
;설정이 너무 귀찮았으므로 조교 요소라고 생각해 생성하지 않는다

