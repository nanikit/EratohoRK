
;────────────────────────────────────
;도구 생성 처리
;※ARG는 요구 편차(0을 기준에 큰 만큼 이 종별로 마력을 소비 하고 싶어한다)
;────────────────────────────────────
@MAKE_TOOLS()
#LOCALSSIZE 50
#DIM LCOUNT
#DIM 가용마력

;이 가격을 웃도는 도구가 존재하면 파탄한다
LOCAL = 1000000
FOR LCOUNT, 0, 50
	SIF ITEMNAME:LCOUNT != "" && ITEM:LCOUNT == 0
		LOCAL = MIN(LOCAL, ITEMPRICE:LCOUNT)
NEXT
;사용 가능 마력 판정
가용마력 = MAKE_TOOLS_MP_ABLE()

SIF FLAG:디버그
	PRINTFORML 『도구 생성 처리』(가용 마력:{가용마력})

;살 수 없는가, 살 수 있는 도구가 이제 없으면 돌아간다
IF 가용마력 < LOCAL || LOCAL == 1000000
	IF FLAG:디버그
		IF LOCAL == 1000000
			PRINTFORMW 구입 가능한 도구가 존재하지 않습니다!
		ELSE
			PRINTFORMW 가용 마력이 부족합니다(그리고{LOCAL - 가용마력})
		ENDIF
	ENDIF
	RETURN RESULT
ENDIF

;변수를 초기화
CALL SET_CRI_VAR
SPLIT "로터/바이브/Ａ비즈/Ｐ밴드//////커스텀//Ａ바이브/Ｃ캐프/Ｂ캐프/Ｐ홀//////Ｅ마스크/채찍/줄////////개그/바늘/관장/삼각목마///////대거울//목걸이", "/", LOCALS
FOR LCOUNT, 0, 50
	IF ITEMNAME:LCOUNT != "" && ITEM:LCOUNT == 0
		CALL SET_CRI_VAR, ITEMNAME:LCOUNT, 1
		CALL SET_CRI_VAR, LOCALS:LCOUNT, 1, 1
	ENDIF
NEXT

;------------------------------------------
;보정 처리
;------------------------------------------

LOCALS = MAKE_TOOLS

CALL CHA_CRI_VAR, "조교 대상이 처녀"
CALLFORM %LOCALS%_MAIDEN_M

CALL CHA_CRI_VAR, "위험한 도구"
CALLFORM %LOCALS%_RISK

CALL CHA_CRI_VAR, "고문도구"
CALLFORM %LOCALS%_TORMENT

CALL CHA_CRI_VAR, "조교 대상의Ｃ감각"
CALLFORM %LOCALS%_C_M

CALL CHA_CRI_VAR, "조교 대상의Ｖ감각"
CALLFORM %LOCALS%_V_M

CALL CHA_CRI_VAR, "조교 대상의Ａ감각"
CALLFORM %LOCALS%_A_M

CALL CHA_CRI_VAR, "조교 대상의Ｂ감각"
CALLFORM %LOCALS%_B_M

CALL CHA_CRI_VAR, "적극성"
CALLFORM %LOCALS%_POSITIVELY_T

CALL CHA_CRI_VAR, "새드마조"
CALLFORM %LOCALS%_SM

CALL CHA_CRI_VAR, "도구 단체 판정"
CALLFORM %LOCALS%_TOOL

CALL CHA_CRI_VAR, "조교자의 광기"
CALLFORM %LOCALS%_INSANE

CALL CHA_CRI_VAR, "가격에 의한 보정"
CALLFORM %LOCALS%_PRICE

CALL CHA_CRI_VAR, "실행 판정"
CALLFORM %LOCALS%_ABLE

;------------------------------------------
;생성 판정
;------------------------------------------

LOCAL:2 = 0

$MAKE_TOOLS

;구입 상황에 의한 판정(이미 소지, 마력 부족)
;판정 처리
FOR LCOUNT, 0, 50
	IF ITEMNAME:LCOUNT == "" || ITEM:LCOUNT > 0
		CALL DIM_CRI_VAR(ITEMNAME:LCOUNT, -999)
		CONTINUE
	ENDIF
	LOCAL = GET_CRI_VAR(ITEMNAME:LCOUNT + "누계")
	IF LOCAL < 0
		LOCAL = ITEMPRICE:LCOUNT * (10 + ABS(LOCAL)) / 10
	ELSE
		LOCAL = ITEMPRICE:LCOUNT * 10 / (10 + ABS(LOCAL))
	ENDIF
	SIF LOCAL > 가용마력
		CALL DIM_CRI_VAR(ITEMNAME:LCOUNT, -999)
NEXT

CALL DEF_CRI_VAR("최대 전자")
LOCAL = GET_CRI_VAR("결정")
LOCALS = %RESULTS%
CALL ANA_CRI_VAR()
IF LOCAL != -1 && ITEMPRICE:LOCALS <= CFLAG:마력
	LOCAL = CFLAG:마력
	CFLAG:마력 -= ITEMPRICE:LOCALS
	가용마력 -= ITEMPRICE:LOCALS
	ITEM:LOCALS = 1
	;연성시 구상 호출
	;CALL KOJO_EVENT(110, XX)
	;CALL VOIDLINE_IF(RESULT)
	SKIPDISP 0
	PRINTFORML %CALLNAME%는%LOCALS%를 연성 했습니다
	PRINTFORMW 마력({LOCAL} → {CFLAG:마력})
	IF FLAG:오토모드 > 0 && FLAG:오토모드표시설정 == 1
		SKIPDISP 1
	ENDIF
	LOCAL:2++
	SIF FLAG:디버그
		PRINTFORML (가용 마력:{가용마력})
	GOTO MAKE_TOOLS
ENDIF

SIF FLAG:디버그
	PRINTW 

RETURN RESULT

;------------------------------------------
;조교 대상이 처녀
;------------------------------------------
@MAKE_TOOLS_MAIDEN_M

IF TALENT:MASTER:처녀
	;소질에 의해 작용의 방향을 결정
	LOCAL = TALENT:심술쟁이 - TALENT:상냥함 - TALENT:겁쟁이 + TALENT:MASTER:정조관념없음 - TALENT:MASTER:정조관념
	;소질에 의해 작용의 강도를 결정
	LOCAL = LOCAL * POWER(2, -TALENT:유치 + 12) / 1024
	CALL ADD_CRI_VAR("바이브"        , LOCAL)
	CALL ADD_CRI_VAR("페니스밴드"  , LOCAL)
ENDIF

;------------------------------------------
;위험한 도구
;------------------------------------------
@MAKE_TOOLS_RISK
;LOCAL에는 리스크를 대입
LOCAL = 1 + TALENT:MASTER:임신
;소질에 의해 작용의 방향을 결정
LOCAL = LOCAL * (TALENT:조급함 - TALENT:신중 - TALENT:겁쟁이 - TALENT:얌전함 + TALENT:충동적 - TALENT:자제적)
;소질에 의해 작용의 강도를 결정
LOCAL = LOCAL * POWER(2, TALENT:MASTER:아픔에약함 - TALENT:MASTER:아픔에강함 + 12) / 1024
CALL ADD_CRI_VAR("로터"      , LOCAL)
CALL ADD_CRI_VAR("바이브"        , LOCAL * 2)
CALL ADD_CRI_VAR("애널비즈"  , LOCAL * 3)
CALL ADD_CRI_VAR("페니스밴드"  , LOCAL * 3)
CALL ADD_CRI_VAR("애널바이브"  , LOCAL * 2)
CALL ADD_CRI_VAR("클리캡"  , LOCAL)
CALL ADD_CRI_VAR("니플캡", LOCAL)
CALL ADD_CRI_VAR("오나홀"    , LOCAL)
CALL ADD_CRI_VAR("아이마스크"    , LOCAL)
CALL ADD_CRI_VAR("채찍"            , LOCAL * 4)
CALL ADD_CRI_VAR("줄"            , LOCAL * 3)
CALL ADD_CRI_VAR("볼개그"  , LOCAL)
CALL ADD_CRI_VAR("바늘"            , LOCAL * 5)
CALL ADD_CRI_VAR("관장세트"    , LOCAL * 3)
CALL ADD_CRI_VAR("삼각목마"      , LOCAL * 8)
CALL ADD_CRI_VAR("대거울"          , LOCAL)
CALL ADD_CRI_VAR("목걸이"          , LOCAL)

;------------------------------------------
;고문도구
;------------------------------------------
@MAKE_TOOLS_TORMENT
;소질에 의해 작용의 방향을 결정
LOCAL = TALENT:MASTER:도착적 + TALENT:MASTER:예속 + TALENT:새드
;소질에 의해 작용의 강도를 결정
LOCAL = LOCAL * POWER(2, TALENT:MASTER:아픔에약함 - TALENT:MASTER:아픔에강함 + TALENT:MASTER:연모 + 12) / 1024
CALL ADD_CRI_VAR("아이마스크"    , LOCAL)
CALL ADD_CRI_VAR("채찍"            , LOCAL * 3)
CALL ADD_CRI_VAR("줄"            , LOCAL * 3)
CALL ADD_CRI_VAR("볼개그"  , LOCAL * 2)
CALL ADD_CRI_VAR("바늘"            , LOCAL * 5)
CALL ADD_CRI_VAR("관장세트"    , LOCAL * 4)
CALL ADD_CRI_VAR("삼각목마"      , LOCAL * 8)
CALL ADD_CRI_VAR("대거울"          , LOCAL)
CALL ADD_CRI_VAR("목걸이"          , LOCAL * 2)

;------------------------------------------
;조교 대상의Ｃ감각
;------------------------------------------
@MAKE_TOOLS_C_M
;소질에 의해 작용의 방향을 결정
LOCAL = TALENT:MASTER:Ｃ민감 - TALENT:MASTER:Ｃ둔감 + TALENT:MASTER:음핵／음경
;소질에 의해 작용의 강도를 결정
LOCAL = LOCAL * POWER(2, TALENT:순수 + 12) / 1024
CALL ADD_CRI_VAR("로터"      , LOCAL)
CALL ADD_CRI_VAR("클리캡"  , LOCAL)
CALL ADD_CRI_VAR("오나홀"    , LOCAL)

;------------------------------------------
;조교 대상의Ｖ감각
;------------------------------------------
@MAKE_TOOLS_V_M
;소질에 의해 작용의 방향을 결정
LOCAL = TALENT:MASTER:Ｖ민감 - TALENT:MASTER:Ｖ둔감 + TALENT:MASTER:음호 + TALENT:MASTER:젖기쉬움 - TALENT:MASTER:젖기어려움
;소질에 의해 작용의 강도를 결정
LOCAL = LOCAL * POWER(2, TALENT:순수 + 12) / 1024
CALL ADD_CRI_VAR("바이브"        , LOCAL)
CALL ADD_CRI_VAR("페니스밴드"  , LOCAL)

;------------------------------------------
;조교 대상의Ａ감각
;------------------------------------------
@MAKE_TOOLS_A_M
;소질에 의해 작용의 방향을 결정
LOCAL = TALENT:MASTER:Ａ민감 - TALENT:MASTER:Ａ둔감 + TALENT:MASTER:음고
;소질에 의해 작용의 강도를 결정
LOCAL = LOCAL * POWER(2, TALENT:순수 + 12) / 1024
CALL ADD_CRI_VAR("애널비즈"  , LOCAL)
CALL ADD_CRI_VAR("페니스밴드"  , LOCAL)
CALL ADD_CRI_VAR("애널바이브"  , LOCAL)
CALL ADD_CRI_VAR("관장세트"    , LOCAL)

;------------------------------------------
;조교 대상의Ｂ감각
;------------------------------------------
@MAKE_TOOLS_B_M
;소질에 의해 작용의 방향을 결정
LOCAL = TALENT:MASTER:Ｂ민감 - TALENT:MASTER:Ｂ둔감 + TALENT:MASTER:음유
;소질에 의해 작용의 강도를 결정
LOCAL = LOCAL * POWER(2, TALENT:순수 + 12) / 1024
CALL ADD_CRI_VAR("니플캡", LOCAL)

;------------------------------------------
;적극성
;------------------------------------------
@MAKE_TOOLS_POSITIVELY_T
;소질에 의해 작용의 방향을 결정
LOCAL = TALENT:헌신적 - TALENT:수동
;작용의 강도를 결정
LOCAL = LOCAL * POWER(2, 0)
CALL ADD_CRI_VAR("로터"      , LOCAL * 2)
CALL ADD_CRI_VAR("바이브"        , LOCAL * 2)
CALL ADD_CRI_VAR("애널비즈"  , LOCAL * 2)
CALL ADD_CRI_VAR("페니스밴드"  , LOCAL * 4)
CALL ADD_CRI_VAR("애널바이브"  , LOCAL * 2)
CALL ADD_CRI_VAR("클리캡"  , LOCAL)
CALL ADD_CRI_VAR("니플캡", LOCAL)
CALL ADD_CRI_VAR("오나홀"    , LOCAL)
CALL ADD_CRI_VAR("아이마스크"    , LOCAL)
CALL ADD_CRI_VAR("채찍"            , LOCAL * 2)
CALL ADD_CRI_VAR("줄"            , LOCAL * 3)
CALL ADD_CRI_VAR("볼개그"  , LOCAL)
CALL ADD_CRI_VAR("바늘"            , LOCAL * 3)
CALL ADD_CRI_VAR("관장세트"    , LOCAL * 2)
CALL ADD_CRI_VAR("삼각목마"      , LOCAL * 3)
CALL ADD_CRI_VAR("대거울"          , LOCAL)
CALL ADD_CRI_VAR("목걸이"          , LOCAL)

;------------------------------------------
;새드마조
;------------------------------------------
@MAKE_TOOLS_SM
;소질에 의해 작용의 방향을 결정
LOCAL = TALENT:새드
;소질에 의해 작용의 강도를 결정
LOCAL = LOCAL * POWER(2, TALENT:MASTER:아픔에약함 - TALENT:MASTER:아픔에강함 + TALENT:MASTER:연모 + 12) / 1024
CALL ADD_CRI_VAR("채찍"            , LOCAL * 2)
CALL ADD_CRI_VAR("줄"            , LOCAL * 3)
CALL ADD_CRI_VAR("볼개그"  , LOCAL)
CALL ADD_CRI_VAR("바늘"            , LOCAL * 3)
CALL ADD_CRI_VAR("관장세트"    , LOCAL * 2)
CALL ADD_CRI_VAR("삼각목마"      , LOCAL * 3)

;------------------------------------------
;도구 단체 판정
;------------------------------------------
@MAKE_TOOLS_TOOL
CALL ADD_CRI_VAR("페니스밴드"  , TALENT:호색 * 4)
CALL ADD_CRI_VAR("줄"            , TALENT:결박능숙 * 4)
CALL ADD_CRI_VAR("바늘"            , TALENT:바느질 * 4)
CALL ADD_CRI_VAR("목걸이"          , TALENT:튀고싶어함 * 4)

;------------------------------------------
;조교자의 광기
;------------------------------------------
@MAKE_TOOLS_INSANE
#DIM LCOUNT
IF TALENT:광기
	FOR LCOUNT, 0, 50
		IF ITEMNAME:LCOUNT != ""
			CALL MUL_CRI_VAR(ITEMNAME:LCOUNT, -50, 100)
		ENDIF
	NEXT
ENDIF

;------------------------------------------
;가격에 의한 보정
;------------------------------------------
@MAKE_TOOLS_PRICE
#DIM LCOUNT
FOR LCOUNT, 0, 50
	IF ITEMNAME:LCOUNT != ""
		LOCAL = GET_CRI_VAR(ITEMNAME:LCOUNT + "누계")
		IF LOCAL > 0
			CALL MUL_CRI_VAR(ITEMNAME:LCOUNT, 100 * POWER(2, 10 + TALENT:충동적 - TALENT:자제적 + TALENT:튀고싶어함) / ITEMPRICE:LCOUNT)
		ELSEIF LOCAL < 0
			CALL MUL_CRI_VAR(ITEMNAME:LCOUNT, 100 * ITEMPRICE:LCOUNT / POWER(2, 10 + TALENT:충동적 - TALENT:자제적 + TALENT:튀고싶어함))
		ENDIF
	ENDIF
NEXT

;------------------------------------------
;실행 판정
;------------------------------------------
@MAKE_TOOLS_ABLE
;조교자에게 페니스가 있으면 페니스밴드는 불요
SIF PENIS(TARGET)
	CALL DIM_CRI_VAR("페니스밴드"  , -999)
;조교 대상으로 페니스가 없으면 오나홀은 불요
SIF !PENIS(MASTER)
	CALL DIM_CRI_VAR("오나홀"    , -999)
;커스텀도구는 미대응
CALL DIM_CRI_VAR("커스텀도구"  , -999)

;------------------------------------------
;사용 가능 마력 판정
;------------------------------------------
@MAKE_TOOLS_MP_ABLE()
#FUNCTION
;마력 부족 기준 설정
RETURNF LIMIT(CFLAG:마력 * (8 + MANAGE_MP_PROP() + TALENT:도구사용) * 10 / 100, 0, CFLAG:마력)


