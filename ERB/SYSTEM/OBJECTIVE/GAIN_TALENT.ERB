
;─
;기능 취득·방폐 처리
;※ARG는 요구 편차(0을 기준에 큰 만큼 이 종별로 마력을 소비 하고 싶어한다)
;─
@GAIN_TALENT_AI()
#DIM LCOUNT
#DIM 기능수
#DIM 가용마력
#DIMS 전기능명
#DIMS 기능명, 15
#DIMS 결정종

;취득(방폐) 가능 기능을 기능명에 격납한다
전기능명 = 약학지식/매혹/금단의지식/도구사용/촬영기술/혀사용/기묘한손가락/결박능숙/새디즘/불결무시/종단
SPLIT 전기능명, "/", 기능명

;기능에 대한 해설@이것 보고_201205
;『약학지식』
;미약·로션의 약효 상승, 또 휴게계 영양제ACT의 회복량 상승
;『매혹』
;조교 대상의 긍정적 행동에 대한 장벽이 낮아진다, 또 호감도 상승도 커진다
;『금단의지식』
;휴게계 영양제ACT의 회복량 상승 정도. 현재는 불우
;『도구사용』
;전반적인 도구 효과의 상승, 또 도구 커스터마이즈가 가능하게
;『촬영기술』
;촬영조수ACT가 가능하게 된다
;『혀사용』
;혀를 사용하는 행동의 효과가 높아진다
;『기묘한손가락』
;손가락을 이용하는 행동의 효과가 높아진다
;『결박능숙』
;줄을 이용하는 행동의 효과가 높아진다. 그것 뿐
;『새디즘』
;고통을 주는 일에 대해서 저항이 없어져, 효과도 오릅니다
;『불결무시』
;더러움에 의한 디메리트를 경감. 현재는 약간 불우

;기능수를 카운트
기능수 = FINDELEMENT(기능명, "종단")

;이 가격을 웃도는 기능이 존재하면 파탄한다
LOCAL = 1000000
FOR LCOUNT, 0, 기능수
	SIF TALENT:(기능명:LCOUNT) == 0 && 기능명:LCOUNT != "새디즘"
		LOCAL = MIN(LOCAL, GAIN_TALENT_V(기능명:LCOUNT, 0))
NEXT
;사용 가능 마력 판정
가용마력 = GAIN_TALENT_MP_ABLE()

SIF FLAG:디버그
	PRINTFORML 『기능 취득 처리』(가용 마력:{가용마력})

;살 수 없는가, 살 수 있는 도구가 이제 없으면 돌아간다
IF 가용마력 < LOCAL || LOCAL == 1000000
	IF FLAG:디버그
		IF LOCAL == 1000000
			PRINTFORMW 취득 가능한 기능이 존재하지 않습니다!
		ELSE
			PRINTFORMW 가용 마력이 부족합니다(그리고{LOCAL - 가용마력})
		ENDIF
	ENDIF
	RETURN RESULT
ENDIF

;변수를 초기화
CALL SET_CRI_VAR
FOR LCOUNT, 0, 기능수
	SIF TALENT:(기능명:LCOUNT) == 0 && 기능명:LCOUNT != "새디즘"
		CALL SET_CRI_VAR, 기능명:LCOUNT, 1
NEXT

;------------------------------------------
;보정 처리
;------------------------------------------

LOCALS = GAIN_TALENT

CALL CHA_CRI_VAR, "조교자의 소질"
CALLFORM %LOCALS%_TALENT_T

CALL CHA_CRI_VAR, "조교 대상의 소질"
CALLFORM %LOCALS%_TALENT_M

CALL CHA_CRI_VAR, "조교자의 광기"
CALLFORM %LOCALS%_INSANE(전기능명)

CALL CHA_CRI_VAR, "가격에 의한 보정"
CALLFORM %LOCALS%_PRICE(전기능명)

CALL CHA_CRI_VAR, "실행 판정"
CALLFORM %LOCALS%_ABLE

;------------------------------------------
;취득 판정
;------------------------------------------

LOCAL:2 = 0

$GAIN_TALENT

;취득 상황에 의한 판정(이미 취득, 마력 부족)
;판정 처리
FOR LCOUNT, 0, 기능수
	IF TALENT:(기능명:LCOUNT) == 1 && 기능명:LCOUNT != "새디즘"
		CALL DIM_CRI_VAR(기능명:LCOUNT, -999)
		CONTINUE
	ENDIF
	LOCAL = GET_CRI_VAR(기능명:LCOUNT + "누계")
	IF LOCAL < 0
		LOCAL = GAIN_TALENT_V(기능명:LCOUNT, 0) * (10 + ABS(LOCAL)) / 10
	ELSE
		LOCAL = GAIN_TALENT_V(기능명:LCOUNT, 0) * 10 / (10 + ABS(LOCAL))
	ENDIF
	SIF LOCAL > 가용마력
		CALL DIM_CRI_VAR(기능명:LCOUNT, -999)
NEXT

CALL DEF_CRI_VAR("최대 전자")
LOCAL = GET_CRI_VAR("결정")
결정종 = %RESULTS%
CALL ANA_CRI_VAR()
IF LOCAL != -1 && GAIN_TALENT_V(결정종, 0) <= CFLAG:마력
	LOCAL = CFLAG:마력
	CFLAG:마력 -= GAIN_TALENT_V(결정종, 0)
	가용마력 -= GAIN_TALENT_V(결정종, 0)
	TALENT:결정종 = 1
	;연성시 구상 호출
	;CALL KOJO_EVENT(110, XX)
	;CALL VOIDLINE_IF(RESULT)
	SKIPDISP 0
	PRINTFORMW %CALLNAME%는[%결정종%]를 취득했습니다
	PRINTFORMW 마력({LOCAL} → {CFLAG:마력})
	IF FLAG:오토모드 > 0 && FLAG:오토모드표시설정 == 1
		SKIPDISP 1
	ENDIF
	LOCAL:2++
	SIF FLAG:디버그
		PRINTFORML (가용 마력:{가용마력})
	GOTO GAIN_TALENT
ENDIF

SIF FLAG:디버그
	PRINTW 

RETURN RESULT

;-------------------------------------------------------------------------
;조교자의 소질을 참조
;-------------------------------------------------------------------------
@GAIN_TALENT_TALENT_T

;조교자가 프라이드높음/낮다
IF TALENT:프라이드높음
	CALL ADD_CRI_VAR("불결무시", -6)
ELSEIF TALENT:프라이드낮음
	CALL ADD_CRI_VAR("불결무시", 6)
ENDIF

;조교자가 무관심/호기심
IF TALENT:무관심
	CALL ADD_CRI_VAR("약학지식", -6)
	CALL ADD_CRI_VAR("금단의지식", -6)
	CALL ADD_CRI_VAR("도구사용", -6)
	CALL ADD_CRI_VAR("촬영기술", -6)
ELSEIF TALENT:호기심
	CALL ADD_CRI_VAR("약학지식", 6)
	CALL ADD_CRI_VAR("금단의지식", 6)
	CALL ADD_CRI_VAR("도구사용", 6)
	CALL ADD_CRI_VAR("촬영기술", 6)
ENDIF

;조교자가 억압/해방
IF TALENT:억압
	CALL ADD_CRI_VAR("새디즘", TALENT:새디즘 ? 6 # -6)
ELSEIF TALENT:해방
	CALL ADD_CRI_VAR("새디즘", TALENT:새디즘 ? -6 # 6)
ENDIF

;조교자가 헌신적
SIF TALENT:헌신적
	CALL ADD_CRI_VAR("불결무시", 3)
	
;-------------------------------------------------------------------------
;조교 대상의 소질을 참조
;-------------------------------------------------------------------------
@GAIN_TALENT_TALENT_M

;조교 대상이 반항적/순수
IF TALENT:MASTER:반항적
	CALL ADD_CRI_VAR("매혹", 6)
	CALL ADD_CRI_VAR("결박능숙", 3)
	CALL ADD_CRI_VAR("새디즘", 3)
ELSEIF TALENT:MASTER:순수
	CALL ADD_CRI_VAR("매혹", -6)
	CALL ADD_CRI_VAR("결박능숙", -3)
	CALL ADD_CRI_VAR("새디즘", -3)
ENDIF

;조교 대상이 무관심/호기심
IF TALENT:MASTER:무관심
	CALL ADD_CRI_VAR("도구사용", -3)
ELSEIF TALENT:MASTER:호기심
	CALL ADD_CRI_VAR("도구사용", 3)
ENDIF

;조교 대상이 감정결핍/감수성
IF TALENT:MASTER:감정결핍
	CALL ADD_CRI_VAR("약학지식", 3)
ELSEIF TALENT:MASTER:감수성
	CALL ADD_CRI_VAR("약학지식", -3)
ENDIF

;조교 대상이튀고싶어함
SIF TALENT:MASTER:튀고싶어함
	CALL ADD_CRI_VAR("촬영기술", 6)

;조교 대상이 정조관념/정조관념없음
IF TALENT:MASTER:정조관념
	CALL ADD_CRI_VAR("매혹", 6)
ELSEIF TALENT:MASTER:정조관념없음
	CALL ADD_CRI_VAR("매혹", -3)
ENDIF

;조교 대상이 억압/해방
IF TALENT:MASTER:억압
	CALL ADD_CRI_VAR("매혹", 3)
ELSEIF TALENT:MASTER:해방
	CALL ADD_CRI_VAR("매혹", -3)
ENDIF

;조교 대상이 수줍음/수치심옅음
IF TALENT:MASTER:수줍음
	CALL ADD_CRI_VAR("촬영기술", 6)
ELSEIF TALENT:MASTER:수치심옅음
	CALL ADD_CRI_VAR("촬영기술", -6)
ENDIF

;조교 대상이 아픔에약함/아픔에강함
IF TALENT:MASTER:아픔에약함
	CALL ADD_CRI_VAR("결박능숙", 6)
	CALL ADD_CRI_VAR("새디즘", 6)
ELSEIF TALENT:MASTER:아픔에강함
	CALL ADD_CRI_VAR("결박능숙", -3)
	CALL ADD_CRI_VAR("새디즘", -3)
ENDIF

;조교 대상이 젖기쉬움/젖기어려움
IF TALENT:MASTER:젖기쉬움
	CALL ADD_CRI_VAR("약학지식", 6)
	CALL ADD_CRI_VAR("혀사용", 3)
ELSEIF TALENT:MASTER:젖기어려움
	CALL ADD_CRI_VAR("약학지식", -6)
	CALL ADD_CRI_VAR("혀사용", -3)
ENDIF

;조교 대상이 약물내성
SIF TALENT:MASTER:약물내성
	CALL ADD_CRI_VAR("약학지식", -9)

;조교 대상이 자위쉬움
SIF TALENT:MASTER:자위쉬움
	CALL ADD_CRI_VAR("결박능숙", 3)

;조교 대상이 쾌감에순수/쾌감을부정
IF TALENT:MASTER:쾌감에순수
	CALL ADD_CRI_VAR("매혹", -3)
ELSEIF TALENT:MASTER:쾌감을부정
	CALL ADD_CRI_VAR("매혹", 3)
ENDIF

;조교 대상이도M
IF TALENT:MASTER:도M
	CALL ADD_CRI_VAR("결박능숙", 3)
	CALL ADD_CRI_VAR("새디즘", 3)
ENDIF

;조교 대상이 도착적
SIF TALENT:MASTER:도착적
	CALL ADD_CRI_VAR("새디즘", 6)

;조교 대상이 회복빠름/회복느림
IF TALENT:MASTER:회복빠름
	CALL ADD_CRI_VAR("약학지식", -6)
ELSEIF TALENT:MASTER:회복느림
	CALL ADD_CRI_VAR("약학지식", 6)
ENDIF

;------------------------------------------
;조교자의 광기
;------------------------------------------
@GAIN_TALENT_INSANE(ARGS)
#DIM LCOUNT
#DIM 기능수
#DIMS 기능명, 15

SIF !TALENT:광기
	RETURN 0

;취득(방폐) 가능 기능을 기능명에 격납한다
SPLIT ARGS, "/", 기능명

;기능수를 카운트
기능수 = FINDELEMENT(기능명, "종단")

FOR LCOUNT, 0, 기능수
	SIF TALENT:(기능명:LCOUNT) == 0 && 기능명:LCOUNT != "새디즘"
		CALL MUL_CRI_VAR(기능명:LCOUNT, -50, 100)
NEXT

;------------------------------------------
;가격에 의한 보정
;------------------------------------------
@GAIN_TALENT_PRICE(ARGS)
#DIM LCOUNT
#DIM 기능수
#DIMS 기능명, 15

;취득(방폐) 가능 기능을 기능명에 격납한다
SPLIT ARGS, "/", 기능명

;기능수를 카운트
기능수 = FINDELEMENT(기능명, "종단")

FOR LCOUNT, 0, 기능수
	LOCAL = GET_CRI_VAR(기능명:LCOUNT + "누계")
	IF LOCAL > 0
		CALL MUL_CRI_VAR(기능명:LCOUNT, 100 * POWER(2, 10 + TALENT:충동적 - TALENT:자제적 + TALENT:튀고싶어함) / GAIN_TALENT_V(기능명:LCOUNT, 0))
	ELSEIF LOCAL < 0
		CALL MUL_CRI_VAR(기능명:LCOUNT, 100 * GAIN_TALENT_V(기능명:LCOUNT, 0) / POWER(2, 10 + TALENT:충동적 - TALENT:자제적 + TALENT:튀고싶어함))
	ENDIF
NEXT

;------------------------------------------
;실행 판정
;------------------------------------------
@GAIN_TALENT_ABLE

;현사양에서는 새디즘 소질을 붙이거나 제외하거나를 반복할 수도 있기 때문에
CALL DIM_CRI_VAR("새디즘", -999)
CALL DIM_CRI_VAR("착의플레이선호", -999)

;------------------------------------------
;사용 가능 마력 판정
;------------------------------------------
@GAIN_TALENT_MP_ABLE()
#FUNCTION
;마력 부족 기준 설정
RETURNF LIMIT(CFLAG:마력 * (8 + MANAGE_MP_PROP() + TALENT:습득빠름 - TALENT:습득느림) * 10 / 100, 0, CFLAG:마력)