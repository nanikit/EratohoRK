;추가 처리 및 서브 이벤트

@EVENTTURNEND
#PRI
#DIM 오토모드카운트
IF FLAG:오토모드 > 0 && FLAG:오토모드표시설정 == 1
	SKIPDISP 1
ENDIF
;최초로 회복 처리를 넣는다
CALL BASE_RECOVERY

;불끈불끈 플래그
FOR LOCAL, 0, CHARANUM
	CFLAG:LOCAL:불끈불끈 += 1
NEXT

;임신 처리
FOR LOCAL, 1, CHARANUM
	CALL PREGNACY_CHECK(LOCAL)
NEXT

FOR LOCAL, 1, CHARANUM
	CALL MASTER_PREGNACY_CHECK(LOCAL)
NEXT
SIF CFLAG:MASTER:임신
	CFLAG:MASTER:임신 ++
CALL MASTER_PREGNACY

;현행판의 거동을 본 한계라면 CFLAG:TARGET:호의 > 3000에서는 문턱이 너무 낮은 느낌인 것로> 5000에. CFLAG:M호의 > 2000도 추가
;한층 더 CFLAG:TARGET:아라이먼트 < 0을 조건으로부터 제외해 본다@/L
SIF TARGET > 0 && !CFLAG:TARGET:노예의목걸이봉인 && CFLAG:TARGET:호의 > 5000 && CFLAG:M호의 > 2000 && TALENT:새디즘 && !ITEM:노예의목걸이
	CALL SLAVE_COLLAR

;일상(비조교시) 이벤트
CALL DAILY_LIFE

;─
;약제의 쿨 다운
;─
IF TARGET > 0
	;로션
	FLAG:로시히트 -= FLAG:로시히트 / (5 - TALENT:약학지식 * 3) + GET_TRAINLV(TARGET) * (1 + ABL:기교) / 3
	SIF FLAG:로시히트 < 0
		FLAG:로시히트 = 0
	;미약
	FLAG:미약히트 -= FLAG:미약히트 / (5 - TALENT:약학지식 * 3) + GET_TRAINLV(TARGET) * (1 + ABL:욕망) / 3
	SIF FLAG:미약히트 < 0
		FLAG:미약히트 = 0
	;영양제
	FLAG:영양히트 -= FLAG:영양히트 / (6 - TALENT:약학지식 * 3) + GET_TRAINLV(TARGET) * (2 + TALENT:치료) / 2
	SIF FLAG:영양히트 < 0
		FLAG:영양히트 = 0
	;치료
	FLAG:치료히트 -= (BASE:체력 / 100) * (5 + GET_TRAINLV(TARGET)) / (10 - TALENT:치료 * 3)
	SIF FLAG:치료히트 < 0
		FLAG:치료히트 = 0
ENDIF
;─
;여러 가지
;─
;CALL EVENTCHECK_M
;FLAG:0 = 0

;일시 갱신
CALL NEXTTIME
IF FLAG:오토모드 > 0
	SKIPDISP 0
	오토모드카운트++
	IF FLAG:오토모드 == 오토모드카운트
		오토모드카운트 = 0
		BEGIN SHOP
	ELSE
		BEGIN TRAIN
	ENDIF
ELSE
	BEGIN SHOP
ENDIF


@PREGNACY_CHECK(ARG)
CFLAG:ARG:질정 = CFLAG:ARG:질정 * MIN(CFLAG:ARG:M호의 + CFLAG:ARG:호의 , 9000) / 10000
SIF TALENT:ARG:임신 || CFLAG:ARG:임신
	CFLAG:ARG:질정 = 0

SIF CFLAG:ARG:질정 > 10000 && !CFLAG:ARG:임신
	CFLAG:ARG:임신 = 1
SIF CFLAG:ARG:임신
	CFLAG:ARG:임신 ++
IF CFLAG:ARG:임신 == 5
		DRAWLINE
		PRINTFORMW %CALLNAME:ARG%의 배가 조금 부풀어 오르고 있다
		PRINTFORMW 아무래도%CALLNAME:MASTER%의 아이를 임신한 것 같다
		CALL KOJO_EVENT(8, 0)
		TALENT:ARG:임신 = 1
		TALENT:ARG:모유체질 = 1
		MAXBASE:ARG:모유 = 10000
ENDIF
IF TALENT:ARG:임신
	IF CFLAG:ARG:임신 == 15
		DRAWLINE
		PRINTFORMW %CALLNAME:ARG%의 배가 많이 부풀어 올라 왔다
		PRINTFORMW 슬슬 태어날 것 같습니다
		CALL KOJO_EVENT(8, 1)
	ELSEIF  CFLAG:ARG:임신 >= 16 + RAND:3
			LOCAL = 0
			DRAWLINE
			CALL KOJO_EVENT(8, 2)
			PRINTFORMW %CALLNAME:ARG%가 해산기있던 것 같습니다.
			PRINTFORMW ………
			PRINTFORMW ……
			PRINTFORMW …
			PRINTFORMW 
			PRINTFORM %CALLNAME:ARG%는%CALLNAME:MASTER%
			PRINTFORM 에 격려받으면서,
		IF RAND:2 == 0
			PRINTFORM 어머니와 닮은 꼴의 사랑스러운 여자 아이
			CFLAG:ARG:아가씨 ++
			CFLAG:MASTER:아가씨 ++
			LOCAL = 0
		ELSE
			PRINTFORM 아버지와 닮은 꼴의 건강한 사내 아이
			CFLAG:ARG:아들 ++
			CFLAG:MASTER:아들 ++
			LOCAL = 1
		ENDIF
		CFLAG:ARG:아이딸린사람 ++
		CFLAG:MASTER:아이딸린사람 ++
		TALENT:ARG:임신 = 0
		TALENT:ARG:모성 = 1
		CFLAG:ARG:임신 = 0
		PRINTFORMW 를 무사히 출산했습니다.
		CALL KOJO_EVENT(8, 3 + LOCAL)
		PRINTFORMW 갓난아기를 포옹했다%CALLNAME:ARG%는, 우리 아이와%CALLNAME:MASTER%에 향하여 미소짓고 있습니다.
	ENDIF
ENDIF

IF !FLAG:임신
	TALENT:ARG:임신 = 0
	TALENT:ARG:모유체질 = 0
	CFLAG:ARG:질정 = 0
	CFLAG:ARG:임신 = 0
ENDIF

;아이딸린사람 플래그는 양쪽 모두의 임신 기능 오프 플래그가 있는 경우만 삭제
IF !FLAG:임신 && FLAG:노예임신 == 0
	CFLAG:ARG:아이딸린사람 = 0
	CFLAG:MASTER:아이딸린사람 = 0
	CFLAG:ARG:아들 = 0
	CFLAG:MASTER:아들 = 0
	CFLAG:ARG:아가씨 = 0
	CFLAG:MASTER:아가씨 = 0
ENDIF

;주인님의 임신 체크
@MASTER_PREGNACY_CHECK(ARG)
SIF !FLAG:노예임신
	RETURN 0
;콘피그로 애정에 의해 임신율 변동을 선택하고 있는 경우
IF FLAG:노예임신 == 1
	CFLAG:ARG:M질정 = CFLAG:ARG:M질정 * MIN(CFLAG:ARG:M호의 + CFLAG:ARG:호의 , 9000) / 10000
;콘피그에 의해 애정에 관계없이 임신을 선택하고 있는 경우
ELSEIF FLAG:노예임신 == 2
	CFLAG:ARG:M질정 = CFLAG:ARG:M질정 * 7 / 10
ENDIF

SIF TALENT:MASTER:임신 || CFLAG:MASTER:임신
	CFLAG:ARG:M질정 = 0

IF RAND:(1 + CFLAG:ARG:M질정 / 10) > RAND:10000 && !CFLAG:MASTER:임신
	;임신 플래그
	CFLAG:MASTER:임신 = 1
	;부친의 캐릭터의 일련 번호를 보존
	CFLAG:MASTER:46 = CFLAG:ARG:91
ENDIF

@MASTER_PREGNACY
;부친 불명의 관계로, 조교자가 노예(주인공)를 임신시켰을 경우의 구상은 아직 쓸 수 없습니다.
;KOJO_EVENT(9)를 테두리로서 준비해 둡니다.
IF CFLAG:MASTER:임신 == 5
	DRAWLINE
	PRINTFORMW %CALLNAME:MASTER%의 배가 조금 부풀어 오르고 있다
	;이 시점에서 부친 불명
	PRINTFORMW 아무래도 임신해 버린 것 같다…
	TALENT:MASTER:임신 = 1
	TALENT:MASTER:모유체질 = 1
	MAXBASE:MASTER:모유 = 10000
ENDIF
IF TALENT:MASTER:임신
	IF CFLAG:MASTER:임신 == 15
		DRAWLINE
		PRINTFORMW %CALLNAME:MASTER%의 배가 많이 부풀어 올라 왔다
		PRINTFORMW 슬슬 태어날 것 같습니다
	ELSEIF CFLAG:MASTER:임신 >= 16 + RAND:3
			;부친 검색
			LOCAL:1 = 0
			FOR LOCAL,1,CHARANUM
				IF CFLAG:MASTER:46 == CFLAG:LOCAL:91
					LOCAL:1 = LOCAL
					BREAK
				ENDIF
			NEXT
			LOCAL = 0
			DRAWLINE
			PRINTFORMW %CALLNAME:MASTER%의 복부에 격렬한 아픔이 달렸다
			PRINTFORMW 마침내 아이가 태어나는 것 같다
			PRINTFORMW ………
			PRINTFORMW ……
			PRINTFORMW …
			PRINTFORMW
			PRINTFORM %CALLNAME:MASTER%는
			IF RAND:2 == 0
				PRINTFORM 사랑스러운 여자 아이
				SIF LOCAL:1 > 0
					CFLAG:(LOCAL:1):아가씨 ++
				CFLAG:MASTER:아가씨 ++
				LOCAL = 0
			ELSE
				PRINTFORM 건강한 사내 아이
				SIF LOCAL:1 > 0
					CFLAG:(LOCAL:1):아들 ++
				CFLAG:MASTER:아들 ++
				LOCAL = 1
			ENDIF
			PRINTFORMW 를 무사히 출산했습니다
			SIF LOCAL:1
				PRINTFORML 태어난 아이는 어딘지 모르게%CALLNAME:(LOCAL:1)%를 닮아 있다…
			LOCAL:2 = TARGET
			TARGET = LOCAL:1
			TARGET = LOCAL:2
			SIF LOCAL:1 > 0
				CFLAG:(LOCAL:1):아이딸린사람 ++
			CFLAG:MASTER:아이딸린사람 ++
			TALENT:MASTER:임신 = 0
			TALENT:MASTER:모성 = 1
			CFLAG:MASTER:임신 = 0
	ENDIF
ENDIF

IF FLAG:노예임신 == 0
	TALENT:MASTER:임신 = 0
	TALENT:MASTER:모유체질 = 0
	CFLAG:MASTER:M질정 = 0
	CFLAG:MASTER:임신 = 0
ENDIF

@SLAVE_COLLAR
DRAWLINE
CALL KOJO_EVENT(7)
CALL VOIDLINE_IF(RESULT)
PRINTFORML 격렬한 조교를 위해 축 늘어지고 있다%CALLNAME:MASTER%에
PRINTFORML %CALLNAME:TARGET%는 스스로의 노예인 것을 나타내는 목걸이를 장착시켰다…
PRINTFORMW 【노예의목걸이】를 손에 넣었습니다.
ITEM:노예의목걸이 = 1
