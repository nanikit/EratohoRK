
;※조교자의 ABLUP 처리는 존재하지 않는 파라미터가 있기 (위해)때문에 군데군데 타협하고 있다

;─
;능력 상승 처리
;─
;-------------------------------------------------
;순종 ABL
;-------------------------------------------------
@ABLUP_0M
#DIM 굴복난이도
#DIM 굴복EXP
#DIM 굴복보정
#DIM 굴복달성
#DIM 공포난이도
#DIM 공포EXP
#DIM 공포보정
#DIM 공포달성

;순종, 굴복 루트
;굴복 난이도 설정
굴복난이도 = POWER(2, ABL:MASTER:순종) * (100 - TALENT:MASTER:프라이드낮음 * 20 + TALENT:MASTER:프라이드높음 * 20)
;굴복 EXP 취득
굴복EXP = EXP:MASTER:굴복경험
;굴복각인이 부족하면 EXP 보정을 줄인다(1 Lv에 대해 1/2)
LOCAL = (MARK:굴복각인 >= 10) ? 0 # (ABL:MASTER:순종 * 2 - 1 - MARK:굴복각인)
굴복보정 = POWER(2, LOCAL < 0 ? 0 # LOCAL)
;굴복 달성 판정
LOCAL = CUP:MASTER:굴복 + CUP:MASTER:공순
굴복달성 = (LOCAL / 2 + 굴복EXP * 20 / 굴복보정) > (굴복난이도 * ABLUP_DIFFICULTY(0))

;공포 루트
;공포 난이도 설정
공포난이도 = POWER(2, ABL:MASTER:순종) * (100 - TALENT:MASTER:겁쟁이 * 20 + TALENT:MASTER:기장 * 20)
;공포 EXP 취득
공포EXP = EXP:MASTER:공포경험
;트라우마가 부족하면 EXP 보정을 줄인다(1 Lv에 대해 1/2)
LOCAL = (MARK:트라우마 >= 10) ? 0 # (ABL:MASTER:순종 * 2 - 1 - MARK:트라우마)
공포보정 = POWER(2, LOCAL < 0 ? 0 # LOCAL)
;공포 달성 판정
공포달성 = (CUP:MASTER:공포 + 공포EXP * 20 / 공포보정) > (공포난이도 * ABLUP_DIFFICULTY(0))

;굴복계·공포계로 조건을 채우면 ABLUP
SIF 굴복달성 || 공포달성
	CALL ABLUP_RISE(MASTER, 0)


;-------------------------------------------------
;욕망 ABL
;-------------------------------------------------
@ABLUP_1M
CALL ABLUP_1(MASTER)
@ABLUP_1T
CALL ABLUP_1(TARGET)

@ABLUP_1(ARG)
#DIM 욕망난이도
#DIM 욕망EXP
#DIM 욕망보정

;욕망 난이도 설정
욕망난이도 = POWER(2, ABL:ARG:욕망) * (100 - TALENT:ARG:쾌감에순수 * 20 + TALENT:ARG:쾌감을부정 * 20)
;욕망 EXP 취득(고도의 EXP(정도)만큼 보정치가 크다)
욕망EXP = EXP:ARG:C경험 + EXP:ARG:B경험 + EXP:ARG:성지식
욕망EXP += EXP:ARG:V경험 * 2 + EXP:ARG:A경험 * 2 + EXP:ARG:절정경험 * 2 + EXP:ARG:사정경험 * 2
욕망EXP += EXP:ARG:자위경험 * 3 + EXP:ARG:분유경험 * 4 + EXP:ARG:이상경험 * 4 + EXP:ARG:V확장경험 * 5 + EXP:ARG:A확장경험 * 5
;조교 대상
IF ARG == MASTER
	;쾌락각인이 부족하면 EXP 보정을 줄인다(1 Lv에 대해 1/2)
	LOCAL = (MARK:쾌락각인 >= 10) ? 0 # (ABL:ARG:욕망 * 2 - 1 - MARK:쾌락각인)
;조교자(타협)
ELSE
	;조교레벨이 부족하면 EXP 보정을 줄인다(1 Lv에 대해 1/2)
	LOCAL = (CFLAG:MASTER:조교레벨 >= 10) ? 0 # (ABL:ARG:욕망 * 2 - 1 - CFLAG:MASTER:조교레벨)
ENDIF
욕망보정 = POWER(2, LOCAL < 0 ? 0 # LOCAL)

;욕망 달성 판정
;조교 대상
IF ARG == MASTER
	SIF (CUP:ARG:욕정 + 욕망EXP * 10 / 욕망보정) > (욕망난이도 * ABLUP_DIFFICULTY(1))
		CALL ABLUP_RISE(MASTER, 1)
;조교자(타협)
ELSE
	SIF (CUP:ARG:쾌C + CUP:ARG:쾌V + CUP:ARG:쾌A + CUP:ARG:쾌B + 욕망EXP * 10 / 욕망보정) > (욕망난이도 * ABLUP_DIFFICULTY(1))
		CALL ABLUP_RISE(TARGET, 1)
ENDIF


;-------------------------------------------------
;기교 ABL
;-------------------------------------------------
@ABLUP_2M
CALL ABLUP_2(MASTER)
@ABLUP_2T
CALL ABLUP_2(TARGET)

@ABLUP_2(ARG)
#DIM 기교난이도
#DIM 기교EXP
#DIM 기교보정
#DIM 상대

;봉사 상대를 대입
IF ARG == MASTER
	상대 = TARGET
ELSE
	상대 = MASTER
ENDIF

;기교 난이도 설정
기교난이도 = POWER(2, ABL:ARG:기교) * (100 - TALENT:ARG:습득빠름 * 20 + TALENT:ARG:습득느림 * 20)
;기교 EXP 취득
기교EXP = EXP:ARG:성지식 + EXP:ARG:구음경험 * 2 + EXP:ARG:수음경험 * 2 + EXP:ARG:성교경험 * 2 + EXP:ARG:가게한경험 * 4
;조교레벨이 부족하면 EXP 보정을 줄인다(1 Lv에 대해 1/2)
LOCAL = (CFLAG:ARG:조교레벨 >= 10) ? 0 # (ABL:ARG:기교 * 2 - 1 - CFLAG:ARG:조교레벨)
기교보정 = POWER(2, LOCAL < 0 ? 0 # LOCAL)

;기교 달성 판정
LOCAL = CUP:상대:쾌C + CUP:상대:쾌V + CUP:상대:쾌A + CUP:상대:쾌B
SIF (LOCAL / 3 + 기교EXP * 10 / 기교보정) > (기교난이도 * ABLUP_DIFFICULTY(2))
	CALL ABLUP_RISE(ARG, 2)


;-------------------------------------------------
;X감각 ABL
;-------------------------------------------------
@ABLUP_3M
CALL ABLUP_SENSITIVITY(MASTER, 3)
@ABLUP_4M
CALL ABLUP_SENSITIVITY(MASTER, 4)
@ABLUP_5M
CALL ABLUP_SENSITIVITY(MASTER, 5)
@ABLUP_6M
CALL ABLUP_SENSITIVITY(MASTER, 6)
@ABLUP_3T
CALL ABLUP_SENSITIVITY(TARGET, 3)
@ABLUP_4T
CALL ABLUP_SENSITIVITY(TARGET, 4)
@ABLUP_5T
CALL ABLUP_SENSITIVITY(TARGET, 5)
@ABLUP_6T
CALL ABLUP_SENSITIVITY(TARGET, 6)

@ABLUP_SENSITIVITY(대상, ARG)
#DIM 부위
#DIM 감각난이도
#DIM 감각EXP
#DIM 감각보정
#DIM 대상

부위 = ARG - 3
SPLIT "C/V/A/B", "/", LOCALS

;감각 난이도 설정
감각난이도 = POWER(2, ABL:대상:@"%LOCALS:부위%감각") * (100 - TALENT:대상:@"%LOCALS:부위%민감" * 20 + TALENT:대상:@"%LOCALS:부위%둔감" * 20)
;감각 EXP 취득
감각EXP = EXP:대상:@"%LOCALS:부위%경험" * 2 + EXP:대상:절정경험
SIF LOCALS:부위 == "C"
	감각EXP += EXP:대상:절정경험
SIF LOCALS:부위 == "V"
	감각EXP += EXP:대상:V확장경험 * 4 + EXP:대상:성교경험 * 2
SIF LOCALS:부위 == "A"
	감각EXP += EXP:대상:A확장경험 * 4 + EXP:대상:성교경험 + EXP:대상:이상경험
SIF LOCALS:부위 == "B"
	감각EXP += EXP:대상:분유경험 * 3
;조교 대상
IF ARG == MASTER
	;쾌락각인이 부족하면 EXP 보정을 줄인다(1 Lv에 대해 1/2)
	LOCAL = (MARK:쾌락각인 >= 10) ? 0 # (ABL:대상:@"%LOCALS:부위%감각" * 2 - 1 - MARK:쾌락각인)
;조교자(타협)
ELSE
	;욕망 ABL가 부족하면 EXP 보정을 줄인다(1 Lv에 대해 1/2)
	LOCAL = (ABL:대상:욕망 >= 10) ? 0 # (ABL:대상:@"%LOCALS:부위%감각" * 2 - 1 - ABL:대상:욕망)
ENDIF
감각보정 = POWER(2, LOCAL < 0 ? 0 # LOCAL)

;감각 달성 판정
SIF (CUP:대상:@"쾌%LOCALS:부위%" + 감각EXP * 10 / 감각보정) > (감각난이도 * ABLUP_DIFFICULTY(ARG) * 4)
	CALL ABLUP_RISE(대상, ARG)


;-------------------------------------------------
;봉사정신 ABL
;-------------------------------------------------
@ABLUP_7M
CALL ABLUP_7(MASTER)
@ABLUP_7T
CALL ABLUP_7(TARGET)

@ABLUP_7(ARG)
#DIM 봉사정신난이도
#DIM 봉사정신EXP
#DIM 봉사정신보정

;봉사정신 난이도 설정
봉사정신난이도 = POWER(2, ABL:ARG:봉사정신) * (100 - TALENT:ARG:헌신적 * 20 + TALENT:ARG:수동 * 20)
;봉사정신 EXP 취득
봉사정신EXP = EXP:ARG:구음경험 + EXP:ARG:수음경험 + EXP:ARG:봉사쾌락경험 * 4
;조교레벨이 부족하면 EXP 보정을 줄인다(1 Lv에 대해 1/2)
LOCAL = (CFLAG:ARG:조교레벨 >= 10) ? 0 # (ABL:ARG:봉사정신 * 2 - 1 - CFLAG:ARG:조교레벨)
봉사정신보정 = POWER(2, LOCAL < 0 ? 0 # LOCAL)

;봉사정신 달성 판정
;조교 대상
IF ARG == MASTER
	SIF (CUP:ARG:공순 + 봉사정신EXP * 10 / 봉사정신보정) > (봉사정신난이도 * ABLUP_DIFFICULTY(7))
		CALL ABLUP_RISE(MASTER, 7)
;조교자(타협)
ELSE
	SIF (SOURCE:MASTER:달성 / 3 + 봉사정신EXP * 10 / 봉사정신보정) > (봉사정신난이도 * ABLUP_DIFFICULTY(7))
		CALL ABLUP_RISE(TARGET, 7)
ENDIF


;-------------------------------------------------
;노출버릇 ABL
;-------------------------------------------------
@ABLUP_8M
CALL ABLUP_8(MASTER)
@ABLUP_8T
;TARGET에는 노출버릇 ABL 상승이 없다(타협)

@ABLUP_8(ARG)
#DIM 노출버릇난이도
#DIM 노출버릇EXP

;노출버릇 난이도 설정
노출버릇난이도 = POWER(2, ABL:ARG:노출버릇) * (100 - TALENT:ARG:해방 * 20 + TALENT:ARG:억압 * 20)
;노출버릇 EXP 취득
노출버릇EXP = EXP:ARG:노출경험 * 3 + EXP:ARG:절정경험

;노출버릇 달성 판정
SIF (CUP:ARG:치정 + 노출버릇EXP * 10) > (노출버릇난이도 * ABLUP_DIFFICULTY(8))
	CALL ABLUP_RISE(ARG, 8)


;-------------------------------------------------
;레즈끼 ABL
;-------------------------------------------------
@ABLUP_9M
CALL ABLUP_9(MASTER)
@ABLUP_9T
CALL ABLUP_9(TARGET)

@ABLUP_9(ARG)
#DIM 레즈기분난이도
#DIM 레즈끼EXP

;레즈 기분 난이도 설정
레즈기분난이도 = POWER(2, ABL:ARG:레즈끼) * (100 - TALENT:ARG:양도 * 20)
;레즈끼 EXP 취득
레즈끼EXP = EXP:ARG:백합경험

;레즈 기분 달성 판정
SIF (SOURCE:ARG:사랑 + SOURCE:ARG:열락 + 레즈끼EXP * 20) > (레즈기분난이도 * ABLUP_DIFFICULTY(9))
	CALL ABLUP_RISE(ARG, 9)


;-------------------------------------------------
;ＢＬ끼 ABL
;-------------------------------------------------
@ABLUP_10M
CALL ABLUP_10(MASTER)
@ABLUP_10T
CALL ABLUP_10(TARGET)

@ABLUP_10(ARG)
#DIM ＢＬ끼난이도
#DIM ＢＬ끼EXP

;ＢＬ끼 난이도 설정
ＢＬ끼난이도 = POWER(2, ABL:ARG:ＢＬ끼) * (100 - TALENT:ARG:양도 * 20)
;ＢＬ끼 EXP 취득
ＢＬ끼EXP = EXP:ARG:BL경험

;ＢＬ끼 달성 판정
SIF (SOURCE:ARG:사랑 + SOURCE:ARG:열락 + ＢＬ끼EXP * 20) > (ＢＬ끼난이도 * ABLUP_DIFFICULTY(10))
	CALL ABLUP_RISE(ARG, 10)


;-------------------------------------------------
;마조끼 ABL
;-------------------------------------------------
@ABLUP_11M
#DIM 마조끼난이도
#DIM 마조끼EXP

;마조끼 난이도 설정
마조끼난이도 = POWER(2, ABL:MASTER:마조끼) * (100 - TALENT:MASTER:도착적 * 20)
;마조끼 EXP 취득
마조끼EXP = EXP:MASTER:고통쾌락경험 * 4 + EXP:MASTER:긴박경험 * 2 + EXP:MASTER:이상경험

;마조끼 달성 판정
SIF (CUP:MASTER:고통 + CUP:MASTER:불쾌 - CUP:MASTER:억울 + 마조끼EXP * 5) > (마조끼난이도 * ABLUP_DIFFICULTY(11))
	CALL ABLUP_RISE(MASTER, 11)


;-------------------------------------------------
;자위중독 ABL
;-------------------------------------------------
@ABLUP_12M
#DIM 자위중독난이도
#DIM 자위중독EXP
#DIM 자위중독보정

;자위중독 난이도 설정
자위중독난이도 = POWER(2, ABL:MASTER:자위중독) * (100 - TALENT:MASTER:자위쉬움 * 20)
;자위중독 EXP 취득
자위중독EXP = EXP:MASTER:자위경험 + EXP:MASTER:조교자위경험
;조교레벨이 부족하면 EXP 보정을 줄인다(1 Lv에 대해 1/2)
LOCAL = ((ABL:MASTER:욕망 + ABL:MASTER:노출버릇) >= 20) ? 0 # (ABL:MASTER:자위중독 * 2 - 1 - (ABL:MASTER:욕망 + ABL:MASTER:노출버릇))
자위중독보정 = POWER(2, LOCAL < 0 ? 0 # LOCAL)

;자위중독 달성 판정
SIF (CUP:MASTER:쾌C + CUP:MASTER:쾌V + CUP:MASTER:쾌A + CUP:MASTER:쾌B + 자위중독EXP * 10 / 자위중독보정) > (자위중독난이도 * ABLUP_DIFFICULTY(12))
	CALL ABLUP_RISE(MASTER, 12)


;-------------------------------------------------
;정액중독 ABL
;-------------------------------------------------
@ABLUP_13M
CALL ABLUP_13(MASTER)
@ABLUP_13T
CALL ABLUP_13(TARGET)

@ABLUP_13(ARG)
#DIM 정액중독난이도
#DIM 정액중독EXP
#DIM 정액중독보정

;정액중독 난이도 설정
정액중독난이도 = POWER(2, ABL:ARG:정액중독) * 100
;정액중독 EXP 취득
정액중독EXP = EXP:ARG:정액경험
;욕망 ABL가 부족하면 EXP 보정을 줄인다(1 Lv에 대해 1/2)
LOCAL = (ABL:ARG:욕망 >= 10) ? 0 # (ABL:ARG:정액중독 - 1 - ABL:ARG:욕망)
정액중독보정 = POWER(2, LOCAL < 0 ? 0 # LOCAL)

;정액중독 달성 판정
SIF (정액중독EXP * 40 / 정액중독보정) > (정액중독난이도 * ABLUP_DIFFICULTY(13))
	CALL ABLUP_RISE(ARG, 13)


;-------------------------------------------------
;레즈중독 ABL
;-------------------------------------------------
@ABLUP_14M
CALL ABLUP_14(MASTER)
@ABLUP_14T
CALL ABLUP_14(TARGET)

@ABLUP_14(ARG)
#DIM 레즈중독난이도
#DIM 레즈중독EXP
#DIM 레즈중독보정

;레즈중독 난이도 설정
레즈중독난이도 = POWER(2, ABL:ARG:레즈중독) * (100 - TALENT:ARG:양도 * 20)
;레즈중독 EXP 취득
레즈중독EXP = EXP:ARG:백합경험
;조교레벨이 부족하면 EXP 보정을 줄인다(1 Lv에 대해 1/2)
LOCAL = (ABL:ARG:레즈끼 >= 10) ? 0 # (ABL:ARG:레즈중독 - 1 - ABL:ARG:레즈끼)
레즈중독보정 = POWER(2, LOCAL < 0 ? 0 # LOCAL)

;레즈중독 달성 판정
;조교 대상
IF ARG == MASTER
	SIF (CFLAG:M호의 / 2 + 레즈중독EXP * 20 / 레즈중독보정) > (레즈중독난이도 * ABLUP_DIFFICULTY(14))
		CALL ABLUP_RISE(MASTER, 14)
;조교자
ELSE
	SIF (CFLAG:호의 / 2 + 레즈중독EXP * 20 / 레즈중독보정) > (레즈중독난이도 * ABLUP_DIFFICULTY(14))
		CALL ABLUP_RISE(TARGET, 14)
ENDIF


;-------------------------------------------------
;고통중독 ABL
;-------------------------------------------------
@ABLUP_15M
#DIM 고통중독난이도
#DIM 고통중독EXP
#DIM 고통중독보정

;고통중독 난이도 설정
고통중독난이도 = POWER(2, ABL:MASTER:고통중독) * (100 - TALENT:MASTER:드M * 20)
;고통중독 EXP 취득
고통중독EXP = EXP:MASTER:고통쾌락경험 * 2 + EXP:MASTER:이상경험
;조교레벨이 부족하면 EXP 보정을 줄인다(1 Lv에 대해 1/2)
LOCAL = ((ABL:MASTER:욕망 + ABL:MASTER:마조끼) >= 20) ? 0 # (ABL:MASTER:고통중독 * 2 - 1 - (ABL:MASTER:욕망 + ABL:MASTER:마조끼))
고통중독보정 = POWER(2, LOCAL < 0 ? 0 # LOCAL)

;고통중독 달성 판정
SIF (CUP:MASTER:고통 + 고통중독EXP * 10 / 고통중독보정) > (고통중독난이도 * ABLUP_DIFFICULTY(15))
	CALL ABLUP_RISE(MASTER, 15)


;-------------------------------------------------
;구속중독 ABL
;-------------------------------------------------
@ABLUP_16M
#DIM 구속중독난이도
#DIM 구속중독EXP
#DIM 구속중독보정

;구속중독 난이도 설정
구속중독난이도 = POWER(2, ABL:MASTER:구속중독) * (100 - TALENT:MASTER:드M * 20)
;구속중독 EXP 취득
구속중독EXP = EXP:MASTER:긴박경험 * 2 + EXP:MASTER:이상경험
;조교레벨이 부족하면 EXP 보정을 줄인다(1 Lv에 대해 1/2)
LOCAL = ((ABL:MASTER:순종 + ABL:MASTER:마조끼) >= 20) ? 0 # (ABL:MASTER:구속중독 * 2 - 1 - (ABL:MASTER:순종 + ABL:MASTER:마조끼))
구속중독보정 = POWER(2, LOCAL < 0 ? 0 # LOCAL)

;구속중독 달성 판정
SIF (CUP:MASTER:불쾌 + 구속중독EXP * 10 / 구속중독보정) > (구속중독난이도 * ABLUP_DIFFICULTY(16))
	CALL ABLUP_RISE(MASTER, 16)


;─
;조교 능력의 상승
;─
;-------------------------------------------------
;회화 ABL
;-------------------------------------------------
@ABLUP_20T
#DIM 회화난이도
#DIM 회화EXP
#DIM 회화보정

;회화 난이도 설정
회화난이도 = POWER(2, ABL:회화) * (100 - TALENT:습득빠름 * 20 + TALENT:습득느림 * 20)
;회화 EXP 취득
회화EXP = EXP:회화경험
;조교레벨이 부족하면 EXP 보정을 줄인다(1 Lv에 대해 1/2)
LOCAL = (CFLAG:조교레벨 >= 20) ? 0 # (ABL:회화 * 2 - 1 - CFLAG:조교레벨)
회화보정 = POWER(2, LOCAL < 0 ? 0 # LOCAL)

;회화 달성 판정
LOCAL = CUP:MASTER:굴복 + CUP:MASTER:공순 * 2 + CUP:MASTER:공포 - CUP:MASTER:반항
SIF (LOCAL / 2 + 회화EXP * 20 / 회화보정) > (회화난이도 * ABLUP_DIFFICULTY(20))
	CALL ABLUP_RISE(TARGET, 20)


;-------------------------------------------------
;애무 ABL
;-------------------------------------------------
@ABLUP_21T
#DIM 애무난이도
#DIM 애무EXP
#DIM 애무보정

;애무 난이도 설정
애무난이도 = POWER(2, ABL:애무) * (100 - TALENT:습득빠름 * 20 + TALENT:습득느림 * 20)
;애무 EXP 취득
애무EXP = EXP:수음경험
;조교레벨이 부족하면 EXP 보정을 줄인다(1 Lv에 대해 1/2)
LOCAL = (CFLAG:조교레벨 >= 20) ? 0 # (ABL:애무 * 2 - 1 - CFLAG:조교레벨)
애무보정 = POWER(2, LOCAL < 0 ? 0 # LOCAL)

;애무 달성 판정
LOCAL = CUP:MASTER:쾌C + CUP:MASTER:쾌V + CUP:MASTER:쾌A + CUP:MASTER:쾌B - CUP:MASTER:억울
SIF (LOCAL / 3 + 애무EXP * 20 / 애무보정) > (애무난이도 * ABLUP_DIFFICULTY(21))
	CALL ABLUP_RISE(TARGET, 21)


;-------------------------------------------------
;도구 ABL
;-------------------------------------------------
@ABLUP_22T
#DIM 도구난이도
#DIM 도구EXP
#DIM 도구보정

;도구 난이도 설정
도구난이도 = POWER(2, ABL:도구) * (100 - TALENT:습득빠름 * 20 + TALENT:습득느림 * 20)
;도구 EXP 취득
도구EXP = EXP:도구사용경험
;조교레벨이 부족하면 EXP 보정을 줄인다(1 Lv에 대해 1/2)
LOCAL = (CFLAG:조교레벨 >= 20) ? 0 # (ABL:도구 * 2 - 1 - CFLAG:조교레벨)
도구보정 = POWER(2, LOCAL < 0 ? 0 # LOCAL)

;도구 달성 판정
SIF (CUP:MASTER:쾌C + CUP:MASTER:쾌V + CUP:MASTER:쾌A + CUP:MASTER:쾌B - CUP:MASTER:불쾌 + 도구EXP * 20 / 도구보정) > (도구난이도 * ABLUP_DIFFICULTY(22))
	CALL ABLUP_RISE(TARGET, 22)


;-------------------------------------------------
;성교 ABL
;-------------------------------------------------
@ABLUP_23T
#DIM 성교난이도
#DIM 성교EXP
#DIM 성교보정

;성교 난이도 설정
성교난이도 = POWER(2, ABL:성교) * (100 - TALENT:습득빠름 * 20 + TALENT:습득느림 * 20)
;성교 EXP 취득
성교EXP = EXP:성교경험
;조교레벨이 부족하면 EXP 보정을 줄인다(1 Lv에 대해 1/2)
LOCAL = (CFLAG:조교레벨 >= 20) ? 0 # (ABL:성교 * 2 - 1 - CFLAG:조교레벨)
성교보정 = POWER(2, LOCAL < 0 ? 0 # LOCAL)

;성교 달성 판정
LOCAL = CUP:MASTER:쾌C + CUP:MASTER:쾌V + CUP:MASTER:쾌A + CUP:MASTER:욕정 + CUP:MASTER:공순 - CUP:MASTER:공포 - CUP:MASTER:억울
SIF (LOCAL / 3 + 성교EXP * 20 / 성교보정) > (성교난이도 * ABLUP_DIFFICULTY(23))
	CALL ABLUP_RISE(TARGET, 23)


;-------------------------------------------------
;수치 ABL
;『부끄러워하게 하는 능력』
;-------------------------------------------------
@ABLUP_24T
#DIM 수치난이도
#DIM 수치EXP
#DIM 수치보정

;수치 난이도 설정
수치난이도 = POWER(2, ABL:수치) * (100 - TALENT:습득빠름 * 20 + TALENT:습득느림 * 20)
;수치 EXP 취득
수치EXP = EXP:MASTER:노출경험 + EXP:MASTER:치욕쾌락경험 * 2
;조교레벨이 부족하면 EXP 보정을 줄인다(1 Lv에 대해 1/2)
LOCAL = (CFLAG:조교레벨 >= 20) ? 0 # (ABL:수치 * 2 - 1 - CFLAG:조교레벨)
수치보정 = POWER(2, LOCAL < 0 ? 0 # LOCAL)

;수치 달성 판정
SIF (CUP:MASTER:욕정 + CUP:MASTER:치정 - CUP:MASTER:억울 + 수치EXP * 20 / 수치보정) > (수치난이도 * ABLUP_DIFFICULTY(24))
	CALL ABLUP_RISE(TARGET, 24)


;-------------------------------------------------
;봉사 ABL
;『봉사시키는 능력』
;-------------------------------------------------
@ABLUP_25T
#DIM 봉사난이도
#DIM 봉사EXP
#DIM 봉사보정

;봉사 난이도 설정
봉사난이도 = POWER(2, ABL:봉사) * (100 - TALENT:습득빠름 * 20 + TALENT:습득느림 * 20)
;봉사 EXP 취득
봉사EXP = EXP:MASTER:구음경험 + EXP:MASTER:수음경험 + EXP:MASTER:봉사쾌락경험 * 2 + EXP:MASTER:가게한경험 * 3 + EXP:MASTER:애정경험 * 4
;조교레벨이 부족하면 EXP 보정을 줄인다(1 Lv에 대해 1/2)
LOCAL = (CFLAG:조교레벨 >= 20) ? 0 # (ABL:봉사 * 2 - 1 - CFLAG:조교레벨)
봉사보정 = POWER(2, LOCAL < 0 ? 0 # LOCAL)

;봉사 달성 판정
SIF (CUP:MASTER:굴복 + CUP:MASTER:공순 - CUP:MASTER:반항 + 봉사EXP * 10 / 봉사보정) > (봉사난이도 * ABLUP_DIFFICULTY(25))
	CALL ABLUP_RISE(TARGET, 25)


;-------------------------------------------------
;가학 ABL
;-------------------------------------------------
@ABLUP_26T
#DIM 가학난이도
#DIM 가학EXP
#DIM 가학보정

;가학 난이도 설정
가학난이도 = POWER(2, ABL:가학) * (100 - TALENT:습득빠름 * 20 + TALENT:습득느림 * 20)
;가학 EXP 취득
가학EXP = EXP:MASTER:긴박경험 + EXP:MASTER:고통쾌락경험 * 2 + EXP:MASTER:V확장경험 * 4 + EXP:MASTER:A확장경험 * 4
;조교레벨이 부족하면 EXP 보정을 줄인다(1 Lv에 대해 1/2)
LOCAL = (CFLAG:조교레벨 >= 20) ? 0 # (ABL:가학 * 2 - 1 - CFLAG:조교레벨)
가학보정 = POWER(2, LOCAL < 0 ? 0 # LOCAL)

;가학 달성 판정
SIF (CUP:MASTER:고통 + CUP:MASTER:불쾌 - CUP:MASTER:반항 + 가학EXP * 10 / 가학보정) > (가학난이도 * ABLUP_DIFFICULTY(26))
	CALL ABLUP_RISE(TARGET, 26)


;-------------------------------------------------
;이상 ABL
;-------------------------------------------------
@ABLUP_27T
#DIM 이상난이도
#DIM 이상EXP
#DIM 이상보정

;이상 난이도 설정
이상난이도 = POWER(2, ABL:이상) * (100 - TALENT:습득빠름 * 20 + TALENT:습득느림 * 20)
;이상 EXP 취득
이상EXP = EXP:MASTER:이상경험
;조교레벨이 부족하면 EXP 보정을 줄인다(1 Lv에 대해 1/2)
LOCAL = (CFLAG:조교레벨 >= 20) ? 0 # (ABL:이상 * 2 - 1 - CFLAG:조교레벨)
이상보정 = POWER(2, LOCAL < 0 ? 0 # LOCAL)

;이상 달성 판정
SIF (SOURCE:MASTER:일탈 + 이상EXP * 20 / 이상보정) > (이상난이도 * ABLUP_DIFFICULTY(27))
	CALL ABLUP_RISE(TARGET, 27)


;-------------------------------------------------
;사역 ABL
;-------------------------------------------------
@ABLUP_28T
;사역 ABL는 상승하지 않는다(잠정)


@ABLUP_RISE(대상, ARG)
#DIM 대상
ABL:대상:ARG++
IF 대상 == MASTER
	CALL KOJO_EVENT(23, ARG)
ELSEIF 대상 == TARGET
	CALL KOJO_EVENT(24, ARG)
ENDIF
PRINTFORMW %CALLNAME:대상%의%ABLNAME:ARG%\@ 20 <= ARG && ARG <= 29 ? 스킬# \@가\@ ABL:대상:ARG > 9 ? {ABL:대상:ARG} # %TOFULL(TOSTR(ABL:대상:ARG))% \@가 되었다

@ABLUP_DIFFICULTY(ARG)
#FUNCTION
RETURNF 6
