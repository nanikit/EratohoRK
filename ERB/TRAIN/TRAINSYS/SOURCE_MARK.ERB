
;─
;각인 취득의 체크, 그리고 각인에 의한 소질 변동(조건 설정은 꽤 적당… 조정이 필요라고 생각한다면 연락해 주세요)
;─
@MARK_CHECK_SOURCE
#DIM LTEMP, 5

;각인은 1회에 1단계까지 오르는 사양입니다
VARSET LOCAL
VARSET LTEMP

;각인:고통각인
;고통이 규정량을 돌파하면 각인 취득
SIF CUP:MASTER:고통 > (400 * (MARK:MASTER:고통각인 + 1))
	LOCAL++
;10 LV가 상한
SIF LOCAL && (MARK:MASTER:고통각인 < 10)
	LTEMP:0 = ++MARK:MASTER:고통각인

;LOCAL:0 초기화
LOCAL = 0

;각인:쾌락각인 지금의 사양에서는 쾌락각인이 와작와작 오르므로 조정  요점 검토
;굴복각인보다 먼저는 오르지 않는다(굴복각인 이하의 레벨이 된다)
;쾌감이 규정량을 돌파하면 각인 취득
SIF (CUP:MASTER:쾌C + CUP:MASTER:쾌V + CUP:MASTER:쾌A + CUP:MASTER:쾌B) > (800 * (MARK:MASTER:쾌락각인 + 1))
	LOCAL ++
;굴복각인 LV를 넘지 않는다. 또 10 LV가 상한
SIF LOCAL && (MARK:MASTER:쾌락각인 < MARK:굴복각인) && (MARK:MASTER:쾌락각인 < 10)
	LTEMP:1 = ++MARK:MASTER:쾌락각인

;굴복각인취득
CALL MARK_CHECK_SOURCE_SURRENDER
LTEMP:2 = RESULT

;각인:반항각인
;(이번 조교 대상의 반응이 온순하면 판정하지 않는다)
IF !IS_COMGRONAME("소극적 따른다") && !IS_COMGRONAME("태연하게 따른다/적극적 따른다/받아들인다")
	LOCAL = CUP:MASTER:반항 > (MARK:반항각인 + 1) * 400
	;조교자의 평시 아라이먼트가 충분히 높면(S튼) 각인 취득을 중단
	SIF MARK:반항각인 > 10 - MARK:5
		LOCAL = 0
	;10-굴복+고통/2을 넘지 않는다
	SIF LOCAL && (MARK:반항각인 < 10-((MARK:굴복각인+MARK:MASTER:고통각인)/2))
		LTEMP:3 = ++MARK:반항각인
ENDIF

;각인:트라우마
LOCAL = CUP:MASTER:공포 > (MARK:MASTER:트라우마 + 1) * 400
;10 LV가 상한
IF LOCAL && (MARK:MASTER:트라우마 < 10)
	LTEMP:4 = ++MARK:MASTER:트라우마
	;이번 조교자 행동을 트라우마의 원으로서 덧쓰기 기억
	CFLAG:MASTER:트라우마 = TFLAG:ACT
ENDIF

CALL MARK_CHECK_SOURCE_MESSAGE(LTEMP:0, LTEMP:1, LTEMP:2, LTEMP:3, LTEMP:4)

;─
;각인 취득 구상과 표시
;─
@MARK_CHECK_SOURCE_MESSAGE(ARG:0, ARG:1, ARG:2, ARG:3, ARG:4)
;각인 취득시 개행
SIF ARG:0 || ARG:1 || ARG:2 || ARG:3 || ARG:4
	PRINTL
;각인미취득시라도 각인 취득시 구상은 부른다
RESULT = 0
CALL KOJO_MARK(ARG:0, ARG:1, ARG:2, ARG:3, ARG:4)

FOR LOCAL, 0, 5
	;각인 취득을 개별적으로 확인
	IF ARG:LOCAL
		;각인 취득 표시
		PRINTFORML %MARKNAME:LOCAL%는{ARG:LOCAL}가 되었다
		;굴복각인취득 때, 굴복P2가 굴복각인의 제곱에 응해 상승
		SIF LOCAL == 2
			CFLAG:MASTER:굴복P2 += POWER(LOCAL:LOCAL, 2) - POWER(LOCAL:LOCAL - 1, 2)
		;고통각인 Lv9 취득시, 기장을 잃는다
		IF LOCAL == 0 && ARG:0 == 9 && TALENT:MASTER:기장
			TALENT:MASTER:기장 = 0
			PRINTFORMW 그리고%CALLNAME:MASTER%는[기장]을 잃었다
		;쾌락각인 Lv5 취득시, 쾌감을부정을 잃는다
		ELSEIF LOCAL == 1 && ARG:1 == 5 && TALENT:MASTER:쾌감을부정
			TALENT:MASTER:쾌감을부정 = 0
			PRINTFORMW 그리고%CALLNAME:MASTER%는[쾌감을부정]을 잃었다
		;쾌락각인 Lv7 취득시, 억압을 잃는다
		ELSEIF LOCAL == 1 && ARG:1 == 7 && TALENT:MASTER:억압
			TALENT:MASTER:억압 = 0
			PRINTFORMW 그리고%CALLNAME:MASTER%는[억압]을 잃었다
		;쾌락각인 Lv10 취득시, 쾌감에순수를 얻는다
		ELSEIF LOCAL == 1 && ARG:1 == 10 && !TALENT:MASTER:쾌감에순수
			TALENT:MASTER:쾌감에순수 = 1
			PRINTFORMW 그리고%CALLNAME:MASTER%는[쾌감에순수]를 얻었다
		;굴복각인 Lv7 취득시, 반항적을 잃는다
		ELSEIF LOCAL == 2 && ARG:2 == 7 && TALENT:MASTER:반항적
			TALENT:MASTER:반항적 = 0
			PRINTFORMW 그리고%CALLNAME:MASTER%는[반항적]을 잃었다
		;굴복각인 Lv10 취득시, 프라이드높음을 잃어, 엔딩용 플래그가 선다
		ELSEIF LOCAL == 2 && ARG:2 == 10
			FLAG:굴복Lv10 += 1
			IF TALENT:MASTER:프라이드높음
				TALENT:MASTER:프라이드높음 = 0
				PRINTFORMW 그리고%CALLNAME:MASTER%는[프라이드높음]을 잃었다
			ENDIF
		;트라우마 Lv10 취득시, 겁쟁이, 유치를 이득,
		;원래 유치를 가지고 있을까 1/3의 확률로 광기를 얻는다
		ELSEIF LOCAL == 4 && ARG:4 == 10
			IF !TALENT:MASTER:겁쟁이
				TALENT:MASTER:겁쟁이 = 1
				PRINTFORMW 그리고%CALLNAME:MASTER%는[겁쟁이]를 얻었다
			ELSEIF !TALENT:MASTER:광기 && (TALENT:MASTER:유치 || !RAND:3)
				TALENT:MASTER:광기 = 1
				PRINTFORMW 그리고%CALLNAME:MASTER%는[광기]를 얻었다
			ELSEIF !TALENT:MASTER:유치
				TALENT:MASTER:유치 = 1
				PRINTFORMW 그리고%CALLNAME:MASTER%는 유아 퇴행 했다
			ENDIF
		ENDIF
	ENDIF
NEXT

;반항각인 Lv상한의 처리
;반항각인은 10-(고통각인+굴복각인)/2를 넘지 않는다
IF MARK:반항각인 > 10-((MARK:굴복각인+MARK:MASTER:고통각인)/2)
	MARK:반항각인 = 10-((MARK:굴복각인+MARK:MASTER:고통각인)/2)
	;구상 호출
	CALL KOJO_EVENT(29)
	PRINTFORML %CALLNAME:TARGET%가 주는 쾌락\@ MARK:MASTER:고통각인 ? 와 고통# \@가%CALLNAME:MASTER%로부터 반항의 기력을 빼앗아 간다…
	PRINTFORMW 반항각인은{MARK:반항각인}가 되었다
ENDIF

;─
;각인:굴복각인
;─
;뽑지않고의 2발와 뽑지않고의세발은 SOURCE_1. ERB에 이동. 결단적으로 신사양에 목수일＋α@revkoishi(14/05/28)
;뽑지않고의 2발와 뽑지않고의세발의 처리를 환송해. SOURCE_1의 처리를 고쳤으므로 여기서 좋다고 말하는 일로@/L(2014/06/05)
@MARK_CHECK_SOURCE_SURRENDER
#DIM LCOUNT

;-------------------------------------------------
;협력적 조교 대상 행동에 의한 굴복
;-------------------------------------------------
SIF IS_COMGRONAME("적극적 따른다/받아들인다/태연하게 따른다") && !IS_NOWCOMNAME("하는대로") && !IS_COMIMPNAME("약간나쁜인상")
	CALL MARK_CHECK_GET("호의적반응")	;【굴복:0】호의적반응(조교 대상이 적극적 따르는 계를 선택)
;다리빨고강제/이라마치오가 다른 ACT분류로부터 파생했을 경우에서도 MENUMATCH라면 줍는 것이 가능@/L
SIF MENUMATCH(TFLAG:ACT, "봉사") && IS_COMGRONAME("적극적 따른다")
	CALL MARK_CHECK_GET("적극봉사")		;【굴복:29】적극봉사(ACT분류가 봉사계중에 적극적으로 따른다)
SIF ((SEX_INITIATIVE(MASTER) && IS_COMGRONAME("적극적 따른다")) || (SEX_INITIATIVE(TARGET) && IS_COMGRONAME("받아들인다"))) ;&& ! IS_NOWCOMNAME("하는대로")
	CALL MARK_CHECK_GET("허리를흔든다")		;【굴복:31】허리를흔든다(성교중 적극적으로 따른다)
SIF IS_NOWCOMNAME("가게해!")
	CALL MARK_CHECK_GET("가게해")		;【굴복:49】가게해!

;-------------------------------------------------
;조교 대상 쾌감에 의한 굴복
;-------------------------------------------------
SIF TCVAR:MASTER:발기도 >= 1000
	CALL MARK_CHECK_GET("발기")			;【굴복:2】발기시켰다(발기도 1000 돌파)
FOR LCOUNT, 0, 4
	SIF PALAM:MASTER:LCOUNT >= 1000
		CALL MARK_CHECK_GET(TOSTR(3 + LCOUNT))	;【굴복:3】【굴복:4】【굴복:5】【굴복:6】쾌X를 1000이상 주었다
NEXT
SIF IS_CONDITION(MASTER, "정욕")
	CALL MARK_CHECK_GET("정욕")			;【굴복:32】정욕(조교 대상 상태가 정욕)
SIF TCVAR:MASTER:초조하게해번 > 1
	CALL MARK_CHECK_GET("초조하게함")		;【굴복:33】초조하게해플레이
SIF PALAM:MASTER:윤활 >= 1000 && !TALENT:MASTER:남자
	CALL MARK_CHECK_GET("젖었다")		;【굴복:62】젖었다(윤활 1000 돌파)

;-------------------------------------------------
;절정에 의한 굴복
;-------------------------------------------------
FOR LCOUNT, 0, 4
	SIF NOWEX:MASTER:LCOUNT
		CALL MARK_CHECK_GET(TOSTR(12 + LCOUNT))	;【굴복:12】【굴복:13】【굴복:14】【굴복:15】X절정
NEXT
SIF (NOWEX:MASTER:C절정 || NOWEX:MASTER:V절정 || NOWEX:MASTER:A절정 || NOWEX:MASTER:B절정) && GETBIT(TEQUIP:자위중, 0)
	CALL MARK_CHECK_GET("자위절정")		;【굴복:26】자위절정
SIF (NOWEX:MASTER:C절정 || NOWEX:MASTER:V절정 || NOWEX:MASTER:A절정 || NOWEX:MASTER:B절정) && TEQUIP:줄
	CALL MARK_CHECK_GET("구속절정")		;【굴복:34】구속절정
SIF ((NOWEX:MASTER:C절정 != 0) + (NOWEX:MASTER:V절정 != 0) + (NOWEX:MASTER:A절정 != 0) + (NOWEX:MASTER:B절정 != 0)) > 1
	CALL MARK_CHECK_GET("다중절정")		;【굴복:37】다중절정(복수 개소 동시절정)
SIF (NOWEX:MASTER:C절정 || NOWEX:MASTER:V절정 || NOWEX:MASTER:A절정 || NOWEX:MASTER:B절정) && (NOWEX:C절정 || NOWEX:V절정 || NOWEX:A절정 || NOWEX:B절정)
	CALL MARK_CHECK_GET("동시절정")		;【굴복:38】동시절정(조교 대상, 조교자 동시절정)
SIF ((TEQUIP:성교중 && (NOWEX:MASTER:V절정 || NOWEX:MASTER:A절정) && (NOWEX:사정 || NOWEX:C절정)) || (TEQUIP:성교봉사중 && (NOWEX:MASTER:사정 || NOWEX:MASTER:C절정) && (NOWEX:V절정 || NOWEX:A절정)))
	CALL MARK_CHECK_GET("성교동시절정")	;【굴복:39】성교동시절정(성교중, 삽입자 사정시에 피삽입자가 A/V절정)
SIF TFLAG:전회질내사정 == 1 && NOWEX:MASTER:V절정
	CALL MARK_CHECK_GET("피질쏘아맞히고V절정")	;【굴복:60】질내사정 V절정(조교자에게 질내사정 되었다고 동시에 조교 대상이 V절정)
SIF NOWEX:MASTER:C절정 && NOWEX:MASTER:V절정 && NOWEX:MASTER:A절정 && NOWEX:MASTER:B절정
	CALL MARK_CHECK_GET("사중절정")		;【굴복:61】사중절정

;-------------------------------------------------
;사정에 의한 굴복
;-------------------------------------------------
;뽑지않고의 2발/뽑지않고의세발의 조정@revkoishi(14/06/07)
SIF NOWEX:MASTER:사정 > 1
	CALL MARK_CHECK_GET("사정")			;【굴복:19】사정
SIF NOWEX:MASTER:사정 == 3
	CALL MARK_CHECK_GET("대량사정")		;【굴복:20】대량사정
SIF NOWEX:MASTER:사정 == 4
	CALL MARK_CHECK_GET("조루사정")		;【굴복:21】사정(조루)
SIF TFLAG:전회질내사정 == 1
	CALL MARK_CHECK_GET("피질내사정")	;【굴복:22】질내사정 되었다
SIF TFLAG:전회질내사정 == 2
	CALL MARK_CHECK_GET("질내사정")		;【굴복:25】질내사정
;드라이사정은 연주한다. 일견 알기 어렵기 때문에@PENIS를+@/L
SIF PENIS(TARGET) && IS_NOWACTNAME("입으로애무를강제/이라마치오") && NOWEX:사정 > 1
	CALL MARK_CHECK_GET("피입안사정")	;【굴복:27】입안사정 되었다(페라/이라마중에 조교자가 사정)
;드라이사정은 연주한다. 일견 알기 어렵기 때문에@PENIS를+@/L
SIF PENIS(MASTER) && (IS_NOWACTNAME("입으로애무") || TEQUIP:조교대상C사용 == 2) && NOWEX:MASTER:사정 > 1
	CALL MARK_CHECK_GET("입안사정")		;【굴복:28】입안사정(조교자 페라중에 사정)
SIF TFLAG:전회질내사정 == 6
	CALL MARK_CHECK_GET("항내사정")		;【굴복:41】애널에 사정
SIF TFLAG:전회질내사정 == 5
	CALL MARK_CHECK_GET("피항내사정")	;【굴복:42】애널에 사정된다
;드라이사정에서의 뽑지않고는 우선 인정하는 것으로 한다@/L
SIF IS_UNEXTRACTION(MASTER) && GROUPMATCH(TFLAG:전회질내사정, 2, 4, 6, 8) > 0
	CALL MARK_CHECK_GET("뽑지않고의 2발")	;【굴복:43】뽑지않고의 2발(사정 후 삽입을 중단하지 않고 사정)
SIF NOWEX:MASTER:사정 == 1
	CALL MARK_CHECK_GET("드라이사정")		;【굴복:44】드라이사정
SIF NOWEX:MASTER:사정 == 5
	CALL MARK_CHECK_GET("전립선사정")	;【굴복:45】전립선사정
SIF IS_UNEXTRACTION(MASTER) > 1 && GROUPMATCH(TFLAG:전회질내사정, 2, 4, 6, 8) > 0
	CALL MARK_CHECK_GET("뽑지않고의세발")	;【굴복:55】뽑지않고의세발(뽑지않고의 2발 후 삽입을 중단하지 않고 사정)

;-------------------------------------------------
;조교 대상치정에 의한 굴복
;-------------------------------------------------
SIF !(TEQUIP:MASTER:코스프레 || TEQUIP:MASTER:하반신속옷 || TEQUIP:MASTER:상반신속옷 || TEQUIP:MASTER:하반신윗도리 || TEQUIP:MASTER:상반신윗도리 || TEQUIP:MASTER:전신윗도리)
	CALL MARK_CHECK_GET("전라")			;【굴복:1】알몸에 벗겼다(MASTER의 의복이 없어진다)(※양말등은 착의에 세지 않는다)
SIF CUP:MASTER:쾌A
	CALL MARK_CHECK_GET("A조교")		;【굴복:9】애널 조교(쾌A발생)
SIF TEQUIP:야외플레이
	CALL MARK_CHECK_GET("야외")			;【굴복:16】야외플레이
SIF TEQUIP:대거울
	CALL MARK_CHECK_GET("대거울")			;【굴복:17】수치플레이
;리액션이 수용인 경우로 한정한다. 방뇨의 경우, 허가를청한다는 거부 취급@/L
SIF IS_NOWACTNAME("방뇨") && IS_COMGRONAME("소극적 따른다/태연하게 따른다/적극적 따른다/받아들인다")
	CALL MARK_CHECK_GET("방뇨")			;【굴복:18】방뇨시킨(조교 대상이 따르는 계를 선택) L
SIF TEQUIP:관장기＋플러그
	CALL MARK_CHECK_GET("관장")			;【굴복:35】관장
SIF TFLAG:실금
	CALL MARK_CHECK_GET("실금")			;【굴복:36】실금
SIF IS_NOWACTNAME("관장세트") && TFLAG:ACT파생 == 1
	CALL MARK_CHECK_GET("배설")			;【굴복:50】배설(관장기＋플러그 해제시)

;-------------------------------------------------
;조교 대상 고통에 의한 굴복
;-------------------------------------------------
SIF CUP:MASTER:공포 || CUP:MASTER:고통
	CALL MARK_CHECK_GET("위압")			;【굴복:7】고통, 공포(하지만 발생)
SIF IS_NOWACTNAME("채찍")
	CALL MARK_CHECK_GET("채찍")			;【굴복:8】채찍으로 두드렸다
SIF TEQUIP:줄
	CALL MARK_CHECK_GET("구속")			;【굴복:10】구속했다
SIF TFLAG:벌플래그 && (CUP:MASTER:공포 || CUP:MASTER:고통)
	CALL MARK_CHECK_GET("벌")		;【굴복:48】벌(벌시, 공포/고통이 발생)
SIF TEQUIP:삼각목마
	CALL MARK_CHECK_GET("삼각목마")		;【굴복:52】삼각목마

;-------------------------------------------------
;결정적 상황에 의한 굴복
;-------------------------------------------------
;리액션이 수용인 경우에 한정@/L
SIF IS_NOWACTNAME("키스") && IS_COMGRONAME("소극적 따른다/태연하게 따른다/적극적 따른다/받아들인다/허가를청한다")
	CALL MARK_CHECK_GET("키스")			;【굴복:11】키스
SIF TEQUIP:바이브
	CALL MARK_CHECK_GET("바이브")		;【굴복:23】바이브
SIF TEQUIP:애널바이브 || TEQUIP:애널비즈
	CALL MARK_CHECK_GET("A도구")		;【굴복:24】애널바이브, 비즈
SIF TEQUIP:성교중 || TEQUIP:성교봉사중
	CALL MARK_CHECK_GET("성교")			;【굴복:30】성교
SIF NOWEX:MASTER:분유
	CALL MARK_CHECK_GET("분유")			;【굴복:40】분유
SIF TEQUIP:성교중 == 6
	CALL MARK_CHECK_GET("A성교·수")	;【굴복:46】애널섹스(접수)
SIF TEQUIP:성교봉사중 == 6
	CALL MARK_CHECK_GET("A성교·공")	;【굴복:47】애널섹스(공격)
SIF IS_NOWACTNAME("공중육변기플레이") && GROUPMATCH(TFLAG:ACT파생, 2, 3)
	CALL MARK_CHECK_GET("공중변소")		;【굴복:51】공중변소(공중육변기플레이 수용시)
SIF IS_NOWACTNAME("피스트퍽/양구멍피스트")
	CALL MARK_CHECK_GET("V확장")		;【굴복:53】V확장(피스트/양구멍피스트 때)
SIF IS_NOWACTNAME("애널피스트/양구멍피스트")
	CALL MARK_CHECK_GET("A확장")		;【굴복:54】A확장(A피스트/양구멍피스트 때)
SIF NOWEX:MASTER:물총
	CALL MARK_CHECK_GET("물총")		;【굴복:59】물총

;-------------------------------------------------
;구상 독자적인 굴복
;-------------------------------------------------
;구상독자실적 용리저브 X
;【굴복:112】【굴복:113】【굴복:114】【굴복:115】【굴복:116】【굴복:117】【굴복:118】【굴복:119】
;【굴복:120】【굴복:121】【굴복:122】【굴복:123】【굴복:124】【굴복:125】【굴복:126】【굴복:127】
TRYCALLFORM KOJO_CHECK_SURRENDER_K{NO:TARGET}

;굴복각인취득 확인
CALL MARK_CHECK_SOURCE_SURRENDER_CHECK
RETURN RESULT

;그 외 굴복 조건은【굴복:\d*】로 검색 가능
;
;56실신은 EVENTCOMEND로
;57, 58 처녀, 동정상실은 EXPCHECK로
;그 밖에도 있을지도, 입니다 라고? 있으면 곤란합니다(강간눈)


@MARK_CHECK_SOURCE_SURRENDER_CHECK
;각인을 MASTER 측에 적용
;CFLAG:MASTER:각인에 MASTER가 받은 모든 각인
;CFLAG:TARGET:각인에 TARGET가 준 모든 각인
;가 들어가는 일이 됩니다
CFLAG:MASTER:각인 |= CFLAG:각인
CFLAG:MASTER:각인2 |= CFLAG:TARGET:각인2	;어디서 사용하는지 모르지만 일단 추가@revkoishi(14/05/26)

;LOCAL:1에 굴복 포인트 총계를 넣고 보정한다
LOCAL:1 = CFLAG:굴복P1
SIF TALENT:MASTER:남자
	TIMES LOCAL:1, 1.10
;후타나리도 남자도 아니다(즉 여자)
SIF TALENT:MASTER:후타나리 !| TALENT:MASTER:남자
	TIMES LOCAL:1, 1.20
SIF TALENT:MASTER:겁쟁이
	TIMES LOCAL:1, 1.05
SIF TALENT:MASTER:기장
	TIMES LOCAL:1, 0.95
SIF TALENT:MASTER:반항적
	TIMES LOCAL:1, 0.95
SIF TALENT:MASTER:프라이드높음
	TIMES LOCAL:1, 1.05
SIF TALENT:MASTER:프라이드낮음
	TIMES LOCAL:1, 0.95
SIF TALENT:위압감
	TIMES LOCAL:1, 1.50

;보정치를 바탕으로 굴복각인 조건을 초과하고 있을까 산출
LOCAL = LOCAL:1 > (MARK:굴복각인 * 5 + MARK:굴복각인 * MARK:굴복각인 / 2)
;10 LV가 상한
SIF LOCAL && (MARK:굴복각인 < 10)
	RETURN ++MARK:굴복각인

RETURN 0

;─
;굴복각인취득 함수 이벤트제입니다. 특정의 조건으로 턴 엔드 하면 굴복 포인트가 저점점 농락 되어 갑니다
;ARGS:0 굴복 실적명 또는 캐릭터 라인 형식의 굴복 실적 번호(0～127)
;결단적으로 신사양에 목수일(14/05/26). 성별로 유효 판정하도록(듯이) 가드@revkoishi(14/06/01)
;─
@MARK_CHECK_GET(ARGS:0)
LOCAL:1 = RESULT
LOCAL:0 = ISNUMERIC(ARGS:0) ? TOINT(ARGS:0) # GET_SURRENDER_NUM(ARGS:0)
IF IS_SURRENDER_ENABLE(LOCAL:0) && !(LOCAL:0 > 63 ? GETBIT(CFLAG:TARGET:각인2, LOCAL:0 - 64) # GETBIT(CFLAG:TARGET:각인, LOCAL:0))
	PRINTFORML 【%GET_SURRENDER_STR_M(LOCAL:0)%】굴복 포인트+{GET_SURRENDER_POINT(LOCAL:0)}
	CFLAG:TARGET:굴복P1 += GET_SURRENDER_POINT(LOCAL:0)
	SETBIT CFLAG:TARGET:\@ LOCAL:0 > 63 ? 각인2 # 각인 \@, LOCAL:0 > 63 ? LOCAL:0 - 64 # LOCAL:0
ENDIF
RETURN LOCAL:1
