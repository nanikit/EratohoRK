;─
;사진의 가치를 계산
;─
@SELL_PHOTO

;득점을 이하의 조건으로 가산해 나간다
VARSET LOCAL

;쾌X * 5
LOCAL += (CUP:MASTER:쾌C + CUP:MASTER:쾌V + CUP:MASTER:쾌A + CUP:MASTER:쾌B) * 5
;X절정 * 1500
LOCAL += (NOWEX:MASTER:C절정 + NOWEX:MASTER:V절정 + NOWEX:MASTER:A절정 + NOWEX:MASTER:B절정) * 1500
;분유+사정 * 1000, 방뇨 * 450
LOCAL += (NOWEX:MASTER:분유 + NOWEX:MASTER:사정) * 1000 + NOWEX:MASTER:방뇨 * 450
;윤활 / 10
LOCAL += CUP:MASTER:윤활 / 10
;욕정
;자제심+4, 충동적-3, 쾌감을부정+3, 쾌감에순수-2
;최대로-5되므로 정말로 유지하기 위해서(때문에)+6
LOCAL += CUP:MASTER:욕정 * (6 + TALENT:MASTER:자제적 * 4 - TALENT:MASTER:충동적 * 3 + TALENT:MASTER:쾌감을부정 * 3 - TALENT:MASTER:쾌감에순수 * 2)
;굴복
;기장+3, 반항적+2, 겁쟁이-2, 순수-1, 프라이드높음+3, 프라이드낮음-2
;최대로-3되므로 정말로 유지하기 위해서(때문에)+4
LOCAL += CUP:MASTER:굴복 * (4 + TALENT:MASTER:기장 * 3 + TALENT:MASTER:반항적 * 2 - TALENT:MASTER:겁쟁이 * 2 - TALENT:MASTER:순수 + TALENT:MASTER:프라이드높음 * 3 - TALENT:MASTER:프라이드낮음 * 2)
;공포
;감정결핍-1, 감수성+2
;최대로-1이지만, 전체 보정은+5
LOCAL += CUP:MASTER:공포 * (5 - TALENT:MASTER:감정결핍 + TALENT:MASTER:감수성 * 2)
;공순
;반항적+2, 순수-1, 거만함+3, 얌전하다-1
;최대로-2이지만, 전체 보정은+5
LOCAL += CUP:MASTER:공순 * (5 + TALENT:MASTER:반항적 * 2 - TALENT:MASTER:순수 + TALENT:MASTER:거만함 * 3 - TALENT:MASTER:얌전함)
;반항
;3 + 반항각인 / 3
LOCAL += CUP:MASTER:반항 * (3 + MARK:반항각인 / 3)
;고통
;조교자가 새디즘 실 2배
LOCAL += CUP:MASTER:고통 * (1 + TALENT:새디즘)
;치정
;부끄러워해+5, 수치심옅음-5
;최대로-5되므로 정말로 유지하기 위해서(때문에)+6
LOCAL += CUP:MASTER:치정 * (6 + TALENT:MASTER:수줍음 * 5 - TALENT:MASTER:수치심옅음 * 5)
;불쾌 * 3
LOCAL += CUP:MASTER:불쾌 * 3
;억울 * 3
LOCAL += CUP:MASTER:억울 * 2

;이후 보정 처리

;(조수 기교+1) * (조수 성교 기능+조교레벨+1) * (100+조교 대상피사경효험)를 걸쳐 10000으로 나눈다
LOCAL *= (ABL:ASSI:기교 + 1) * (ABL:ASSI:성교 + CFLAG:ASSI:조교레벨 + 1) * (100 + EXP:MASTER:피사경효험) / 10000

;조수가 사진을 신문의 기사로 했을 경우
;노출, 일탈 SOURCE를 더해, 조수 기교+7를 걸쳐 6으로 나눈다
SIF TFLAG:조수SPACT == 100
	LOCAL = (LOCAL + SOURCE:MASTER:노출 + SOURCE:MASTER:일탈) * (7 + ABL:ASSI:기교) / 6

PRINT 이번 촬영의 득점:
PRINTVL LOCAL
;전혀 득점을 획득 할 수 없었던 경우
IF LOCAL == 0
	PRINTL 이번 촬영은 완전하게 실패에 끝났다…
	PRINTFORML %CALLNAME:ASSI%가 너무나 조교에 서툴렀을지도 모른다
;CFLAG:MASTER:합의 =촬영 기준치
;촬영 기준치 미만에서는 마력은 얻을 수 없다
ELSEIF LOCAL < CFLAG:MASTER:합의
	PRINTL 이번 촬영은 전회정도의 성과라고는 말할 수 없는 것 같다
;촬영 기준치를 채우면
ELSE
	;400 + 기준치의 1/5 + 조교레벨의 제곱(+α)*20을 기준치에 가산
	CFLAG:MASTER:합의 += 400 + CFLAG:MASTER:합의 / 5 + (CFLAG:MASTER:조교레벨 + 1) * (CFLAG:MASTER:조교레벨 + 2) * 20
	PRINTL 이번 촬영은 좋은 결과와 같다. 마력조차 배여 오고 있다…
	;이번 득점의 것 1/4의 마력을 얻는다
	PRINTFORMW %CALLNAME%는{LOCAL / 4}의 마력을 얻었다
	CFLAG:마력 += LOCAL / 4
	PRINTL 
ENDIF

;─
;밀크의 가치를 계산
;─
@SELL_MILK
;조교시간, B절정, 분유*3, 헌신적*2, 매혹*5, 조교레벨의 총계를 마력으로서 얻는다
LOCAL = TFLAG:조교시간 + EX:MASTER:B절정 + EX:MASTER:분유 * 3 + TALENT:MASTER:헌신적 * 2 + TALENT:MASTER:매혹 * 5 + CFLAG:MASTER:조교레벨
CFLAG:마력 += LOCAL
PRINTFORML %CALLNAME:MASTER%로부터 짠 밀크는{LOCAL}의 마력으로서%CALLNAME%에 흡수되었다

;조교시간 리셋트
TFLAG:조교시간 = 0

;─
;모유체질 취득의 체크
;─
@MILK_CHECK

;기준치를 생성
VARSET LOCAL
;쾌B/100의 평방근을 가산(0-5)
LOCAL:1 = SQRT(CUP:MASTER:쾌B / 100)
LOCAL += LIMIT(LOCAL:1, 0, 5)
;조교 대상 B경험/4의 평방근을 가산(0-5)
LOCAL:1 = SQRT(EXP:MASTER:B경험 / 4)
LOCAL += LIMIT(LOCAL:1, 0, 5)

;(기준치+조교시간)가(12+(0-4)-조교 대상 헌신적-유치*2)를 웃돌면
IF LOCAL + TFLAG:조교시간 > 12 + RAND:5 - TALENT:MASTER:헌신적 - TALENT:유치 * 2
	;모유체질 취득
	TALENT:MASTER:모유체질 = 1
	PRINTFORMW %CALLNAME:MASTER%는 모유가 나오게 되었다
	;모유 게이지를 신설(MAX10000)
	MAXBASE:MASTER:모유 = 10000
	;조교시간 리셋트
	TFLAG:조교시간 = 0
ELSE
	;조교시간 경과
	TFLAG:조교시간 += 1
ENDIF
