;─
;애무, 성교등의 계속, 해제, 처리
;─
@SHOW_TOUCH
FOR LOCAL, 1, CHARANUM
	SIF LOCAL != TARGET
		CONTINUE
	SELECTCASE TEQUIP:LOCAL:접촉P
		CASE 3
			PRINT 자위 한다 
		CASE 4
			PRINT 페라한다 
		CASE 6
			PRINT 역강간 
		CASE 7
			PRINT 역애널 
		CASE 8
			PRINT 풋잡한다 
	ENDSELECT
	SELECTCASE TEQUIP:LOCAL:접촉C
		CASE 1
			PRINT 가랑이 
		CASE 3
			PRINT 클리 애무 
		CASE 4
			PRINT 쿤닐링구스 
	ENDSELECT
	SELECTCASE TEQUIP:LOCAL:접촉손가락
		CASE 1
			PRINT 자위 
		CASE 5
			PRINT 가슴 비비어 사랑 
	ENDSELECT
	SELECTCASE TEQUIP:LOCAL:접촉구
		CASE 1
			PRINT 페라 
		CASE 2
			PRINT 쿤닐링구스시킨다 
		CASE 4
			PRINT 키스 
		CASE 5
			PRINT 유두 들이마셔 
		CASE 7
			PRINT 애널 봉사 
	ENDSELECT
	SELECTCASE TEQUIP:LOCAL:접촉가슴
		CASE 1
			PRINT 파이즈리 
		CASE 3
			PRINT 가슴애무 
		CASE 4
			PRINT 유두빨고 
	ENDSELECT
	SELECTCASE TEQUIP:LOCAL:접촉질
		CASE 1
			PRINT 성교 
		CASE 3
			PRINT 손가락 넣고 
	ENDSELECT
	SELECTCASE TEQUIP:LOCAL:접촉A
		CASE 1
			PRINT 애널섹스 
		CASE 3
			PRINT 애널애무 
		CASE 4
			PRINT 애널빨고 
	ENDSELECT
	SELECTCASE TEQUIP:LOCAL:접촉발
		CASE 1
			PRINT 풋잡 
	ENDSELECT
	PRINTFORML (%CALLNAME:LOCAL%)
NEXT
PRINTL 


;------------------------------------------------------------------
;접촉 정보의 취득, 지문 표시
;------------------------------------------------------------------
@TOUCH_SUCCESSION
VARSET LOCAL

;성교 상태 보존
LOCAL:0 = TEQUIP:성교중
LOCAL:1 = TEQUIP:성교봉사중
;C사용 상태 보존
LOCAL:2 = TEQUIP:조교대상C사용

SELECTCASE TFLAG:ACT
	;회화계 현상 유지
	CASE 0 TO 4, 9	
	;애무/자위 한다
	CASE 10
		;자위
		IF TFLAG:ACT파생 == 1
			CALL TOUCH_SET("손가락", "P")
			;조교 대상 삽입을 해제
			TEQUIP:성교봉사중 = 0
		;애무
		ELSEIF !TFLAG:ACT파생
			CALL TOUCH_SET("손가락", "C")
		ENDIF
	;가슴애무/유두 들이마셔/젖의 몸싸움
	CASE 11
		;유두 들이마셔
		IF TFLAG:ACT파생 == 1
			CALL TOUCH_SET("구", "B")
		;젖의 몸싸움
		ELSEIF TFLAG:ACT파생 == 2
			CALL TOUCH_SET("손가락", "B")
			CALL TOUCH_SET("B", "손가락")
		;그 외
		ELSE
			CALL TOUCH_SET("손가락", "B")
		ENDIF
	;쿤닐링구스/페라한다
	CASE 12
		IF TFLAG:ACT파생
			CALL TOUCH_SET("구", "P")
		ELSE
			CALL TOUCH_SET("구", "C")
		ENDIF
		;삽입 해제
		CALL SEX_SET()
		;가슴
		TEQUIP:접촉가슴 = TEQUIP:접촉가슴 == 4 ? 0  # TEQUIP:접촉가슴
	;애널애무
	CASE 13
		CALL TOUCH_SET("손가락", "A")
	;애널빨고
	CASE 14
		CALL TOUCH_SET("구", "A")
		;삽입 해제
		CALL SEX_SET()
	;키스
	CASE 15
		CALL TOUCH_SET("구", "구")
	;조개 우려라/가랑이 한다
	CASE 16
		IF TFLAG:ACT파생
			CALL TOUCH_SET("C", "P")
		ELSE
			CALL TOUCH_SET("C", "C")
		ENDIF
		;삽입 해제
		CALL SEX_SET()
	;파이즈리한다
	CASE 17
		CALL TOUCH_SET("B", "P")
		;삽입 해제
		CALL SEX_SET()
	;풋잡한다
	CASE 18
		CALL TOUCH_SET("다리", "P")
		;삽입 해제
		CALL SEX_SET()
	;로터
	CASE 20
		IF PENIS(MASTER)
			CALL TOUCH_SET("손가락", "P")
		ELSE
			CALL TOUCH_SET("손가락", "C")
		ENDIF
		;조교 대상 삽입을 해제
		TEQUIP:성교봉사중 = 0
	;성교계는 배타 설정이 된다
	;정상위
	CASE 30
		CALL TOUCH_SET("P", "V", 1)
	;후배위
	CASE 31
		CALL TOUCH_SET("P", "V", 1)
	;승마위
	CASE 32
		CALL TOUCH_SET("P", "V", 1)
	;대면좌위
	CASE 33
		CALL TOUCH_SET("P", "V", 1)
	;배면좌위
	CASE 34
		CALL TOUCH_SET("P", "V", 1)
	;애널
	CASE 35
		CALL TOUCH_SET("P", "A", 1)
	;역강간
	CASE 103
		CALL TOUCH_SET("V", "P", 1)
	;자위/애무 강제
	CASE 50
		;자위
		IF TFLAG:ACT파생
			CALL TOUCH_SET("P", "손가락")
			;조교자 삽입을 해제
			TEQUIP:성교중 = 0
		;애무
		ELSE
			CALL TOUCH_SET("C", "손가락")
		ENDIF
	;펠라치오/쿤닐링구스 강제
	CASE 51
		IF TFLAG:ACT파생
			CALL TOUCH_SET("P", "구")
		ELSE
			CALL TOUCH_SET("C", "구")
		ENDIF
		;삽입 해제
		CALL SEX_SET()
	;파이즈리
	CASE 52
		;손에 의한 C애무를 해제
		CALL TOUCH_SET("P", "손가락", 2)
		CALL TOUCH_SET("P", "B")
		CALL TOUCH_SET("P", "구")
		;삽입 해제
		CALL SEX_SET()
	;가랑이
	CASE 53
		;조교 대상의 하반신 접촉을 해제
		CALL TOUCH_SET("C", , 2)
		CALL TOUCH_SET("P", , 2)
		CALL TOUCH_SET("A", , 2)
		CALL TOUCH_SET("V", , 2)
		CALL TOUCH_SET("P", "C")
		;삽입 해제
		CALL SEX_SET()
	;풋잡
	CASE 54
		CALL TOUCH_SET("P", "다리", 1)
		;삽입 해제
		CALL SEX_SET()
	;다리빨고강제
	CASE 55
		CALL TOUCH_SET("다리", "구", 1)
		;삽입 해제
		CALL SEX_SET()
	;이라마치오
	CASE 56
		CALL TOUCH_SET("P", "구", 1)
		;삽입 해제
		CALL SEX_SET()
	;가슴애무강제
	CASE 57
		;조교자의 입접촉을 해제
		CALL TOUCH_SET(, "구", 2)
		IF TFLAG:ACT파생
			CALL TOUCH_SET("B", "구")
		ELSE
			CALL TOUCH_SET("B", "손가락")
		ENDIF
	;스팽킹
	CASE 60
		;조교자의 입접촉을 해제
		CALL TOUCH_SET(, "구", 2)
	;정상위시킨다
	CASE 95
		CALL TOUCH_SET("V", "P", 1)
	;후배위시킨다
	CASE 96
		CALL TOUCH_SET("V", "P", 1)
	;대면좌위시킨다
	CASE 97
		CALL TOUCH_SET("V", "P", 1)
	;배면좌위시킨다
	CASE 98
		CALL TOUCH_SET("V", "P", 1)
	;애널섹스시킨다
	CASE 99
		CALL TOUCH_SET("A", "P", 1)
	CASEELSE
		CALL TOUCH_SET(, , 1)
ENDSELECT
IF TEQUIP:안면승마
	IF TALENT:남자
		CALL TOUCH_SET("P", "구")
	ELSE
		CALL TOUCH_SET("C", "구")
	ENDIF
ELSEIF TEQUIP:안면승마애널
	CALL TOUCH_SET("A", "구")
ENDIF

;성교세트
CALL SEX_SET(TOSTR(TFLAG:ACT))

IF TEQUIP:MASTER:접촉C == 3 || TEQUIP:MASTER:접촉P == 3
	TEQUIP:조교대상C사용 = 1
ELSEIF TEQUIP:MASTER:접촉C == 4 || TEQUIP:MASTER:접촉P == 4
	TEQUIP:조교대상C사용 = 2
ELSE
	TEQUIP:조교대상C사용 = 0
ENDIF

SIF !TEQUIP:성교중
	TEQUIP:접촉P = 0
IF !TEQUIP:성교봉사중
	TEQUIP:접촉질 = 0
	TEQUIP:접촉A = 0
ENDIF

;성교 상태가 해제되었을 경우, 지문 표시
IF LOCAL:1 > TEQUIP:성교봉사중 && !TEQUIP:성교봉사중
	PRINTFORMW %CALLNAME:TARGET%는%CALLNAME:MASTER%의\@ TEQUIP:페니스밴드 ? 의욕형 # 페니스 \@를 해방했다…
	PRINTL 
ELSEIF LOCAL:0 > TEQUIP:성교중 && !TEQUIP:성교중
	PRINTFORMW %CALLNAME:TARGET%는%CALLNAME:MASTER%로부터\@ TEQUIP:조교자페니스밴드 ? 의욕형 # 페니스 \@를 뽑아 냈다…
	PRINTL 
ENDIF

;C사용 상태가 해제되었을 경우, 지문 표시
IF LOCAL:2 == 2 && !TEQUIP:조교대상C사용
	PRINTFORMW %CALLNAME:TARGET%는%CALLNAME:MASTER%의 사타구니로부터 입을 떼어 놓아, 얼굴을 올렸다…
	PRINTL 
ELSEIF LOCAL:2 == 1 && !TEQUIP:조교대상C사용
	PRINTFORMW %CALLNAME:TARGET%는%CALLNAME:MASTER%의\@ PENIS(MASTER) ? 페니스 # 클리토리스 \@로부터 손을 떼어 놓았다…
	PRINTL 
ENDIF


;-----------------------------------------------------------------------
;MASTER와 TARGET의 접촉 부위를 설정한다
;-----------------------------------------------------------------------
@TOUCH_SET(ARGS:0, ARGS:1, ARG)
#DIM LCOUNT
#DIM T접촉
#DIM M접촉
#DIM 점유한계
#LOCALSIZE 1
;ARGS:0 TARGET의 접촉 부위(1=페니스 2=클리토리스 3=손가락 4=입 5=가슴 6=질 7=애널 8=다리)
;ARGS:1 MASTER의 접촉 부위(1=페니스 2=클리토리스 3=손가락 4=입 5=가슴 6=질 7=애널 8=다리)
;ARG 1이라면 리셋트 해 지정한 접촉을 추가한다. 2라면 지정한 접촉을 해제한다. 이 때, ARGS:0·ARGS:1을 생략 하면 각각 모든 것을 해제한다
T접촉 = TOUCH_PART(ARGS:0)
M접촉 = TOUCH_PART(ARGS:1)

;ARG가 1의 경우, 전접촉 TEQUIP 리셋트
IF ARG == 1
	VARSET TEQUIP:MASTER:0, 0, 101, 109
	VARSET TEQUIP:TARGET:0, 0, 101, 109
ENDIF

;ARG가 2의 경우, 생략 된 TEQUIP의 리셋트
;MASTER의 특정의 부위와 접촉하고 있는 TARGET의 접촉 부위를 리셋트
IF !T접촉 && M접촉 && ARG == 2
	FOR LCOUNT, 1, 8
		TEQUIP:(100 + LCOUNT) = TEQUIP:(100 + LCOUNT) == M접촉 ? 0 # TEQUIP:(100 + LCOUNT)
	NEXT
	TEQUIP:MASTER:(M접촉+100) = 0
	RETURN 0
;TARGET의 특정의 부위와 접촉하고 있는 MASTER의 접촉 부위를 리셋트
ELSEIF T접촉 && !M접촉 && ARG == 2
	FOR LCOUNT, 1, 8
		TEQUIP:MASTER:(100 + LCOUNT) = TEQUIP:MASTER:(100 + LCOUNT) == T접촉 ? 0 # TEQUIP:MASTER:(100 + LCOUNT)
	NEXT
	TEQUIP:(T접촉+100) = 0
	RETURN 0
ENDIF

;다리는 불리면 캔슬한다
TEQUIP:접촉발 = 0

;T접촉이 부정하면 처리 중단
SIF T접촉 < 1
	RETURN 0

;손가락(손)은 2 개있으므로 점유 한계를 2에
IF M접촉 == 3
	점유한계 = 2
;그 외의 점유 한계는 1
ELSE
	점유한계 = 1
ENDIF

;전접촉 TEQUIP에 대해서 처리
FOR LCOUNT, 1, 8
	;다리라면 캔슬한다
	SIF TEQUIP:MASTER:(100 + LCOUNT) == 8
		TEQUIP:MASTER:(100 + LCOUNT) = 0
	;TARGET의 지정 부위가 이미 사용되고 있었을 경우
	IF TEQUIP:MASTER:(100 + LCOUNT) == M접촉
		;해제의 경우
		IF ARG == 2
			TEQUIP:MASTER:(100 + LCOUNT) = 0
		;세트의 경우
		ELSE
			;점유 한계를 감산
			점유한계 --
			;새롭게 더해지는 지정을 포함해, 한계 미만이라면 접촉 해제
			SIF 점유한계 < 1
				TEQUIP:MASTER:(100 + LCOUNT) = 0
		ENDIF
	ENDIF
NEXT

;해제의 경우
IF ARG == 2
	TEQUIP:(T접촉+100) = TEQUIP:(T접촉+100) == M접촉 ? 0 # TEQUIP:(T접촉+100)
;세트의 경우
ELSE
	;새롭게 접촉손가락정을 더한다
	TEQUIP:(T접촉+100) = M접촉
	TEQUIP:MASTER:(M접촉+100) = T접촉
ENDIF

@TOUCH(ARG, ARG:1, ARG:2)
;ARG MASTER의 부위를 점유중의 TARGET:X의 부위(1=페니스 2=클리토리스 3=손가락 4=입 5=가슴 6=질 7=애널)
;ARG:1 MASTER의 부위(1=페니스 2=클리토리스 3=손가락 4=입 5=가슴 6=질 7=애널)
;ARG:2진이라면 전턴의 이력을 본다
;반환값 MASTER의 부위를 점유중의 TARGET:X가 있다면 그 캐릭터 번호를 돌려준다
;MASTER_POSE(4, 1, 1)라면 전턴(제 3 인수)에 누군가가 MASTER의 페니스(제 2 인수)를 입(제 1 인수)으로 점유 하고 있었을 경우, 그 캐릭터 번호를 돌려준다
#FUNCTION
ARG:2 = ARG:2 ? 1 # 0
FOR LOCAL, 1, CHARANUM
	SIF TEQUIP:LOCAL:(100 + ARG + 10 * ARG:2) == ARG:1
		RETURNF LOCAL
NEXT


;-------------------------------------------------
;함수명:TOUCH_PART
;개  요점:접촉 부위 캐릭터 라인→수치 변환 함수
;인  수:ARGS:0… 접촉 부위를 나타내는 캐릭터 라인(부위명이나 부위 번호)
;반환값:접촉 부위를 나타내는 수치
;비  고:식 안의 함수
;-------------------------------------------------
@TOUCH_PART(ARGS:0)
#FUNCTION
#LOCALSIZE 1
SELECTCASE ARGS:0
	CASE "P", "페니스"
		LOCAL:0 = 1
	CASE "C", "클리토리스"
		LOCAL:0 = 2
	CASE "손가락", "손"
		LOCAL:0 = 3
	CASE "구", "혀"
		LOCAL:0 = 4
	CASE "B", "가슴"
		LOCAL:0 = 5
	CASE "V", "질"
		LOCAL:0 = 6
	CASE "A", "애널"
		LOCAL:0 = 7
	CASE "다리"
		LOCAL:0 = 8
	CASEELSE
		LOCAL:0 = TOINT(ARGS:0)
		LOCAL:0 = INRANGE(LOCAL:0, 1, 8) ? LOCAL:0 # 0
ENDSELECT
RETURNF LOCAL:0
