
;────────────────────────────────────
;자세 변경 지문
;
;조교자 행동에 의해 자세가 변경이 되었을 경우 여기서 표시합니다.
;구상으로 지정하는 것에 의해 치환한 문장을 표시해 불리지 않도록 해야할 것인가도 모르지만
;지금은 아직 할 수 없습니다.
;────────────────────────────────────
@POSE_CHANGE
#LOCALSIZE 3
VARSET LOCAL, -1
;변화 내용의 체크
SIF TCVAR:MASTER:자세 != TCVAR:MASTER:전회자세
	LOCAL = TCVAR:MASTER:자세
SIF TCVAR:MASTER:위치전후 != TCVAR:MASTER:전회위치전후
	LOCAL:1 = TCVAR:MASTER:위치전후
SIF TCVAR:MASTER:위치상하 != TCVAR:MASTER:전회위치상하
	LOCAL:2 = TCVAR:MASTER:위치상하

;아무것도 변함없으면 돌아온다
SIF LOCAL < 0 && LOCAL:1 < 0 && LOCAL:2 < 0
	RETURN 0

;구상 호출
CALL KOJO_POSE(LOCAL, LOCAL:1, LOCAL:2)

;스레에의 대응 수단
SIF MENU(GET_ACTM(TFLAG:ACT)) == "불명"
	PRINTFORMW ＠＠＠에러입니다. ACT 번호{TFLAG:ACT}입니다, 스레로 보고 주십시오＠＠＠

;위치상하
SELECTCASE LOCAL:2
	;자유롭게 되었다
	CASE 0
		PRINTFORM %CALLNAME:TARGET%는\@ TCVAR:MASTER:전회위치상하 == 1 ? %CALLNAME:MASTER% 위에서 내리고 # 상을 타고 있다%CALLNAME:MASTER%를 밀치고 \@
		IF LOCAL >= 0
			PRINTFORML 와
			;자세
			SELECTCASE LOCAL
				;자유
				CASE 0
					PRINTFORMW %CALLNAME:MASTER%에는 편한 자세를 시켰다…
				;위로돌리고
				CASE 1
					PRINTFORMW %CALLNAME:MASTER%는 위로 향해 재웠다…
				;안정도
				CASE 2
					PRINTFORMW %CALLNAME:MASTER%를\@ TEQUIP:야외플레이 ? 지면 # 침대 \@ 위에 앉게 했다…
				;네발로 엎드림
				CASE 3
					PRINTFORMW %CALLNAME:MASTER%에 네발로 엎드림에\@ TCVAR:MASTER:위치전후 == 2 ? 는 엉덩이를 향해라 # \@같게 말했다…
				;기립
				CASE 4
					PRINTFORMW %CALLNAME:MASTER%에\@ TEQUIP:야외플레이 ? 지면 # 마루 \@에 서도록(듯이) 말했다…
				;무릎서기
				CASE 10
					PRINTFORMW %CALLNAME:MASTER%에\@ TEQUIP:야외플레이 ? 지면 # 침대 \@ 위에서 무릎서기가 되도록(듯이) 말했다…
			ENDSELECT
		ELSE
			PRINTFORMW …
		ENDIF
	;위를 탔다
	CASE 1
		PRINTFORML %CALLNAME:TARGET%는\@ TCVAR:MASTER:전회위치상하 == 2 ? 상을 타고 있다%CALLNAME:MASTER%와 신체를 교체 # %CALLNAME:MASTER% 위에 덥쳐 \@, 깔아누르려고 했다…
	;위에 실었다
	CASE 2
		;정상위
		IF IS_NOWACTNAME("정상위시킨다")
			PRINTFORM %CALLNAME:TARGET%는 다리를 열어,%CALLNAME:MASTER%에 전부터 몸을 씌워 오도록(듯이) 말했다…
		ELSE
			PRINTFORM %CALLNAME:TARGET%는%CALLNAME:MASTER%에,\@ TCVAR:MASTER:위치전후 == 3 ? 등을 # 신체를 \@넘어 위가 되도록(듯이) 말했다…
		ENDIF
	;상하 변화 없음
	CASEELSE
		PRINTFORM %CALLNAME:TARGET%는
		IF LOCAL >= 0
			;자세
			SELECTCASE LOCAL
				;위로돌리고
				CASE 1
					;간 후
					IF NOWEX:MASTER:Ｃ절정 || NOWEX:MASTER:Ｖ절정 || NOWEX:MASTER:Ａ절정 || NOWEX:MASTER:Ｂ절정 || NOWEX:MASTER:사정
						PRINTFORM , 절정의 여운으로 넋을 잃어, 힘 없게 가로놓였다%CALLNAME:MASTER%를 봐 만족스럽게 뺨을 느슨한
					ELSE
						PRINTFORM %CALLNAME:MASTER%를 위로 향해 재우고
					ENDIF
				;안정도
				CASE 2
					;갈것같다, 전회가 위로돌리고
					IF TCVAR:MASTER:갈것같다 && TCVAR:MASTER:전회자세 == 1
						PRINTFORM , 쾌감을 추구해 몸을 일으켜, 앉도록(듯이) 허리를 당긴다%CALLNAME:MASTER%를 바라봐 미소를 띄우고
					;간 후
					ELSEIF NOWEX:MASTER:Ｃ절정 || NOWEX:MASTER:Ｖ절정 || NOWEX:MASTER:Ａ절정 || NOWEX:MASTER:Ｂ절정 || NOWEX:MASTER:사정
						PRINTFORM , 절정의 여운으로 넋을 잃어, 축 주저앉았다%CALLNAME:MASTER%를 봐 만족스럽게 뺨을 느슨한
					ELSE
						PRINTFORM %CALLNAME:MASTER%를\@ TEQUIP:야외플레이 ? 지면 # 침대 \@ 위에 자리등키
					ENDIF
				;네발로 엎드림
				CASE 3
					PRINTFORM %CALLNAME:MASTER%를 네발로 엎드림에\@ TCVAR:MASTER:위치전후 == 2 ? 시켜 엉덩이를 향해라 # \@시키고
				;기립
				CASE 4
					PRINTFORM %CALLNAME:MASTER%에\@ TEQUIP:야외플레이 ? 지면 # 마루 \@에 립 더해라
				;무릎서기
				CASE 10
					;갈것같다
					IF TCVAR:MASTER:갈것같다
						PRINTFORM , 쾌감을 추구해 허리를 띄워, 무릎서기가 되었다%CALLNAME:MASTER%를 바라봐 미소를 띄우고
					ELSE
						PRINTFORM %CALLNAME:MASTER%를\@ TEQUIP:야외플레이 ? 지면 # 침대 \@ 위에서 무릎서기에 시키고
					ENDIF
				;자유
				CASEELSE
					PRINTFORM %CALLNAME:MASTER%에 편한 자세를 시키고
			ENDSELECT
			SIF LOCAL:1 >= 0
				PRINTFORML 와
		ENDIF
		IF LOCAL:1 >= 0
			;위치전후
			SELECTCASE LOCAL:1
				CASE 0
					SELECTCASE TCVAR:MASTER:위치상하
						CASE 0
							PRINTFORMW 신체를 떼어 놓아,%CALLNAME:MASTER%의 전신을 핥아대도록(듯이) 응시하기 시작했다…
						CASEELSE
							PRINTFORMW 신체를 겹친 채로,%CALLNAME:MASTER%의 전신을 핥아대도록(듯이) 응시하기 시작했다…
					ENDSELECT
				CASE 1
					SELECTCASE TCVAR:MASTER:위치상하
						CASE 0
							PRINTFORMW %CALLNAME:MASTER%의 정면으로 돌아 들어가,\@ RAND:2 ? 그 눈동자를 들여다 본… # 의미 있는듯이 미소지은… \@
						CASE 1
							PRINTFORMW %CALLNAME:MASTER%의 얼굴이 보이도록(듯이) 다시 향해, 싫은 것 같게 웃어 내려다 보았다…
						CASE 2
							PRINTFORMW %CALLNAME:MASTER%의 얼굴이 보이도록(듯이) 방향을 변화시켰다…
					ENDSELECT
				CASE 2
					SELECTCASE TCVAR:MASTER:위치상하
						CASE 0, 1
							IF MENU(GET_ACTM(TFLAG:ACT)) == "가학"
								PRINTFORMW %CALLNAME:MASTER%의 배후에 돌아 들어가, 끈적거리는 엷은 웃음을 떠올렸다…
							ELSEIF RAND:2
								PRINTFORMW %CALLNAME:MASTER%의 배후에 돌아 들어가,\@ RAND:2 ? 어깨 너머에 귓전에 얼굴을 댄… # 어깨를 껴안은… \@
							ELSE
								PRINTFORMW %CALLNAME:MASTER%의 신체를 전후 반대로 돌려, 등 너머로 신체를 대기 시작했다…
							ENDIF
						CASE 2
							PRINTFORMW 등을 돌려 걸치도록(듯이)%CALLNAME:MASTER%의 방향을 변화시켰다…
					ENDSELECT
				CASE 3
					SELECTCASE TCVAR:MASTER:위치상하
						CASE 0, 2
							PRINTFORMW %CALLNAME:MASTER%에 등을 돌렸다…
						CASE 1
							PRINTFORMW %CALLNAME:MASTER%의 얼굴에 등을 돌리도록(듯이), 넘은 채로 방향을 바꾸었다…
					ENDSELECT
			ENDSELECT
		ELSE
			PRINTFORMW …
		ENDIF
ENDSELECT
PRINTL 
CALL POSE_SHOW
