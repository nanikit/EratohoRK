;─
;010, 애무/자위 한다
;─
;리액션전(S/N/H)(쾌C, 쾌B, 접촉, 사랑, 성행동, 불결)
@ACTION_APPLY_10
;애무 보정치, 기묘한손가락소질에 응해, 쾌CSOURCE 증가
SOURCE:MASTER:쾌C = 75 + ACT_FROM(TARGET, 1) * (2 + TALENT:기묘한손가락) / 2
;탈의의 처리
;벽통과가 있으면 3/4로 무시한다
IF !TALENT:벽통과 || !RAND:4
	;억압/수줍음/착의플레이선호 소질이 없는 경우
	IF !TALENT:억압 && !TALENT:수줍음 && !TALENT:착의플레이선호
		;해방 소질, 조교자 욕망, 조교레벨에 응해 성공율 상승
		IF TALENT:해방 * 4 + GET_ABL(TARGET, "욕망") / 10 + GET_TRAINLV(TARGET) / 5 > 3 + RAND:3
			;전라까지 강제탈의
			TFLAG:강제탈의 = 8
			;2/3의 확률로 액세서리까지 강제탈의에 경감
			SIF RAND:3
				TFLAG:강제탈의 = 7
		;1/3의 확률로 상＋시모카미착 강제탈의
		ELSEIF !RAND:3
			TFLAG:강제탈의 = 3
		ENDIF
	ENDIF
ENDIF
;조교자 악세사리 존재시,1/3의 확률
;또는 자위시 페니스에 어떠한 더러움이 붙어 있다(2는 페니스 더러움인 것으로 카운트 하지 않는다)
;또는 애무시 바기나에 애액, 페니스 이외의 더러움이 붙어 있다(젖어 있거나 삽입이 있거나는 카운트 하지 않는다)
;경우, 액세서리 자주탈의
;※장갑 따위를 벗어 맨손으로 애무하기 위해(때문에) 액세서리 자주탈의가 발생
SIF TEQUIP:악세사리 && (!RAND:3 || (TFLAG:ACT파생 == 1 && STAIN:MASTER:페니스 > 2) || (TFLAG:ACT파생 == 0 && STAIN:MASTER:바기나 > 3))
	TFLAG:자주탈의 = 6
;필요하면 미약을 사용
CALL LOVEPOTION_USE
;조교레벨에 응해 접촉 SOURCE 증가
SOURCE:MASTER:접촉 = 100 + GET_TRAINLV(TARGET) * 5
;쾌CSOURCE에 응해 성행동SOURCE 증가
SOURCE:MASTER:성행동 = 50 + SOURCE:MASTER:쾌C / 10
;접촉 SOURCE, 조교자 봉사정신에 응해 사랑 SOURCE 증가
SOURCE:MASTER:사랑 = SOURCE:MASTER:접촉 / 2 + GET_ABL(TARGET, "봉사정신") * 5
;조교자의 손에 애액/페니스/모유 더러움이 존재하면 불결 SOURCE소증가
SIF STAIN:손 & (1 + 2 + 16)
	SOURCE:MASTER:불결 += 50
;조교자의 손에 정액/애널 더러움이 존재하면 불결 SOURCE 나카마시가
SIF STAIN:손 & (4 + 8)
	SOURCE:MASTER:불결 += 100
;조교자의 손에 피/점액 더러움이 존재하면 불결 SOURCE 오마스가
SIF STAIN:손 & (32 + 64)
	SOURCE:MASTER:불결 += 300
;더러움이 서로 옮긴다
IF TFLAG:ACT파생 >= 1
	STAIN:MASTER:페니스 |= STAIN:손
	STAIN:손 |= STAIN:MASTER:페니스
ELSE
	STAIN:MASTER:바기나 |= STAIN:손
	STAIN:손 |= STAIN:MASTER:바기나
ENDIF
;조교 대상 자위를 중단
TEQUIP:자위중 &= ~1
;조교자 기교, 애무, 기묘한손가락소질에 응해 발기도 상승
TCVAR:MASTER:이번발기 += 100 + (GET_ABL(TARGET, "기교") + GET_ABL(TARGET, "애무") + TALENT:기묘한손가락 * 50)
TFLAG:경과시간 += 2

;리액션 후(S/N/H)(쾌C, 쾌B, 접촉, 사랑, 성행동, 불결)
@ACTION_APPLY2_10

;─
;011, 가슴애무/유두 들이마셔/젖의 몸싸움
;─
;리액션전(S/N/H)(쾌B, 접촉, 사랑, 성행동, 상해, 불결)
@ACTION_APPLY_11
;탈의의 처리
;벽통과가 있으면 3/4로 무시한다
IF !TALENT:벽통과 || !RAND:4
	;억압/수줍음/착의플레이선호 소질이 없는 경우
	IF !TALENT:억압 && !TALENT:수줍음 && !TALENT:착의플레이선호
		;해방 소질, 조교자 욕망, 조교레벨에 응해 성공율 상승
		IF TALENT:해방 * 2 + GET_ABL(TARGET, "욕망") / 10 + GET_TRAINLV(TARGET) / 5 > 1 + RAND:3
			;상＋시모카미착 강제탈의
			TFLAG:강제탈의 = 4
		;1/3의 확률로 상반신윗도리 강제탈의
		ELSEIF !RAND:3
			TFLAG:강제탈의 = 1
		ENDIF
	ENDIF
ENDIF
;조교자 악세사리 존재시, 유두 들이마셔 파생은 아니고,
;1/3의 확률이나 조교 대상의 가슴이 애액/페니스 더러워지고 이외의 더러움을 가진다
;경우, 액세서리 자주탈의
;※장갑 따위를 벗어 맨손으로 애무하기 위해(때문에) 액세서리 자주탈의가 발생
SIF TEQUIP:악세사리 && TFLAG:ACT파생 != 1 && (!RAND:3 || STAIN:MASTER:가슴 > 3)
	TFLAG:자주탈의 = 6
;통상의 가슴애무 때
IF TFLAG:ACT파생 == 0
	;애무 보정치, 기묘한손가락소질에 응해 쾌BSOURCE 상승
	SOURCE:MASTER:쾌B = 75 + ACT_FROM(TARGET, 4) * (2 + TALENT:기묘한손가락) / 3
	;필요하면 미약을 사용
	CALL LOVEPOTION_USE
	;조교레벨에 응해 접촉 SOURCE 증가
	SOURCE:MASTER:접촉 = 100 + GET_TRAINLV(TARGET) * 5
	;접촉 SOURCE, 조교자 봉사정신에 응해 사랑 SOURCE 증가
	SOURCE:MASTER:사랑 = SOURCE:MASTER:접촉 / 2 + GET_ABL(TARGET, "봉사정신") * 5
	;쾌BSOURCE에 응해 성행동SOURCE 증가
	SOURCE:MASTER:성행동 = 100 + SOURCE:MASTER:쾌B / 10
	;조교자의 손에 애액/페니스/모유 더러움이 존재하면 불결 SOURCE소증가
	SIF STAIN:손 & (1 + 2 + 16)
		SOURCE:MASTER:불결 += 80
	;조교자의 손에 정액/애널 더러움이 존재하면 불결 SOURCE 나카마시가
	SIF STAIN:손 & (4 + 8)
		SOURCE:MASTER:불결 += 160
	;조교자의 손에 피/점액 더러움이 존재하면 불결 SOURCE 오마스가
	SIF STAIN:손 & (32 + 64)
		SOURCE:MASTER:불결 += 500
	;더러움이 서로 옮긴다
	STAIN:MASTER:가슴 |= STAIN:손
	STAIN:손 |= STAIN:MASTER:가슴
	;조교자 기교, 애무, 기묘한손가락소질에 응해 발기도 상승
	TCVAR:MASTER:이번발기 += 80 + (GET_ABL(TARGET, "기교") + GET_ABL(TARGET, "애무") + TALENT:기묘한손가락 * 50) / 3
;유두 들이마셔 때
ELSEIF TFLAG:ACT파생 == 1
	;애무 보정치, 혀사용 소질에 응해 쾌BSOURCE 상승
	SOURCE:MASTER:쾌B = 150 + ACT_FROM(TARGET, 4) * (3 + TALENT:혀사용) / 3
	;필요하면 미약을 사용
	CALL LOVEPOTION_USE
	;고양이혀 소질시 쾌BSOURCE 상승
	SIF TALENT:고양이혀
		SOURCE:MASTER:쾌B *= 12 / 10
	;조교레벨에 응해 접촉 SOURCE 증가
	SOURCE:MASTER:접촉 = 75 + GET_TRAINLV(TARGET) * 10
	;접촉 SOURCE, 조교자 봉사정신에 응해 사랑 SOURCE 증가
	SOURCE:MASTER:사랑 = SOURCE:MASTER:접촉 / 2 + GET_ABL(TARGET, "봉사정신") * 5
	;쾌BSOURCE에 응해 성행동SOURCE 증가
	SOURCE:MASTER:성행동 = 50 + SOURCE:MASTER:쾌B / 10
	;조교자의 입에 애액/페니스/모유 더러움이 존재하면 불결 SOURCE소증가
	SIF STAIN:구 & (1 + 2 + 16)
		SOURCE:MASTER:불결 += 100
	;조교자의 입에 정액/애널 더러움이 존재하면 불결 SOURCE 나카마시가
	SIF STAIN:구 & (4 + 8)
		SOURCE:MASTER:불결 += 200
	;조교자의 입에 피/점액 더러움이 존재하면 불결 SOURCE 오마스가
	SIF STAIN:구 & (32 + 64)
		SOURCE:MASTER:불결 += 600
	;더러움이 서로 옮긴다
	STAIN:MASTER:가슴 |= STAIN:구
	STAIN:구 |= STAIN:MASTER:가슴
	;조교자 기교, 애무, 혀 사용해, 고양이혀 소질에 응해 발기도 상승
	TCVAR:MASTER:이번발기 += 80 + (GET_ABL(TARGET, "기교") + GET_ABL(TARGET, "애무") + TALENT:혀사용 * 50 + TALENT:고양이혀 * 50) / 3
;그 외시
ELSE
	;조교 대상의 반응에 의해 요구에 응하지 않을 가능성이 있을테니까
	;여기는 조교자가 조교 대상의 가슴을 비빌 때까지 처리해, 조교 대상의 움직임은 COM의 (분)편으로 처리합니다.
	;이 종류의 행동은 전부 이와 같이 처리합니다.
	
	;상반신 전부 자주탈의
	TFLAG:자주탈의 = 4
	;애무 보정치, 기묘한손가락소질에 응해 쾌BSOURCE 상승
	SOURCE:MASTER:쾌B = ACT_FROM(TARGET, 4) * (2 + TALENT:기묘한손가락) / 2
	;조교자, 조교 대상 조교레벨에 응해 접촉 SOURCE 증가
	SOURCE:MASTER:접촉 = 50 + GET_TRAINLV(TARGET) * 5 + CFLAG:MASTER:조교레벨 * 5
	;필요하면 미약을 사용
	CALL LOVEPOTION_USE
	;조교 대상 호의, 조교자 봉사정신에 응해 사랑 SOURCE 증가
	SOURCE:MASTER:사랑 = 50 + CFLAG:M호의 / 50 + GET_ABL(TARGET, "봉사정신") * 5
	;쾌BSOURCE에 응해 성행동SOURCE 증가
	SOURCE:MASTER:성행동 = 50 + SOURCE:MASTER:쾌B / 8
	;조교자의 손에 애액/페니스/모유 더러움이 존재하면 불결 SOURCE소증가
	SIF STAIN:손 & (1 + 2 + 16)
		SOURCE:MASTER:불결 += 85
	;조교자의 손에 정액/애널 더러움이 존재하면 불결 SOURCE 나카마시가
	SIF STAIN:손 & (4 + 8)
		SOURCE:MASTER:불결 += 180
	;조교자의 손에 피/점액 더러움이 존재하면 불결 SOURCE 오마스가
	SIF STAIN:손 & (32 + 64)
		SOURCE:MASTER:불결 += 550
	;더러움이 서로 옮긴다
	STAIN:MASTER:가슴 |= STAIN:손
	STAIN:손 |= STAIN:MASTER:가슴
	;조교자, 조교 대상 자위를 중단
	TEQUIP:자위중 = 0
	;조교자 기교, 애무, 기묘한손가락소질에 응해 발기도 상승
	TCVAR:MASTER:이번발기 += 80 + (GET_ABL(TARGET, "기교") + GET_ABL(TARGET, "애무") + TALENT:기묘한손가락 * 50) / 3
ENDIF
;안면승마 계 해제
CALL FACESITTING_SET(0)
TFLAG:경과시간 += 2

;리액션 후(접촉, 사랑, 성행동, 달성, 쾌b)
@ACTION_APPLY2_11
;젖의 몸싸움 파생시
IF TFLAG:ACT파생 == 2
	;실행했다
	IF IS_COMGRONAME("소극적 따른다/태연하게 따른다/적극적 따른다/받아들인다")
		;조교 대상 조교레벨에 응해 접촉 SOURCE 증가
		SOURCE:MASTER:접촉 += 70 + CFLAG:MASTER:조교레벨 * 3
		;조교 대상 호의, 봉사정신에 응해 사랑 SOURCE 증가
		SOURCE:MASTER:사랑 += 20 + CFLAG:M호의 / 40 + GET_ABL(MASTER, "봉사정신") * 5
		;애무 보정, 조교 대상기묘한손가락소질에 응해 보정 가격인상승
		LOCAL = 35 + ACT_TO(TARGET, 4) * (3 + TALENT:MASTER:기묘한손가락) / 5
		;보정치에 응해 성행동, 달성, 쾌bSOURCE 증가
		SOURCE:MASTER:성행동 += 50 + LOCAL / 8
		SOURCE:MASTER:달성 += LOCAL / 4
		SOURCE:쾌B += LOCAL
		;더러움이 서로 옮긴다
		STAIN:MASTER:손 |= STAIN:가슴
		STAIN:가슴 |= STAIN:MASTER:손
	;그 외의 경우
	ELSE
		;반항 선행시, REACT분류에 요구에 응하지 않는 것을 세트
		SIF PALAM:MASTER:치정 <= PALAM:MASTER:반항
			CALL SET_COMGRO("거부")
	ENDIF
ENDIF

;─
;012, 쿤닐링구스/페라한다
;─
;리액션전(S/N/H)(쾌C, 접촉, 사랑, 성행동, 상해, 불결)
@ACTION_APPLY_12
;애무 보정치, 혀사용에 응해 쾌CSOURCE 상승
SOURCE:MASTER:쾌C = 200 + ACT_FROM(TARGET, 1) * (3 + TALENT:혀사용) / 3
;탈의의 처리
;벽통과가 있으면 3/4로 무시한다
IF !TALENT:벽통과 || !RAND:4
	;억압/수줍음/착의플레이선호 소질이 없는 경우
	IF !TALENT:억압 && !TALENT:수줍음 && !TALENT:착의플레이선호
		;해방 소질, 조교자 욕망, 조교레벨에 응해 성공율 상승
		IF TALENT:해방 * 2 + GET_ABL(TARGET, "욕망") / 10 + GET_TRAINLV(TARGET) / 5 > 1 + RAND:3
			;하반신 전부 강제탈의
			TFLAG:강제탈의 = 5
		;1/3의 확률로 하반신윗도리 강제탈의
		ELSEIF !RAND:3
			TFLAG:강제탈의 = 2
		ENDIF
	ENDIF
ENDIF
;필요하면 미약을 사용
CALL LOVEPOTION_USE
;윤활 상승(페라로 증가 보정, W페라로 보정 배증)
PALAM:MASTER:윤활 += 20 + TFLAG:ACT파생 * 20
;고양이혀 소질시 쾌CSOURCE 상승
SIF TALENT:고양이혀
	SOURCE:MASTER:쾌C *= 12 / 10
;조교레벨에 응해 접촉 SOURCE 증가
SOURCE:MASTER:접촉 = 150 + GET_TRAINLV(TARGET) * 5
;접촉 SOURCE, 조교자 봉사정신에 응해 사랑 SOURCE 증가
SOURCE:MASTER:사랑 = SOURCE:MASTER:접촉 / 2 + GET_ABL(TARGET, "봉사정신") * 5
;쾌CSOURCE에 응해 성행동SOURCE 증가
SOURCE:MASTER:성행동 = 80 + SOURCE:MASTER:쾌C / 10
;조교자의 입에 애액/페니스/모유 더러움이 존재하면 불결 SOURCE소증가
SIF STAIN:구 & (1 + 2 + 16)
	SOURCE:MASTER:불결 += 60
;조교자의 입에 정액/애널 더러움이 존재하면 불결 SOURCE 나카마시가
SIF STAIN:구 & (4 + 8)
	SOURCE:MASTER:불결 += 120
;조교자의 입에 피/점액 더러움이 존재하면 불결 SOURCE 오마스가
SIF STAIN:구 & (32 + 64)
	SOURCE:MASTER:불결 += 360
;더러움이 서로 옮긴다
IF TFLAG:ACT파생 >= 1
	STAIN:MASTER:페니스 |= STAIN:구
	STAIN:구 |= STAIN:MASTER:페니스
ELSE
	STAIN:MASTER:바기나 |= STAIN:구
	STAIN:구 |= STAIN:MASTER:바기나
ENDIF
;조교 대상 자위를 중단
TEQUIP:자위중 &= ~1
;조교자 기교, 애무, 혀 사용해, 고양이혀 소질에 응해 발기도 상승
TCVAR:MASTER:이번발기 += 150 + (GET_ABL(TARGET, "기교") + GET_ABL(TARGET, "애무") + TALENT:혀사용 * 50 + TALENT:고양이혀 * 50)
TFLAG:경과시간 += 2

;리액션 후(S/N/H)(쾌C, 접촉, 사랑, 성행동, 상해, 불결)
@ACTION_APPLY2_12

;─
;013, 애널애무
;─
;리액션전(N/H)[벌](쾌A, 접촉, 성행동, 상해, 불결, 일탈)
@ACTION_APPLY_13
;애무 보정치, 기묘한손가락소질에 응해, 쾌ASOURCE 증가
SOURCE:MASTER:쾌A = 60 + ACT_FROM(TARGET, 3) * (2 + TALENT:기묘한손가락) / 2
;전립선 고문에 파생하고 있으면 쾌A증가
SIF TFLAG:ACT파생 == 1
	TIMES SOURCE:MASTER:쾌A, 2.50
;애널바이브 출납에 파생하고 있으면 도구 강화, 애무 보정치, 도구사용 소질에 응해 쾌ASOURCE 증가
SIF TFLAG:ACT파생 == 2
	SOURCE:MASTER:쾌A += 40 + ITEM:9 * 100 + ACT_FROM(TARGET, 12) * (TALENT:도구사용 + 3) / 3
;탈의의 처리
;벽통과가 있으면 3/4로 무시한다
IF !TALENT:벽통과 || !RAND:4
	;억압/수줍음/착의플레이선호 소질이 없는 경우
	IF !TALENT:억압 && !TALENT:수줍음 && !TALENT:착의플레이선호
		;해방 소질, 조교자 욕망, 조교레벨에 응해 성공율 상승
		IF TALENT:해방 * 2 + GET_ABL(TARGET, "욕망") / 10 + GET_TRAINLV(TARGET) / 5 > 1 + RAND:3
			;하반신 전부 강제탈의
			TFLAG:강제탈의 = 5
		ELSE
			;최악이어도 하반신윗도리 강제탈의까지 시행
			TFLAG:강제탈의 = 2
		ENDIF
	ENDIF
ENDIF
;조교자 악세사리 존재시,2/3의 확률이나 조교 대상의 가슴이 애널 더러워지고 이외의 더러움을 가진다
;경우, 액세서리 자주탈의
;※장갑 따위를 벗어 맨손으로 애무하기 위해(때문에) 액세서리 자주탈의가 발생
SIF TEQUIP:악세사리 && (RAND:3 || STAIN:MASTER:애널 > 8)
	TFLAG:자주탈의 = 6
;필요하면 로션, 미약을 사용
CALL LOTION_USE
CALL LOVEPOTION_USE
;윤활 부족시, 쾌ASOURCE에 감소 보정, 아픔 SOURCE 증가
CALL SHORTAGE_LUBRICATION("500/M쾌A, T, 50/M 상해, A, 500")
;A경험 부족시, 쾌ASOURCE에 감소 보정, 상해, 일탈 SOURCE 증가
IF EXP:MASTER:A경험 < 1
	TIMES SOURCE:MASTER:쾌A , 0.60
	SOURCE:MASTER:아픔 += 600
	SOURCE:MASTER:일탈 += 1200
ELSEIF EXP:MASTER:A경험 < 6
	TIMES SOURCE:MASTER:쾌A , 0.75
	SOURCE:MASTER:아픔 += 200
	SOURCE:MASTER:일탈 += 700 - EXP:MASTER:A경험 * 100
ELSEIF EXP:MASTER:A경험 < 11
	TIMES SOURCE:MASTER:쾌A , 0.90
	SOURCE:MASTER:아픔 += 100
	SOURCE:MASTER:일탈 += 100 - EXP:MASTER:A경험 * 10
ELSEIF EXP:MASTER:A경험 < 21
	SOURCE:MASTER:일탈 += 5
ENDIF
;조교레벨에 응해 접촉 SOURCE 증가
SOURCE:MASTER:접촉 = 80 + GET_TRAINLV(TARGET) * 5
;쾌ASOURCE에 응해 성행동SOURCE 증가
SOURCE:MASTER:성행동 = 80 + SOURCE:MASTER:쾌A / 10
;조교자의 손에 애액/페니스/모유 더러움이 존재하면 불결 SOURCE소증가
SIF STAIN:손 & (1 + 2 + 16)
	SOURCE:MASTER:불결 += 10
;조교자의 손에 정액/애널 더러움이 존재하면 불결 SOURCE 나카마시가
SIF STAIN:손 & (4 + 8)
	SOURCE:MASTER:불결 += 30
;조교자의 손에 피/점액 더러움이 존재하면 불결 SOURCE 오마스가
SIF STAIN:손 & (32 + 64)
	SOURCE:MASTER:불결 += 100
;더러움이 서로 옮긴다
STAIN:MASTER:애널 |= STAIN:손
STAIN:손 |= STAIN:MASTER:애널
;남자는 A로 전립선자극
SIF TALENT:MASTER:남자
	TCVAR:MASTER:전립선자극 += 1
;전립선 고문에 파생하고 있으면 한층 더 전립선자극이 붙는다
SIF TFLAG:ACT파생 == 1
	TCVAR:MASTER:전립선자극 += 1
;조교 대상 A감각, 마조끼, 조교자 기교에 응해 발기도 상승
TCVAR:MASTER:이번발기 += 100 + (GET_ABL(MASTER, "A감각") + GET_ABL(MASTER, "마조끼") * GET_ABL(TARGET, "기교") / 50)
TFLAG:경과시간 += 2

;리액션 후(N/H)[벌](쾌A, 접촉, 성행동, 상해, 불결, 일탈)
@ACTION_APPLY2_13

;─
;014, 애널빨고
;─
;리액션전(N/H)[벌](쾌A, 접촉, 성행동, 상해, 불결, 일탈)
@ACTION_APPLY_14
;애무 보정치, 혀사용 소질에 응해, 쾌ASOURCE 증가
SOURCE:MASTER:쾌A = 60 + ACT_FROM(TARGET, 3) * (3 + TALENT:혀사용) / 3
;탈의의 처리
;벽통과가 있으면 3/4로 무시한다
IF !TALENT:벽통과 || !RAND:4
	;억압/수줍음/착의플레이선호 소질이 없는 경우
	IF !TALENT:억압 && !TALENT:수줍음 && !TALENT:착의플레이선호
		;해방 소질, 조교자 욕망, 조교레벨에 응해 성공율 상승
		IF TALENT:해방 * 2 + GET_ABL(TARGET, "욕망") / 10 + GET_TRAINLV(TARGET) / 5 > 1 + RAND:3
			;하반신 전부 강제탈의
			TFLAG:강제탈의 = 5
		ELSE
			;최악이어도 하반신윗도리 강제탈의까지 시행
			TFLAG:강제탈의 = 2
		ENDIF
	ENDIF
ENDIF
;필요하면 미약을 사용
CALL LOVEPOTION_USE
;윤활 상승
PALAM:MASTER:윤활 += 20
;고양이혀 소질시 쾌ASOURCE 상승
SIF TALENT:고양이혀
	SOURCE:MASTER:쾌A *= 12 / 10
;A경험 부족시, 쾌ASOURCE에 감소 보정, 일탈 SOURCE 증가
IF EXP:MASTER:A경험 < 1
	TIMES SOURCE:MASTER:쾌A , 0.80
	SOURCE:MASTER:일탈 += 1000
ELSEIF EXP:MASTER:A경험 < 6
	TIMES SOURCE:MASTER:쾌A , 0.85
	SOURCE:MASTER:일탈 += 500 - EXP:MASTER:A경험 * 50
ELSEIF EXP:MASTER:A경험 < 11
	TIMES SOURCE:MASTER:쾌A , 0.95
	SOURCE:MASTER:일탈 += 150 - EXP:MASTER:A경험 * 12
ELSEIF EXP:MASTER:A경험 < 21
	SOURCE:MASTER:일탈 += 10
ENDIF
;조교레벨에 응해 접촉 SOURCE 증가
SOURCE:MASTER:접촉 = 120 + GET_TRAINLV(TARGET) * 5
;쾌ASOURCE에 응해 성행동SOURCE 증가
SOURCE:MASTER:성행동 = 80 + SOURCE:MASTER:쾌A / 10
;조교자의 입에 애액/페니스/모유 더러움이 존재하면 불결 SOURCE소증가
SIF STAIN:구 & (1 + 2 + 16)
	SOURCE:MASTER:불결 += 10
;조교자의 입에 정액/애널 더러움이 존재하면 불결 SOURCE 나카마시가
SIF STAIN:구 & (4 + 8)
	SOURCE:MASTER:불결 += 30
;조교자의 입에 피/점액 더러움이 존재하면 불결 SOURCE 오마스가
SIF STAIN:구 & (32 + 64)
	SOURCE:MASTER:불결 += 100
;더러움이 서로 옮긴다
STAIN:MASTER:애널 |= STAIN:구
STAIN:구 |= STAIN:MASTER:애널
;조교 대상 A감각에 응해 발기도 상승
TCVAR:MASTER:이번발기 += 80 + GET_ABL(MASTER, "A감각")
;안면승마 계 해제
CALL FACESITTING_SET(0)
TFLAG:경과시간 += 2

;리액션 후(N/H)[벌](쾌A, 접촉, 성행동, 상해, 불결, 일탈)
@ACTION_APPLY2_14
;현재 처리 없음

;─
;015, 키스
;─
;리액션전(S/N)(접촉, 사랑, 성행동, 상해, 불결)
@ACTION_APPLY_15
;조교레벨에 응해 접촉 SOURCE 증가
SOURCE:MASTER:접촉 = 150 + GET_TRAINLV(TARGET) * 5
;조교 대상 호의, 조교자 애무, 기교, 혀사용 소질, 봉사정신에 응해 사랑 SOURCE 증가
SOURCE:MASTER:사랑 = 100 + CFLAG:M호의 / 50 + GET_ABL(TARGET, "애무") * (GET_ABL(TARGET, "기교") / 10 + TALENT:혀사용 * 2) + GET_ABL(TARGET, "봉사정신") * 5
;1/3의 확률로 액세서리 강제탈의
SIF !RAND:3
	TFLAG:강제탈의 = 6
;필요하면 미약을 사용
CALL LOVEPOTION_USE
;사랑 SOURCE에 응해 성행동SOURCE 증가
SOURCE:MASTER:성행동 = 50 + SOURCE:MASTER:사랑 / 15
;조교자의 입에 애액/페니스/모유 더러움이 존재하면 불결 SOURCE소증가
SIF STAIN:구 & (1 + 2 + 16)
	SOURCE:MASTER:불결 += 180
;조교자의 입에 정액/애널 더러움이 존재하면 불결 SOURCE 나카마시가
SIF STAIN:구 & (4 + 8)
	SOURCE:MASTER:불결 += 350
;조교자의 입에 피/점액 더러움이 존재하면 불결 SOURCE 오마스가
SIF STAIN:구 & (32 + 64)
	SOURCE:MASTER:불결 += 700
;더러움이 서로 옮긴다
STAIN:MASTER:구 |= STAIN:구
STAIN:구 |= STAIN:MASTER:구
;안면승마 계 해제
CALL FACESITTING_SET(0)
;조교자 기교, 애무, 혀 사용해, 고양이혀 소질에 응해 발기도 상승
TCVAR:MASTER:이번발기 += 50 + (GET_ABL(TARGET, "기교") + GET_ABL(TARGET, "애무") + TALENT:혀사용 * 50 + TALENT:고양이혀 * 50) / 2
TFLAG:경과시간 += 1

;리액션 후(S/N)(접촉, 사랑, 성행동, 상해, 불결)
@ACTION_APPLY2_15
;현재 처리 없음

;─
;016, 조개 우려라/가랑이 한다
;─
;리액션전(S/N)(쾌C, 접촉, 사랑, 성행동, 불결, 쾌c)
@ACTION_APPLY_16
;애무 보정치에 응해, 쾌C/쾌cSOURCE 증가, 한층 더 젖기쉬움, 젖기어려움 소질에 의해 증감
SOURCE:MASTER:쾌C = 80 + ACT_FROM(TARGET, 1) * (3 + TALENT:젖기쉬움 - TALENT:젖기어려움) * (2*TALENT:허리사용 + 1) / 3
SOURCE:쾌C = 30 + ACT_TO(TARGET, 1) * (3 + TALENT:젖기쉬움 - TALENT:젖기어려움) * (2*TALENT:MASTER:허리사용 + 1) / 3
;탈의의 처리
;벽통과가 있으면 3/4로 무시한다
IF !TALENT:벽통과 || !RAND:4
	;억압/수줍음/착의플레이선호 소질이 없는 경우
	IF !TALENT:억압 && !TALENT:수줍음 && !TALENT:착의플레이선호
		;해방 소질, 조교자 욕망, 조교레벨에 응해 성공율 상승
		IF TALENT:해방 * 2 + GET_ABL(TARGET, "욕망") / 10 + GET_TRAINLV(TARGET) / 5 > 1 + RAND:3
			;하반신 전부 강제탈의
			TFLAG:강제탈의 = 5
		ELSE
			;최악이어도 하반신윗도리 강제탈의까지 시행
			TFLAG:강제탈의 = 2
		ENDIF
	ENDIF
ENDIF
;하반신 전부 자주탈의
TFLAG:자주탈의 = 5
;필요하면 로션, 미약을 사용
CALL LOTION_USE
CALL LOVEPOTION_USE
;윤활 부족시, 쾌C/쾌cSOURCE에 감소 보정
CALL SHORTAGE_LUBRICATION("500/M쾌C, T, 75/T쾌C, T, 75")
;조교레벨에 응해 접촉 SOURCE 증가, 가랑이 파생시 감소
SOURCE:MASTER:접촉 = 200 + GET_TRAINLV(TARGET) * 5 - TFLAG:ACT파생 * 50
;접촉 SOURCE, 조교 대상 봉사정신에 응해 사랑 SOURCE 증가
SOURCE:MASTER:사랑 = SOURCE:MASTER:접촉 / 2 + GET_ABL(TARGET, "봉사정신") * 5
;쾌CSOURCE에 응해 성행동SOURCE 증가
SOURCE:MASTER:성행동 = 80 + SOURCE:MASTER:쾌C / 10
;조교자의 바기나에 애액/페니스/모유 더러움이 존재하면 불결 SOURCE소증가
SIF STAIN:바기나 & (1 + 2 + 16)
	SOURCE:MASTER:불결 += 50
;조교자의 바기나에 정액/애널 더러움이 존재하면 불결 SOURCE 나카마시가
SIF STAIN:바기나 & (4 + 8)
	SOURCE:MASTER:불결 += 100
;조교자의 바기나에 피/점액 더러움이 존재하면 불결 SOURCE 오마스가
SIF STAIN:바기나 & (32 + 64)
	SOURCE:MASTER:불결 += 300
;더러움이 서로 옮긴다
IF TFLAG:ACT파생 == 1
	STAIN:MASTER:페니스 |= STAIN:바기나
	STAIN:바기나 |= STAIN:MASTER:페니스
ELSE
	STAIN:MASTER:바기나 |= STAIN:바기나
	STAIN:바기나 |= STAIN:MASTER:바기나
ENDIF
;조교자, 조교 대상 자위를 중단
TEQUIP:자위중 = 0
;안면승마 계 해제
CALL FACESITTING_SET(0)
;조교자 기교, 애무에 응해 발기도 상승
TCVAR:MASTER:이번발기 += 150 + (GET_ABL(TARGET, "기교") + GET_ABL(TARGET, "애무") )
TFLAG:경과시간 += 3

;리액션 후(S/N)(쾌C, 접촉, 사랑, 성행동, 불결, 쾌c)
@ACTION_APPLY2_16
;현재 처리 없음

;─
;017, 파이즈리한다
;─
;리액션전(S/N)(쾌C, 접촉, 사랑, 상해, 성행동, 불결, 쾌b)
@ACTION_APPLY_17
;애무 보정치에 응해 쾌CSOURCE 증가, 또한 거유, 빈유소질에 의해 증감
SOURCE:MASTER:쾌C = 70 + ACT_FROM(TARGET, 1) * (4 + TALENT:거유 - TALENT:빈유) / 4
;애무 보정치에 응해 쾌bSOURCE 증가
SOURCE:쾌B = 20 + ACT_TO(TARGET, 4) * (TALENT:MASTER:허리사용 + 1) / 2
;탈의의 처리
;벽통과가 있으면 3/4로 무시한다
IF !TALENT:벽통과 || !RAND:4
	;억압/수줍음/착의플레이선호 소질이 없는 경우
	IF !TALENT:억압 && !TALENT:수줍음 && !TALENT:착의플레이선호
		;해방 소질, 조교자 욕망, 조교레벨에 응해 성공율 상승
		IF TALENT:해방 * 2 + GET_ABL(TARGET, "욕망") / 10 + GET_TRAINLV(TARGET) / 5 > 1 + RAND:3
			;하반신 전부 강제탈의
			TFLAG:강제탈의 = 5
			;조교자 욕망, 조교레벨에 응해 성공율 상승
			;액세서리까지 강제탈의에 발전
			SIF GET_ABL(TARGET, "욕망") / 10 + GET_TRAINLV(TARGET) > 4 + RAND:5
				TFLAG:강제탈의 = 7
		ELSE
			;최악이어도 하반신윗도리 강제탈의까지 시행
			TFLAG:강제탈의 = 2
		ENDIF
	ENDIF
ENDIF
;상반신 전부 자주탈의
TFLAG:자주탈의 = 4
;필요하면 로션, 미약을 사용
CALL LOVEPOTION_USE
CALL LOTION_USE
;윤활 부족시, 쾌CSOURCE에 감소 보정
CALL SHORTAGE_LUBRICATION("500/M쾌C, T, 75")
;빈유소질시, 쾌CSOURCE에 감소 보정
SIF TALENT:빈유
	TIMES SOURCE:MASTER:쾌C , 0.80
;고양이혀 소질시 쾌CSOURCE 상승
SIF TALENT:고양이혀
	SOURCE:MASTER:쾌C *= 11 / 10
;조교레벨에 응해 접촉 SOURCE 증가
SOURCE:MASTER:접촉 = 250 + GET_TRAINLV(TARGET) * 5
;접촉 SOURCE, 조교 대상 봉사정신에 응해 사랑 SOURCE 증가
SOURCE:MASTER:사랑 = SOURCE:MASTER:접촉 / 2 + GET_ABL(TARGET, "봉사정신") * 5
;쾌CSOURCE에 응해 성행동SOURCE 증가
SOURCE:MASTER:성행동 = 40 + SOURCE:MASTER:쾌C / 10
;조교자의 입에 애액/페니스/모유 더러움이 존재하면 불결 SOURCE소증가
SIF STAIN:구 & (1 + 2 + 16)
	SOURCE:MASTER:불결 += 20
;조교자의 입에 정액/애널 더러움이 존재하면 불결 SOURCE 나카마시가
SIF STAIN:구 & (4 + 8)
	SOURCE:MASTER:불결 += 50
;조교자의 입에 피/점액 더러움이 존재하면 불결 SOURCE 오마스가
SIF STAIN:구 & (32 + 64)
	SOURCE:MASTER:불결 += 150
;조교자의 가슴에 애액/페니스/모유 더러움이 존재하면 불결 SOURCE소증가
SIF STAIN:가슴 & (1 + 2 + 16)
	SOURCE:MASTER:불결 += 50
;조교자의 가슴에 정액/애널 더러움이 존재하면 불결 SOURCE 나카마시가
SIF STAIN:가슴 & (4 + 8)
	SOURCE:MASTER:불결 += 100
;조교자의 가슴에 피/점액 더러움이 존재하면 불결 SOURCE 오마스가
SIF STAIN:가슴 & (32 + 64)
	SOURCE:MASTER:불결 += 300
;더러움이 서로 옮긴다
STAIN:MASTER:페니스 |= STAIN:구
STAIN:MASTER:페니스 |= STAIN:가슴
STAIN:구 |= STAIN:MASTER:페니스
STAIN:가슴 |= STAIN:MASTER:페니스
;조교자, 조교 대상 자위를 중단
TEQUIP:자위중 = 0
;조교자 기교, 애무에 응해 발기도 상승, 거유, 빈유, 음유소질에 의해 증감
TCVAR:MASTER:이번발기 += 150 + (GET_ABL(TARGET, "기교") + GET_ABL(TARGET, "애무") + TALENT:거유 * 50 - TALENT:빈유 * 50 + TALENT:음유 * 50)
TFLAG:경과시간 += 3

;리액션 후(S/N)(쾌C, 접촉, 사랑, 상해, 성행동, 불결, 쾌b)
@ACTION_APPLY2_17

;─
;018, 풋잡한다
;─
;리액션전(S/N/H)[벌](쾌C, 접촉, 사랑, 성행동, 불결, 일탈)
@ACTION_APPLY_18
;애무 보정치, 미각 소질에 응해 쾌CSOURCE 증가
SOURCE:MASTER:쾌C = 40 + ACT_FROM(TARGET, 1) * (3 + TALENT:미각) / 3
;조교레벨에 응해 접촉 SOURCE 증가
SOURCE:MASTER:접촉 = 60 + GET_TRAINLV(TARGET) * 10
;접촉 SOURCE, 조교 대상 봉사정신에 응해 사랑 SOURCE 증가
SOURCE:MASTER:사랑 = SOURCE:MASTER:접촉 / 2 + GET_ABL(TARGET, "봉사정신") * 5
;쾌CSOURCE에 응해 성행동SOURCE 증가
SOURCE:MASTER:성행동 = SOURCE:MASTER:쾌C / 10
;새디즘 소질이 존재하면, 쾌CSOURCE에 응해 아픔 SOURCE 증가
SIF TALENT:새디즘
	SOURCE:MASTER:아픔 = SOURCE:MASTER:쾌C / 4
;일탈 SOURCE 증가, 조교레벨에 응해 경감
SOURCE:MASTER:일탈 = 200 - GET_TRAINLV(TARGET) * 10
;일탈 SOURCE가 부의 값을 받지 않게 조정
SOURCE:MASTER:일탈 = SOURCE:MASTER:일탈 < 0 ? 0 # SOURCE:MASTER:일탈
;탈의의 처리
;벽통과가 있으면 3/4로 무시한다
IF !TALENT:벽통과 || !RAND:4
	;억압/수줍음/착의플레이선호 소질이 없는 경우, 하반신윗도리 강제탈의
	SIF !TALENT:억압 && !TALENT:수줍음 && !TALENT:착의플레이선호
		TFLAG:강제탈의 = 2
ENDIF
;조교자 양말 착용중, 조교 대상 페니스에 페니스 더러워지고 이외의 더러움이 존재하면,
;액세서리 자주탈의
SIF TEQUIP:양말 && STAIN:MASTER:페니스 > 2
	TFLAG:자주탈의 = 6
;조교자의 다리에 애액/페니스/모유 더러움이 존재하면 불결 SOURCE소증가
SIF STAIN:다리 & (1 + 2 + 16)
	SOURCE:MASTER:불결 += 35
;조교자의 다리에 정액/애널 더러움이 존재하면 불결 SOURCE 나카마시가
SIF STAIN:다리 & (4 + 8)
	SOURCE:MASTER:불결 += 80
;조교자의 다리에 피/점액 더러움이 존재하면 불결 SOURCE 오마스가
SIF STAIN:다리 & (32 + 64)
	SOURCE:MASTER:불결 += 200
;윤활 부족시, 쾌CSOURCE에 감소 보정
CALL SHORTAGE_LUBRICATION("500/M쾌C, T, 75")
;더러움이 서로 옮긴다
STAIN:MASTER:페니스 |= STAIN:다리
STAIN:다리 |= STAIN:MASTER:페니스
;안면승마는 안면승마애널에 이행
IF TEQUIP:안면승마
	CALL FACESITTING_SET(2)
ENDIF
;조교자, 조교 대상 자위를 중단
TEQUIP:자위중 = 0
;조교자 기교, 애무에 응해 발기도 상승
TCVAR:MASTER:이번발기 += 100 + (GET_ABL(TARGET, "기교") + GET_ABL(TARGET, "애무"))
TFLAG:경과시간 += 2

;리액션 후(S/N/H)[벌](쾌C, 접촉, 사랑, 성행동, 불결, 일탈)
@ACTION_APPLY2_18
;현재 처리 없음

;─
;019, 손가락삽입해라
;─
;리액션전(N/H)[벌](쾌V, 접촉, 성행동, 상해, 사랑, 불결)
@ACTION_APPLY_19
;애무 보정치, 기묘한손가락소질에 응해, 쾌VSOURCE 증가
SOURCE:MASTER:쾌V = 90 + ACT_FROM(TARGET, 3) * (2 + TALENT:기묘한손가락) / 2
;G스팟 고문에 파생하고 있으면 쾌V증가
SIF TFLAG:ACT파생 == 1
	TIMES SOURCE:MASTER:쾌V, 2.50
;바이브 출납에 파생하고 있으면 도구 강화, 애무 보정치, 도구사용 소질에 응해 쾌VSOURCE 증가
SIF TFLAG:ACT파생 == 2
	SOURCE:MASTER:쾌V += 50 + ITEM:9 * 100 + ACT_FROM(TARGET, 12) * (TALENT:도구사용 + 3) / 3
;탈의의 처리
;벽통과가 있으면 3/4로 무시한다
IF !TALENT:벽통과 || !RAND:4
	;착의플레이선호 소질이 있는 경우
	IF TALENT:착의플레이선호
		;조교레벨, 해방 소질에 응해 성공율 상승해, 속옷만 강제탈의
		SIF GET_TRAINLV(TARGET) + TALENT:해방 * 3 > 2 + RAND:3
			TFLAG:강제탈의 = 9
	;억압/수줍음 소질이 없는 경우
	ELSEIF !TALENT:억압 && !TALENT:수줍음
		;하반신 전부 강제탈의
		TFLAG:강제탈의 = 5
	ENDIF
	;조교 대상 하반신윗도리가 스커트가 아니고, 하반신 전부 강제탈의가 아닌 경우, 하반신윗도리 강제탈의
	SIF TEQUIP:MASTER:하반신윗도리 > 1 && TFLAG:강제탈의 != 5
		TFLAG:강제탈의 = 2
ENDIF
;조교자 악세사리 존재시,2/3의 확률이나 조교 대상의 V가 애액 더러워지고 이외의 더러움을 가진다
;경우, 액세서리 자주탈의
;※장갑 따위를 벗어 맨손으로 애무하기 위해(때문에) 액세서리 자주탈의가 발생
SIF TEQUIP:악세사리 && (RAND:3 || STAIN:MASTER:바기나-1)
	TFLAG:자주탈의 = 6
;필요하면 로션, 미약을 사용
CALL LOTION_USE
CALL LOVEPOTION_USE
;윤활 부족시, 쾌VSOURCE에 감소 보정, 아픔 SOURCE 증가
CALL SHORTAGE_LUBRICATION("500/M쾌V, T, 50/M 상해, A, 500")
;V경험 부족시, 쾌VSOURCE에 감소 보정, 상해, 일탈 SOURCE 증가
IF EXP:MASTER:V경험 < 1
	TIMES SOURCE:MASTER:쾌V , 0.60
	SOURCE:MASTER:아픔 += 600
	SOURCE:MASTER:일탈 += 1200
ELSEIF EXP:MASTER:V경험 < 6
	TIMES SOURCE:MASTER:쾌V , 0.75
	SOURCE:MASTER:아픔 += 200
	SOURCE:MASTER:일탈 += 700 - EXP:MASTER:A경험 * 100
ELSEIF EXP:MASTER:V경험 < 11
	TIMES SOURCE:MASTER:쾌V , 0.90
	SOURCE:MASTER:아픔 += 100
	SOURCE:MASTER:일탈 += 100 - EXP:MASTER:A경험 * 10
ELSEIF EXP:MASTER:V경험 < 21
	SOURCE:MASTER:일탈 += 5
ENDIF
;처녀시, 한층 더 쾌V에 감소 보정
SIF TALENT:MASTER:처녀
	TIMES SOURCE:MASTER:쾌V , 0.80

;조교레벨에 응해 접촉 SOURCE 증가
SOURCE:MASTER:접촉 = 80 + GET_TRAINLV(TARGET) * 5
;쾌VSOURCE에 응해 성행동SOURCE 증가
SOURCE:MASTER:성행동 = 80 + SOURCE:MASTER:쾌V / 10
;조교자의 손에 페니스/모유 더러움이 존재하면 불결 SOURCE소증가
SIF STAIN:손 & (2 + 16)
	SOURCE:MASTER:불결 += 10
;조교자의 손에 정액/애널 더러움이 존재하면 불결 SOURCE 나카마시가
SIF STAIN:손 & (4 + 8)
	SOURCE:MASTER:불결 += 30
;조교자의 손에 피/점액 더러움이 존재하면 불결 SOURCE 오마스가
SIF STAIN:손 & (32 + 64)
	SOURCE:MASTER:불결 += 100
;더러움이 서로 옮긴다
STAIN:MASTER:바기나 |= STAIN:손
STAIN:손 |= STAIN:MASTER:바기나
TCVAR:MASTER:전립선자극 += 1
;G스팟 고문에 파생하고 있으면 한층 더 전립선자극이 붙는다
SIF TFLAG:ACT파생 == 1
	TCVAR:MASTER:전립선자극 += 1
;조교 대상 V감각, 조교자 기교에 응해 발기도 상승
TCVAR:MASTER:이번발기 += 100 + (GET_ABL(MASTER, "V감각") * GET_ABL(TARGET, "기교") / 20)
TFLAG:경과시간 += 2

;리액션 후(N/H)[벌](쾌A, 접촉, 성행동, 상해, 불결, 일탈)
@ACTION_APPLY2_19

;─
;027, 밀어넘어뜨린다
;─
;리액션전(S/N)(쾌B, 중독충족)
@ACTION_APPLY_27
;밀어넘어뜨린다
IF TEQUIP:밀어넘어뜨리기중 == 0
	TFLAG:밀어넘어뜨리기경과 = TFLAG:경과시간
	TEQUIP:밀어넘어뜨리기중 = 1
;밀어넘어뜨리기 종료
ELSE
	TFLAG:밀어넘어뜨리기경과 = TFLAG:경과시간
	TEQUIP:밀어넘어뜨리기중 = 0
ENDIF
;조교자, 조교 대상 자위를 중단
TEQUIP:자위중 = 0
;조교 대상 욕망에 응해 발기도 상승
TCVAR:MASTER:이번발기 += 100 + GET_ABL(MASTER, "욕망") * 3
TFLAG:경과시간 += 1

;리액션 후(S/N)(쾌B, 중독충족)
@ACTION_APPLY2_27
;현재 처리 없음


