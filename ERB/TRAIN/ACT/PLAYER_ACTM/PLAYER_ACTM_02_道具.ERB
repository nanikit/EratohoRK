;─
;도구(로터 20/바이 21/A바이 22/A비 23/C사랑 24/니풀 25/P대 26/관장 68)
;─
@ACT_M2(ARG = 0)
;1이상이라면 판정을 실시하지 않는다
#DIM LCOUNT, 1

;함수 초기화
CALL ACT_M_INIT(2)

;처리 개시

LOCALS = ACT_ACTM2

CALL CHA_CRI_VAR, "기준치"
CALLFORM %LOCALS%_REFERENCE

CALL CHA_CRI_VAR, @"오늘의 방침(%POLICY("오늘")%)"
CALLFORM %LOCALS%_POLYCY

CALL CHA_CRI_VAR, @"턴방침(%POLICY("지금")%)"
CALLFORM %LOCALS%_MENU

CALL CHA_CRI_VAR, "조교자의 소질"
CALLFORM %LOCALS%_TALENT_T

CALL CHA_CRI_VAR, "조교 대상의 소질"
CALLFORM %LOCALS%_TALENT_M

CALL CHA_CRI_VAR, "조교 대상이 처녀"
CALLFORM %LOCALS%_MAIDEN_M

CALL CHA_CRI_VAR, "조교자의 능력"
CALLFORM %LOCALS%_ABL_T

CALL CHA_CRI_VAR, "조교 대상의 능력"
CALLFORM %LOCALS%_ABL_M

CALL CHA_CRI_VAR, "조교 대상의 윤활"
CALLFORM %LOCALS%_MOIST_M

CALL CHA_CRI_VAR, "조교자의 죄악감"
CALLFORM %LOCALS%_SIN_T

CALL CHA_CRI_VAR, "조교자의 ALI"
CALLFORM %LOCALS%_ALI_T

CALL CHA_CRI_VAR, "조교 대상의 체력"
CALLFORM %LOCALS%_PHYSICAL_M

CALL CHA_CRI_VAR, @"조교자 상태(%CONDITION(TCVAR:상태변화)%)"
CALLFORM %LOCALS%_CONDITION_T

CALL CHA_CRI_VAR, "조교 대상 자위중"
CALLFORM %LOCALS%_MASTURBATION_M

CALL CHA_CRI_VAR, "조교자 자위중"
CALLFORM %LOCALS%_MASTURBATION_T

CALL CHA_CRI_VAR, "좀더하드하게해"
CALLFORM %LOCALS%_PLEASE_HARD

CALL CHA_CRI_VAR, "허가를청한다"
CALLFORM %LOCALS%_PLEASE_HELP

CALL CHA_CRI_VAR, "연속행동 보정"
CALLFORM %LOCALS%_PREV

CALL CHA_CRI_VAR, "조교 대상의 복장"
CALLFORM %LOCALS%_CLOTH_M

CALL CHA_CRI_VAR, "벌"
CALLFORM %LOCALS%_OSHIOKI

CALL CHA_CRI_VAR, "구상 보정치"
CALL ACT_ACTM_KOJO_CORRECTION_T, 2

CALL CHA_CRI_VAR, "ACT 보정치"
CALLFORM %LOCALS%_CORRECTION(ARG)


;ARG가 1이상이라면 처리 중단
SIF ARG
	RETURN RESULT

CALL CHA_CRI_VAR, "실행 판정"
CALLFORM %LOCALS%_ABLE

;─
;최종 판정
;─
CALL DEF_CRI_VAR("최대 전자")
LOCAL = GET_CRI_VAR("결정")
IF LOCAL == -1
	SIF FLAG:디버그
		PRINTL (도구 ACT분류에 실행 가능한 ACT가 존재하지 않습니다)
	TFLAG:ACT = -1
	CALL ACT_MENU_RESULT(2, "도구")
ELSE
	TFLAG:ACT = GET_ACTNUM(RESULTS)
ENDIF
CALL ANA_CRI_VAR()

;-------------------------------------------------------------------------
;기준치
;-------------------------------------------------------------------------
@ACT_ACTM2_REFERENCE

;필요하지 않을지도라고 생각했으므로 시험삼아 무효화@이것 보고
[SKIPSTART]
CALL ADD_CRI_VAR("로터", 10)
CALL ADD_CRI_VAR("바이브", 12 - TEQUIP:바이브 * 3)
CALL ADD_CRI_VAR("애널바이브", 10 - TEQUIP:애널바이브 * 3)
CALL ADD_CRI_VAR("애널비즈", 10 + TEQUIP:애널비즈 * 2)
CALL ADD_CRI_VAR("C애무도구", 15 - TEQUIP:클리캡 * 5 - TEQUIP:오나홀 * 5)
CALL ADD_CRI_VAR("니플캡", 15 - TEQUIP:니플캡 * 5)
CALL ADD_CRI_VAR("관장세트", 4 + TEQUIP:관장기＋플러그 * 2)
[SKIPEND]

;-------------------------------------------------------------
;오늘의 방침
;오늘의 방침(0=언제나 대로/1=A개발/2=벌/3=하드/4=러브러브/5=이상/6=야외/7=이상/8=휴게)
;-------------------------------------------------------------
@ACT_ACTM2_POLYCY

;오늘의 방침이 A개발
IF TFLAG:오늘의방침 == 1
	CALL ADD_CRI_VAR("애널바이브", 10)
	CALL ADD_CRI_VAR("애널비즈", 10)
ENDIF

;-------------------------------------------------------------------------
;조교 방침(1=휴게/2=소프트/3=노멀/4=하드/5=이상)
;-------------------------------------------------------------------------
@ACT_ACTM2_MENU
SELECTCASE POLICY("지금")
	CASE "하드"
		CALL ADD_CRI_VAR("관장세트", 6)
	CASE "이상"
		CALL ADD_CRI_VAR("관장세트", 4)
ENDSELECT

;-------------------------------------------------------------------------
;조교자의 소질을 참조
;-------------------------------------------------------------------------
@ACT_ACTM2_TALENT_T

;조교자가 겁쟁이/기장
IF TALENT:겁쟁이
	CALL ADD_CRI_VAR("애널바이브", TEQUIP:애널바이브 ? 0 # -3)
	CALL ADD_CRI_VAR("애널비즈", TEQUIP:애널비즈 ? 0 # -2)
	CALL ADD_CRI_VAR("관장세트", -5)
ELSEIF TALENT:기장
	CALL ADD_CRI_VAR("애널바이브", TEQUIP:애널바이브 ? 0 # 3, TEQUIP:애널바이브 ? 0 # 2)
	CALL ADD_CRI_VAR("애널비즈", TEQUIP:애널비즈 ? 0 # 2, TEQUIP:애널바이브 ? 0 # 4)
	CALL ADD_CRI_VAR("관장세트", , 6)
ENDIF

;조교자가 억압/해방
IF TALENT:억압
	CALL ADD_CRI_VAR("바이브", TEQUIP:바이브 ? 0 # -3)
	CALL ADD_CRI_VAR("애널바이브", TEQUIP:애널바이브 ? 0 # -5)
	CALL ADD_CRI_VAR("애널비즈", TEQUIP:애널비즈 ? 0 # -3)
	CALL ADD_CRI_VAR("관장세트", TEQUIP:관장기＋플러그 ? 0 # -5)
ELSEIF TALENT:해방
	CALL ADD_CRI_VAR("바이브", , TEQUIP:바이브 ? 0 # 4)
	CALL ADD_CRI_VAR("애널바이브", , TEQUIP:애널바이브 ? 0 # 7)
	CALL ADD_CRI_VAR("애널비즈", , TEQUIP:애널비즈 ? 0 # 4)
	CALL ADD_CRI_VAR("관장세트", , TEQUIP:관장기＋플러그 ? 0 # 7)
ENDIF

;조교자가 악취민감
SIF TALENT:악취민감
	CALL ADD_CRI_VAR("관장세트", -8)

;조교자가 상냥함
SIF TALENT:상냥함
	CALL ADD_CRI_VAR("관장세트", TEQUIP:관장기＋플러그 ? 0 # -5)

;조교자가 광기
SIF TALENT:광기
	CALL ADD_CRI_VAR("관장세트", , TEQUIP:관장기＋플러그 ? 0 # 8)

;-------------------------------------------------------------------------
;조교 대상의 소질을 참조
;-------------------------------------------------------------------------
@ACT_ACTM2_TALENT_M

;조교 대상이음핵／음경
IF TALENT:MASTER:음핵／음경
	CALL ADD_CRI_VAR("로터", 3, 3)
	CALL ADD_CRI_VAR("C애무도구", (TEQUIP:클리캡 || TEQUIP:오나홀) ? 0 # 4, (TEQUIP:클리캡 || TEQUIP:오나홀) ? 0 # 5)
ENDIF

;조교 대상이 음란
SIF TALENT:MASTER:음란
	CALL ADD_CRI_VAR("바이브", TEQUIP:바이브 ? 0 # 4, TEQUIP:바이브 ? 0 # 5)

;조교 대상이 음고
IF TALENT:MASTER:음고
	CALL ADD_CRI_VAR("애널바이브", TEQUIP:애널바이브 ? 0 # 3, TEQUIP:애널바이브 ? 0 # 7)
	CALL ADD_CRI_VAR("애널비즈", 2, 2)
	CALL ADD_CRI_VAR("관장세트", , TEQUIP:관장기＋플러그 ? 0 # 14)
ENDIF

;조교 대상이 음유
IF TALENT:MASTER:음유
	CALL ADD_CRI_VAR("니플캡", TEQUIP:니플캡 ? 0 # 4, TEQUIP:니플캡 ? 0 # 5)
ENDIF

;조교 대상이 C민감/둔감
IF TALENT:MASTER:C민감
	CALL ADD_CRI_VAR("로터", 3, 2)
	CALL ADD_CRI_VAR("C애무도구", (TEQUIP:클리캡 || TEQUIP:오나홀) ? 0 # 1, (TEQUIP:클리캡 || TEQUIP:오나홀) ? 0 # 6)
ELSEIF TALENT:MASTER:C둔감
	CALL ADD_CRI_VAR("로터", -3, -3)
	CALL ADD_CRI_VAR("C애무도구", (TEQUIP:클리캡 || TEQUIP:오나홀) ? 0 # -1, (TEQUIP:클리캡 || TEQUIP:오나홀) ? 0 # 6)
ENDIF

;조교 대상이 V민감/둔감
IF TALENT:MASTER:V민감
	CALL ADD_CRI_VAR("바이브", TEQUIP:바이브 ? 0 # 4, TEQUIP:바이브 ? 0 # 2)
	CALL ADD_CRI_VAR("페니스밴드", TEQUIP:페니스밴드 ? 0 # 3, TEQUIP:페니스밴드 ? 0 # 1)
ELSEIF TALENT:MASTER:V둔감
	CALL ADD_CRI_VAR("바이브", TEQUIP:바이브 ? 0 # -4, TEQUIP:바이브 ? 0 # -2)
	CALL ADD_CRI_VAR("페니스밴드", TEQUIP:페니스밴드 ? 0 # -3, TEQUIP:페니스밴드 ? 0 # -1)
ENDIF

;조교자가 V민감/둔감
IF TALENT:TARGET:V민감
	CALL ADD_CRI_VAR("페니스밴드", TEQUIP:페니스밴드 ? 0 # 3, TEQUIP:페니스밴드 ? 0 # 1)
ELSEIF TALENT:TARGET:V둔감
	CALL ADD_CRI_VAR("페니스밴드", TEQUIP:페니스밴드 ? 0 # -3, TEQUIP:페니스밴드 ? 0 # -1)
ENDIF

;조교 대상이 A민감/둔감
IF TALENT:MASTER:A민감
	CALL ADD_CRI_VAR("애널바이브", TEQUIP:애널바이브 ? 0 # 3, TEQUIP:애널바이브 ? 0 # 2)
	CALL ADD_CRI_VAR("애널비즈", 2, 4)
ELSEIF TALENT:MASTER:A둔감
	CALL ADD_CRI_VAR("애널바이브", TEQUIP:애널바이브 ? 0 # -3, TEQUIP:애널바이브 ? 0 # -2)
	CALL ADD_CRI_VAR("애널비즈", -2, -4)
ENDIF

;조교 대상이 B민감/둔감
IF TALENT:MASTER:B민감
	CALL ADD_CRI_VAR("니플캡", TEQUIP:니플캡 ? 0 # 3, TEQUIP:니플캡 ? 0 # 3)
ELSEIF TALENT:MASTER:B둔감
	CALL ADD_CRI_VAR("니플캡", TEQUIP:니플캡 ? 0 # -3, TEQUIP:니플캡 ? 0 # -3)
ENDIF

;조교 대상이 후타나리
SIF TALENT:MASTER:후타나리
	CALL ADD_CRI_VAR("C애무도구", (TEQUIP:클리캡 || TEQUIP:오나홀) ? 0 # TALENT:호기심 * 3 - TALENT:무관심, (TEQUIP:클리캡 || TEQUIP:오나홀) ? 0 # 2)

;-----------------------------------------
;조교 대상이 처녀
;-----------------------------------------
@ACT_ACTM2_MAIDEN_M

IF TALENT:MASTER:처녀
	CALL ADD_CRI_VAR("바이브", CFLAG:합의 ? -5 # -10, CFLAG:합의 ? 5 # 0)
	CALL ADD_CRI_VAR("페니스밴드", CFLAG:합의 ? -5 # -10, CFLAG:합의 ? 5 # 0)
ENDIF

;-------------------------------------------------------------------------
;조교자의 능력을 참조
;-------------------------------------------------------------------------
@ACT_ACTM2_ABL_T

;조교자의 가학이[0→0, 5→5]를 가산(0～5)
CALL ADD_CRI_VAR("관장세트", LINE_CALC(ABL:가학, "0→0, 5→5, LIMIT/0～5"))

;-------------------------------------------------------------------------
;조교 대상의 능력을 참조
;-------------------------------------------------------------------------
@ACT_ACTM2_ABL_M

;조교 대상의 C감각이[0→0～1, 4→0～9]를 가산(0～9)
CALL ADD_CRI_VAR("로터", , LINE_CALC(ABL:MASTER:C감각, "0→1, 4→9, LIMIT/0～9"))
;조교 대상의 C감각이[0→1, 5→6]을 가산(1～6)(클리캡, 오나홀 모두 비착용시)
SIF !TEQUIP:클리캡 && !TEQUIP:오나홀
	CALL ADD_CRI_VAR("C애무도구", LINE_CALC(ABL:MASTER:C감각, "0→1, 5→6, LIMIT/1～6"))

;조교 대상의 V감각이[0→0, 1→1～2, 5→3～8]을 가산(0～8)(바이브비착용시)
IF !TEQUIP:바이브
	CALL ADD_CRI_VAR("바이브", LINE_CALC(ABL:MASTER:V감각, "1→1, 5→3, LIMIT/0～3"), LINE_CALC(ABL:MASTER:V감각, "0→0, 5→5, LIMIT/0～5"))
	CALL ADD_CRI_VAR("페니스밴드", LINE_CALC(ABL:MASTER:V감각, "1→1, 5→3, LIMIT/0～3"), LINE_CALC(ABL:MASTER:V감각, "0→0, 5→5, LIMIT/0～5"))
ENDIF

;조교 대상의 A감각이[0→0, 1→1～2, 5→3～8]을 가산(0～8)
CALL ADD_CRI_VAR("애널비즈", LINE_CALC(ABL:MASTER:A감각, "1→1, 5→3, LIMIT/0～3"), LINE_CALC(ABL:MASTER:A감각, "0→0, 5→5, LIMIT/0～5"))
;조교 대상의 A감각이[0→0, 1→1～2, 5→3～8]을 가산(0～8)(A바이브비착용시)
SIF !TEQUIP:애널바이브
	CALL ADD_CRI_VAR("애널바이브", LINE_CALC(ABL:MASTER:A감각, "1→1, 5→3, LIMIT/0～3"), LINE_CALC(ABL:MASTER:A감각, "0→0, 5→5, LIMIT/0～5"))

;조교 대상의 B감각이[0→0, 1→1～2, 5→3～8]을 가산(0～8)(B캡비착용시)
SIF !TEQUIP:니플캡
	CALL ADD_CRI_VAR("니플캡", LINE_CALC(ABL:MASTER:B감각, "1→1, 5→3, LIMIT/0～3"), LINE_CALC(ABL:MASTER:B감각, "0→0, 5→5, LIMIT/0～5"))

;-------------------------------------------------------------------------
;PALAM군
;여기는 개별적으로
;-------------------------------------------------------------------------
;------------------------------------------
;윤활
;------------------------------------------
@ACT_ACTM2_MOIST_M

;조교자에게 약학지식이 없는 경우, 윤활의 체크
IF !TALENT:약학지식
	;조교 대상의 윤활이[0→-5, 100→-3, 250→-1, 500→0]을 가산(-5～0)
	CALL ADD_CRI_VAR("바이브", LINE_CALC(PALAM:MASTER:윤활, "0→-5, 100→-3, 250→-1, 5→0, LIMIT/-5～0"))
	CALL ADD_CRI_VAR("페니스밴드", LINE_CALC(PALAM:MASTER:윤활, "0→-5, 100→-3, 250→-1, 5→0, LIMIT/-5～0"))
	CALL ADD_CRI_VAR("애널바이브", LINE_CALC(PALAM:MASTER:윤활, "0→-5, 100→-3, 250→-1, 5→0, LIMIT/-5～0"))
	;조교 대상의 윤활이[0→-5, 100→-1, 250→0]을 가산(-5～0)
	CALL ADD_CRI_VAR("애널비즈", LINE_CALC(PALAM:MASTER:윤활, "0→-5, 100→-1, 250→0, LIMIT/-5～0"))
ENDIF

;-------------------------------------
;조교자의 죄악감
;-------------------------------------
@ACT_ACTM2_SIN_T

;죄악감이[80→0, 100→-1]을 가산(-1～0)
CALL ADD_CRI_VAR("애널비즈", LINE_CALC(CFLAG:죄악감, "80→0, 100→-1, LIMIT/-1～0"))
;죄악감이[60→0, 80→-1, 100→-2]를 가산(-2～0)
CALL ADD_CRI_VAR("애널바이브", LINE_CALC(CFLAG:죄악감, "60→0, 100→-2, LIMIT/-2～0"))
;죄악감이[0→0, 20→-1, 100→-5]를 가산(-5～0)
CALL ADD_CRI_VAR("관장세트", LINE_CALC(CFLAG:죄악감, "0→0, 100→-5, LIMIT/-5～0"))

;-------------------------------------
;조교자의 ALI
;-------------------------------------
@ACT_ACTM2_ALI_T

;아라이먼트가[-50→-3, 50→3]을 가산(-3～3)
CALL ADD_CRI_VAR("로터", LINE_CALC(CFLAG:아라이먼트, "-50→-3, 50→3, LIMIT/-3～3"))
;아라이먼트가[-50→3, 50→-3]을 가산(-3～3)
CALL ADD_CRI_VAR("관장세트", LINE_CALC(CFLAG:아라이먼트, "-50→3, 50→-3, LIMIT/-3～3"))

;─
;게이지나 상태에 의한 변동
;─
;-------------------------------------
;조교 대상의 체력
;-------------------------------------
@ACT_ACTM2_PHYSICAL_M

;조교 대상의 체력이[0→-3, 50→-1, 250→0]을 가산(-3～0)
CALL ADD_CRI_VAR("로터", LINE_CALC(BASE:MASTER:체력, "0→3, 50→1, 250→0, LIMIT/0～3"))
;※지속해 꾸짖는 도구를 멈춘다
;조교 대상의 체력이[0→+-5, 50→+-3, 250→+-1, 500→0]을 가산(-5～5)
LOCAL = TEQUIP:바이브 ? 1 # -1
CALL ADD_CRI_VAR("바이브", LOCAL * LINE_CALC(BASE:MASTER:체력, "0→5, 50→3, 250→1, 500→0, LIMIT/0～5"))
LOCAL = TEQUIP:페니스밴드 ? 1 # -1
CALL ADD_CRI_VAR("페니스밴드", LOCAL * LINE_CALC(BASE:MASTER:체력, "0→5, 50→3, 250→1, 500→0, LIMIT/0～5"))
LOCAL = TEQUIP:애널바이브 ? 1 # -1
CALL ADD_CRI_VAR("애널바이브", LOCAL * LINE_CALC(BASE:MASTER:체력, "0→5, 50→3, 250→1, 500→0, LIMIT/0～5"))
;조교 대상의 체력이[0→-4/8, 50→-2/4, 250→0]을 가산(-4～8)
LOCAL = TEQUIP:애널비즈 ? 2 # -1
CALL ADD_CRI_VAR("애널비즈", LOCAL * LINE_CALC(BASE:MASTER:체력, "0→4, 50→2, 250→0, LIMIT/0～4"))
;조교 대상의 체력이[0→+-3, 50→+-1, 250→0]을 가산(-3～3)
LOCAL = TEQUIP:클리캡 ? 1 # -1
CALL ADD_CRI_VAR("C애무도구", LOCAL * LINE_CALC(BASE:MASTER:체력, "0→3, 50→1, 250→0, LIMIT/0～3"))
LOCAL = TEQUIP:니플캡 ? 1 # -1
CALL ADD_CRI_VAR("니플캡", LOCAL * LINE_CALC(BASE:MASTER:체력, "0→3, 50→1, 250→0, LIMIT/0～3"))
;조교 대상의 체력이[0→-7/14, 50→-5/10, 250→-2/4]를 가산(-7～14)
LOCAL = TEQUIP:관장기＋플러그 ? 2 # -1
CALL ADD_CRI_VAR("관장세트", LOCAL * LINE_CALC(BASE:MASTER:체력, "0→7, 50→5, 250→2, 500→0, LIMIT/0～7"))

;-------------------------------------
;조교자 상태(0=통상/1=피폐/2=쇠약/3=무기력/4=몽롱/5=정욕/6=분노/7=싫증/8=울적)
;-------------------------------------
@ACT_ACTM2_CONDITION_T
SELECTCASE GET_CONDITION(TARGET)
	CASE "정욕"
		CALL ADD_CRI_VAR("페니스밴드", TEQUIP:페니스밴드 ? 0 # 8)
	CASE "지루함"
		CALL ADD_CRI_VAR("바이브", TEQUIP:바이브 ? 5 # 0)
		CALL ADD_CRI_VAR("페니스밴드", TEQUIP:페니스밴드 ? 5 # 0)
		CALL ADD_CRI_VAR("애널바이브", TEQUIP:애널바이브 ? 5 # 0)
		CALL ADD_CRI_VAR("애널비즈", TEQUIP:애널비즈 ? 5 # 0, TEQUIP:애널비즈 ? 4 # 0)
		CALL ADD_CRI_VAR("C애무도구", (TEQUIP:클리캡 || TEQUIP:오나홀) ? 5 # 0)
		CALL ADD_CRI_VAR("니플캡", TEQUIP:니플캡 ? 5 # 0)
		CALL ADD_CRI_VAR("관장세트", TEQUIP:관장기＋플러그 ? 5 # 0, TEQUIP:관장기＋플러그 ? 4 # 0)
	CASE "울적"
		CALL ADD_CRI_VAR("관장세트", , 10)
ENDSELECT

;─
;전회의 행동이나 상황에 의한 변동
;─
;-------------------------------------
;조교 대상이 자위중
;-------------------------------------
@ACT_ACTM2_MASTURBATION_M

IF GETBIT(TEQUIP:자위중, 0)
	CALL ADD_CRI_VAR("바이브", TEQUIP:바이브 ? 0 # 3, TEQUIP:바이브 ? 0 # 6)
	CALL ADD_CRI_VAR("페니스밴드", TEQUIP:페니스밴드 ? 0 # 3, TEQUIP:페니스밴드 ? 0 # 6)
	CALL ADD_CRI_VAR("애널바이브", TEQUIP:애널바이브 ? 0 # 1, TEQUIP:애널바이브 ? 0 # 8)
	CALL ADD_CRI_VAR("C애무도구", (TEQUIP:클리캡 || TEQUIP:오나홀) ? 0 # 4, (TEQUIP:클리캡 || TEQUIP:오나홀) ? 0 # 5)
	CALL ADD_CRI_VAR("니플캡", TEQUIP:니플캡 ? 0 # 4, TEQUIP:니플캡 ? 0 # 5)
ENDIF

;-------------------------------------
;조교자 자위중
;-------------------------------------
@ACT_ACTM2_MASTURBATION_T
IF GETBIT(TEQUIP:자위중, 1)
	CALL ADD_CRI_VAR("관장세트", -3)
ENDIF

;-------------------------------------
;좀더하드하게해
;-------------------------------------
@ACT_ACTM2_PLEASE_HARD
IF IS_NOWCOMNAME("좀더하드하게해")
	CALL ADD_CRI_VAR("관장세트", TEQUIP:관장기＋플러그 ? 0 # 2, TEQUIP:관장기＋플러그 ? 0 # 2)
ENDIF


;-------------------------------------
;허가를 청했는지 개, 조교자 상태가 울적 이외
;-------------------------------------
@ACT_ACTM2_PLEASE_HELP
IF IS_NOWCOMNAME("허가를청한다") && TCVAR:상태변화 < 8
	CALL ADD_CRI_VAR("바이브", TEQUIP:바이브 ? 3 # 0)
	CALL ADD_CRI_VAR("페니스밴드", TEQUIP:페니스밴드 ? 3 # 0)
	CALL ADD_CRI_VAR("애널바이브", TEQUIP:애널바이브 ? 3 # 0)
	CALL ADD_CRI_VAR("애널비즈", TEQUIP:애널비즈 ? 5 # 0)
	CALL ADD_CRI_VAR("C애무도구", (TEQUIP:클리캡 || TEQUIP:오나홀) ? 2 # 0)
	CALL ADD_CRI_VAR("니플캡", TEQUIP:니플캡 ? 2 # 0)
	CALL ADD_CRI_VAR("관장세트", TEQUIP:관장기＋플러그 > 1 ? 5 # 0)
ENDIF

;─
;같은 행동 연속 실행의 확률을 다운합니다
;─
@ACT_ACTM2_PREV

SELECTCASE TFLAG:전ACT
	CASE 20 TO 26, 68
		CALL ADD_CRI_VAR(GET_ACTNAME(TFLAG:전ACT), -3 * TFLAG:동일행동보너스, -4 * TFLAG:동일행동보너스)
ENDSELECT

;─
;TEQUIP 처리
;─
;도구는 벽통과로 투과 할 수 없기로 한다.
;--------------------------------------
;조교 대상의 복장
;-------------------------------------
@ACT_ACTM2_CLOTH_M

;조교 대상의 하반신윗도리에 방해될 때
IF CLOTHESTYPE(1, 1) == 2
	CALL ADD_CRI_VAR("로터", -3)
	CALL ADD_CRI_VAR("C애무도구", -5)
;조교 대상의 하반신윗도리가 있을 때
ELSEIF CLOTHESTYPE(1, 1)
	CALL ADD_CRI_VAR("로터", -1)
	CALL ADD_CRI_VAR("바이브", -3)
	CALL ADD_CRI_VAR("페니스밴드", -3)
	CALL ADD_CRI_VAR("애널바이브", -3)
	CALL ADD_CRI_VAR("애널비즈", -2)
	CALL ADD_CRI_VAR("C애무도구", -1)
	CALL ADD_CRI_VAR("관장세트", -2)
ENDIF

;조교 대상의 상반신윗도리가 있을 때
SIF CLOTHESTYPE(1, 3)
	CALL ADD_CRI_VAR("니플캡", -3)

;조교 대상의 하반신속옷이 있을 때
IF CLOTHESTYPE(1, 0)
	CALL ADD_CRI_VAR("로터", -1)
	CALL ADD_CRI_VAR("바이브", -3)
	CALL ADD_CRI_VAR("페니스밴드", -3)
	CALL ADD_CRI_VAR("애널바이브", -3)
	CALL ADD_CRI_VAR("애널비즈", -2)
	CALL ADD_CRI_VAR("C애무도구", -1)
ENDIF

;조교 대상의 상반신속옷이 있을 때
SIF CLOTHESTYPE(1, 2)
	CALL ADD_CRI_VAR("니플캡", -3)

;-------------------------------------
;벌중
;-------------------------------------
@ACT_ACTM2_OSHIOKI

SIF TFLAG:벌플래그
	CALL ADD_CRI_VAR("관장세트", 3, 3)

;─
;보정치 계산
;─
@ACT_ACTM2_CORRECTION(ARG)
#DIM LCOUNT

;ARG가 0이외라면 보정치를 계산해, 이후의 처리를 실시하지 않는다
IF ARG
	;68에 관해서는 여기서 계산하지 않는다
	FOR LCOUNT, 20, 30
		CALL ACT_M_RES_CALC(LCOUNT, GET_CRI_VAR(GET_ACTNAME(LCOUNT) + "누계"))
	NEXT
ELSE
	;보정치를 판정치에 가산한다
	CALL ACT_ACTM_CORRECTION(2)
ENDIF

;─
;그 외 변동이나 실행 불가능의 판정
;─
@ACT_ACTM2_ABLE
#DIM LCOUNT

;불가능 판정
CALL ACT_ACTM_ABLE(2)

