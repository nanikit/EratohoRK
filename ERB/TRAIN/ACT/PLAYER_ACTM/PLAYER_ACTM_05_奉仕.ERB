;─
;봉사(손으로 50/입으로 51/가슴으로 52/가랑이 53/족지 55/이라 56/승마 32/가슴을 57)
;─
@ACT_M5(ARG = 0)
;1이상이라면 판정을 실시하지 않는다
#DIM LCOUNT, 1

;함수 초기화
CALL ACT_M_INIT(5)

;처리 개시

LOCALS = ACT_ACTM5

CALL CHA_CRI_VAR, "기준치"
CALLFORM %LOCALS%_REFERENCE

CALL CHA_CRI_VAR, @"이번 방침(%POLICY("지금")%)"
CALLFORM %LOCALS%_COURCE

CALL CHA_CRI_VAR, "조교자의 소질"
CALLFORM %LOCALS%_TALENT_T

CALL CHA_CRI_VAR, "조교 대상의 소질"
CALLFORM %LOCALS%_TALENT_M

CALL CHA_CRI_VAR, "조교자의 성별 의식"
CALLFORM %LOCALS%_GENDER_T

CALL CHA_CRI_VAR, "조교자의 능력"
CALLFORM %LOCALS%_ABL_T

CALL CHA_CRI_VAR, "조교 대상의 능력"
CALLFORM %LOCALS%_ABL_M

CALL CHA_CRI_VAR, "조교자의 죄악감"
CALLFORM %LOCALS%_SIN_T

CALL CHA_CRI_VAR, "조교자의 ALI"
CALLFORM %LOCALS%_ALI_T

CALL CHA_CRI_VAR, "조교자의 사정치"
CALLFORM %LOCALS%_EJACULATION_T

CALL CHA_CRI_VAR, "조교자의 쾌B"
CALLFORM %LOCALS%_PREASURE_B_T

CALL CHA_CRI_VAR, "조교 대상의 체력"
CALLFORM %LOCALS%_PHYSICAL_M

CALL CHA_CRI_VAR, "조교 대상의 기력"
CALLFORM %LOCALS%_MENTAL_M

CALL CHA_CRI_VAR, @"조교자 상태(%CONDITION(TCVAR:상태변화)%)"
CALLFORM %LOCALS%_CONDITION_T

CALL CHA_CRI_VAR, "식스나인 유발"
CALLFORM %LOCALS%_69

CALL CHA_CRI_VAR, "조교 대상의 더러움"
CALLFORM %LOCALS%_STAIN_M

CALL CHA_CRI_VAR, "줄"
CALLFORM %LOCALS%_ROPE

CALL CHA_CRI_VAR, "아이마스크"
CALLFORM %LOCALS%_EYEMASK

CALL CHA_CRI_VAR, "조교 대상 자위중"
CALLFORM %LOCALS%_MASTURBATION_M

CALL CHA_CRI_VAR, "삽입 보정"
CALLFORM %LOCALS%_INSERT

CALL CHA_CRI_VAR, "조교 대상의 의복"
CALLFORM %LOCALS%_CLOTH_M

CALL CHA_CRI_VAR, "허가를 청한다"
CALLFORM %LOCALS%_PLEASE_HELP

CALL CHA_CRI_VAR, "체위"
CALLFORM %LOCALS%_S_POSITION

CALL CHA_CRI_VAR, "연속 행동 보정"
CALLFORM %LOCALS%_PREV

CALL CHA_CRI_VAR, "구상 보정치"
CALL ACT_ACTM_KOJO_CORRECTION_T, 5

CALL CHA_CRI_VAR, "ACT 보정치"
CALLFORM %LOCALS%_CORRECTION(ARG)

;ARG가 1이상이라면 처리 중단
SIF ARG
	RETURN RESULT

CALL CHA_CRI_VAR, "실행 판정"
CALLFORM %LOCALS%_ABLE

;─
;최종 판정
;─
CALL DEF_CRI_VAR("최대 전자")
LOCAL = GET_CRI_VAR("결정")
IF LOCAL == -1
	SIF FLAG:디버그
		PRINTL (봉사 ACT 분류에 실행 가능한 ACT가 존재하지 않습니다)
	TFLAG:ACT = -1
	CALL ACT_MENU_RESULT(2, "봉사")
ELSE
	TFLAG:ACT = GET_ACTNUM(RESULTS)
ENDIF
CALL ANA_CRI_VAR()

;-------------------------------------------------------------------------
;기준치
;-------------------------------------------------------------------------
@ACT_ACTM5_REFERENCE

;필요하지 않을지도라고 생각했으므로 시험삼아 무효화@이것 보고
[SKIPSTART]
CALL ADD_CRI_VAR("손으로 애무를 강제", 10)
CALL ADD_CRI_VAR("입으로 애무를 강제", 10)
CALL ADD_CRI_VAR("파이즈리", 10)
CALL ADD_CRI_VAR("가랑이", 10)
CALL ADD_CRI_VAR("다리 빨고 강제", 2)
CALL ADD_CRI_VAR("이라마치오", 0)
CALL ADD_CRI_VAR("승마위", 5)
CALL ADD_CRI_VAR("가슴 애무 강제", 10)
[SKIPEND]

;------------------------------------------
;이번 조교 방침
;------------------------------------------
@ACT_ACTM5_COURCE
;턴 방침(1=휴게/2=소프트/3=노멀/4=하드/5=이상)
SELECTCASE POLICY("지금")
	CASE "소프트"
		CALL ADD_CRI_VAR("가슴 애무 강제", IS_POLICY("오늘", "러브러브")? 15 # 3)
	CASE "노멀"
		CALL ADD_CRI_VAR("손으로 애무를 강제", IS_POLICY("오늘", "봉사")?  5 # 2)
		CALL ADD_CRI_VAR("구에서 애무를 강제", IS_POLICY("오늘", "봉사")? 15 # 3)
		CALL ADD_CRI_VAR("가슴 애무 강제"    , IS_POLICY("오늘", "봉사")?  4 # 2)
	CASE "하드"
		CALL ADD_CRI_VAR("손으로 애무를 강제", -5)
		CALL ADD_CRI_VAR("구에서 애무를 강제", -3)
		CALL ADD_CRI_VAR("파이즈리"      ,  3)
		CALL ADD_CRI_VAR("가랑이"          ,  2)
		CALL ADD_CRI_VAR("다리 빨고 강제"    ,  5)
		CALL ADD_CRI_VAR("이라마치오"    , 10)
	CASE "이상"
		CALL ADD_CRI_VAR("다리 빨고 강제", 15)
		CALL ADD_CRI_VAR("이라마치오",  5)
ENDSELECT

;-------------------------------------------------------------------------
;조교자의 소질을 참조
;-------------------------------------------------------------------------
@ACT_ACTM5_TALENT_T
;조교자가 겁쟁이/기장
IF TALENT:겁쟁이
	CALL ADD_CRI_VAR("이라마치오", -3)
ELSEIF TALENT:기장
	CALL ADD_CRI_VAR("이라마치오", , 4)
ENDIF

;조교자가 얌전하다/거만함
IF TALENT:얌전함
	CALL ADD_CRI_VAR("다리 빨고 강제", -2, -2)
ELSEIF TALENT:거만함
	CALL ADD_CRI_VAR("다리 빨고 강제", 2, 2)
ENDIF

;조교자가 프라이드높음/낮다
IF TALENT:프라이드높음
	CALL ADD_CRI_VAR("다리 빨고 강제", 1, 6)
	CALL ADD_CRI_VAR("승마위"    , , -2)
ELSEIF TALENT:프라이드낮음
	CALL ADD_CRI_VAR("다리 빨고 강제", -5)
	CALL ADD_CRI_VAR("승마위", , 2)
ENDIF

;조교자가 선을그음
SIF TALENT:선을그음
	CALL ADD_CRI_VAR("다리 빨고 강제", -3)

;조교자가튀고싶어함
SIF TALENT:튀고싶어함
	CALL ADD_CRI_VAR("다리 빨고 강제", 3)

;조교자가 억압/해방
IF TALENT:억압
	CALL ADD_CRI_VAR("손으로 애무를 강제", , 4)
	CALL ADD_CRI_VAR("다리 빨고 강제", -3)
	CALL ADD_CRI_VAR("이라마치오", -3)
ELSEIF TALENT:해방
	CALL ADD_CRI_VAR("파이즈리"  , , 4)
	CALL ADD_CRI_VAR("가랑이"      , , 4)
	CALL ADD_CRI_VAR("다리 빨고 강제", , 4)
	CALL ADD_CRI_VAR("승마위"    , , 4)
ENDIF

;조교자가 수동
IF TALENT:수동
	CALL ADD_CRI_VAR("승마위"        , , 5)
	CALL ADD_CRI_VAR("손으로 애무를 강제", , 4)
	CALL ADD_CRI_VAR("구에서 애무를 강제", , 3)
	CALL ADD_CRI_VAR("가슴 애무 강제"    , , 4)
ENDIF

;조교자가 쾌감을부정
SIF TALENT:쾌감을부정
	CALL ADD_CRI_VAR("다리 빨고 강제", , 5)

;조교자가 새디즘
IF TALENT:새디즘
	CALL ADD_CRI_VAR("다리 빨고 강제", ,  4)
	CALL ADD_CRI_VAR("이라마치오", ,  4)
	CALL ADD_CRI_VAR("가슴 애무 강제", , -4)
ENDIF

;조교자가 심술쟁이/상냥함
IF TALENT:심술쟁이
	CALL ADD_CRI_VAR("다리 빨고 강제", ,  4)
	CALL ADD_CRI_VAR("가슴 애무 강제", , -2)
	CALL ADD_CRI_VAR("승마위"    , , 4)
ELSEIF TALENT:상냥함
	CALL ADD_CRI_VAR("다리 빨고 강제", -3)
	CALL ADD_CRI_VAR("가슴 애무 강제",  3)
	CALL ADD_CRI_VAR("승마위"    , -3)
ENDIF

;조교자가 유치
IF TALENT:유치
	CALL ADD_CRI_VAR("파이즈리"  ,  5)
	CALL ADD_CRI_VAR("가슴 애무 강제", -5)
ENDIF

;조교자가 광기
SIF TALENT:유치
	CALL ADD_CRI_VAR("다리 빨고 강제", 5)

;조교자가 C민감
SIF TALENT:C민감
	CALL ADD_CRI_VAR("다리 빨고 강제", , -8)

;조교자가 작은체형 체형
SIF TALENT:작은체형
	CALL ADD_CRI_VAR("승마위", -2, -4)

;조교자가 빈유
SIF TALENT:빈유
	CALL ADD_CRI_VAR("가슴 애무 강제", -5)

;조교자가 거유
SIF TALENT:거유
	CALL ADD_CRI_VAR("가슴 애무 강제", 5)

;조교자가 모유체질
SIF TALENT:모유체질
	CALL ADD_CRI_VAR("가슴 애무 강제", 8)

;조교자가 미각
SIF TALENT:미각
	CALL ADD_CRI_VAR("다리 빨고 강제", , 4)

;-------------------------------------------------------------------------
;조교 대상의 소질을 참조
;-------------------------------------------------------------------------
@ACT_ACTM5_TALENT_M
;조교 대상이 고양이혀
SIF TALENT:MASTER:고양이혀
	CALL ADD_CRI_VAR("구에서 애무를 강제", -10 + TALENT:도착적 * 2 - TALENT:드M * 5)

;조교 대상이 혀사용
SIF TALENT:MASTER:혀사용
	CALL ADD_CRI_VAR("구에서 애무를 강제", 3, 4)

;조교 대상이기묘한손가락
SIF TALENT:MASTER:기묘한손가락
	CALL ADD_CRI_VAR("손으로 애무를 강제", 3, 4)

;조교 대상이 허리사용
SIF TALENT:MASTER:허리사용
	CALL ADD_CRI_VAR("승마위", 5)

;조교 대상이 음란
SIF TALENT:MASTER:음란
	CALL ADD_CRI_VAR("승마위", 3, 4)

;조교 대상이 음유
SIF TALENT:MASTER:음유
	CALL ADD_CRI_VAR("파이즈리", 3, 4)

;조교 대상이 빈유
SIF TALENT:MASTER:빈유
	CALL ADD_CRI_VAR("파이즈리", -5)

;조교 대상이 거유
SIF TALENT:MASTER:거유
	CALL ADD_CRI_VAR("파이즈리", 5)

;-------------------------------------------------------------------------
;조교자의 성별의 기호를 참조
;-------------------------------------------------------------------------
@ACT_ACTM5_GENDER_T
;조교자에게 양도가 없고, 여자 아이끼리
IF !TALENT:남자 && !TALENT:MASTER:남자 && !TALENT:양도
	;조교자의 레즈끼가[0→-2, 2→2]를 가산(-2~2)
	CALL ADD_CRI_VAR("손으로 애무를 강제", LINE_CALC(ABL:레즈끼, "0→-2,2→2,LIMIT/-2～2"))
	CALL ADD_CRI_VAR("다리 빨고 강제", LINE_CALC(ABL:레즈끼, "0→-2,2→0,LIMIT/-2～0"))
	;조교자의 레즈끼가[0→-4, 4→0]을 가산(-4~0)
	CALL ADD_CRI_VAR("가랑이", LINE_CALC(ABL:레즈끼, "0→-4,4→0,LIMIT/-4～0"))
	;조교자의 레즈끼가[0→-5, 5→3]을 가산(-5~3)
	CALL ADD_CRI_VAR("구에서 애무를 강제", LINE_CALC(ABL:레즈끼, "0→-5,5→3,LIMIT/-5～3"))
	CALL ADD_CRI_VAR("파이즈리", LINE_CALC(ABL:레즈끼, "0→-5,5→0,LIMIT/-5～0"))
	CALL ADD_CRI_VAR("이라마치오", LINE_CALC(ABL:레즈끼, "0→-5,5→0,LIMIT/-5～0"))
	CALL ADD_CRI_VAR("승마위", LINE_CALC(ABL:레즈끼, "0→-5,5→0,LIMIT/-5～0"))
	
	;조교자의 레즈중독이[0→0, 5→8]을 가산(0~8)
	CALL ADD_CRI_VAR("구에서 애무를 강제", , LINE_CALC(ABL:레즈중독, "0→0,5→8,LIMIT/0～8"))
	CALL ADD_CRI_VAR("파이즈리", , LINE_CALC(ABL:레즈중독, "0→0,5→8,LIMIT/0～8"))
	CALL ADD_CRI_VAR("가랑이", , LINE_CALC(ABL:레즈중독, "0→0,5→8,LIMIT/0～8"))
ENDIF

;조교자에게 양도가 없고, 남자끼리
IF TALENT:남자 && TALENT:MASTER:남자 && !TALENT:양도
	;조교자의 ＢＬ끼이[0→-3, 3→0]을 가산(-3~0)
	CALL ADD_CRI_VAR("손으로 애무를 강제", LINE_CALC(ABL:ＢＬ끼, "0→-3,3→0,LIMIT/-3～0"))
	CALL ADD_CRI_VAR("다리 빨고 강제", LINE_CALC(ABL:ＢＬ끼, "0→-3,3→0,LIMIT/-3～0"))
	;조교자의 ＢＬ끼이[0→-4, 4→0]을 가산(-4~0)
	CALL ADD_CRI_VAR("이라마치오", LINE_CALC(ABL:ＢＬ끼, "0→-4,4→0,LIMIT/-4～0"))
	;조교자의 ＢＬ끼이[0→-5, 5→0]을 가산(-5~0)
	CALL ADD_CRI_VAR("구에서 애무를 강제", LINE_CALC(ABL:ＢＬ끼, "0→-5,5→0,LIMIT/-5～0"))
ENDIF

;조교자가 남성혐오로 조교 대상이 남자. 진전도에 응해 완화하도록(듯이)
IF TALENT:남성혐오 && TALENT:MASTER:남자
	CALL ADD_CRI_VAR("손으로 애무를 강제", CFLAG:M호의 + CFLAG:호의 >= 10000 ? -1 # -3)
	CALL ADD_CRI_VAR("구에서 애무를 강제", CFLAG:M호의 + CFLAG:호의 >= 10000 ? -3 # -5)
	CALL ADD_CRI_VAR("다리 빨고 강제", , CFLAG:M호의 + CFLAG:호의 >= 10000 ? 1 # 4)
ENDIF

;─
;능력, 파라미터에 의한 변동
;─
;-------------------------------------------------------------------------
;조교자의 능력을 참조
;-------------------------------------------------------------------------
@ACT_ACTM5_ABL_T

;조교자의 C감각이[0→0, 3→4, 6→7]을 가산(0~4)
CALL ADD_CRI_VAR("구에서 애무를 강제", LINE_CALC(ABL:C감각, "0→0,3→4,6→7,LIMIT/0～7"))
;조교자의 C감각이[1→0, 5→4]를 가산(0~4)
CALL ADD_CRI_VAR("파이즈리", LINE_CALC(ABL:C감각, "1→0,5→4,LIMIT/0～4"))
CALL ADD_CRI_VAR("가랑이", LINE_CALC(ABL:C감각, "1→0,5→4,LIMIT/0～4"))
;조교자의 C감각이[2→0, 5→3~5]를 가산(0~5)
CALL ADD_CRI_VAR("이라마치오", LINE_CALC(ABL:C감각, "2→0,5→3,LIMIT/0～3"), LINE_CALC(ABL:C감각, "2→0,5→2,LIMIT/0～2"))
CALL ADD_CRI_VAR("승마위", LINE_CALC(ABL:C감각, "2→0,5→3,LIMIT/0～3"), LINE_CALC(ABL:C감각, "2→0,5→2,LIMIT/0～2"))
;조교자의 C감각이[0→0, 2→3, 5→5]를 가산(0~5)
CALL ADD_CRI_VAR("손으로 애무를 강제", LINE_CALC(ABL:C감각, "0→0,2→3,5→5,LIMIT/0～5"))

;조교자의 성교가[0→0, 5→3~5]를 가산(0~5)
CALL ADD_CRI_VAR("승마위", LINE_CALC(ABL:성교, "0→0,5→3,LIMIT/0～3"), LINE_CALC(ABL:성교, "0→0,5→2,LIMIT/0～2"))

;-------------------------------------------------------------------------
;조교 대상의 능력을 참조
;-------------------------------------------------------------------------
@ACT_ACTM5_ABL_M

;조교 대상의 V감각이[0→0, 5→2~8]을 가산(0~8)
CALL ADD_CRI_VAR("승마위", LINE_CALC(ABL:MASTER:V감각, "0→0,5→2,LIMIT/0～2"), LINE_CALC(ABL:MASTER:V감각, "0→0,5→6,LIMIT/0～6"))

;-------------------------------------
;조교자의 죄악감
;-------------------------------------
@ACT_ACTM5_SIN_T

;죄악감이[60→0, 100→-2]를 가산(-2~0)
CALL ADD_CRI_VAR("풋잡", LINE_CALC(CFLAG:죄악감, "60→0,100→-2,LIMIT/-2～0"))
;죄악감이[40→0, 100→-3]을 가산(-3~0)
CALL ADD_CRI_VAR("다리 빨고 강제", LINE_CALC(CFLAG:죄악감, "40→0,100→-3,LIMIT/-3～0"))
;죄악감이[0→0, 100→-5]를 가산(-5~0)
CALL ADD_CRI_VAR("이라마치오", LINE_CALC(CFLAG:죄악감, "0→0,100→-5,LIMIT/-5～0"))

;-------------------------------------
;조교자의 ALI
;-------------------------------------
@ACT_ACTM5_ALI_T

;아라이먼트가[-50→-3, 50→3]을 가산(-3~3)
CALL ADD_CRI_VAR("손으로 애무를 강제", LINE_CALC(CFLAG:아라이먼트, "-50→-3,50→3,LIMIT/-3～3"))
;아라이먼트가[-50→3, 50→-3]을 가산(-3~3)
CALL ADD_CRI_VAR("이라마치오", LINE_CALC(CFLAG:아라이먼트, "-50→3,50→-3,LIMIT/-3～3"))
;아라이먼트가[-50→-6, 0→0]을 가산(-6~0)
CALL ADD_CRI_VAR("가슴 애무 강제", LINE_CALC(CFLAG:아라이먼트, "-50→-6,0→0,LIMIT/-6～0"))

;─
;게이지나 상태에 의한 변동
;─
;-------------------------------------
;조교자의 사정치
;-------------------------------------
@ACT_ACTM5_EJACULATION_T

;조교자의 사정치가[4000→0, 8000→3]을 가산(0~3)
CALL ADD_CRI_VAR("손으로 애무를 강제", LINE_CALC(BASE:사정, "4000→0,8000→3,LIMIT/0～3"))
CALL ADD_CRI_VAR("파이즈리", LINE_CALC(BASE:사정, "4000→0,8000→3,LIMIT/0～3"))
CALL ADD_CRI_VAR("가랑이", LINE_CALC(BASE:사정, "4000→0,8000→3,LIMIT/0～3"))
CALL ADD_CRI_VAR("승마위", LINE_CALC(BASE:사정, "4000→0,8000→3,LIMIT/0～3"))
;조교자의 사정치가[4000→0, 8000→5]를 가산(0~5)
CALL ADD_CRI_VAR("구에서 애무를 강제", LINE_CALC(BASE:사정, "4000→0,8000→5,LIMIT/0～5"))
CALL ADD_CRI_VAR("이라마치오", LINE_CALC(BASE:사정, "4000→0,8000→5,LIMIT/0～5"))

;----------------------------------
;조교자의 쾌B
;---------------------------------
@ACT_ACTM5_PREASURE_B_T

;조교자의 쾌B가[0→0, 5000→3]을 가산(0~3)
CALL ADD_CRI_VAR("가슴 애무 강제", LINE_CALC(PALAM:쾌B, "0→0,5000→3,LIMIT/0～3"))

;-------------------------------------
;조교 대상의 체력
;-------------------------------------
@ACT_ACTM5_PHYSICAL_M

;조교 대상의 체력이[0→-3, 1000→0]을 가산(-3~0)
CALL ADD_CRI_VAR("파이즈리", LINE_CALC(BASE:MASTER:체력, "0→-3,1000→0,LIMIT/-3～0"))
;조교 대상의 체력이[0→-5, 1500→0]을 가산(-5~0)
CALL ADD_CRI_VAR("이라마치오", LINE_CALC(BASE:MASTER:체력, "0→-5,1500→0,LIMIT/-5～0"))
CALL ADD_CRI_VAR("승마위", LINE_CALC(BASE:MASTER:체력, "0→-5,1500→0,LIMIT/-5～0"))

;-------------------------------------
;조교 대상의 기력
;-------------------------------------
@ACT_ACTM5_MENTAL_M

;조교 대상의 기력이[0→-3, 500→0]을 가산(-3~0)
CALL ADD_CRI_VAR("구에서 애무를 강제", LINE_CALC(BASE:MASTER:기력, "0→-3,500→0,LIMIT/-3～0"))
;조교 대상의 기력이[0→-5, 750→0]을 가산(-5~0)
CALL ADD_CRI_VAR("다리 빨고 강제", LINE_CALC(BASE:MASTER:기력, "0→-5,750→0,LIMIT/-5～0"))

;-------------------------------------
;조교자 상태
;-------------------------------------
@ACT_ACTM5_CONDITION_T

;조교자 상태(0=통상/1=피폐/2=쇠약/3=무기력/4=몽롱/5=정욕/6=분노/7=싫증/8=울적)
SELECTCASE GET_CONDITION(TARGET)
	CASE "피폐", "쇠약"
		CALL ADD_CRI_VAR("손으로 애무를 강제", -1 - TFLAG:조교자소모도 / 2)
		CALL ADD_CRI_VAR("구에서 애무를 강제", -2 - TFLAG:조교자소모도 / 2)
		CALL ADD_CRI_VAR("파이즈리", -2 - TFLAG:조교자소모도)
		CALL ADD_CRI_VAR("가랑이", -3 - TFLAG:조교자소모도)
		CALL ADD_CRI_VAR("이라마치오", -3 - TFLAG:조교자소모도 * 2)
		CALL ADD_CRI_VAR("승마위", -1 - TFLAG:조교자소모도 * 2)
	CASE "정욕"
		CALL ADD_CRI_VAR("구에서 애무를 강제", 3, 6)
		CALL ADD_CRI_VAR("가슴 애무 강제", 2, 4)
	CASE "분노"
		CALL ADD_CRI_VAR("이라마치오", 3, 6)
	CASE "지루함"
		CALL ADD_CRI_VAR("가랑이", , 2)
		CALL ADD_CRI_VAR("다리 빨고 강제", , 4)
	CASE "울적"
		CALL ADD_CRI_VAR("다리 빨고 강제", 1, 4)
		CALL ADD_CRI_VAR("이라마치오", 1, 4)
ENDSELECT

;─
;전회의 행동이나 상황에 의한 변동
;─
;식스나인에의 경향
@ACT_ACTM5_69

IF TEQUIP:안면승마 || TEQUIP:안면승마애널
	CALL ADD_CRI_VAR("가랑이", -5)
	CALL ADD_CRI_VAR("다리 빨고 강제", -5)
	CALL ADD_CRI_VAR("승마위", -5)
ENDIF
IF TEQUIP:조교대상C사용 == 2
	CALL ADD_CRI_VAR("구에서 애무를 강제", 5)
ENDIF

;더러운 따위의 처리(조교자에게 불결무시가 없는 경우)
@ACT_ACTM5_STAIN_M

IF !TALENT:불결무시
	;조교 대상의 입
	;<정령><A>의 몇개의 더러움이 있다
	IF STAIN:MASTER:구 & 0b00001100
		CALL ADD_CRI_VAR("구에서 애무를 강제", -2)
		CALL ADD_CRI_VAR("다리 빨고 강제", -1)
		CALL ADD_CRI_VAR("이라마치오", -2)
	ENDIF
	;<피><점액>의 몇개의 더러움이 있다
	IF STAIN:MASTER:구 & 0b01100000
		CALL ADD_CRI_VAR("구에서 애무를 강제", -5)
		CALL ADD_CRI_VAR("다리 빨고 강제", -3)
		CALL ADD_CRI_VAR("이라마치오", -5)
	ENDIF
	;조교 대상의 손
	;<정령><A>의 몇개의 더러움이 있다
	SIF STAIN:MASTER:손 & 0b00001100
		CALL ADD_CRI_VAR("손으로 애무를 강제", -2)
	;<피><점액>의 몇개의 더러움이 있다
	SIF STAIN:MASTER:손 & 0b01100000
		CALL ADD_CRI_VAR("손으로 애무를 강제", -5)
	;조교 대상의 V
	;<피><점액>의 몇개의 더러움이 있다
	IF STAIN:MASTER:바기나 & 0b01100000
		CALL ADD_CRI_VAR("파이즈리", -5)
		CALL ADD_CRI_VAR("승마위", -3)
	ENDIF
	;조교 대상의 B
	;<정령><A>의 몇개의 더러움이 있다
	SIF STAIN:MASTER:가슴 & 0b00001100
		CALL ADD_CRI_VAR("가랑이", -2)
	;<피><점액>의 몇개의 더러움이 있다
	SIF STAIN:MASTER:가슴 & 0b01100000
		CALL ADD_CRI_VAR("가랑이", -5)
	;관장
	SIF TEQUIP:관장기＋플러그
		CALL ADD_CRI_VAR("가랑이", -3, -4)
ENDIF

;-------------------------------------
;줄
;-------------------------------------
@ACT_ACTM5_ROPE

IF TEQUIP:줄
	CALL ADD_CRI_VAR("구에서 애무를 강제", -5)
	CALL ADD_CRI_VAR("가랑이", -5)
	CALL ADD_CRI_VAR("이라마치오", 10)
	CALL ADD_CRI_VAR("가슴 애무 강제", -5)
ENDIF

;-------------------------------------
;아이마스크
;-------------------------------------
@ACT_ACTM5_EYEMASK

IF TEQUIP:아이마스크
	CALL ADD_CRI_VAR("손으로 애무를 강제", -3)
	CALL ADD_CRI_VAR("구에서 애무를 강제", -2)
	CALL ADD_CRI_VAR("파이즈리", -5)
	CALL ADD_CRI_VAR("가랑이", -5)
	CALL ADD_CRI_VAR("승마위", -7)
	CALL ADD_CRI_VAR("가슴 애무 강제", -3)
ENDIF

;-------------------------------------
;조교 대상 자위중
;-------------------------------------
@ACT_ACTM5_MASTURBATION_M

SIF GETBIT(TEQUIP:자위중, 0)
	CALL ADD_CRI_VAR("구에서 애무를 강제", 5)

;-------------------------------------
;삽입
;-------------------------------------
@ACT_ACTM5_INSERT

IF TEQUIP:성교중
	CALL ADD_CRI_VAR("승마위", 10)
	CALL ADD_CRI_VAR("가슴 애무 강제", 10)
ELSEIF TEQUIP:성교봉사중
	CALL ADD_CRI_VAR("가슴 애무 강제", 10)
ENDIF

;-------------------------------------
;조교 대상의 의복
;-------------------------------------
@ACT_ACTM5_CLOTH_M

;조교 대상이 악세사리에 장갑을 몸에 지니고 있을 때
SIF TEQUIP:MASTER:악세사리 == 1
	CALL ADD_CRI_VAR("손으로 애무를 강제", 1, 2)

;조교 대상이 하반신 속옷을 입고 있을 때
IF CLOTHESTYPE(1, 0)
	CALL ADD_CRI_VAR("가랑이", -5)
	CALL ADD_CRI_VAR("승마위", -5)
ENDIF

;조교 대상이 상반신 속옷을 입고 있을 때
SIF CLOTHESTYPE(1, 2)
	CALL ADD_CRI_VAR("파이즈리", -5)

;조교 대상의 하반신 윗도리에 방해될 때
SIF CLOTHESTYPE(1, 1) == 2
	CALL ADD_CRI_VAR("가랑이", -3)

;-------------------------------------
;허가를 청한다
;-------------------------------------
@ACT_ACTM5_PLEASE_HELP

;허가를 청했는지 개, 조교자 상태가 울적 이외
SIF IS_NOWCOMNAME("허가를 청한다") && TCVAR:상태변화 < 8
	CALL ADD_CRI_VAR("이라마치오", -5)

;─
;체위 관련
;─
@ACT_ACTM5_S_POSITION

;체위 변경의 허들
SIF IS_PREVACTNAME("승마위")
	CALL ADD_CRI_VAR(GET_ACTNAME(TFLAG:전ACT), 5, 4)

;─
;같은 행동 연속 실행의 확률을 다운합니다
;─
@ACT_ACTM5_PREV

SIF INRANGE(TFLAG:ACT, 50, 57)
	CALL ADD_CRI_VAR(GET_ACTNAME(TFLAG:전ACT), -3 * TFLAG:동일행동보너스, -4 * TFLAG:동일행동보너스)

;─
;보정치 계산
;─
@ACT_ACTM5_CORRECTION(ARG)
#DIM LCOUNT

;ARG가 0이외라면 보정치를 계산해, 이후의 처리를 실시하지 않는다
IF ARG
	;32에 관해서는 여기서 계산하지 않는다
	FOR LCOUNT, 50, 58
		CALL ACT_M_RES_CALC(LCOUNT, GET_CRI_VAR(GET_ACTNAME(LCOUNT) + "누계"))
	NEXT
ELSE
	;보정치를 판정치에 가산한다
	CALL ACT_ACTM_CORRECTION(5)
ENDIF

;─
;그 외 변동이나 실행 불가능의 판정
;─
@ACT_ACTM5_ABLE
#DIM LCOUNT

;불가능 판정
CALL ACT_ACTM_ABLE(5)

;봉사계 승마위를 일단 실행 불가에.
;성교계 보정을 고려하는 것이 지난했던 싶은
;ACTM_NUM도 하는 김에 죽이고 있다
CALL DIM_CRI_VAR("승마위", -999)

