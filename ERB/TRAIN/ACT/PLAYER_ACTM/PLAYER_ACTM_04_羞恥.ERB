;─
;수치(자위 40/개장 41/보이고 42/대거울 43/야외 44/방뇨 45/코스(미실장)46/방치 73)
;─
@ACT_M4(ARG = 0)
;1이상이라면 판정을 실시하지 않는다
#DIM LCOUNT, 1

;함수 초기화
CALL ACT_M_INIT(4)

;처리 개시

LOCALS = ACT_ACTM4

CALL CHA_CRI_VAR, "기준치"
CALLFORM %LOCALS%_REFERENCE

CALL CHA_CRI_VAR, "조교자의 소질"
CALLFORM %LOCALS%_TALENT_T

CALL CHA_CRI_VAR, "조교 대상의 소질"
CALLFORM %LOCALS%_TALENT_M

CALL CHA_CRI_VAR, "조교 대상의 능력"
CALLFORM %LOCALS%_ABL_M

CALL CHA_CRI_VAR, "조교자의 능력"
CALLFORM %LOCALS%_ABL_T

CALL CHA_CRI_VAR, "조교 대상의 욕정"
CALLFORM %LOCALS%_DESIRE_M

CALL CHA_CRI_VAR, "조교자의 죄악감"
CALLFORM %LOCALS%_SIN_T

CALL CHA_CRI_VAR, "조교자의 ALI"
CALLFORM %LOCALS%_ALI_T

CALL CHA_CRI_VAR, "조교 대상의 체력"
CALLFORM %LOCALS%_PHYSICAL_M

CALL CHA_CRI_VAR, "조교 대상의 기력"
CALLFORM %LOCALS%_MENTAL_M

CALL CHA_CRI_VAR, "조교자의 흥미"
CALLFORM %LOCALS%_INTEREST_T

CALL CHA_CRI_VAR, "조교자의 만족"
CALLFORM %LOCALS%_SATISFACTION_T

CALL CHA_CRI_VAR, @"조교자 상태(%CONDITION(TCVAR:상태변화)%)"
CALLFORM %LOCALS%_CONDITION_T

CALL CHA_CRI_VAR, @"조교 대상 상태(%CONDITION(TCVAR:MASTER:상태변화)%)"
CALLFORM %LOCALS%_CONDITION_M

CALL CHA_CRI_VAR, "미약"
CALLFORM %LOCALS%_DRAG

CALL CHA_CRI_VAR, "이뇨제"
CALLFORM %LOCALS%_DIURETIC

CALL CHA_CRI_VAR, "장착 도구"
CALLFORM %LOCALS%_TOOLS

CALL CHA_CRI_VAR, "줄"
CALLFORM %LOCALS%_ROPE

CALL CHA_CRI_VAR, "아이마스크"
CALLFORM %LOCALS%_EYEMASK

CALL CHA_CRI_VAR, "볼개그"
CALLFORM %LOCALS%_GAG

CALL CHA_CRI_VAR, "삼각목마"
CALLFORM %LOCALS%_HORSE

CALL CHA_CRI_VAR, "야외플레이"
CALLFORM %LOCALS%_OUT

CALL CHA_CRI_VAR, "대거울"
CALLFORM %LOCALS%_T_MILLER

CALL CHA_CRI_VAR, "도구를 떼었다"
CALLFORM %LOCALS%_TAKE_OFF_TOOLS

CALL CHA_CRI_VAR, "조교자 자위중"
CALLFORM %LOCALS%_MASTURBATION_T

CALL CHA_CRI_VAR, "조교 대상 자위중"
CALLFORM %LOCALS%_MASTURBATION_M

CALL CHA_CRI_VAR, "조교 대상의 의복"
CALLFORM %LOCALS%_CLOTH_M

CALL CHA_CRI_VAR, "좀더하드하게해"
CALLFORM %LOCALS%_PLEASE_HARD

CALL CHA_CRI_VAR, "허가를청한다"
CALLFORM %LOCALS%_PLEASE_HELP

CALL CHA_CRI_VAR, "연속행동 보정"
CALLFORM %LOCALS%_PREV

CALL CHA_CRI_VAR, "구상 보정치"
CALL ACT_ACTM_KOJO_CORRECTION_T, 4

CALL CHA_CRI_VAR, "ACT 보정치"
CALLFORM %LOCALS%_CORRECTION(ARG)

;ARG가 1이상이라면 처리 중단
SIF ARG
	RETURN RESULT

CALL CHA_CRI_VAR, "실행 판정"
CALLFORM %LOCALS%_ABLE

;─
;최종 판정
;─
CALL DEF_CRI_VAR("최대 전자")
LOCAL = GET_CRI_VAR("결정")
IF LOCAL == -1
	SIF FLAG:디버그
		PRINTL (수치 ACT분류에 실행 가능한 ACT가 존재하지 않습니다)
	TFLAG:ACT = -1
	CALL ACT_MENU_RESULT(2, "수치")
ELSE
	TFLAG:ACT = GET_ACTNUM(RESULTS)
ENDIF
CALL ANA_CRI_VAR()

;-------------------------------------------------------------------------
;기준치
;-------------------------------------------------------------------------
@ACT_ACTM4_REFERENCE

;필요하지 않을지도라고 생각했으므로 시험삼아 무효화@이것 보고
[SKIPSTART]
CALL ADD_CRI_VAR("자위", 10)
CALL ADD_CRI_VAR("비패개장", 10)
CALL ADD_CRI_VAR("자위과시하고", 8)
CALL ADD_CRI_VAR("수치플레이", 8)
CALL ADD_CRI_VAR("야외플레이", 0)
CALL ADD_CRI_VAR("방뇨", 5)
CALL ADD_CRI_VAR("코스프레", 5)
CALL ADD_CRI_VAR("방치플레이", 0)
[SKIPEND]

;-------------------------------------------------------------------------
;조교자의 소질을 참조
;-------------------------------------------------------------------------
@ACT_ACTM4_TALENT_T

;조교자가 프라이드높음/낮다
IF TALENT:프라이드높음
	CALL ADD_CRI_VAR("자위과시하고", GETBIT(TEQUIP:자위중, 1) ? 0 # -5)
ELSEIF TALENT:프라이드낮음
	CALL ADD_CRI_VAR("자위과시하고", GETBIT(TEQUIP:자위중, 1) ? 0 # 5)
ENDIF

;조교자가 자제적/충동적
IF TALENT:자제적
	CALL ADD_CRI_VAR("자위과시하고", GETBIT(TEQUIP:자위중, 1) ? 0 # -3)
	CALL ADD_CRI_VAR("야외플레이", -3)
ELSEIF TALENT:충동적
	CALL ADD_CRI_VAR("자위과시하고", GETBIT(TEQUIP:자위중, 1) ? 0 # 3)
	CALL ADD_CRI_VAR("야외플레이", 3)
ENDIF

;조교자가 무관심/호기심
IF TALENT:무관심
	CALL ADD_CRI_VAR("자위", GETBIT(TEQUIP:자위중, 0) ? 0 # -2)
	CALL ADD_CRI_VAR("비패개장", -3)
	CALL ADD_CRI_VAR("방치플레이", 3, 4)
ELSEIF TALENT:호기심
	CALL ADD_CRI_VAR("자위", GETBIT(TEQUIP:자위중, 0) ? 0 # 1, GETBIT(TEQUIP:자위중, 0) ? 0 # 3)
	CALL ADD_CRI_VAR("비패개장", 2, 3)
ENDIF

;조교자가 선을그음
SIF TALENT:선을그음 && !TEQUIP:야외플레이
	CALL ADD_CRI_VAR("야외플레이", -5)

;조교자가튀고싶어함 한편, 조교자가 자위하고 있지 않다
SIF TALENT:튀고싶어함 && !GETBIT(TEQUIP:자위중, 1)
	CALL ADD_CRI_VAR("자위과시하고", 5, 5)

;조교자가 억압/해방
IF TALENT:억압
	CALL ADD_CRI_VAR("자위과시하고", GETBIT(TEQUIP:자위중, 1) ? 0 # -3)
	CALL ADD_CRI_VAR("야외플레이", TEQUIP:야외플레이 ? 0 # -5)
	CALL ADD_CRI_VAR("방뇨", -5)
	CALL ADD_CRI_VAR("코스프레", -5)
ELSEIF TALENT:해방
	CALL ADD_CRI_VAR("자위과시하고", , GETBIT(TEQUIP:자위중, 1) ? 0 # 5)
	CALL ADD_CRI_VAR("야외플레이", , TEQUIP:야외플레이 ? 0 # 7)
	CALL ADD_CRI_VAR("방뇨", , 7)
	CALL ADD_CRI_VAR("코스프레", , 7)
ENDIF

;조교자가 부끄러워해/수치심옅음
IF TALENT:수줍음
	CALL ADD_CRI_VAR("자위과시하고", GETBIT(TEQUIP:자위중, 1) ? 0 # -7)
	CALL ADD_CRI_VAR("수치플레이", TEQUIP:대거울 ? 0 # -5)
ELSEIF TALENT:수치심옅음
	CALL ADD_CRI_VAR("자위과시하고", , GETBIT(TEQUIP:자위중, 1) ? 0 # 5)
	CALL ADD_CRI_VAR("수치플레이", TEQUIP:대거울 ? 0 # 5)
ENDIF

;조교자가 젖기쉬움
SIF TALENT:젖기쉬움
	CALL ADD_CRI_VAR("비패개장", , 2)

;조교자가 자위쉬움 한편, 조교자가 자위하고 있지 않다
SIF TALENT:자위쉬움 && !GETBIT(TEQUIP:자위중, 1)
	CALL ADD_CRI_VAR("자위과시하고", 3, 1)

;조교자가 불결무시, 악취둔감/민감
IF TALENT:불결무시 || TALENT:악취둔감
	CALL ADD_CRI_VAR("방뇨", 1, 4)
ELSEIF TALENT:악취민감
	CALL ADD_CRI_VAR("방뇨", 4)
ENDIF

;조교자가 쾌감에순수/부정
IF TALENT:쾌감에순수
	CALL ADD_CRI_VAR("자위과시하고", GETBIT(TEQUIP:자위중, 1) ? 0 # 3)
ELSEIF TALENT:쾌감을부정
	CALL ADD_CRI_VAR("자위과시하고", , GETBIT(TEQUIP:자위중, 1) ? 0 # -6)
ENDIF

;조교자가 남성혐오 한편, 조교 대상이 남자
SIF TALENT:남성혐오 && TALENT:MASTER:남자
	CALL ADD_CRI_VAR("자위", -5)

;조교자가 새디즘
IF TALENT:새디즘
	CALL ADD_CRI_VAR("야외플레이", , TEQUIP:야외플레이 ? 0 # 4)
	CALL ADD_CRI_VAR("방뇨", 3, 3)
ENDIF

;조교자가 신중하고, 야외플레이 안이 아니다
SIF TALENT:신중 && !TEQUIP:야외플레이
	CALL ADD_CRI_VAR("야외플레이", -3)

;조교자가 심술쟁이
IF TALENT:심술쟁이
	CALL ADD_CRI_VAR("수치플레이", TEQUIP:대거울 ? 0 # 3)
	CALL ADD_CRI_VAR("야외플레이", TEQUIP:야외플레이 ? 0 # 1)
	CALL ADD_CRI_VAR("방뇨", 3)
	CALL ADD_CRI_VAR("방치플레이", 3)
ENDIF

;조교자가 유치
SIF TALENT:유치
	CALL ADD_CRI_VAR("방뇨", , 4)

;조교자가 매혹 한편, 조교 대상이 자위하고 있지 않다
SIF TALENT:매혹 && !GETBIT(TEQUIP:자위중, 0)
	CALL ADD_CRI_VAR("자위", , 6)

;-------------------------------------------------------------------------
;조교 대상의 소질을 참조
;-------------------------------------------------------------------------
@ACT_ACTM4_TALENT_M

;조교 대상이 자위쉬움 한편, 조교 대상이 자위하고 있지 않다
SIF TALENT:MASTER:자위쉬움 && !GETBIT(TEQUIP:자위중, 0)
	CALL ADD_CRI_VAR("자위", 3, 1)

;─
;능력, 파라미터에 의한 변동
;─
;-------------------------------------------------------------------------
;조교 대상의 능력을 참조
;-------------------------------------------------------------------------
@ACT_ACTM4_ABL_M

;조교 대상의 노출버릇. 조교가 진행된 상태로 보다 하드한 메뉴를 선택하는 경향
;가[0→2, 5→-5]를 가산(0～7)
CALL ADD_CRI_VAR("비패개장", LINE_CALC(ABL:MASTER:노출버릇, "0→2, 5→-5, LIMIT/-5～2"))
;가[2→0, 5→2～8]을 가산(0～8)
CALL ADD_CRI_VAR("방뇨", LINE_CALC(ABL:MASTER:노출버릇, "2→0, 5→2, LIMIT/0～2"), LINE_CALC(ABL:MASTER:노출버릇, "2→0, 5→6, LIMIT/0～6"))
;가[1→0, 5→0～9]를 가산(0～9)
CALL ADD_CRI_VAR("코스프레", , LINE_CALC(ABL:MASTER:노출버릇, "1→0, 5→9, LIMIT/0～9"))
;가[2→0, 5→0～7]을 가산(0～7)
CALL ADD_CRI_VAR("방치플레이", , LINE_CALC(ABL:MASTER:노출버릇, "2→0, 5→7, LIMIT/0～7"))
;가[0→0, 2→0～4, 4→0]을 가산(0～4)
CALL ADD_CRI_VAR("자위", , LINE_CALC(ABL:MASTER:노출버릇, "0→0, 2→4, 4→0, LIMIT/0～4"))
;가[0→0, 2→2, 4→0]을 가산(0～2)
CALL ADD_CRI_VAR("자위과시하고", LINE_CALC(ABL:MASTER:노출버릇, "0→0, 2→2, 4→0, LIMIT/0～2"))
;※캔슬에 보정은 붙지 않는다
;가[0→0/0, 4→0/3～5, 8→0/0]을 가산(0～5)
SIF !TEQUIP:대거울
	CALL ADD_CRI_VAR("수치플레이", LINE_CALC(ABL:MASTER:노출버릇, "0→0, 4→3, 8→0, LIMIT/0～3"), LINE_CALC(ABL:MASTER:노출버릇, "0→0, 4→2, 8→0, LIMIT/0～2"))
;가[2→0/0, 5→0/4～8]을 가산(0～8)
SIF !TEQUIP:야외플레이
	CALL ADD_CRI_VAR("야외플레이", LINE_CALC(ABL:MASTER:노출버릇, "2→0, 5→4, LIMIT/0～4"), LINE_CALC(ABL:MASTER:노출버릇, "2→0, 5→4, LIMIT/0～4"))

;-------------------------------------------------------------------------
;조교자의 능력을 참조
;-------------------------------------------------------------------------
@ACT_ACTM4_ABL_T

;조교자의 이상이[0→0, 5→3～5]를 가산(0～5)
CALL ADD_CRI_VAR("방치플레이", LINE_CALC(ABL:이상, "0→0, 5→3, LIMIT/0～3"), LINE_CALC(ABL:이상, "0→0, 5→2, LIMIT/0～2"))

;-------------------------------------------------------------------------
;PALAM군
;여기는 개별적으로
;-------------------------------------------------------------------------
;----------------------------------
;욕정
;----------------------------------
@ACT_ACTM4_DESIRE_M

;잠정의 처리. 조교자가 심술궂은들, 욕정한 조교 대상을…
IF TALENT:심술쟁이
	;조교 대상이 자위중
	IF GETBIT(TEQUIP:자위중, 0)
		;조교자의 욕정이[3000→0, 7500→5]를 가산(0～5)
		CALL ADD_CRI_VAR("자위", LINE_CALC(PALAM:MASTER:욕정, "3000→0, 7500→5, LIMIT/0～5"))
	;조교 대상은 자위하고 있지 않다
	ELSE
		;조교자의 욕정이[3000→0/2, 7500→5/7]을 가산(0～7)(조교자 자위에 의해 증가)
		CALL ADD_CRI_VAR("방치플레이", LINE_CALC(PALAM:MASTER:욕정, "3000→0, 7500→5, LIMIT/0～5") + GETBIT(TEQUIP:자위중, 1))
	ENDIF
ENDIF

;-------------------------------------
;조교자의 죄악감
;-------------------------------------
@ACT_ACTM4_SIN_T

;죄악감이[0→0, 100→-5～-2]를 가산(-5～0)
CALL ADD_CRI_VAR("방뇨", LINE_CALC(CFLAG:죄악감, "0→0, 100→-2, LIMIT/-2～0"), LINE_CALC(CFLAG:죄악감, "0→0, 100→-3, LIMIT/-3～0"))
;죄악감이[0→0, 100→-7～-4]를 가산(-7～0)
CALL ADD_CRI_VAR("방치플레이", LINE_CALC(CFLAG:죄악감, "0→0, 100→-4, LIMIT/-4～0"), LINE_CALC(CFLAG:죄악감, "0→0, 100→-3, LIMIT/-3～0"))
;※캔슬에 보정은 붙지 않는다
;죄악감이[0→0, 100→0/-5～-2]를 가산(-5～0)
SIF !TEQUIP:야외플레이
	CALL ADD_CRI_VAR("야외플레이", LINE_CALC(CFLAG:죄악감, "0→0, 100→-2, LIMIT/-2～0"), LINE_CALC(CFLAG:죄악감, "0→0, 100→-3, LIMIT/-3～0"))

;-------------------------------------
;조교자의 ALI
;-------------------------------------
@ACT_ACTM4_ALI_T

;아라이먼트가[0→0, 50→0/3]을 가산(0～3)(조교 대상 자위중은 보정 없음)
SIF !GETBIT(TEQUIP:자위중, 0)
	CALL ADD_CRI_VAR("자위", LINE_CALC(CFLAG:아라이먼트, "0→0, 50→3, LIMIT/0～3"))
;아라이먼트가[0→0, 50→0/3]을 가산(0～3)
CALL ADD_CRI_VAR("비패개장", LINE_CALC(CFLAG:아라이먼트, "0→0, 50→3, LIMIT/0～3"))
;아라이먼트가[-50→3, 50→-3]을 가산(-3～3)
CALL ADD_CRI_VAR("방치플레이", LINE_CALC(CFLAG:아라이먼트, "-50→3, 50→-3, LIMIT/-3～3"))
;아라이먼트가[-50→3, 0→0]을 가산(0～3)(캔슬에는 보정 없음)
SIF !TEQUIP:야외플레이
	CALL ADD_CRI_VAR("야외플레이", LINE_CALC(CFLAG:아라이먼트, "-50→3, 0→0, LIMIT/0～3"))
;아라이먼트가[-50→3, 0→0]을 가산(0～3)
CALL ADD_CRI_VAR("방뇨", LINE_CALC(CFLAG:아라이먼트, "-50→3, 0→0, LIMIT/0～3"))

;─
;게이지나 상태에 의한 변동
;─
;-------------------------------------
;조교 대상의 체력
;-------------------------------------
@ACT_ACTM4_PHYSICAL_M

;조교자의 체력이[0→-1, 500→0]을 가산(-1～0)
CALL ADD_CRI_VAR("수치플레이", LINE_CALC(BASE:체력, "0→-1, 500→0, LIMIT/-1～0"))
;조교자의 체력이[0→3/-3, 1500→0/0]을 가산(-3～3)(조교자 자위중은 정)
LOCAL = GETBIT(TEQUIP:자위중, 1) ? 1 # -1
CALL ADD_CRI_VAR("자위과시하고", LOCAL * LINE_CALC(BASE:체력, "0→3, 1500→0, LIMIT/0～3"))
;조교자의 체력이[0→3/-3, 1500→0/0]을 가산(-3～3)(실행중은 정)
LOCAL = TEQUIP:야외플레이 ? 1 # -1
CALL ADD_CRI_VAR("야외플레이", LOCAL * LINE_CALC(BASE:체력, "0→3, 1500→0, LIMIT/0～3"))

;-------------------------------------
;조교 대상의 기력
;-------------------------------------
@ACT_ACTM4_MENTAL_M

;조교자의 기력이[0→-2, 500→0]을 가산(-2～0)
CALL ADD_CRI_VAR("비패개장", LINE_CALC(BASE:기력, "0→-2, 500→0, LIMIT/-2～0"))
;조교자의 기력이[0→2, 500→0]을 가산(0～2)
CALL ADD_CRI_VAR("방치플레이", LINE_CALC(BASE:기력, "0→2, 500→0, LIMIT/0～2"))
;조교자의 기력이[0→5/-5, 500→0/0]을 가산(-5～5)(조교자 자위중은 정)
LOCAL = GETBIT(TEQUIP:자위중, 1) ? 1 # -1
CALL ADD_CRI_VAR("자위과시하고", LOCAL * LINE_CALC(BASE:기력, "0→5, 750→0, LIMIT/0～5"))
;조교자의 기력이[0→3/-3, 500→0/0]을 가산(-3～3)(실행중은 정)
LOCAL = TEQUIP:야외플레이 ? 1 # -1
CALL ADD_CRI_VAR("야외플레이", LOCAL * LINE_CALC(BASE:기력, "0→3, 500→0, LIMIT/0～3"))

;-------------------------------------
;조교자의 흥미
;-------------------------------------
@ACT_ACTM4_INTEREST_T

;조교자의 흥미가[500→1～2, 750→3～6]을 가산(-2～6)
CALL ADD_CRI_VAR("비패개장", LINE_CALC(BASE:흥미, "500→1, 750→3, LIMIT/-1～3"), LINE_CALC(BASE:흥미, "500→1, 750→3, LIMIT/-1～3"))
;조교자의 흥미가 500 미만이라면
IF BASE:흥미 < 500
	CALL ADD_CRI_VAR("코스프레", , 4)
	CALL ADD_CRI_VAR("방치플레이", 3)
ENDIF

;-------------------------------------
;조교자의 만족
;-------------------------------------
@ACT_ACTM4_SATISFACTION_T

;조교자의 만족이[500→3/-3, 750→5/-5]를 가산(-5～5)(실행중은 정)
LOCAL = TEQUIP:야외플레이 ? 1 # -1
CALL ADD_CRI_VAR("야외플레이", LOCAL * LINE_CALC(BASE:만족, "500→3, 750→5, LIMIT/0～5"))

;-------------------------------------
;조교자 상태
;-------------------------------------
@ACT_ACTM4_CONDITION_T

;조교자 상태(0=통상/1=피폐/2=쇠약/3=무기력/4=몽롱/5=정욕/6=분노/7=싫증/8=울적)
SELECTCASE TCVAR:상태변화
	CASE 1 TO 3
		CALL ADD_CRI_VAR("자위과시하고", GETBIT(TEQUIP:자위중, 1) ? 5 # -5)
		CALL ADD_CRI_VAR("야외플레이", TEQUIP:야외플레이 ? 5 # -5)
	CASE 7
		CALL ADD_CRI_VAR("자위", GETBIT(TEQUIP:자위중, 0) ? 5 # 0)
		CALL ADD_CRI_VAR("코스프레", TEQUIP:MASTER:코스프레 ? 5 # 0)
		CALL ADD_CRI_VAR("방치플레이", GETBIT(TEQUIP:자위중, 0) ? 0 # 5)
	CASE 8
		CALL ADD_CRI_VAR("방뇨", , 6)
ENDSELECT

;-------------------------------------
;조교 대상 상태
;-------------------------------------
@ACT_ACTM4_CONDITION_M

;조교 대상 상태가 피폐/쇠약/무기력
IF 1 <= TCVAR:MASTER:상태변화 && TCVAR:MASTER:상태변화 <= 3
	CALL ADD_CRI_VAR("자위", (GETBIT(TEQUIP:자위중, 0) ? 1 # -1) * (TFLAG:소모도 * 2 + TCVAR:MASTER:상태변화 + 1))
	CALL ADD_CRI_VAR("수치플레이", (TEQUIP:대거울 && TCVAR:MASTER:상태변화 == 3 ? 1 # -1) * (TFLAG:소모도 + 3))
	CALL ADD_CRI_VAR("야외플레이", (TEQUIP:야외플레이 ? 1 # -1) * (TFLAG:소모도 * 2 + TCVAR:MASTER:상태변화), (TEQUIP:야외플레이 ? 1 # -1) * 5)
ENDIF

;─
;전회의 행동이나 상황에 의한 변동
;─
;-------------------------------------
;미약
;-------------------------------------
@ACT_ACTM4_DRAG
SIF TEQUIP:미약
	CALL ADD_CRI_VAR("방치플레이", TALENT:심술쟁이 * 3 - TALENT:상냥함 * 3, 4)

;-------------------------------------
;이뇨제
;-------------------------------------
@ACT_ACTM4_DIURETIC
SIF TEQUIP:이뇨제
	CALL ADD_CRI_VAR("방뇨", 3, 4)

;-------------------------------------
;장착 도구류
;-------------------------------------
@ACT_ACTM4_TOOLS

;바이브/A바이브/클리캡/오나홀/니플캡 장착중에서, 조교 대상 자위중이 아니다
SIF (TEQUIP:바이브 || TEQUIP:애널바이브 || TEQUIP:클리캡 || TEQUIP:오나홀 || TEQUIP:니플캡) && !GETBIT(TEQUIP:자위중, 0)
	CALL ADD_CRI_VAR("자위", 3 + TEQUIP:바이브 + TEQUIP:애널바이브 + TEQUIP:클리캡 + TEQUIP:오나홀 + TEQUIP:니플캡)

;-------------------------------------
;줄
;-------------------------------------
@ACT_ACTM4_ROPE
SIF TEQUIP:줄
	CALL ADD_CRI_VAR("방치플레이", 5)

;-------------------------------------
;아이마스크
;-------------------------------------
@ACT_ACTM4_EYEMASK
IF TEQUIP:아이마스크
	CALL ADD_CRI_VAR("수치플레이", TEQUIP:대거울 ? 0 # -10)
	CALL ADD_CRI_VAR("방치플레이", 3)
ENDIF

;-------------------------------------
;볼개그
;-------------------------------------
@ACT_ACTM4_GAG
SIF TEQUIP:볼개그
	CALL ADD_CRI_VAR("방치플레이", , 3)

;-------------------------------------
;삼각목마
;-------------------------------------
@ACT_ACTM4_HORSE
SIF TEQUIP:삼각목마
	CALL ADD_CRI_VAR("방치플레이", , 7)

;-------------------------------------
;야외플레이
;-------------------------------------
@ACT_ACTM4_OUT
SIF TEQUIP:야외플레이
	CALL MUL_CRI_VAR("방치플레이", 150)

;-------------------------------------
;대거울
;-------------------------------------
@ACT_ACTM4_T_MILLER
IF TEQUIP:대거울
	CALL ADD_CRI_VAR("자위", GETBIT(TEQUIP:자위중, 0) ? 0 # 3)
	CALL ADD_CRI_VAR("방뇨", , 4)
ENDIF

;-------------------------------------
;요구되어 도구를 떼었다
;-------------------------------------
@ACT_ACTM4_TAKE_OFF_TOOLS

;캔슬에 응해 장착을 주저
SIF !TEQUIP:야외플레이
	CALL ADD_CRI_VAR("야외플레이", -CANCEL_TEQUIP_COUNT("야외플레이") / 20)
SIF !TEQUIP:대거울
	CALL ADD_CRI_VAR("수치플레이", -CANCEL_TEQUIP_COUNT("대거울") / 20)

;-------------------------------------
;조교자 자위중
;-------------------------------------
@ACT_ACTM4_MASTURBATION_T

;조교자만
IF TEQUIP:자위중 == 2
	CALL ADD_CRI_VAR("자위", 5)
	CALL ADD_CRI_VAR("방치플레이", , 10)
ENDIF

;-------------------------------------
;조교 대상 자위중
;-------------------------------------
@ACT_ACTM4_MASTURBATION_M

;조교 대상만
IF TEQUIP:자위중 == 1
	CALL ADD_CRI_VAR("비패개장", -5)
	;조교자의 하반신윗도리로 방해되지 않을 때
	SIF CLOTHESTYPE(0, 1) != 2
		CALL ADD_CRI_VAR("자위과시하고", 5)
	CALL ADD_CRI_VAR("방뇨", -3)
ENDIF

;-------------------------------------
;조교 대상의 의복
;-------------------------------------
@ACT_ACTM4_CLOTH_M

;조교 대상의 하반신윗도리로 방해될 때
IF CLOTHESTYPE(1, 1) == 2
	CALL ADD_CRI_VAR("자위", -5)
	CALL ADD_CRI_VAR("수치플레이", TEQUIP:대거울 ? 0 # -5)
	CALL ADD_CRI_VAR("방뇨", -10)
ENDIF

;-------------------------------------
;좀더하드하게해
;-------------------------------------
@ACT_ACTM4_PLEASE_HARD
IF IS_NOWCOMNAME("좀더하드하게해")
	CALL ADD_CRI_VAR("수치플레이", TEQUIP:대거울 ? -5 # 5)
	CALL ADD_CRI_VAR("야외플레이", TEQUIP:야외플레이 ? -5 # 5)
ENDIF

;-------------------------------------
;허가를청한다
;-------------------------------------
@ACT_ACTM4_PLEASE_HELP
;허가를 청했는지 개, 조교자 상태가 울적 이외
IF IS_NOWCOMNAME("허가를청한다") && TCVAR:상태변화 < 8
	CALL ADD_CRI_VAR("수치플레이", TEQUIP:대거울 ? 5 # -5)
	CALL ADD_CRI_VAR("야외플레이", TEQUIP:야외플레이 ? 5 # -5)
ENDIF

;─
;같은 행동 연속 실행의 확률을 다운합니다
;─
@ACT_ACTM4_PREV
SELECTCASE TFLAG:전ACT
	CASE 40 TO 47, 73
		CALL ADD_CRI_VAR(GET_ACTNAME(TFLAG:전ACT), -3 * TFLAG:동일행동보너스, -4 * TFLAG:동일행동보너스)
ENDSELECT

;─
;보정치 계산
;─
@ACT_ACTM4_CORRECTION(ARG)
#DIM LCOUNT

;ARG가 0이외라면 보정치를 계산해, 이후의 처리를 실시하지 않는다
IF ARG
	;73에 관해서는 여기서 계산하지 않는다
	FOR LCOUNT, 40, 47
		CALL ACT_M_RES_CALC(LCOUNT, GET_CRI_VAR(GET_ACTNAME(LCOUNT) + "누계"))
	NEXT
ELSE
	;보정치를 판정치에 가산한다
	CALL ACT_ACTM_CORRECTION(4)
ENDIF

;─
;그 외 변동이나 실행 불가능의 판정
;─
@ACT_ACTM4_ABLE
#DIM LCOUNT

;불가능 판정
CALL ACT_ACTM_ABLE(4)
