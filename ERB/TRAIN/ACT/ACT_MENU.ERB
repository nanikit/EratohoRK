;────────────────────────────────────
;조교 유별의 판정(회화 0/애무 1/도구 2/성교 3/수치 4/봉사 5/가학 6/이상 7/사역 8/성적봉사 10)
;────────────────────────────────────
@ACT_MENU
#DIM LCOUNT

;변수를 초기화
CALL SET_CRI_VAR
FOR LCOUNT, 0, 11
	;휴식을 튕긴다
	;사역은 처리하지 않는다
	SIF LCOUNT == 8 || LCOUNT == 9
		CONTINUE
	CALL SET_CRI_VAR, MENU(LCOUNT), 1
NEXT

LOCALS = ACT_MENU

CALL CHA_CRI_VAR, "줄거리"
CALLFORM %LOCALS%_STORY_T

CALL CHA_CRI_VAR, "상황 판단"
CALLFORM %LOCALS%_ESTIMATE_T

CALL CHA_CRI_VAR, "조교자의 소질"
CALLFORM %LOCALS%_TALENT_T

CALL CHA_CRI_VAR, "조교 대상의 소질"
CALLFORM %LOCALS%_TALENT_M

CALL CHA_CRI_VAR, "조교자가 임신"
CALLFORM %LOCALS%_MATERNITY_T

CALL CHA_CRI_VAR, "조교자가 처녀"
CALLFORM %LOCALS%_MAIDEN_T

CALL CHA_CRI_VAR, "조교 대상이 처녀"
CALLFORM %LOCALS%_MAIDEN_M

CALL CHA_CRI_VAR, "조교 대상이 동정"
CALLFORM %LOCALS%_CHERRY_M

CALL CHA_CRI_VAR, "조교자의 능력"
CALLFORM %LOCALS%_ABL_T

CALL CHA_CRI_VAR, "조교 대상의 능력"
CALLFORM %LOCALS%_ABL_M

CALL CHA_CRI_VAR, "성별 관계"
CALLFORM %LOCALS%_SEX

CALL CHA_CRI_VAR, @"오늘의 방침(%POLICY("오늘")%)"
CALLFORM %LOCALS%_POLICY

CALL CHA_CRI_VAR, @"이번 방침(%POLICY("지금")%)"
CALLFORM %LOCALS%_COURCE

CALL CHA_CRI_VAR, "조교 대상의 윤활"
CALLFORM %LOCALS%_MOIST_M

CALL CHA_CRI_VAR, "조교 대상의 욕정"
CALLFORM %LOCALS%_DESIRE_M

CALL CHA_CRI_VAR, "조교 대상의 굴복"
CALLFORM %LOCALS%_YIELD_M

CALL CHA_CRI_VAR, "조교 대상의 공순"
CALLFORM %LOCALS%_ALLEGIANCE_M

CALL CHA_CRI_VAR, "조교 대상의 사정 게이지"
CALLFORM %LOCALS%_ECSTASY_M

IF TCVAR:MASTER:요점강제발기
	CALL CHA_CRI_VAR, "요점강제발기"
ELSE
	CALL CHA_CRI_VAR, "발기도"
ENDIF
CALLFORM %LOCALS%_ERECTION_M

CALL CHA_CRI_VAR, "조교자의 쾌감"
CALLFORM %LOCALS%_PREASURE_T

CALL CHA_CRI_VAR, "조교자의 체력"
CALLFORM %LOCALS%_PHYSICAL_T

CALL CHA_CRI_VAR, "조교자의 기력"
CALLFORM %LOCALS%_MENTAL_T

CALL CHA_CRI_VAR, "조교자의 이성"
CALLFORM %LOCALS%_REASON_T

CALL CHA_CRI_VAR, "조교자의 흥미"
CALLFORM %LOCALS%_INTEREST_T

CALL CHA_CRI_VAR, "조교자의 초조"
CALLFORM %LOCALS%_IRRITATION_T

CALL CHA_CRI_VAR, "조교자의 호감도"
CALLFORM %LOCALS%_LIKE_T

CALL CHA_CRI_VAR, "조교 대상의 호감도"
CALLFORM %LOCALS%_LIKE_M

CALL CHA_CRI_VAR, @"조교자 상태(%CONDITION(TCVAR:상태변화)%)"
CALLFORM %LOCALS%_CONDITION_T

CALL CHA_CRI_VAR, @"조교 대상 상태(%CONDITION(TCVAR:MASTER:상태변화)%)"
CALLFORM %LOCALS%_CONDITION_M

CALL CHA_CRI_VAR, "조교 대상의 피로"
CALLFORM %LOCALS%_TIRED_M

CALL CHA_CRI_VAR, "최초 회화 보정"
CALLFORM %LOCALS%_FIRST

CALL CHA_CRI_VAR, "대거울 소지 보너스"
CALLFORM %LOCALS%_MILLER

CALL CHA_CRI_VAR, "더하드하게"
CALLFORM %LOCALS%_PLEASE_HARD

CALL CHA_CRI_VAR, "용서를빈다"
CALLFORM %LOCALS%_PLEASE_HELP

CALL CHA_CRI_VAR, "기분 좋게 해"
CALLFORM %LOCALS%_PLEASE_PREASURE

CALL CHA_CRI_VAR, "날뛴다"
CALLFORM %LOCALS%_VIOLENT

CALL CHA_CRI_VAR, "도구 해제"
CALLFORM %LOCALS%_UNEQUIP

CALL CHA_CRI_VAR, "쓰러트리기 연속 애무 보정"
CALLFORM %LOCALS%_PETTING_CONTINUE

CALL CHA_CRI_VAR, "식스나인 유발"
CALLFORM %LOCALS%_69

CALL CHA_CRI_VAR, "조교자의 복장"
CALLFORM %LOCALS%_CLOTH_T

CALL CHA_CRI_VAR, "조교 대상의 복장"
CALLFORM %LOCALS%_CLOTH_M

CALL CHA_CRI_VAR, "미약"
CALLFORM %LOCALS%_DRAG

CALL CHA_CRI_VAR, "바이브"
CALLFORM %LOCALS%_VIBE

CALL CHA_CRI_VAR, "애널바이브"
CALLFORM %LOCALS%_AVIBE

CALL CHA_CRI_VAR, "애널비즈"
CALLFORM %LOCALS%_ABEADS

CALL CHA_CRI_VAR, "관장＋애널 플러그"
CALLFORM %LOCALS%_APLUG

CALL CHA_CRI_VAR, "클리캡"
CALLFORM %LOCALS%_CRICAP

CALL CHA_CRI_VAR, "오나홀"
CALLFORM %LOCALS%_ONAHOLE

CALL CHA_CRI_VAR, "니플캡"
CALLFORM %LOCALS%_NIPPLECAP

CALL CHA_CRI_VAR, "줄"
CALLFORM %LOCALS%_ROPE

CALL CHA_CRI_VAR, "아이마스크"
CALLFORM %LOCALS%_EYEMASK

CALL CHA_CRI_VAR, "볼개그"
CALLFORM %LOCALS%_GAG

CALL CHA_CRI_VAR, "삼각목마"
CALLFORM %LOCALS%_HORSE

CALL CHA_CRI_VAR, "야외플레이"
CALLFORM %LOCALS%_OUT

CALL CHA_CRI_VAR, "대거울"
CALLFORM %LOCALS%_T_MILLER

CALL CHA_CRI_VAR, "도구 해제 제한"
CALLFORM %LOCALS%_KILL_UNEQUIP

CALL CHA_CRI_VAR, "조교자 자위중"
CALLFORM %LOCALS%_MASTURBATION_T

CALL CHA_CRI_VAR, "조교 대상 자위중"
CALLFORM %LOCALS%_MASTURBATION_M

CALL CHA_CRI_VAR, "벌"
CALLFORM %LOCALS%_OSHIOKI

CALL CHA_CRI_VAR, "삽입 보정"
CALLFORM %LOCALS%_INSERT

CALL CHA_CRI_VAR, "애태움중"
CALLFORM %LOCALS%_IRRITATE

CALL CHA_CRI_VAR, "Ａ섹스 회피"
CALLFORM %LOCALS%_AGUARD

CALL CHA_CRI_VAR, "Ａ섹스 체위 제한"
CALLFORM %LOCALS%_ASEX_LIMITATION

CALL CHA_CRI_VAR, "구상 보정"
CALLFORM %LOCALS%_KOJO_CORRECTION_T

CALL CHA_CRI_VAR, "실행 판정"
CALLFORM %LOCALS%_ABLE

;가능한ACT계열수리셋트
;※히트치의 보정에 사용하므로 여기서 계산
TFLAG:가능한ACT계열수 = 0
;가능한ACT계열수세트
FOR LCOUNT, 0, 11
	;휴식을 튕긴다
	;사역은 처리하지 않는다
	SIF LCOUNT == 8 || LCOUNT == 9
		CONTINUE
	;-98이상을 가능이라고 판단한다(아마 지금은 괜찮다고 생각하지만)@이것 보고
	SIF GET_CRI_VAR(MENU(LCOUNT) + "누계") > -99
		TFLAG:가능한ACT계열수++
NEXT

CALL CHA_CRI_VAR, "히트치"
CALLFORM %LOCALS%_HEAT

CALL CHA_CRI_VAR, "ACT 보정치"
CALLFORM %LOCALS%_CORRECTION

;──────────────────────────────
;최종 판정
;──────────────────────────────
CALL DEF_CRI_VAR("최대 우선 지정-애무/성적봉사/성교/도구/봉사/수치/가학/사역/이상/회화")
CALLF GET_CRI_VAR("누계")
CALL ACT_MENU_RESULT()
CALL ANA_CRI_VAR()

;──────────────────────────────
;ACT분류판정
;갈라 놓을 때의 우선순서는 애무 1＞성적봉사 10＞성교 3＞도구 2＞봉사 5＞수치 4＞가학 6＞사역 8＞이상 7＞회화 0
;
;ARG = 0:기준치 보존 1:분류 호출 2:ARGS로 지정한 만큼 종류를 무효화
;──────────────────────────────
@ACT_MENU_RESULT(ARG = 0, ARGS)
#DIM LCOUNT
#DIM LTEMP, 2
#DIMS LTEMPS

;ARG가 0이라면
IF ARG == 0
	;결과를 보존
	FOR LCOUNT, 0, 9
		LOCAL:LCOUNT = RESULT:LCOUNT
	NEXT
ENDIF
;분류명 read
;※사역을 처리하게 되면 추가한다
SPLIT "회화/애무/도구/성교/수치/봉사/가학/이상/성적봉사", "/", LOCALS
;ARG가 1이라면
IF ARG == 1
	;우선순서 read
	LTEMPS = 애무/성적봉사/성교/도구/봉사/수치/가학/사역/이상/회화
	;최우선 분류를 찾는다
	LTEMP = -99
	LTEMP:1 = -1
	FOR LCOUNT, 0, 9
		;수치가 작은, 무효인 경우는 무시
		IF LOCAL:LCOUNT < LTEMP || LOCAL:LCOUNT <= -99
			CONTINUE
		;수치가 같은 경우는 우선순서에 따라 변환
		ELSEIF LOCAL:LCOUNT == LTEMP
			SIF STRFIND(LTEMPS, LOCALS:LCOUNT) > STRFIND(LTEMPS, LOCALS:(LTEMP:1))
				CONTINUE
		;물론 수치가 큰 경우도 변환
		ENDIF
		;변환시는 분류 포인터를 움직인다
		LTEMP = LOCAL:LCOUNT
		LTEMP:1 = LCOUNT
	NEXT
	;최우선 분류가 존재하지 않으면 1을 돌려준다
	SIF LTEMP:1 == -1
		RETURN 1
	;최우선 분류를 이번 분류로 지정한다
	TFLAG:ACT분류 = MENUNUM(LOCALS:(LTEMP:1))
	RETURN 0
;ARG가 2라면
ELSEIF ARG == 2
	;지정된 만큼 종류를 찾는다
	LTEMP = FINDELEMENT(LOCALS, ARGS)
	;존재하지 않으면 날린다
	SIF LTEMP == -1
		RETURN -1
	;존재하면 분류를 판정 대상에서 제외한다
	LOCAL:LTEMP = -99
	;이번 분류가 지정된 것이라면, 미선택에 되돌린다
	SIF TFLAG:ACT분류 == MENUNUM(LOCALS:LTEMP)
		TFLAG:ACT분류 = -1
ENDIF

;-------------------------------------------------------------------------
;줄거리 보정
;-------------------------------------------------------------------------
@ACT_MENU_STORY_T

[SKIPSTART]
;엑스트라 모드 이외에서는
IF FLAG:모드 != 9
	;성교, 성적봉사에 마이나스 보정(20일 경과로 완전하게 해소)
	CALL ADD_CRI_VAR("성교", MIN(DAY / 2 - 10, 0))
	CALL ADD_CRI_VAR("성적봉사", MIN(DAY / 2 - 10, 0))
ENDIF
[SKIPEND]

;-------------------------------------------------------------------------
;상황 판단
;-------------------------------------------------------------------------
@ACT_MENU_ESTIMATE_T
#DIM LCOUNT

FOR LCOUNT, 0, 11
	SIF LCOUNT == 9
		CONTINUE
	CALL ADD_CRI_VAR(MENU(LCOUNT), EST_TO_ACTM("취득", MENU(LCOUNT)))
NEXT

;-------------------------------------------------------------------------
;조교자의 소질을 참조
;-------------------------------------------------------------------------
@ACT_MENU_TALENT_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;조교자가 프라이드높음/낮다
IF TALENT:LOCAL:프라이드높음
	CALL ADD_CRI_VAR("봉사",  9)
ELSEIF TALENT:LOCAL:프라이드낮음
	CALL ADD_CRI_VAR("봉사", -9)
ENDIF

;조교자가 무관심/호기심
IF TALENT:LOCAL:무관심
	CALL ADD_CRI_VAR("회화", -15)
	CALL ADD_CRI_VAR("애무",  -6)
	CALL ADD_CRI_VAR("도구", , -9)
ELSEIF TALENT:LOCAL:호기심
	CALL ADD_CRI_VAR("회화", 3, 18)
	CALL ADD_CRI_VAR("애무", 6)
	CALL ADD_CRI_VAR("도구", 3,  9)
	CALL ADD_CRI_VAR("성적봉사", 3,  6)
ENDIF

;조교자가 억압/해방
IF TALENT:LOCAL:억압
	CALL ADD_CRI_VAR("도구",  -6)
	CALL ADD_CRI_VAR("성교",  -6)
	CALL ADD_CRI_VAR("수치", -12)
	CALL ADD_CRI_VAR("가학",  -3)
	CALL ADD_CRI_VAR("이상",  -9)
	CALL ADD_CRI_VAR("성적봉사",  -3)
ELSEIF TALENT:LOCAL:해방
	CALL ADD_CRI_VAR("도구",   6)
	CALL ADD_CRI_VAR("성교",   6)
	CALL ADD_CRI_VAR("수치",  12)
	CALL ADD_CRI_VAR("가학",   3)
	CALL ADD_CRI_VAR("이상",   9)
	CALL ADD_CRI_VAR("성적봉사",   3)
ENDIF

;조교자가 수줍음/수치심옅음
IF TALENT:LOCAL:수줍음
	CALL ADD_CRI_VAR("수치", -9)
	CALL ADD_CRI_VAR("성적봉사", -6)
ELSEIF TALENT:LOCAL:수치심옅음
	CALL ADD_CRI_VAR("수치",  9)
	CALL ADD_CRI_VAR("성적봉사",  6)
ENDIF

;조교자가 혀사용
SIF TALENT:LOCAL:혀사용
	CALL ADD_CRI_VAR("애무", 9)

;조교자가 바느질(바늘 소지시)
SIF TALENT:LOCAL:바느질 && ITEM:바늘
	CALL ADD_CRI_VAR("가학", 9)

;조교자가 거친혀
SIF TALENT:LOCAL:거친혀
	CALL ADD_CRI_VAR("애무", -9)

;조교자가 요령 있는 손가락
SIF TALENT:LOCAL:기묘한손가락
	CALL ADD_CRI_VAR("애무", 9)

;조교자가 허리사용
IF TALENT:LOCAL:허리사용
	CALL ADD_CRI_VAR("성교", 9)
	CALL ADD_CRI_VAR("성적봉사", 6)
ENDIF

;조교자가 결박능숙(줄소지시)
SIF TALENT:LOCAL:결박능숙 && ITEM:줄
	CALL ADD_CRI_VAR("가학", 9)

;조교자가 도구사용
SIF TALENT:LOCAL:도구사용
	CALL ADD_CRI_VAR("도구", 9)

;조교자가 자위쉬움
SIF TALENT:LOCAL:자위쉬움
	CALL ADD_CRI_VAR("수치", 9)

;조교자가 헌신적
SIF TALENT:LOCAL:헌신적
	CALL ADD_CRI_VAR("봉사", -9)

;조교자가 수동
SIF TALENT:LOCAL:수동
	CALL ADD_CRI_VAR("봉사", 9)

;조교자가 쾌감에솔직/부정
IF TALENT:LOCAL:쾌감에솔직
	CALL ADD_CRI_VAR("성교",  9)
	CALL ADD_CRI_VAR("봉사",  6)
	CALL ADD_CRI_VAR("성적봉사",  9)
ELSEIF TALENT:LOCAL:쾌감을부정
	CALL ADD_CRI_VAR("성교", -9)
	CALL ADD_CRI_VAR("봉사", -6)
	CALL ADD_CRI_VAR("성적봉사", -6)
ENDIF

;조교자가 도착적
IF TALENT:LOCAL:도착적
	CALL ADD_CRI_VAR("가학", 3)
	CALL ADD_CRI_VAR("이상", 3)
ENDIF

;조교자가 상냥함
SIF TALENT:LOCAL:상냥함
	CALL ADD_CRI_VAR("가학", -9)

;조교자가 유치
IF TALENT:LOCAL:유치
	CALL ADD_CRI_VAR("애무", , 15)
	CALL ADD_CRI_VAR("수치", -9)
	CALL ADD_CRI_VAR("가학", -3)
	CALL ADD_CRI_VAR("이상", , 12)
ENDIF

;조교자가 광기
SIF TALENT:LOCAL:광기
	CALL ADD_CRI_VAR("이상", , 20)
	
;-------------------------------------------------------------------------
;조교 대상의 소질을 참조
;-------------------------------------------------------------------------
@ACT_MENU_TALENT_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? MASTER # ARG
;조교 대상이 위압감
IF TALENT:LOCAL:위압감
	CALL ADD_CRI_VAR("봉사", -3)
	CALL ADD_CRI_VAR("가학", -3)
ENDIF

;조교 대상이 매혹
IF TALENT:LOCAL:매혹
	CALL ADD_CRI_VAR("애무", , 2)
	CALL ADD_CRI_VAR("성교", , 4)
	CALL ADD_CRI_VAR("성적봉사", , 5)
ENDIF

;조교 대상이 사교적/대인기피
IF TALENT:LOCAL:사교적
	CALL ADD_CRI_VAR("회화",  3)
	CALL ADD_CRI_VAR("도구",   , -2)
	CALL ADD_CRI_VAR("봉사",   ,  2)
	CALL ADD_CRI_VAR("성적봉사",   ,  2)
ELSEIF TALENT:LOCAL:대인기피
	CALL ADD_CRI_VAR("회화", -3)
	CALL ADD_CRI_VAR("도구",   ,  2)
	CALL ADD_CRI_VAR("봉사",   , -2)
	CALL ADD_CRI_VAR("성적봉사",   , -2)
ENDIF

;-------------------------------------
;조교자가 임신중
;-------------------------------------
@ACT_MENU_MATERNITY_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG
;임신중
IF TALENT:LOCAL:임신
	CALL ADD_CRI_VAR("가학",  -5)
	CALL ADD_CRI_VAR("이상", -10)
	CALL ADD_CRI_VAR("성적봉사",  -5)
ENDIF

;-------------------------------------------------------------------------
;동정·처녀 관계
;인수는 조교자·조교 대상
;조교 대상이 처녀때, 함부로 성교하지 않는 사양입니다. 동정도 같은 처리를 실시하지만 효과는 약간 낮습니다.
;조교 대상이 처녀에 합의미취득 한편, 조교 대상이 여자 아이
;-------------------------------------------------------------------------
@ACT_MENU_MAIDEN_T, ARG = -1, ARG:1 = -1
#LOCALSIZE 2
LOCAL = ARG == -1 ? TARGET # ARG
LOCAL:1 = ARG == -1 ? MASTER # ARG:1
;조교자가 처녀로, 조교 대상으로 페니스가 있다
IF TALENT:LOCAL:처녀 && PENIS(LOCAL:1)
	;조교자가 정조관념/정조관념없음
	LOCAL:1 = CFLAG:LOCAL:호의 + CFLAG:LOCAL:M호의 / 2
	IF TALENT:LOCAL:정조관념
		CALL ADD_CRI_VAR("성교", LINE_CALC(LOCAL:1, "0→-12, 1200→0, LIMIT/-24～0"))
		CALL ADD_CRI_VAR("성적봉사", LINE_CALC(LOCAL:1, "0→-12, 2400→0, LIMIT/-24～0"))
	ELSEIF TALENT:LOCAL:정조관념없음
		CALL ADD_CRI_VAR("성교", LINE_CALC(LOCAL:1, "0→-4, 400→0, LIMIT/-8～0"))
		CALL ADD_CRI_VAR("성적봉사", LINE_CALC(LOCAL:1, "0→-4, 800→0, LIMIT/-8～0"))
	ELSE
		CALL ADD_CRI_VAR("성교", LINE_CALC(LOCAL:1, "0→-8, 800→0, LIMIT/-16～0"))
		CALL ADD_CRI_VAR("성적봉사", LINE_CALC(LOCAL:1, "0→-8, 1600→0, LIMIT/-16～0"))
	ENDIF
	;조교자가 겁쟁이
	IF TALENT:LOCAL:겁쟁이
		CALL ADD_CRI_VAR("성교", -4)
		CALL ADD_CRI_VAR("성적봉사", -2)
	ENDIF
	;조교자가 선을그음
	IF TALENT:LOCAL:선을그음
		CALL ADD_CRI_VAR("성교", -4)
		CALL ADD_CRI_VAR("성적봉사", -2)
	ENDIF
ENDIF

;-----------------------------------------
;조교 대상이 처녀
;-----------------------------------------
@ACT_MENU_MAIDEN_M, ARG = -1, ARG:1 = 1
#LOCALSIZE 2
LOCAL = ARG == -1 ? TARGET # ARG
LOCAL:1 = ARG == -1 ? MASTER # ARG:1
IF TALENT:(LOCAL:1):처녀 && !CFLAG:LOCAL:합의 && !TALENT:(LOCAL:1):후타나리
	CALL ADD_CRI_VAR("성교", -5, -2)
	;조교자가 겁쟁이/자제적/선을그음/신중
	SIF TALENT:LOCAL:겁쟁이 || TALENT:LOCAL:자제적 || TALENT:LOCAL:선을그음 || TALENT:LOCAL:신중
		CALL ADD_CRI_VAR("성교", -3)
	;조교자가 기장/충동적/조급함, 또는 조교자 상태가 정욕/분노/싫증/광기의 언젠가
	SIF TALENT:LOCAL:기장 || TALENT:LOCAL:충동적 || TALENT:LOCAL:조급함 || TCVAR:상태변화 > 4
		CALL ADD_CRI_VAR("성교", , 6)
	;뒤를 사용할 수 있는 상태라면 패널티를 경감(조교 대상의Ａ경험 3이상)
	SIF EXP:(LOCAL:1):Ａ경험 > 2
		CALL ADD_CRI_VAR("성교", 3)
	;조교자의 조교 Lv가 1이상
	SIF CFLAG:LOCAL:조교레벨 > 0
		CALL ADD_CRI_VAR("성교", GET_TRAINLV(LOCAL) / 3)
	;조교자에게로의 호의가 1000 초과
	SIF CFLAG:LOCAL:M호의 > 1000
		CALL ADD_CRI_VAR("성교", CFLAG:LOCAL:M호의 / 500)
ENDIF

@ACT_MENU_CHERRY_M, ARG = -1, ARG:1 = -1
#LOCALSIZE 2
LOCAL = ARG == -1 ? TARGET # ARG
LOCAL:1 = ARG == -1 ? MASTER # ARG:1
;조교 대상이 동정에 합의미취득 한편, 조교 대상이 남자 또는 후타나리 처녀
IF TALENT:(LOCAL:1):동정 && !CFLAG:합의 && (TALENT:(LOCAL:1):남자 || TALENT:(LOCAL:1):처녀)
	CALL ADD_CRI_VAR("성교", -3, -1)
	;조교자가 겁쟁이/자제적/선을그음/신중
	SIF TALENT:LOCAL:겁쟁이 || TALENT:LOCAL:자제적 || TALENT:LOCAL:선을그음 || TALENT:LOCAL:신중
		CALL ADD_CRI_VAR("성교", -2)
	;조교자가 기장/충동적/조급함, 또는 조교자 상태가 정욕/분노/싫증/광기의 언젠가
	SIF TALENT:LOCAL:기장 || TALENT:LOCAL:충동적 || TALENT:LOCAL:조급함 || TCVAR:상태변화 > 4
		CALL ADD_CRI_VAR("성교", , 3)
	;뒤를 사용할 수 있는 상태라면 패널티를 경감(조교 대상의Ａ경험 2이상)
	SIF EXP:(LOCAL:1):Ａ경험 > 1
		CALL ADD_CRI_VAR("성교", 2)
	;조교자의 조교 Lv가 1이상
	SIF CFLAG:LOCAL:조교레벨 > 0
		CALL ADD_CRI_VAR("성교", GET_TRAINLV(LOCAL) / 3)
	;조교자에게로의 호의가 1000 초과
	SIF CFLAG:LOCAL:M호의 > 1000
		CALL ADD_CRI_VAR("성교", CFLAG:LOCAL:M호의 / 500)
ENDIF

;-------------------------------------------------------------------------
;조교자의 능력을 참조
;-------------------------------------------------------------------------
@ACT_MENU_ABL_T, ARG = -1
#LOCALSIZE 1
#DIM LCOUNT
LOCAL = ARG == -1 ? TARGET # ARG

;이미 있는 것을 살릴 때에 사용해,
;신규 개척시는 부호를 반대로 해 사용하는 것(지금은 좋아)
[SKIPSTART]

;기본적인 능력 보정
FOR LCOUNT, 0, 11
	;조교 종별로부터 대응하는 ABL를 산출해, ABL 보정치의 것 1/5을 가산
	CALL ADD_CRI_VAR(MENU(LCOUNT), GET_ABL(LOCAL, ABLNAME:(20 + LCOUNT)) * 2 / 10)
NEXT

;ＣＶ감각의 합계치가[0→0, 1→1, 4→2, 7→3]을 가산(0～3)
CALL ADD_CRI_VAR("성교", LINE_CALC(ABL:LOCAL:Ｃ감각 + ABL:LOCAL:Ｖ감각, "1→1, 7→3, LIMIT/0～3"))

;조교자의Ｖ감각
CALL ADD_CRI_VAR("성적봉사", GET_ABL(LOCAL, "Ｖ감각") / 20)

;조교자의 노출증
SIF ABL:LOCAL:노출증
	CALL ADD_CRI_VAR("수치", GET_ABL(LOCAL, "노출증") / 20)

[SKIPEND]

;-------------------------------------------------------------------------
;조교 대상의 능력을 참조
;-------------------------------------------------------------------------
@ACT_MENU_ABL_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? MASTER # ARG

;조교 대상의 순종과 봉사정신의 합계치가[0→0, 1→1, 4→2, 7→3]을 가산(0～3)
CALL ADD_CRI_VAR("봉사", LINE_CALC(ABL:LOCAL:순종 + ABL:LOCAL:봉사정신, "1→1, 7→3, LIMIT/0～3"))
;조교 대상의 순종과 봉사정신의 합계치가[1→0, 4→1, 7→2]를 가산(0～2)
CALL ADD_CRI_VAR("성적봉사", LINE_CALC(ABL:LOCAL:순종 + ABL:LOCAL:봉사정신, "4→1, 7→2, LIMIT/0～2"))

;조교 대상의ＣＶ감각의 합계치가[0→0, 1→0～1, 4→0～2, 7→0～3]을 가산(0～3)
CALL ADD_CRI_VAR("도구", , LINE_CALC(ABL:LOCAL:Ｃ감각 + ABL:LOCAL:Ｖ감각, "1→1, 7→3, LIMIT/0～3"))
CALL ADD_CRI_VAR("성교", , LINE_CALC(ABL:LOCAL:Ｃ감각 + ABL:LOCAL:Ｖ감각, "1→1, 7→3, LIMIT/0～3"))

;조교 대상의Ａ감각이[0→0～1, 1→0～2, 3→0～4, 5→0～6]을 가산(0～6)
CALL ADD_CRI_VAR("도구", , LINE_CALC(ABL:LOCAL:Ａ감각, "1→2, 5→6, LIMIT/1～6"))
;조교 대상의Ａ감각이[0→0, 1→0～1, 3→0～3, 5→0～5]를 가산(0～5)
CALL ADD_CRI_VAR("이상", , LINE_CALC(ABL:LOCAL:Ａ감각, "1→1, 5→5, LIMIT/0～5"))
;조교 대상의Ａ감각이[0→0, 3→1, 5→2]를 가산(0～2)
CALL ADD_CRI_VAR("성교", LINE_CALC(ABL:LOCAL:Ａ감각, "3→1, 5→2, LIMIT/0～2"))

;조교 대상의Ｂ감각이[0→0, 3→0～1, 5→0～2]를 가산(0～2)
CALL ADD_CRI_VAR("애무", , LINE_CALC(ABL:LOCAL:Ｂ감각, "3→1, 5→2, LIMIT/0～2"))
;조교 대상의Ｂ감각이[0→0, 1→0, 3→0～1]을 가산(0～1)
CALL ADD_CRI_VAR("봉사", , LINE_CALC(ABL:LOCAL:Ｂ감각, "1→0, 3→1, LIMIT/0～1"))

;조교 대상의 마조 상관 능력(마조끼/고통중독/구속중독의 합계)
;※왠지 레즈끼를 보고 있었으므로 마조끼로 변경
;가[0→0, 3→0～2, 6→0～4, 9→0～6, 12→0～8]을 가산(0～8)
CALL ADD_CRI_VAR("가학", , LINE_CALC(ABL:LOCAL:마조끼 + ABL:LOCAL:고통중독 + ABL:LOCAL:구속중독, "0→0, 12→8, LIMIT/0～8"))
;가[0→0, 3→0, 6→0～2, 9→0～4, 12→0～6]을 가산(0～6)
CALL ADD_CRI_VAR("이상", , LINE_CALC(ABL:LOCAL:마조끼 + ABL:LOCAL:고통중독 + ABL:LOCAL:구속중독, "3→0, 12→6, LIMIT/0～6"))
;가[0→0, 9→0, 12→0～2]를 가산(0～2)
CALL ADD_CRI_VAR("사역", , LINE_CALC(ABL:LOCAL:마조끼 + ABL:LOCAL:고통중독 + ABL:LOCAL:구속중독, "9→0, 12→2, LIMIT/0～2"))

;-------------------------------------------------------------------------
;성별 관계
;인수는 조교자·조교 대상
;동성으로 대응의 능력이나 소질이 없는 경우는 몸의 접촉을 피하도록(듯이)
;-------------------------------------------------------------------------
@ACT_MENU_SEX, ARG = -1, ARG:1 = -1
LOCAL = ARG == -1 ? TARGET # ARG
LOCAL:1 = ARG == -1 ? MASTER # ARG:1

;조교자에게 바이가 없고, 남자끼리
IF TALENT:LOCAL:남자 && TALENT:(LOCAL:1):남자 && !TALENT:LOCAL:바이
	;조교자의ＢＬ끼이[0→-2, 2→-1, 4→0]을 가산(-2～0)
	CALL ADD_CRI_VAR("봉사", LINE_CALC(ABL:LOCAL:ＢＬ끼, "0→-2, 4→0, LIMIT/-2～0"))
	;조교자의ＢＬ끼이[0→-4, 1→-3, 2→-2, 3→-1, 4→0]을 가산(-4～0)
	CALL ADD_CRI_VAR("애무", LINE_CALC(ABL:LOCAL:ＢＬ끼, "0→-4, 4→0, LIMIT/-4～0"))
	CALL ADD_CRI_VAR("성교", LINE_CALC(ABL:LOCAL:ＢＬ끼, "0→-4, 4→0, LIMIT/-4～0"))
ENDIF
;조교자에게 바이가 없고, 여자 아이끼리
IF !TALENT:LOCAL:남자 && !TALENT:(LOCAL:1):남자 && !TALENT:LOCAL:바이
	;조교자 레즈끼가[0→-2, 2→-1, 4→0]을 가산(-2～0)
	CALL ADD_CRI_VAR("봉사", LINE_CALC(ABL:LOCAL:레즈끼, "0→-2, 4→0, LIMIT/-2～0"))
	;조교자 레즈끼가[0→-4, 1→-3, 2→-2, 3→-1, 4→0]을 가산(-4～0)
	CALL ADD_CRI_VAR("애무", LINE_CALC(ABL:LOCAL:레즈끼, "0→-4, 4→0, LIMIT/-4～0"))
	CALL ADD_CRI_VAR("성교", LINE_CALC(ABL:LOCAL:레즈끼, "0→-4, 4→0, LIMIT/-4～0"))
	CALL ADD_CRI_VAR("성적봉사", LINE_CALC(ABL:LOCAL:레즈끼, "0→-4, 4→0, LIMIT/-4～0"))
	
	;조교자의 레즈중독이[0→0, 1→1, 3→2, 5→3]을 가산(0～3)
	CALL ADD_CRI_VAR("애무", LINE_CALC(ABL:LOCAL:레즈중독, "1→1, 5→3, LIMIT/0～3"))
	CALL ADD_CRI_VAR("성교", LINE_CALC(ABL:LOCAL:레즈중독, "1→1, 5→3, LIMIT/0～3"))
	CALL ADD_CRI_VAR("성적봉사", LINE_CALC(ABL:LOCAL:레즈중독, "1→1, 5→3, LIMIT/0～3"))
	CALL ADD_CRI_VAR("봉사", LINE_CALC(ABL:LOCAL:레즈중독, "1→1, 5→3, LIMIT/0～3"))
ENDIF

;조교자가 남성혐오 한편, 조교 대상이 남자
IF TALENT:LOCAL:남성혐오 && TALENT:(LOCAL:1):남자
	CALL ADD_CRI_VAR("회화",  2)
	CALL ADD_CRI_VAR("애무", -3)
	CALL ADD_CRI_VAR("성교", -4)
	CALL ADD_CRI_VAR("봉사", -2)
	CALL ADD_CRI_VAR("가학",  1)
	CALL ADD_CRI_VAR("성적봉사", -4)
ENDIF

[SKIPSTART]
/////////////////////////////////////
오늘의 방침
	0=통상         (언제나 대로)
	1=애널       (애널을 괴롭혀)
	2=벌     (벌 해)
	3=하드       (격렬하게 해)
	4=러브러브(응석부리게 해)
	5=이상         (망가질 때까지 해)
	6=야외         (밖에 나가고 싶다)
	7=아이 만들기       (임신하게 하고 싶다)
	8=휴식         (쉬게 해)
	9=안아       (안기고 싶다)
	10= 봉사      (봉사하고 싶다)
	11=귀여워해  (귀여워해)
/////////////////////////////////////
[SKIPEND]
@ACT_MENU_POLICY
SELECTCASE POLICY("오늘")
	CASE "애널"
		;조교자가 후타나리일까 페니스밴드 소지의 경우는 성교계를 유발하도록(듯이)
		IF (CFLAG:M호의 + CFLAG:호의 >= 10000) && (TALENT:TARGET:후타나리 || ITEM:페니스밴드)
			CALL ADD_CRI_VAR("회화", -10)
			CALL ADD_CRI_VAR("애무",  -8)
			CALL ADD_CRI_VAR("도구", -10)
			CALL ADD_CRI_VAR("성교",  10)
			CALL ADD_CRI_VAR("수치", -10)
			CALL ADD_CRI_VAR("봉사",  -6)
			CALL ADD_CRI_VAR("가학", -10)
			CALL ADD_CRI_VAR("이상", -10)
			CALL ADD_CRI_VAR("성적봉사", -10)
		;그 외의 경우
		ELSE
			CALL ADD_CRI_VAR("애무",  10)
			CALL ADD_CRI_VAR("도구",  10)
			CALL ADD_CRI_VAR("가학", ITEM:관장세트 ? 10 # -10)
			CALL ADD_CRI_VAR("성적봉사", -10)
		ENDIF
	CASE "벌"
		;벌시는 벌플래그가 설 뿐
		;어떠한 손질을 해도 되는 것인지도 모른다
	CASE "하드"
		CALL ADD_CRI_VAR("도구",   5)
		CALL ADD_CRI_VAR("봉사",   8)
		CALL ADD_CRI_VAR("가학",  10)
	CASE "러브러브"
		;조교 대상이 여자인가개페니스밴드 소지의 경우는 성적봉사계를 유발하도록(듯이)
		IF (CFLAG:M호의 + CFLAG:호의 >= 10000) && (ITEM:페니스밴드 && !PENIS(MASTER))
			CALL ADD_CRI_VAR("회화",  -7)
			CALL ADD_CRI_VAR("애무",  -8)
			CALL ADD_CRI_VAR("도구", -10)
			CALL ADD_CRI_VAR("성교",  -5)
			CALL ADD_CRI_VAR("수치", -10)
			CALL ADD_CRI_VAR("봉사",  -3)
			CALL ADD_CRI_VAR("가학", -10)
			CALL ADD_CRI_VAR("이상", -10)
			CALL ADD_CRI_VAR("성적봉사",  10)
		ELSE
			CALL ADD_CRI_VAR("애무",   8)
			CALL ADD_CRI_VAR("도구", -10)
			CALL ADD_CRI_VAR("성교",  -5)
			CALL ADD_CRI_VAR("수치", -10)
			CALL ADD_CRI_VAR("봉사",  10)
			CALL ADD_CRI_VAR("가학", -10)
			CALL ADD_CRI_VAR("이상", -10)
			CALL ADD_CRI_VAR("성적봉사",  -5)
		ENDIF
	CASE "이상"
		CALL ADD_CRI_VAR("가학",  10)
		CALL ADD_CRI_VAR("이상",  10, 10)
		CALL ADD_CRI_VAR("사역",  10)
	CASE "자 만들기"
		CALL ADD_CRI_VAR("회화",  -8)
		CALL ADD_CRI_VAR("애무",  -8)
		CALL ADD_CRI_VAR("도구", -10)
		CALL ADD_CRI_VAR("성교", -10)
		CALL ADD_CRI_VAR("수치", -10)
		CALL ADD_CRI_VAR("봉사",  -4)
		CALL ADD_CRI_VAR("가학", -10)
		CALL ADD_CRI_VAR("이상", -10)
		CALL ADD_CRI_VAR("성적봉사",  10)
	CASE "안아"
		CALL ADD_CRI_VAR("회화", -10)
		CALL ADD_CRI_VAR("애무",  -9)
		CALL ADD_CRI_VAR("도구", -10)
		CALL ADD_CRI_VAR("성교",  10)
		CALL ADD_CRI_VAR("수치", -10)
		CALL ADD_CRI_VAR("봉사",  -2)
		CALL ADD_CRI_VAR("가학", -10)
		CALL ADD_CRI_VAR("이상", -10)
		CALL ADD_CRI_VAR("성적봉사", -10)
	CASE "봉사"
		CALL ADD_CRI_VAR("회화", -10)
		CALL ADD_CRI_VAR("애무", -10)
		CALL ADD_CRI_VAR("도구", -10)
		CALL ADD_CRI_VAR("성교", -10)
		CALL ADD_CRI_VAR("수치", -10)
		CALL ADD_CRI_VAR("봉사",  10)
		CALL ADD_CRI_VAR("가학", -10)
		CALL ADD_CRI_VAR("이상", -10)
		CALL ADD_CRI_VAR("성적봉사",  -5)
	CASE "귀여워해"
		CALL ADD_CRI_VAR("회화",   5)
		CALL ADD_CRI_VAR("애무",  10)
		CALL ADD_CRI_VAR("도구", -10)
		CALL ADD_CRI_VAR("수치", -10)
		CALL ADD_CRI_VAR("봉사", -10)
		CALL ADD_CRI_VAR("가학", -10)
		CALL ADD_CRI_VAR("이상", -10)
		CALL ADD_CRI_VAR("성적봉사", -10)
		;조교 대상이 남자가 아닌 경우, 조건을 채우고 있으면 성교계에+보정
		SIF !TALENT:MASTER:남자 && (CFLAG:M호의 + CFLAG:호의 >= 10000)
			CALL ADD_CRI_VAR("성교", 5)
	CASEELSE
		;none
ENDSELECT

;------------------------------------------
;이번 조교 방침
;------------------------------------------
@ACT_MENU_COURCE

SELECTCASE POLICY("지금")
	CASE "소프트"
		CALL ADD_CRI_VAR("회화",   7)
		CALL ADD_CRI_VAR("애무",   5)
		CALL ADD_CRI_VAR("성교",  -3)
		CALL ADD_CRI_VAR("가학", -10)
		CALL ADD_CRI_VAR("이상", -10)
		CALL ADD_CRI_VAR("성적봉사",  -3)
	CASE "노멀"
		CALL ADD_CRI_VAR("가학",  -5)
		CALL ADD_CRI_VAR("이상",  -5)
	CASE "하드"
		CALL ADD_CRI_VAR("회화",  -5)
		CALL ADD_CRI_VAR("애무",  -5)
		CALL ADD_CRI_VAR("봉사",  -3)
	CASE "이상"
		CALL ADD_CRI_VAR("회화",  -5)
		CALL ADD_CRI_VAR("애무",  -5)
		CALL ADD_CRI_VAR("봉사",  -3)
		CALL ADD_CRI_VAR("가학",  10)
		CALL ADD_CRI_VAR("이상",  10, 10)
		CALL ADD_CRI_VAR("사역",  10)
;	CASE "SM"
;		CALL ADD_CRI_VAR("회화", -10)
;		CALL ADD_CRI_VAR("애무", -10)
;		SIF ! TALENT:MASTER:남자
;			CALL ADD_CRI_VAR("성교", -20)
;		CALL ADD_CRI_VAR("봉사", , -10)
;		CALL ADD_CRI_VAR("가학", 15)
;		CALL ADD_CRI_VAR("이상", 10)
;		CALL ADD_CRI_VAR("성적봉사", -20)
ENDSELECT

;-------------------------------------------------------------------------
;PALAM군
;여기는 개별적으로
;-------------------------------------------------------------------------
;------------------------------------------
;윤활
;------------------------------------------
@ACT_MENU_MOIST_M, ARG = -1
#LOCALSIZE 2
LOCAL = ARG == -1 ? TARGET # ARG

;조교 대상의 윤활 PALAM가[0→-6, 1000→-4, 2000→-2, 3000→0]을 가산(-6～0)
LOCAL:1 = LINE_CALC(PALAM:LOCAL:윤활, "0→-6, 3000→0, LIMIT/-6～0")
;조교자가 신중하면보다 감산
IF TALENT:신중
	LOCAL:1 = LOCAL:1 * 8 / 5
;조교자가 성격이 급하면 경감
ELSEIF TALENT:조급함
	LOCAL:1 = LOCAL:1 * 2 / 5
ENDIF
;도구는 성교보다 감산량이 작다
CALL ADD_CRI_VAR("성교", LOCAL:1)
CALL ADD_CRI_VAR("도구", LOCAL:1 * 2 / 5)

;----------------------------------
;욕정
;----------------------------------
@ACT_MENU_DESIRE_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;조교 대상의 욕정 PALAM가[0→3～6, 1000→2～4, 2000→1～2, 3000→0, 4000→-2, 5000→-4]를 가산(-4～6)
CALL ADD_CRI_VAR("애무", LINE_CALC(PALAM:LOCAL:욕정, "0→3, 3000→0, 5000→-4, LIMIT/-4～3"), LINE_CALC(PALAM:LOCAL:욕정, "0→3, 3000→0, LIMIT/0～3"))
;조교 대상의 욕정 PALAM가[0→-2, 1000→-1, 2000→2, 3000→4, 4000→6, 5000→4]를 가산(-2～6)
CALL ADD_CRI_VAR("성교", LINE_CALC(PALAM:LOCAL:욕정, "0→-2, 2000→0, 2000→2, 4000→6, 5000→4, LIMIT/-2～6"))
CALL ADD_CRI_VAR("성적봉사", LINE_CALC(PALAM:LOCAL:욕정, "0→-2, 2000→0, 2000→2, 4000→6, 5000→4, LIMIT/-2～6"))
;조교 대상의 욕정 PALAM가[0→1, 1000→2, 2000→3, 3000→4, 4000→3, 5000→2]를 가산(1～4)
CALL ADD_CRI_VAR("도구", LINE_CALC(PALAM:LOCAL:욕정, "0→1, 3000→4, 5000→2, LIMIT/1～4"))

;----------------------------------
;굴복
;----------------------------------
@ACT_MENU_YIELD_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;조교 대상의 굴복 PALAM가[4000→-2～-1, 5000→-4～-2, 6000→-5～-2, 7000→-7～-3, 8000→-8～-3]을 가산(-8～0)
CALL ADD_CRI_VAR("가학", LINE_CALC(PALAM:LOCAL:굴복, "4000→-1, 8000→-3, LIMIT/-3～0"), LINE_CALC(PALAM:LOCAL:굴복, "4000→-1, 8000→-5, LIMIT/-5～0"))
;조교 대상의 굴복 PALAM가[4000→-2～0, 5000→-4～0, 6000→-6～0, 7000→-8～0, 8000→-10～0]을 가산(-10～0)
CALL ADD_CRI_VAR("사역", , LINE_CALC(PALAM:LOCAL:굴복, "4000→-2, 8000→-10, LIMIT/-10～0"))

;----------------------------------
;공순
;----------------------------------
@ACT_MENU_ALLEGIANCE_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;조교 대상의 공순 PALAM가[4000→0, 8000→3]을 가산(0～3)
CALL ADD_CRI_VAR("애무", LINE_CALC(PALAM:LOCAL:공순, "4000→0, 8000→3, LIMIT/0～3"))
;조교 대상의 공순 PALAM가[4000→1, 5000→2, 6000→3, 7000→4, 8000→5]를 가산(0～6)
CALL ADD_CRI_VAR("성교", LINE_CALC(PALAM:LOCAL:공순, "4000→1, 8000→5, LIMIT/0～6"))
CALL ADD_CRI_VAR("봉사", LINE_CALC(PALAM:LOCAL:공순, "4000→1, 8000→5, LIMIT/0～6"))
CALL ADD_CRI_VAR("성적봉사", LINE_CALC(PALAM:LOCAL:공순, "4000→1, 8000→5, LIMIT/0～6"))
;조교 대상의 공순 PALAM가[4000→0, 5000→-1, 6000→-2, 7000→-3, 8000→-4]를 가산(-4～0)
CALL ADD_CRI_VAR("가학", LINE_CALC(PALAM:LOCAL:공순, "4000→0, 8000→-4, LIMIT/-4～0"))

;----------------------------------
;조교 대상의 사정 게이지
;----------------------------------
@ACT_MENU_ECSTASY_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? MASTER # ARG
;조교 대상의 사정 BASE가[0%→-3, 100%→0]을 가산(-3～0)
SIF PENIS(LOCAL) && !TEQUIP:성교봉사중
	CALL ADD_CRI_VAR("성적봉사", LINE_CALC(10 * BASE:LOCAL:사정 / MAXBASE:LOCAL:사정, "0→-3, 10→0, LIMIT/-3～0"))

;----------------------------------
;발기도
;
;요점강제발기 모드시는 성적봉사의 패널티 없음
;에 일단 해 본다
;---------------------------------
@ACT_MENU_ERECTION_M
;발기도(요점강제발기 모드)
IF TCVAR:MASTER:요점강제발기
	;조교 대상의 발기도가[400→5, 1000→0]을 가산(0～5)
	CALL ADD_CRI_VAR("애무", LINE_CALC(TCVAR:MASTER:발기도, "400→5, 1000→0, LIMIT/0～5"))
ELSE
	;조교 대상의 발기도가[400→-2, 1000→-1]을 가산(-3～0)
	CALL ADD_CRI_VAR("성적봉사", LINE_CALC(TCVAR:MASTER:발기도, "400→-2, 1000→-1, LIMIT/-3～0"))
ENDIF

;-------------------------------------------------------------------------
;조교자의 베이스를 참조
;-------------------------------------------------------------------------
;----------------------------------
;조교자의 쾌감(쾌cvab의 합계)
;---------------------------------
@ACT_MENU_PREASURE_T, ARG = -1
#LOCALSIZE 2
LOCAL = ARG == -1 ? TARGET # ARG

;조교자 쾌감의 합계를 계산
LOCAL:1 = PALAM:쾌Ｃ + PALAM:쾌Ｖ + PALAM:쾌Ａ + PALAM:쾌Ｂ
;합계가[0→0, 600→1, 1200→2, 1800→3, 2400→4]를 가산(0～4)
CALL ADD_CRI_VAR("봉사", LINE_CALC(LOCAL:1, "0→0, 2400→4, LIMIT/0～4"))
CALL ADD_CRI_VAR("성적봉사", LINE_CALC(LOCAL:1, "0→0, 2400→4, LIMIT/0～4"))
;합계가[600→0, 1200→1, 1800→2, 2400→3]을 가산(0～4)
CALL ADD_CRI_VAR("성교", LINE_CALC(LOCAL:1, "600→0, 2400→3, LIMIT/0～4"))
;합계가[1200→0, 1800→-1, 2400→-2]를 가산(-2～0)
CALL ADD_CRI_VAR("애무", LINE_CALC(LOCAL:1, "1200→0, 2400→-2, LIMIT/-2～0"))
CALL ADD_CRI_VAR("도구", LINE_CALC(LOCAL:1, "1200→0, 2400→-2, LIMIT/-2～0"))

;-------------------------------------
;조교자의 체력
;-------------------------------------
@ACT_MENU_PHYSICAL_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;조교자의 체력이[0→4, 500→2, 1000→0, 1500→-2]를 가산(-2～4)
CALL ADD_CRI_VAR("회화", LINE_CALC(BASE:LOCAL:체력, "0→4, 1500→-2, LIMIT/-2～4"))
;조교자의 체력이[0→2, 500→1, 1000→0]을 가산(0～2)
CALL ADD_CRI_VAR("봉사", LINE_CALC(BASE:LOCAL:체력, "0→2, 1000→0, LIMIT/0～2"))
;조교자의 체력이[0→-2, 500→-1, 1000→0]을 가산(-2～0)
CALL ADD_CRI_VAR("애무", LINE_CALC(BASE:LOCAL:체력, "0→-2, 1000→0, LIMIT/-2～0"))
CALL ADD_CRI_VAR("가학", LINE_CALC(BASE:LOCAL:체력, "0→-2, 1000→0, LIMIT/-2～0"))
;조교자의 체력이[0→-3, 500→-2, 1000→-1, 1500→0]을 가산(-3～0)
CALL ADD_CRI_VAR("성교", LINE_CALC(BASE:LOCAL:체력, "0→-3, 1500→0, LIMIT/-3～0"))

;-------------------------------------
;조교자의 기력
;-------------------------------------
@ACT_MENU_MENTAL_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;조교자의 기력이[0→3, 250→2, 500→1, 1000→0]을 가산(0～3)
CALL ADD_CRI_VAR("봉사", LINE_CALC(BASE:LOCAL:기력, "0→3, 500→1, 1000→0, LIMIT/0～3"))
;조교자의 기력이[0→2, 250→1, 500→0, 1000→-1]을 가산(-1～2)
CALL ADD_CRI_VAR("사역", LINE_CALC(BASE:LOCAL:기력, "0→2, 500→0, 1000→-1, LIMIT/-1～2"))
;조교자의 기력이[0→-2, 250→-1, 500→0, 1000→2]를 가산(-2～2)
CALL ADD_CRI_VAR("애무", LINE_CALC(BASE:LOCAL:기력, "0→-2, 1000→2, LIMIT/-2～2"))
CALL ADD_CRI_VAR("성교", LINE_CALC(BASE:LOCAL:기력, "0→-2, 1000→2, LIMIT/-2～2"))
;조교자의 기력이[0→-2, 250→-1, 500→0, 1000→1]을 가산(-2～1)
CALL ADD_CRI_VAR("도구", LINE_CALC(BASE:LOCAL:기력, "0→-2, 500→0, 1000→1, LIMIT/-2～1"))
;조교자의 기력이[0→-2, 250→-1, 500→0]을 가산(-2～0)
CALL ADD_CRI_VAR("수치", LINE_CALC(BASE:LOCAL:기력, "0→-2, 500→0, LIMIT/-2～0"))
CALL ADD_CRI_VAR("성적봉사", LINE_CALC(BASE:LOCAL:기력, "0→-2, 500→0, LIMIT/-2～0"))
;조교자의 기력이[0→-3, 250→-2, 500→-1, 1000→1]을 가산(-3～1)
CALL ADD_CRI_VAR("가학", LINE_CALC(BASE:LOCAL:기력, "0→-3, 1000→1, LIMIT/-3～1"))
CALL ADD_CRI_VAR("이상", LINE_CALC(BASE:LOCAL:기력, "0→-3, 1000→1, LIMIT/-3～1"))

;-------------------------------------
;조교자의 이성
;-------------------------------------
@ACT_MENU_REASON_T, ARG = -1
#LOCALSIZE 1
#DIM LCOUNT
LOCAL = ARG == -1 ? TARGET # ARG

;조교자의 이성이[0→0～4, 250→0～2, 500→0]을 가산(0～4)
FOR LCOUNT, 0, 11
	CALL ADD_CRI_VAR, MENU(LCOUNT), , LINE_CALC(BASE:LOCAL:이성, "0→4, 500→0, LIMIT/0～4")
NEXT
;조교자의 이성이 750을 초과하고 있으면-4～0을 가산
SIF BASE:LOCAL:이성 > 750
	CALL ADD_CRI_VAR("이상", , -4)

;-------------------------------------
;조교자의 흥미
;-------------------------------------
@ACT_MENU_INTEREST_T, ARG = -1
#LOCALSIZE 2
#DIM LCOUNT
LOCAL = ARG == -1 ? TARGET # ARG

;조교자의 흥미를 취득한다
LOCAL:1 = BASE:LOCAL:흥미
FOR LCOUNT, 0, 11
	;이것들은[0→3, 250→1, 500→0, 750→-2, 1000→-3](-3～3)
	SIF MENU(LCOUNT) == "사역"
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→3, 1000→-3, LIMIT/-3～3"))
	;이것들은[0→-3, 250→-2, 500→0, 750→1, 1000→3](-3～3)
	SIF GROUPMATCH(MENU(LCOUNT), "회화", "애무", "성교")
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→-3, 1000→3, LIMIT/-3～3"))
	;이것들은[0→-2, 250→-1, 500→0, 750→1, 1000→2](-2～2)
	SIF GROUPMATCH(MENU(LCOUNT), "도구", "봉사")
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→-2, 1000→2, LIMIT/-2～2"))
	;이것들은[0→-3, 250→-2, 500→-1, 750→0, 1000→1](-3～1)
	SIF MENU(LCOUNT) == "성적봉사"
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→-3, 1000→1, LIMIT/-3～1"))
NEXT

;-------------------------------------
;조교자의 초조
;-------------------------------------
@ACT_MENU_IRRITATION_T, ARG = -1
#LOCALSIZE 2
#DIM LCOUNT
LOCAL = ARG == -1 ? TARGET # ARG

;조교자의 초조를 취득한다
LOCAL:1 = BASE:LOCAL:초조
FOR LCOUNT, 0, 11
	;이것들은[200→2, 400→3, 600→4, 800→5](2～5)
	SIF MENU(LCOUNT) == "가학"
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "200→2, 800→5, LIMIT/2～5"))
	;이것들은[0→1, 200→2, 400→3, 600→4, 800→5](1～5)
	SIF MENU(LCOUNT) == "사역"
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→1, 800→5, LIMIT/1～5"))
	;이것들은[400→0, 600→-2, 800→-3](-3～0)
	SIF GROUPMATCH(MENU(LCOUNT), "도구", "성교")
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "400→0, 800→-3, LIMIT/-3～0"))
	;이것들은[200→0, 400→-1, 600→-3, 800→-5](-5～0)
	SIF GROUPMATCH(MENU(LCOUNT), "회화", "애무")
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "400→-1, 800→-5, LIMIT/-5～0"))
	;이것들은[0→0, 200→-1, 400→-2, 600→-3, 800→-4](-4～0)
	SIF MENU(LCOUNT) == "성적봉사"
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→0, 800→-4, LIMIT/-4～0"))
NEXT

;-------------------------------------
;조교자의 호의
;-------------------------------------
@ACT_MENU_LIKE_T

;조교자의 호의가[-2000→-6, 2000→0]을 가산(-6～0)
CALL ADD_CRI_VAR("성적봉사", LINE_CALC(CFLAG:호의, "-2000→-6, 2000→0, LIMIT/-6～0"))
;조교자의 호의가[-2000→-5, 2000→0]을 가산(-5～0)
CALL ADD_CRI_VAR("성교", LINE_CALC(CFLAG:호의, "-2000→-5, 2000→0, LIMIT/-5～0"))
;조교자의 호의가[-2000→-4, 2000→4]를 가산(-4～4)
CALL ADD_CRI_VAR("애무", LINE_CALC(CFLAG:호의, "-2000→-4, 2000→4, LIMIT/-4～4"))

;-------------------------------------
;조교 대상의 호의
;주인공으로부터 조교자에게로의 호의는 조교자 자신이 보유 하고 있는데 주의
;-------------------------------------
@ACT_MENU_LIKE_M

;조교 대상의 호의가[-2000→-10, 2000→0]을 가산(-10～0)
CALL ADD_CRI_VAR("성적봉사", LINE_CALC(CFLAG:M호의, "-2000→-10, 2000→0, LIMIT/-10～0"))
;조교 대상의 호의가[-2000→-12, 2000→0]을 가산(-12～0)
CALL ADD_CRI_VAR("성교", LINE_CALC(CFLAG:M호의, "-2000→-12, 2000→0, LIMIT/-12～0"))
;조교 대상의 호의가[-2000→-4, 2000→4]를 가산(-4～4)
CALL ADD_CRI_VAR("봉사", LINE_CALC(CFLAG:M호의, "-2000→-4, 2000→4, LIMIT/-4～4"))

;-------------------------------------
;조교자 상태
;-------------------------------------
@ACT_MENU_CONDITION_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;조교자 상태(0=통상/1=피폐/2=쇠약/3=무기력/4=몽롱/5=정욕/6=분노/7=싫증/8=울적)
SELECTCASE TCVAR:상태변화
	;피폐·쇠약
	CASE 1, 2
		;조교자의 피로
		CALL ADD_CRI_VAR("회화",   TCVAR:상태변화 * 2 + TFLAG:조교자소모도)
		CALL ADD_CRI_VAR("성교", -(TCVAR:상태변화 * 3 + TFLAG:조교자소모도))
		CALL ADD_CRI_VAR("가학", -(TCVAR:상태변화 * 2 + TFLAG:조교자소모도))
		CALL ADD_CRI_VAR("성적봉사", -(TCVAR:상태변화 * 3 + TFLAG:조교자소모도))
	;무기력
	CASE 3
		CALL ADD_CRI_VAR("애무", -3)
		CALL ADD_CRI_VAR("도구", -1)
		CALL ADD_CRI_VAR("성교", -5)
		CALL ADD_CRI_VAR("봉사",  5)
		CALL ADD_CRI_VAR("가학", -3)
		CALL ADD_CRI_VAR("이상", -1)
		CALL ADD_CRI_VAR("사역",  3)
	;몽롱
	CASE 4
		CALL ADD_CRI_VAR("애무", -3)
		CALL ADD_CRI_VAR("도구", -7)
		CALL ADD_CRI_VAR("성교", -3)
		CALL ADD_CRI_VAR("수치",   , 6)
		CALL ADD_CRI_VAR("봉사",   , 3)
		CALL ADD_CRI_VAR("가학",   , 2)
		CALL ADD_CRI_VAR("이상",   , 2)
		CALL ADD_CRI_VAR("성적봉사",   , 2)
	;정욕
	CASE 5
		CALL ADD_CRI_VAR("애무",  2)
		CALL ADD_CRI_VAR("도구",  1)
		CALL ADD_CRI_VAR("성교", TALENT:충동적 ? 7 # 4, 5)
		CALL ADD_CRI_VAR("수치", -3)
		CALL ADD_CRI_VAR("봉사",  2, 3)
		CALL ADD_CRI_VAR("가학",   , 2)
		CALL ADD_CRI_VAR("이상",   , 2)
		CALL ADD_CRI_VAR("성적봉사", TALENT:충동적 ? 7 # 4, 5)
	;분노
	CASE 6
		;TALENT:충동적 =충동적  TALENT:자제적 =자제적
		CALL ADD_CRI_VAR("가학", , 7 + TALENT:충동적 * 5 - TALENT:자제적 * 3)
	;지루함
	CASE 7
		CALL ADD_CRI_VAR("회화", -5)
		CALL ADD_CRI_VAR("이상",   , 5)
		CALL ADD_CRI_VAR("사역",   , 5)
	;울적
	CASE 8
		CALL ADD_CRI_VAR("애무", ,  5)
		CALL ADD_CRI_VAR("도구", ,  5)
		CALL ADD_CRI_VAR("성교", ,  5)
		CALL ADD_CRI_VAR("수치", ,  5)
		CALL ADD_CRI_VAR("봉사", ,  5)
		CALL ADD_CRI_VAR("가학", ,  8)
		CALL ADD_CRI_VAR("이상", , 11)
		CALL ADD_CRI_VAR("사역", ,  8)
		CALL ADD_CRI_VAR("성적봉사", ,  5)
ENDSELECT

;-------------------------------------
;조교 대상 상태
;욕정하고 있는 조교 대상을 봐 조교자가 어떻게 생각할까
;즉 소질의 참조처는 조교자
;-------------------------------------
@ACT_MENU_CONDITION_M, ARG = -1
#DIM DYNAMIC TGT
TGT = ARG == -1 ? TARGET # ARG

;조교 대상 상태가 정욕
IF IS_CONDITION(TARGET, "정욕")
	;조교자가 반항적/자제적/쾌감을부정/새드/심술쟁이중 2개 이상 소지
	IF TRUECHECK(TALENT:TGT:반항적, TALENT:TGT:자제적, TALENT:TGT:쾌감을부정, TALENT:TGT:새드, TALENT:TGT:심술쟁이) > 1
		CALL ADD_CRI_VAR("수치", 2)
		CALL ADD_CRI_VAR("봉사", 1)
		CALL ADD_CRI_VAR("성적봉사", 4)
	ELSE
		CALL ADD_CRI_VAR("성교", 3)
		CALL ADD_CRI_VAR("성적봉사", 4)
	ENDIF
ENDIF

;-------------------------------------
;조교 대상의 피로
;TFLAG:소모도 =피로도
;성적봉사가 없는 것은 위화감
;-------------------------------------
@ACT_MENU_TIRED_M

;조교 대상의 소모 플래그(피로)
IF TFLAG:소모도
	CALL ADD_CRI_VAR("성교", -TFLAG:소모도 / 2)
	CALL ADD_CRI_VAR("봉사", -TFLAG:소모도 / 2)
	CALL ADD_CRI_VAR("가학", -TFLAG:소모도)
	CALL ADD_CRI_VAR("이상", -TFLAG:소모도 / 2)
	CALL ADD_CRI_VAR("사역", -TFLAG:소모도 / 2)
ENDIF

;-------------------------------------
;최초 회화 보정
;-------------------------------------
@ACT_MENU_FIRST

;최초로 높은 확률 회화
SIF !TFLAG:경과시간
	CALL ADD_CRI_VAR("회화", 10)

;-------------------------------------
;대거울 보너스
;-------------------------------------
@ACT_MENU_MILLER
SIF ITEM:대거울
	CALL ADD_CRI_VAR("수치", , 3)

;──────────────────────────────
;상황이나 전회의 커맨드에 의한 변동
;직전의 조교 대상의 반응
;-------------------------------------
;더하드하게
;-------------------------------------
@ACT_MENU_PLEASE_HARD

IF IS_NOWCOMNAME("더하드하게")
	CALL ADD_CRI_VAR("애무", -2)
	CALL ADD_CRI_VAR("성교", -1)
	CALL ADD_CRI_VAR("봉사", -1)
	CALL ADD_CRI_VAR("가학",  4)
	CALL ADD_CRI_VAR("이상",  2)
ENDIF

;-------------------------------------
;용서를빈다
;새드 OR심술쟁이
;-------------------------------------
@ACT_MENU_PLEASE_HELP

IF IS_NOWCOMNAME("용서를빈다")
	;조교 대상이 울적 이외
	IF TCVAR:상태변화 < 8
		CALL ADD_CRI_VAR("가학", -5, (TALENT:새드 || TALENT:심술쟁이) ? 4 # 0)
		CALL ADD_CRI_VAR("이상", -5, (TALENT:새드 || TALENT:심술쟁이) ? 4 # 0)
	ENDIF
ENDIF

;-------------------------------------
;기분 좋게 해
;-------------------------------------
@ACT_MENU_PLEASE_PREASURE

IF IS_NOWCOMNAME("기분좋게해줘")
	CALL ADD_CRI_VAR("애무", 2)
	CALL ADD_CRI_VAR("도구", 3)
	CALL ADD_CRI_VAR("성교", 2)
	CALL ADD_CRI_VAR("성적봉사", 3)
ENDIF

;-------------------------------------
;날뛴다
;-------------------------------------
@ACT_MENU_VIOLENT

IF IS_NOWCOMNAME("날뛴다")
	CALL ADD_CRI_VAR("애무", -2)
	CALL ADD_CRI_VAR("성교", -5)
	CALL ADD_CRI_VAR("가학",  5)
	CALL ADD_CRI_VAR("사역",  5)
	CALL ADD_CRI_VAR("성적봉사", -6)
ENDIF

;-------------------------------------
;도구 해제
;-------------------------------------
@ACT_MENU_UNEQUIP

IF IS_NOWCOMNAME("도구를제외한다")
	CALL ADD_CRI_VAR("가학", 10)
ENDIF

;-------------------------------------
;쓰러트리기시의 연속 애무 보정
;-------------------------------------
@ACT_MENU_PETTING_CONTINUE

;쓰러트리기중으로, 전의 턴의 메뉴가 애무
IF TEQUIP:쓰러트리기중 && TFLAG:전ACT분류 == 1
	CALL ADD_CRI_VAR("애무", 20)
ENDIF

;-------------------------------------
;식스나인 유발
;-------------------------------------
@ACT_MENU_69

;식스나인이 되는 경향(TFLAG:전ACT분류 =전의 턴의 조교 메뉴)
SELECTCASE TFLAG:전ACT분류
	CASE 1
		CALL ADD_CRI_VAR("봉사", 1, 2)
	CASE 5
		CALL ADD_CRI_VAR("애무", 1, 2)
ENDSELECT

;--------------------------------------
;조교자의 복장
;-------------------------------------
@ACT_MENU_CLOTH_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;복장
;LOCAL:3(성교), LOCAL:10(성교 봉사)에 영향
;조교자가 하반신윗도리를 입고 있다
IF TEQUIP:LOCAL:하반신윗도리
	CALL ADD_CRI_VAR("성교", -2)
	CALL ADD_CRI_VAR("성적봉사", -2)
ENDIF
;조교자가 상반신윗도리를 입고 있다
IF TEQUIP:LOCAL:상반신윗도리
	CALL ADD_CRI_VAR("성교", -1)
	CALL ADD_CRI_VAR("성적봉사", -1)
ENDIF
;조교자가 전신윗도리를 입고 있다
IF TEQUIP:LOCAL:전신윗도리
	CALL ADD_CRI_VAR("성교", -3)
	CALL ADD_CRI_VAR("성적봉사", -3)
ENDIF

;--------------------------------------
;조교 대상의 복장
;대상의 복장은 2개소로 정의되고 있었다
;그 중 1개소를 comment out
;-------------------------------------
@ACT_MENU_CLOTH_M, ARG = -1, ARG:1 = -1
#LOCALSIZE 2
LOCAL = ARG   == -1 ? TARGET # ARG
LOCAL = ARG:1 == -1 ? MASTER # ARG:1

;;조교 대상이 하반신윗도리를 입고 있다
;IF TEQUIP:(LOCAL:1):하반신윗도리
;	;조교자에게 벽통과가 없다
;	SIF ! TALENT:LOCAL:벽통과
;		CALL ADD_CRI_VAR("성교", -2)
;	CALL ADD_CRI_VAR("성적봉사", -2)
;ENDIF
;조교 대상이 상반신윗도리를 입고 있다
;IF TEQUIP:(LOCAL:1):상반신윗도리
;	;조교자에게 벽통과가 없다
;	SIF ! TALENT:LOCAL:벽통과
;		CALL ADD_CRI_VAR("성교", -1)
;	CALL ADD_CRI_VAR("성적봉사", -1)
;ENDIF
;조교 대상이 전신윗도리를 입고 있다
;IF TEQUIP:(LOCAL:1):전신윗도리
;	;조교자에게 벽통과가 없다
;	SIF ! TALENT:LOCAL:벽통과
;		CALL ADD_CRI_VAR("성교", -3)
;	CALL ADD_CRI_VAR("성적봉사", -3)
;ENDIF
;윗도리
;조교 대상의 하반신윗도리로 방해될 때
IF CLOTHESTYPE(1, 1) == 2
	CALL ADD_CRI_VAR("회화", 1, 4)
	;조교자에게 벽통과가 없다
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("애무", , -2)
	CALL ADD_CRI_VAR("도구", -2, -3)
	;조교자에게 벽통과가 없다
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("성교", -1, -5)
	CALL ADD_CRI_VAR("수치", , -7)
	CALL ADD_CRI_VAR("성적봉사", -1, -5)
;조교 대상의 하반신윗도리가 있을 때
ELSEIF CLOTHESTYPE(1, 1)
	CALL ADD_CRI_VAR("회화", , 3)
	;조교자에게 벽통과가 없다
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("애무", , -1)
	CALL ADD_CRI_VAR("도구", -1, -2)
	;조교자에게 벽통과가 없다
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("성교", , -1)
	CALL ADD_CRI_VAR("성적봉사", , -1)
ENDIF
;조교 대상의 상반신윗도리가 있을 때
IF CLOTHESTYPE(1, 3)
	CALL ADD_CRI_VAR("회화", , 2)
	;조교자에게 벽통과가 없다
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("애무", , -2)
	CALL ADD_CRI_VAR("도구", , -2)
	CALL ADD_CRI_VAR("수치", , -4)
ENDIF

;속옷
;조교 대상의 하반신속옷이 있을 때
IF CLOTHESTYPE(1, 0)
	CALL ADD_CRI_VAR("회화", , 1)
	;조교자에게 벽통과가 없다
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("애무", , -1)
	CALL ADD_CRI_VAR("도구", -1, -1)
	;조교자에게 벽통과가 없다
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("성교", , -4)
	CALL ADD_CRI_VAR("성적봉사", , -4)
ENDIF
;조교 대상의 상반신속옷이 있을 때
IF CLOTHESTYPE(1, 2)
	CALL ADD_CRI_VAR("회화", , 2)
	;조교자에게 벽통과가 없다
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("애무", , -2)
	CALL ADD_CRI_VAR("도구", , -4)
	CALL ADD_CRI_VAR("수치", , -2)
ENDIF

;----------------------------------------------------------
;TEQUIP 관련
;---------------------------------------------------------
;-------------------------------------
;미약
;-------------------------------------
@ACT_MENU_DRAG

IF TEQUIP:미약
	CALL ADD_CRI_VAR("회화", , 4)
	CALL ADD_CRI_VAR("애무", TEQUIP:미약 / 2)
	CALL ADD_CRI_VAR("도구", TEQUIP:미약 / 2)
	CALL ADD_CRI_VAR("성교", TEQUIP:미약 / 2)
	CALL ADD_CRI_VAR("수치", TEQUIP:미약 / 3)
	CALL ADD_CRI_VAR("성적봉사", TEQUIP:미약 / 2)
ENDIF

;-------------------------------------
;바이브
;조수의 A감각이 높으면 성교 하고 싶어해?
;-------------------------------------
@ACT_MENU_VIBE

;바이브
IF TEQUIP:바이브
	CALL ADD_CRI_VAR("애무", ,  3)
	CALL ADD_CRI_VAR("도구", ,  6)
	CALL ADD_CRI_VAR("성교", , -9)
	SIF ASSI >= 0
		CALL ADD_CRI_VAR("성교", GET_ABL(ASSI, "Ａ감각"))
	CALL ADD_CRI_VAR("수치", 7)
	CALL ADD_CRI_VAR("가학", , 2)
ENDIF

;-------------------------------------
;A바이브
;-------------------------------------
@ACT_MENU_AVIBE

;애널바이브
IF TEQUIP:애널바이브
	CALL ADD_CRI_VAR("애무", , 3)
	CALL ADD_CRI_VAR("도구", , 6)
	;바이브가 없는 경우 섹스 하고 싶어한다
	CALL ADD_CRI_VAR("성교", TEQUIP:바이브 ? 0 # 8)
	CALL ADD_CRI_VAR("수치", 8)
	CALL ADD_CRI_VAR("가학", , 5)
	CALL ADD_CRI_VAR("이상", , 4)
ENDIF

;-------------------------------------
;애널비즈
;-------------------------------------
@ACT_MENU_ABEADS

;애널비즈
IF TEQUIP:애널비즈
	CALL ADD_CRI_VAR("애무", , 5)
	CALL ADD_CRI_VAR("도구", , 8)
	CALL ADD_CRI_VAR("성교", TEQUIP:바이브 ? 0 # 2)
	CALL ADD_CRI_VAR("수치", 5)
	CALL ADD_CRI_VAR("가학", , 3)
	CALL ADD_CRI_VAR("이상", , 1)
ENDIF

;-------------------------------------
;애널 플러그
;-------------------------------------
@ACT_MENU_APLUG

;관장＋애널 플러그
IF TEQUIP:관장기＋플러그
	CALL ADD_CRI_VAR("회화", , 5)
	CALL ADD_CRI_VAR("애무", , 3)
	CALL ADD_CRI_VAR("도구", , 3)
	CALL ADD_CRI_VAR("성교", , TEQUIP:바이브 ? 0 # 1)
	CALL ADD_CRI_VAR("수치", 8)
	CALL ADD_CRI_VAR("가학", , 7)
	CALL ADD_CRI_VAR("이상", , 7)
ENDIF

;-------------------------------------
;클리캡
;-------------------------------------
@ACT_MENU_CRICAP

;클리캡
IF TEQUIP:클리캡
	CALL ADD_CRI_VAR("애무", , 2)
	CALL ADD_CRI_VAR("도구", , 2)
	CALL ADD_CRI_VAR("수치", 4)
ENDIF

;-------------------------------------
;오나홀
;-------------------------------------
@ACT_MENU_ONAHOLE

IF TEQUIP:오나홀
	CALL ADD_CRI_VAR("애무", , 2)
	CALL ADD_CRI_VAR("도구", , 2)
	CALL ADD_CRI_VAR("수치", 4)
	CALL ADD_CRI_VAR("이상", 1)
ENDIF

;-------------------------------------
;니플캡
;-------------------------------------
@ACT_MENU_NIPPLECAP

IF TEQUIP:니플캡
	CALL ADD_CRI_VAR("애무", , 2)
	CALL ADD_CRI_VAR("도구", , 2)
	CALL ADD_CRI_VAR("수치", 4 + TALENT:MASTER:남자)
	CALL ADD_CRI_VAR("이상", , TALENT:MASTER:남자 ? 5 # 0)
ENDIF

;-------------------------------------
;줄
;-------------------------------------
@ACT_MENU_ROPE

IF TEQUIP:줄
	CALL ADD_CRI_VAR("회화",  3)
	CALL ADD_CRI_VAR("성교", , -1)
	CALL ADD_CRI_VAR("봉사", -7)
	CALL ADD_CRI_VAR("가학",  3)
	CALL ADD_CRI_VAR("이상",  , 2)
	CALL ADD_CRI_VAR("성적봉사", -3)
ENDIF

;-------------------------------------
;아이마스크
;-------------------------------------
@ACT_MENU_EYEMASK

IF TEQUIP:아이마스크
	CALL ADD_CRI_VAR("회화",   , 4)
	CALL ADD_CRI_VAR("수치",  3, 4)
	CALL ADD_CRI_VAR("봉사", -5)
	CALL ADD_CRI_VAR("가학",  1)
	CALL ADD_CRI_VAR("이상",   , 3)
ENDIF

;-------------------------------------
;볼개그
;-------------------------------------
@ACT_MENU_GAG

IF TEQUIP:볼개그
	CALL ADD_CRI_VAR("회화", -10)
	CALL ADD_CRI_VAR("봉사",  -9)
	CALL ADD_CRI_VAR("가학",   2)
	CALL ADD_CRI_VAR("이상", , 4)
ENDIF

;-------------------------------------
;삼각목마
;-------------------------------------
@ACT_MENU_HORSE

IF TEQUIP:삼각목마
	CALL ADD_CRI_VAR("회화",   , 6)
	CALL ADD_CRI_VAR("애무", -6)
	CALL ADD_CRI_VAR("도구", -6)
	CALL ADD_CRI_VAR("수치", -3)
	CALL ADD_CRI_VAR("가학",  7, 2)
	CALL ADD_CRI_VAR("이상",  3, 7)
ENDIF

;-------------------------------------
;야외플레이
;-------------------------------------
@ACT_MENU_OUT

SIF TEQUIP:야외플레이
	CALL ADD_CRI_VAR("수치", , 6)

;-------------------------------------
;대거울
;-------------------------------------
@ACT_MENU_T_MILLER

SIF TEQUIP:대거울
	CALL ADD_CRI_VAR("수치", , 8)

;-------------------------------------
;도구 해제 제한
;체력이 있는데 풀 아머 하고 있다면 도구 메뉴를 선택하지 않도록 (도구 해제가 되기 때문에)
;조금 처리를 바꾸어 보는 테스트. 변변치않았으면 미안해요.
;·남자라면Ｖ계 없음에서도 풀 아머로 간주하도록(듯이)
;-------------------------------------
@ACT_MENU_KILL_UNEQUIP, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? MASTER # ARG

IF (BASE:LOCAL:체력 + BASE:LOCAL:기력) > 500
	SIF (TALENT:LOCAL:남자 || TEQUIP:바이브 || TEQUIP:페니스밴드) && TEQUIP:애널바이브 && (TEQUIP:클리캡 || TEQUIP:오나홀) && TEQUIP:니플캡
		CALL ADD_CRI_VAR("도구", -15)
ENDIF

;-------------------------------------
;조교자 자위중
;-------------------------------------
@ACT_MENU_MASTURBATION_T

;조교자 자위중
IF GETBIT(TEQUIP:자위중, 1)
	CALL ADD_CRI_VAR("회화",  , 4)
	CALL ADD_CRI_VAR("도구",  , 4)
	CALL ADD_CRI_VAR("성교",  , 4)
	CALL ADD_CRI_VAR("수치", 2, 4)
	CALL ADD_CRI_VAR("봉사", 2, 4)
	CALL ADD_CRI_VAR("성적봉사",  , 4)
ENDIF

;-------------------------------------
;조교 대상 자위중
;-------------------------------------
@ACT_MENU_MASTURBATION_M

;자위중
IF GETBIT(TEQUIP:자위중, 0)
	CALL ADD_CRI_VAR("회화", 2, 4)
	CALL ADD_CRI_VAR("도구", 2, 4)
	CALL ADD_CRI_VAR("성교", ,  4)
	CALL ADD_CRI_VAR("수치", 2, 4)
	CALL ADD_CRI_VAR("봉사",  , 4)
	CALL ADD_CRI_VAR("성적봉사",  -10)
ENDIF

;-------------------------------------
;벌중
;-------------------------------------
@ACT_MENU_OSHIOKI

;벌플래그
IF TFLAG:벌플래그
	CALL ADD_CRI_VAR("가학", , 4)
	CALL ADD_CRI_VAR("이상", , 2)
	CALL ADD_CRI_VAR("사역", , 1)
	CALL ADD_CRI_VAR("봉사", -999)
ENDIF

;──────────────────────────────
;실행 불가능의 판정
;──────────────────────────────
;-------------------------------------
;삽입
;우선, 현행의 삽입 속행 사양(pa0903판 시점)에 맞춘다(COMABLE로 속행하지 않는 커맨드를 차는 것 낸다)
;라고 할까 이 사양이 있는 경우, 서투르게 허가되지 않는 것에 떨어뜨리면 커맨드 전멸로 에러가 될 것
;
;삽입 속행 제한가능으로 실행 가능한 커맨드가 없는 것은 이 시점에서 찬다
;보정은 성교(성적봉사) > 애무 > 회화·가학
;분노 보정은 이 사양이라면 해제에 향하기 쉬워질 것도 아니기 때문에 놓아둔다
;로시에 삽입 해제라든지 그러한 별이벤트를 사이에 두어 주는 편이내림?
;페니반시는 커맨드 금지에 의한 조치가 움직이지 않기 때문에, 추가로 속행시키기 쉽게 하기 위해서 보정을 준다
;-------------------------------------
@ACT_MENU_INSERT

;성교/성교봉사중
IF TEQUIP:성교중 || TEQUIP:성교봉사중
	;페니스밴드 삽입
	IF TEQUIP:성교중 && TEQUIP:조교자페니스밴드
		CALL ADD_CRI_VAR("성교", 5)
	;페니스밴드 삽입 이외
	ELSE
		CALL ADD_CRI_VAR("도구", -999)
		CALL ADD_CRI_VAR("수치", -999)
		CALL ADD_CRI_VAR("봉사", -999)
		CALL ADD_CRI_VAR("가학", , 2)
		CALL ADD_CRI_VAR("이상", -999)
		CALL ADD_CRI_VAR("사역", -999)
	ENDIF
	CALL ADD_CRI_VAR("회화", , 2)
	CALL ADD_CRI_VAR("애무", , 4)
	;성교중
	IF TEQUIP:성교중
		CALL ADD_CRI_VAR("성교", 5, 5)
		SIF !TEQUIP:조교자페니스밴드
			CALL ADD_CRI_VAR("성적봉사", -999)
	;성교봉사중
	ELSE
		CALL ADD_CRI_VAR("성적봉사", 5, 5)
		CALL ADD_CRI_VAR("성교", -999)
	ENDIF
ENDIF

;----------------------------------------------------------
;애태움중, Ｃ자극을 중단하지 않도록
;----------------------------------------------------------
@ACT_MENU_IRRITATE

;애태움중
IF TCVAR:MASTER:애태움수
	;애태움 종료의 순간은 거기에 집중. 혹은 성교봉사중이 아닌 경우
	;(성교 봉사에 관해서는 삽입 보정의 내용으로 문제 없는 것이 아닐까)
	IF TCVAR:MASTER:애태움수 == 1 || !TEQUIP:성교봉사중
		;향후 재고의 필요유리카
		CALL ADD_CRI_VAR("회화", -999)
		CALL ADD_CRI_VAR("도구", -999)
		CALL ADD_CRI_VAR("성교", -999)
		CALL ADD_CRI_VAR("수치", -999)
		CALL ADD_CRI_VAR("봉사", -999)
		CALL ADD_CRI_VAR("가학", -999)
		CALL ADD_CRI_VAR("이상", -999)
		CALL ADD_CRI_VAR("사역", -999)
	ENDIF
ENDIF

;----------------------------------------------------------
;A섹스 회피
;----------------------------------------------------------
@ACT_MENU_AGUARD

;조교 방침이 이상의 경우,50%의 확률로 판정을 실시한다
IF IS_POLICY("지금", "이상") && RAND:2
	;ＡＳ가능이지만, 익숙해지지 않기 때문에 가드 한다. 다만 50%(?!)
	SIF EXP:MASTER:Ａ경험 < 2 && !TEQUIP:애널바이브 && !TEQUIP:애널비즈 && !TEQUIP:관장기＋플러그
		CALL ADD_CRI_VAR("성교", -999)
ENDIF

;----------------------------------------------------------
;애널섹스중, 소프트 방침으로 다른 체위로 변경하지 않도록
;----------------------------------------------------------
@ACT_MENU_ASEX_LIMITATION

IF IS_POLICY("지금", "소프트")
	SIF TEQUIP:성교중 == 6
		CALL ADD_CRI_VAR("성교", -999)
	SIF TEQUIP:성교봉사중 == 6
		CALL ADD_CRI_VAR("성적봉사", -999)
ENDIF

;----------------------------------------------------------
;실행 판정
;----------------------------------------------------------
@ACT_MENU_ABLE

;불가능 판정
FOR LOCAL, 0, 11
	CALL ACTM_ABLE(LOCAL)
	SIF !RESULT
		CALL DIM_CRI_VAR(MENU(LOCAL), -999)
NEXT

;-------------------------------------
;히트치
;-------------------------------------
@ACT_MENU_HEAT
#LOCALSIZE 1
#DIM LCOUNT

FOR LCOUNT, 0, 11
	SIF LCOUNT == 9
		CONTINUE
	LOCAL = TFLAG:(@"%MENU(LCOUNT)%히트") * LIMIT(TFLAG:가능한ACT계열수 - 1, 0, 4) / 4
	CALL ADD_CRI_VAR(MENU(LCOUNT), -LOCAL)
NEXT

;-------------------------------------
;ACT 보정치
;-------------------------------------
@ACT_MENU_CORRECTION
#DIM LCOUNT

FOR LCOUNT, 0, 11
	;보정치를 가산
	CALL ACT_M_RES_MENU(LCOUNT)
	CALL ADD_CRI_VAR(MENU(LCOUNT), RESULT)
NEXT
