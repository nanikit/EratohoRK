;==================================================
;
;커맨드 종료 처리
;
;==================================================
@EVENTCOMEND
;카운트 변수
#DIM LCOUNT, 2
;조교 대상 호감도 증감치
#DIM M호의증감
;조교자 호감도 증감치
#DIM T호의증감
;조교 대상 경험 가격인상승치
#DIM M경험상승
;조교자 경험 가격인상승치
#DIM T경험상승
;죄악감 상승치
#DIM 죄악감상승
;아라이먼트변동 값
#DIM 아라변동
;조교 대상 체력 회복치
#DIM M체력회복
;조교 대상 기력 회복치
#DIM M기력회복
;조교자 체력 회복치
#DIM T체력회복
;조교자 기력 회복치
#DIM T기력회복
;히트치 감소량
#DIM 쿨치
;조교 계속 기준치
#DIM 계속기준치

;────────────────────────────────────
;절정계 전회 턴 참조 플래그의 리셋트
;────────────────────────────────────
;LOCAL 초기화
VARSET LOCAL
FOR LCOUNT, 0, CHARANUM
	VARSET TCVAR:LCOUNT:0, 0, 80, 90
NEXT

;────────────────────────────────────
;모유와 사진의 계산
;────────────────────────────────────
;조수ACT가 사진촬영
SIF TFLAG:조수ACT == 52
	CALL SELL_PHOTO


;────────────────────────────────────
;호감도의 계산（조교 대상）
;────────────────────────────────────
;LOCAL 초기화
VARSET LOCAL

;호감도업, 매혹 소질을 바탕으로 증감치의 종을 재배한다
M호의증감 = TFLAG:호감도업 + TALENT:매혹 * (RAND:3 + 1)

;CUP:MASTER:쾌Ｘ에 의해 증감치 증가
FOR LCOUNT, 0, 4
	SIF CUP:MASTER:LCOUNT > 100
		M호의증감 += 1 + CUP:MASTER:LCOUNT / 1000
NEXT

;그 외 요소에 의한 증감
SIF M호의증감 > 0
	M호의증감 = M호의증감 * (10 + GET_ABL(MASTER, "순종")) * (2 + CFLAG:MASTER:조교레벨 / 3) * (11 - MARK:반항각인) * (3 + TALENT:MASTER:순수 - TALENT:MASTER:반항적 + TALENT:매혹 + TALENT:사교적 - TALENT:대인기피) / 100
SIF M호의증감 < 0
	M호의증감 = M호의증감 * (1 + MARK:반항각인) * (100 - GET_ABL(MASTER, "순종")) * (3 + TALENT:MASTER:반항적 - TALENT:MASTER:순수 + TALENT:MASTER:거만함 - TALENT:위압감) / 100

;호의 증감치를 결정
SIF M호의증감 != 0
	M호의증감 = ABLOSI_10(M호의증감 * 50)

;호감도를 증감
CFLAG:M호의 += M호의증감

;지문 표시
SIF M호의증감 != 0
	PRINTFORML %CALLNAME:TARGET%에의 호감도\@ M호의증감 >= 0 ? + # \@{M호의증감}  (현재가{CFLAG:M호의})


;────────────────────────────────────
;호감도의 계산（조교자）
;────────────────────────────────────
;LOCAL 초기화
VARSET LOCAL

;PALAM 상승등을 바탕으로 증감치의 종을 재배한다
T호의증감 = BASE:만족 / 200 + (CUP:쾌Ｃ + CUP:쾌Ｖ + CUP:쾌Ａ + CUP:쾌Ｂ) / 250 + (CUP:MASTER:욕정 + CUP:MASTER:굴복 - CUP:MASTER:반항) / 150 + (SOURCE:MASTER:사랑 + SOURCE:MASTER:달성) / 100 + (NOWEX:Ｃ절정 + NOWEX:Ｖ절정 + NOWEX:Ａ절정 + NOWEX:Ｂ절정 + NOWEX:분유 + NOWEX:사정) * 2

;조교 대상 호감도 상승중 또한 리액션이 온순하면 추가로 증가
SIF M호의증감 > 0 && TFLAG:REACT분류 < 5
	T호의증감 += RAND:M호의증감 * (3 + TALENT:MASTER:매혹 * 2 + TALENT:MASTER:사교적 - TALENT:MASTER:대인기피 + TALENT:MASTER:감수성 - TALENT:MASTER:감정결핍) / 8
;조교자 쾌감이 크면 추가로 증가
FOR LCOUNT, 0, 4
	SIF CUP:LCOUNT > 100
		T호의증감 += 1
NEXT

;조교 대상으로 매혹 소질이 있으면 추가로 증가
SIF TALENT:MASTER:매혹
	T호의증감 += 1

;초조로 상쇄
IF BASE:초조 > 0
	;호의 증감치가 커질 정도로 초조에의 상쇄 효율은 떨어진다
	IF BASE:초조 - T호의증감 / 3 > BASE:초조 / 2
		BASE:초조 -= T호의증감 / 3
	ELSEIF BASE:초조 - T호의증감 / 3 > 0
		BASE:초조 /= 2
	ELSE
		BASE:초조 /= 3
	ENDIF
	;초조가 커질 정도로 호의 증감치는 비율로 줄어든다
	T호의증감 = T호의증감 * (MAXBASE:초조 - BASE:초조) / MAXBASE:초조
ENDIF

;현재의 조교 대상 호감도가 큰 만큼 커지는 보정치를 만든다
LOCAL = (CFLAG:M호의 - 500) / 200
;보정치를-5～5의 범위에 거둔다
LOCAL = LIMIT(LOCAL, -5, 5)
;조교 대상 호감도 상승, 하강 예정에 따라 다른 보정을 실시한다
SIF T호의증감 > 0
	T호의증감 = T호의증감 * (10 + GET_ABL(MASTER, "순종")) * (6 + LOCAL + TALENT:MASTER:매혹 * 2 + TALENT:MASTER:사교적 - TALENT:MASTER:대인기피) / ((30 + MARK:MASTER:트라우마) * 10)
SIF T호의증감 < 0
	T호의증감 = T호의증감 * (100 - GET_ABL(MASTER, "순종")) * (3 + MARK:반항각인 + TALENT:MASTER:반항적 * 2 - TALENT:MASTER:순수) / ((20 + CFLAG:MASTER:조교레벨 * 2 + LOCAL * 3) * 10)
;호감도 증감은-5～40의 범위에 거둔다
T호의증감 = LIMIT(T호의증감, -5, 40)

;호감도를 증감
CFLAG:호의 += T호의증감

;지문 표시
IF T호의증감 != 0
	IF TALENT:MASTER:독심술 == 1
		PRINTFORML %CALLNAME:MASTER%에의 호감도가\@ T호의증감 >= 0 ? 오른 것 같다 # 내린 것 같다 \@
	ELSEIF TALENT:MASTER:독심술 == 2
		PRINTFORML %CALLNAME:MASTER%에의 호감도\@ T호의증감 >= 0 ? + # \@{T호의증감}  (현재가{CFLAG:호의})
	ENDIF
ENDIF


;────────────────────────────────────
;경험치의 계산（조교 대상）
;────────────────────────────────────
;LOCAL 초기화
VARSET LOCAL

;경험치업을 바탕으로 상승치의 종을 재배한다
M경험상승 = TFLAG:경험치업

;욕정 상승치의 것 1/100만 경험 가격인상승(상한 20)
M경험상승 += LIMIT(CUP:MASTER:욕정 / 100, 0, 20)
;욕정 상승치가 500을 돌파하면 0～1의 부의 보정이 걸린다
SIF CUP:MASTER:욕정 > 500
	M경험상승 -= RAND:2

;공순 상승치의 것 3/200만 경험 가격인상승(상한 30)
M경험상승 += LIMIT(3 * CUP:MASTER:공순 / 200, 0, 30)
;공순 상승치가 200을 돌파하면 0～1의 부의 보정이 걸린다
SIF CUP:MASTER:공순 > 200
	M경험상승 -= RAND:2

;치정상승치의 RAND:(1/125)만 경험 가격인상승(상한 15)
LOCAL = LIMIT(CUP:MASTER:치정 / 125, 0, 16) + 1
M경험상승 += RAND:LOCAL

;반항 상승치의 것 3/1000만 경험 가격인상승을 억제(하한-6)
M경험상승 -= LIMIT(3 * CUP:MASTER:반항 / 1000, 0, 6)

;억울상승치의 것 1/240만 경험 가격인상승을 억제(하한-5)
M경험상승 -= LIMIT(CUP:MASTER:억울 / 240, 0, 5)

;봉사의 추가 경험치
IF TFLAG:ACT분류 == 5
	;REACT분류가 소극적 따른다
	SIF IS_COMGRONAME("소극적으로 한다")
		M경험상승 += 4
	;REACT분류가 적극적 따른다
	SIF IS_COMGRONAME("적극적으로 한다")
		M경험상승 += 7
ENDIF

;순종과 습득빠름/늦은 것 보정
M경험상승 = (M경험상승 + 1) * (50 + GET_ABL(MASTER, "순종") * 2) * (2 + TALENT:MASTER:습득빠름 - TALENT:MASTER:습득느림) / 120

;조교 대상 절정시, 경험치배
FOR LCOUNT, 0, 4
	SIF NOWEX:MASTER:LCOUNT
		M경험상승 *= 2
NEXT
;조교자 절정시, 경험치 3배
FOR LCOUNT, 0, 4
	SIF NOWEX:LCOUNT
		M경험상승 *= 3
NEXT

;조교자가 조교 대상보다 경험 풍부(빈약)일 때 경험 가격인상승(하강)
M경험상승 = M경험상승 * (CFLAG:조교레벨 + 5) / (CFLAG:MASTER:조교레벨 + 5)

;최저 1의 경험치는 손에 들어 온다
SIF M경험상승 < 1
	M경험상승 = 1

IF FLAG:3134 == 1
	TIMES M경험상승, 1.50
ELSEIF FLAG:3134 == 2
	TIMES M경험상승 , 2.00
ENDIF

;지문 표시
IF M경험상승 > 0
	CFLAG:MASTER:조교경험 += M경험상승
	PRINTFORML %CALLNAME:MASTER%경험치+{M경험상승}
ENDIF


;────────────────────────────────────
;경험치의 계산（조교자）
;────────────────────────────────────
;상승치 초기화
T경험상승 = 0

;각 파라미터의 상승치를 본다
FOR LCOUNT, 0, 50
	;윤활은 무시
	SIF LCOUNT == 4
		CONTINUE
	;PALAM 상승치의 것 9/2500을 경험치에 가산(상한 9)
	T경험상승 += LIMIT(9 * CUP:MASTER:LCOUNT / 2500, 0, 9)
NEXT

;굴복과 공순은 높게 평가되어 반항과 억울은 부의 요소로 한다

;굴복 상승치의 것 3/2500만 경험 가격인상승(상한 3)
T경험상승 += LIMIT(3 * CUP:MASTER:굴복 / 2500, 0, 3)
;굴복 상승치가 200을 돌파하면 0～1의 정의 보정이 걸린다
SIF CUP:MASTER:굴복 > 200
	T경험상승 += RAND:2

;공순 상승치의 것 3/2500만 경험 가격인상승(상한 3)
T경험상승 += LIMIT(3 * CUP:MASTER:공순 / 2500, 0, 3)
;공순 상승치가 200을 돌파하면 0～1의 정의 보정이 걸린다
SIF CUP:MASTER:공순 > 200
	T경험상승 += RAND:2

;반항 상승치의 것 9/2500만 경험 가격인상승을 억제(하한-9)
T경험상승 -= LIMIT(9 * CUP:MASTER:반항 / 2500, 0, 9)

;억울상승치의 것 9/2500만 경험 가격인상승을 억제(하한-9)
T경험상승 -= LIMIT(9 * CUP:MASTER:억울 / 2500, 0, 9)

;조교 대상의 반응에 의한 변동
;REACT분류가 소극적 따른다
SIF IS_COMGRONAME("소극적으로 한다")
	T경험상승 += 2
;REACT분류가 적극적 따른다
SIF IS_COMGRONAME("적극적으로 한다")
	T경험상승 += 5
;REACT분류가용서를빈다
SIF IS_COMGRONAME("용서를빈다")
	T경험상승 += 1
;REACT분류가날뛴다
SIF IS_COMGRONAME("날뛴다")
	T경험상승 -= 3
;REACT분류가 요구에 응하지 않는다
SIF IS_COMGRONAME("거부")
	T경험상승 -= 1

;조교 대상 절정시, 경험치 3배
FOR LCOUNT, 0, 4
	SIF NOWEX:MASTER:LCOUNT
		T경험상승 *= 3
NEXT
;조교자 절정시, 경험치배
FOR LCOUNT, 0, 4
	SIF NOWEX:LCOUNT
		T경험상승 *= 2
NEXT

;조교 대상으로부터 정액을 짜내면 경험 가격인상승
T경험상승 += DOWNBASE:MASTER:사정 / 100

;조교 대상이 조교자보다 경험 풍부(빈약)일 때 경험 가격인상승(하강)
T경험상승 = T경험상승 * (CFLAG:MASTER:조교레벨 + 5) / (CFLAG:조교레벨 + 5)

;습득빠름/늦은 것 보정
T경험상승 = T경험상승 * (3 + TALENT:습득빠름 * 2 - TALENT:습득느림 * 2) / 2

;상승치가-5를 밑돌지 않는 한 최저 1의 경험치는 손에 들어 온다
SIF T경험상승 > -5 && T경험상승 < 1
	T경험상승 = 1

;지문 표시
IF T경험상승 > 0
	CFLAG:조교경험 += T경험상승
	PRINTFORML %CALLNAME:TARGET%경험치+{T경험상승}
ENDIF


;────────────────────────────────────
;죄악감과 아라이먼트변동
;────────────────────────────────────

;이른바 업치

;상승치등 초기화
죄악감상승 = 0
VARSET LOCAL

;공포와 억울을 본다(3/1000상승, 상한 3)
죄악감상승 += LIMIT(3 * (CUP:MASTER:공포 + CUP:MASTER:억울) / 1000, 0, 3)
;1000를 돌파했을 경우 0～1의 추가 보정
SIF (CUP:MASTER:공포 + CUP:MASTER:억울) > 1000
	죄악감상승 += RAND:2

;새드, 벌모드, 조교 대상의 반항이 격렬한, 1개에서도 채우면 고통과 불쾌는 업에 가산하지 않습니다
IF !TALENT:새드 && (MARK:반항각인 * 150 < CUP:MASTER:반항) && !TFLAG:벌플래그
	;공포와 억울을 본다(1/300상승, 상한 4)
	죄악감상승 += LIMIT((CUP:MASTER:고통 + CUP:MASTER:불쾌) / 300, 0, 4)
	;한층 더 RAND:(1/600) 상승(0～2)
	LOCAL = LIMIT((CUP:MASTER:고통 + CUP:MASTER:불쾌) / 600, 0, 2) + 1
	죄악감상승 += RAND:LOCAL
ENDIF

;상냥한 성격이라면 하드한 메뉴를 해낸 것 뿐이라도 죄악감
SIF (TALENT:얌전함 || TALENT:상냥함) && (TFLAG:턴방침 > 3 || TFLAG:ACT분류 == 6 || TFLAG:ACT분류 == 7 || TFLAG:ACT분류 == 8)
	죄악감상승 += 2

;소질에 의한 보정
SIF TALENT:얌전함 || TALENT:상냥함
	TIMES 죄악감상승 , 1.50
SIF TALENT:거만함 || TALENT:새드 || TALENT:심술쟁이
	TIMES 죄악감상승 , 0.50

;죄악감 증가
CFLAG:죄악감 += 죄악감상승

;죄악감의 소멸, 이것도 조교중에서는 내리는 스피드가 꽤 늦습니다
;CFLAG:죄악감이 크고, 죄악감 상승이 작을 정도 감소 속도는 빠르다(-3～0)
LOCAL = (6 * CFLAG:죄악감) - (100 * 죄악감상승)
LOCAL = (LOCAL / 200) + ((LOCAL % 200) > RAND:200)
CFLAG:죄악감 -= LIMIT(LOCAL, 0, 3)

;죄악감이 10 이하의 때, 죄악감 상승이 0 이하라면 1/2의 확률로 죄악감-1
SIF CFLAG:죄악감 <= 10 && 죄악감상승 <= 0
	CFLAG:죄악감 -= RAND:2

;죄악감은 부가 되지 않는다
SIF CFLAG:죄악감 < 0
	CFLAG:죄악감 = 0

;사용 변수 초기화
아라변동 = 0
VARSET LOCAL

;MASTER의 응답으로 기본치가 정해진다
;좋은인상
IF TFLAG:REACT인상 == 1
	아라변동 = 500
;약간좋은인상
ELSEIF TFLAG:REACT인상 == 2
	아라변동 = 200
;나쁜인상
ELSEIF TFLAG:REACT인상 == 3
	아라변동 = -200
;약간나쁜인상
ELSEIF TFLAG:REACT인상 == 4
	아라변동 = -500
;0로부터 멀어진다
ELSEIF TFLAG:REACT인상 == 5
	IF CFLAG:아라이먼트 > 0
		아라변동 = 200
	ELSE
		아라변동 = -200
	ENDIF
;0에 가까워진다
ELSEIF TFLAG:REACT인상 == 6
	IF CFLAG:아라이먼트 > 0
		아라변동 = -200
	ELSE
		아라변동 = 200
	ENDIF
ELSE
;영향없음
	아라변동 = 0
ENDIF

;아라이먼트변동경향의 변동
;연속으로 정(부)에 아라이먼트가 계속 변동하면 정(부)에 가산계속 된다
;아라이먼트변동의 정부가 변화하면 리셋트(+-0도 리셋트 대상)
IF 아라변동 > 0
	IF TFLAG:아라이먼트변동경향 >= 0
		TFLAG:아라이먼트변동경향 ++
	ELSE
		TFLAG:아라이먼트변동경향 = 0
	ENDIF
ELSEIF 아라변동 < 0
	IF TFLAG:아라이먼트변동경향 <= 0
		TFLAG:아라이먼트변동경향 --
	ELSE
		TFLAG:아라이먼트변동경향 = 0
	ENDIF
ELSE
	TFLAG:아라이먼트변동경향 = 0
ENDIF

;성격으로 배율이 정해진다.
;최소치 50(M데레), 최대치 150(S튼), 소질 변동하지 않는다/시키지 않는 캐릭터에서는 LOCAL = 100
LOCAL = (CFLAG:SM성격 + CFLAG:츤데레 + 200) / 2

;M데레라면 아라이먼트변동이+의 방향으로 보정이 걸린다
IF 아라변동 < 0
	아라변동 = 아라변동 * LOCAL / 100
ELSE
	아라변동 = 아라변동 * (200 - LOCAL) / 100
ENDIF

;빗치라면 보다 극단적인 값을 받는다. (최대 25％의 증감)
아라변동 = 아라변동 * (CFLAG:빗치 + 300) / 300

;평시아 라이먼트로부터 멀어질 때, 현아라이먼트가 극단적인 값이면 변동을 0에 접근한다
;평시아 라이먼트가 0인 것을 전제로 했을 경우, 평시아 라이먼트와 아라이먼트의……
;차이가 0때 1배, 차이가 20시약 0.4배, 차이가 40시약 0.2배
;※평시아 라이먼트는 10배로 보존되고 있으므로, 사용할 때는 1/10으로 한다

;아라이먼트변동을 보면 평시아 라이먼트로부터 멀어질 때
IF (CFLAG:평시아 / 10 > CFLAG:아라이먼트 && 아라변동 < 0) || (CFLAG:평시아 / 10 < CFLAG:아라이먼트 && 아라변동 > 0)
	
	;LOCAL:1에 분자(아라이먼트가 0에서멀어짐와 작아진다)를 대입
	LOCAL:1 = 20 - ABS(CFLAG:아라이먼트) / 5
	;LOCAL:2에 분모(아라이먼트가 평시부터 멀어지면 커진다)를 대입
	LOCAL:2 = LOCAL:1 + ABS(CFLAG:평시아 / 10 - CFLAG:아라이먼트)
	;※결과적으로, 0또는 평시부터 아라이먼트가 멀 때 보정이 0배에 가까워진다
	
	;분자는 최저 0
	SIF LOCAL:1 < 0
		LOCAL:1 = 0
	;분모는 최저 1(0으로 제산의 방지)
	SIF LOCAL:2 <= 0
		LOCAL:2 = 1
	
	;아라이먼트 극단화 억제 계산
	아라변동 = 아라변동 * LOCAL:1 / LOCAL:2
ENDIF

;아라이먼트변동
CFLAG:아라이먼트 += 아라변동 / 100


;────────────────────────────────────
;벌관련의 처리
;────────────────────────────────────

;LOCAL 초기화
VARSET LOCAL

;초조에 의한 가산
LOCAL += ABSQSI(DOWNBASE:만족 - DOWNBASE:초조) * POWER(2, -6 + TALENT:충동적 - TALENT:자제적 + 10) / 1024
;반항에 의한 가산
LOCAL += SQRT(CUP:MASTER:반항) * POWER(2, -6 + TALENT:비관적 - TALENT:낙관적 + 10) / 1024

;야무짐, 관념, 수용력, 핏기에 따라 보정 후, 벌포인트에 LOCAL 가산
LOCAL:1 = TALENT:반항적 - TALENT:겁쟁이 + TALENT:비관적 - TALENT:낙관적
LOCAL:2 = TALENT:조급함 - TALENT:신중 + TALENT:충동적 - TALENT:자제적
TFLAG:벌포인트 += LOCAL * POWER(2, (LOCAL ? LOCAL:1 # -LOCAL:1) + LOCAL:2 + 10) / 1024
;벌포인트는 부가 되지 않는다
SIF TFLAG:벌포인트 < 0
	TFLAG:벌포인트 = 0

;LOCAL 초기화
VARSET LOCAL

;벌안에 넣을 예정의 조건
;TALENT:심술쟁이 - TALENT:상냥함

;벌모드의 발생과 해소
;벌중은
IF TFLAG:벌플래그 > 0
	;조교 대상을 손상하거나 굴복 시키거나로 안절부절 해소. 이 경우 간단하게 항복시키는 것보다 억지로 꺾게 하는 편이 기분 좋다고 생각하기 때문에, 공순의 비중을 굴복보다 낮게 설정했습니다
	LOCAL = CUP:MASTER:굴복 / 15 + CUP:MASTER:공포 / 30 + CUP:MASTER:공순 / 30 - CUP:MASTER:반항 / 20 + CUP:MASTER:고통 / 20 + CUP:MASTER:치정 / 75 + CUP:MASTER:불쾌 / 60 + CUP:MASTER:억울 / 75
	;초조를 감쇠시켜, 반만큼 만족이 증가
	BASE:초조 -= LOCAL
	BASE:만족 += LOCAL / 2
	;벌선언의 경우는 이 턴 결코 벌모드 해소하지 않습니다（애원을 들어주었을 경우만이 예외）
	IF !IS_NOWACTNAME("벌이라고선언한다") || IS_COMGRONAME("용서를빈다")
		;조교 대상의 항복, 조교자의 만족, 2개의 조건의 어떤 것인지를 채우면 벌모드 해소
		IF (CUP:MASTER:굴복 + CUP:MASTER:공포 + CUP:MASTER:공순 > CUP:MASTER:반항 + CUP:MASTER:불쾌 + CUP:MASTER:억울) || (BASE:만족 - BASE:초조 > 100 + RAND:50)
			;벌포인트를 반 미만에 저감
			TFLAG:벌포인트 -= TFLAG:벌포인트 / 2 - 5
			;초조 반감
			BASE:초조 /= 2
			;벌포인트를 0～30의 범위에 거둔다
			TFLAG:벌포인트 = LIMIT(TFLAG:벌포인트, 0, 30)
			;벌종료
			TFLAG:벌플래그 = 0
		ENDIF
	ENDIF
;조금 복잡한 계산을 하고 있는 것 같지만, 벌포인트를 100까지 모으면 벌모드라고 하는 인식으로 괜찮습니다.
ELSEIF TFLAG:벌포인트 + RAND:40 - RAND:10 > 100 + TALENT:심술쟁이 * 3 - TALENT:상냥함 * RAND:5 - TALENT:새드 * 5 - TALENT:프라이드높음 * (1 + RAND:3) + TALENT:프라이드낮음 * (1 + RAND:2)
	;벌개시
	TFLAG:벌플래그 = 1
	;벌포인트 반감
	TFLAG:벌포인트 /= 2
	;쓰러트리기, 성교 해제
	TEQUIP:쓰러트리기중 = 0
	TEQUIP:성교중 = 0
	TEQUIP:성교봉사중 = 0
ENDIF
;오늘의방침가 벌이면 항상 벌중
IF TFLAG:오늘의방침 == 2
	TFLAG:벌플래그 = 1
ENDIF


;────────────────────────────────────
;TEQUIP 처리
;────────────────────────────────────
CALL EVENT_EQUIP


;────────────────────────────────────
;발기 계산
;────────────────────────────────────
;나머지 정력이 적으면 발기 하기 어려워지도록(듯이)

;LOCAL 초기화
VARSET LOCAL

;조교 대상으로 남성기가 존재하는 경우
IF PENIS(MASTER)
	;이성이 낮아지면 보정치가 감소(-6～0)
	LOCAL -= 6 - LIMIT(6 * BASE:MASTER:이성 / 500, 0, 6)
	;미약에 의해 보정치 감소
	SIF TEQUIP:미약
		LOCAL -= 5
	;나머지 정력이 적게 되면 보정 가격인상승(0～9)
	LOCAL += 9 - LIMIT(18 * BASE:MASTER:사정 / MAXBASE:MASTER:사정, 0, 9)
	;나머지 정력이 0이라면 한층 더 보정치+1, 요점강제발기 온
	IF BASE:MASTER:사정 == 0
		LOCAL += 1
		TCVAR:MASTER:요점강제발기 = 1
	ENDIF
	;나머지 체력이 적게 되면 보정 가격인상승(0～7)
	LOCAL += 7 - LIMIT(14 * BASE:MASTER:체력 / MAXBASE:MASTER:체력, 0, 7)
	;나머지 기력이 적게 되면 보정 가격인상승(0～7)
	LOCAL += 7 - LIMIT(14 * BASE:MASTER:기력 / MAXBASE:MASTER:기력, 0, 7)
	;발기도가 800 초과라면 추가 보정
	IF TCVAR:MASTER:발기도 > 800
		LOCAL = LOCAL + (20 - LOCAL) * (TCVAR:MASTER:발기도 - 800) / 700
	ENDIF
	;보정치는 20이상이 되지 않는다
	SIF LOCAL >= 20
		LOCAL = 19
ENDIF
;현재의 발기도를 기억
TCVAR:MASTER:전발기도 = TCVAR:MASTER:발기도
;보정치가 큰 만큼 발기도 상승을 감쇠

;발기의 처리
;전립선자극·강
IF TCVAR:MASTER:전립선자극 > 1
	;거의 일발로 펄떡펄떡
	TCVAR:MASTER:발기도 += 500 + TCVAR:MASTER:이번발기 * (20 - LOCAL) / 10
;전립선자극·미만
ELSEIF TCVAR:MASTER:전립선자극 == 1
	;고정치+100붙어 있는
	TCVAR:MASTER:발기도 += 100 + TCVAR:MASTER:이번발기 * (20 - LOCAL) / 20
;통상（요점강제발기는 아니다）
ELSEIF !TCVAR:MASTER:요점강제발기
	TCVAR:MASTER:발기도 += TCVAR:MASTER:이번발기 * (20 - LOCAL) / 20
ENDIF

;사정 처리
IF NOWEX:MASTER:사정
	;전립선자극·강
	IF TCVAR:MASTER:전립선자극 > 1
		;감쇠하지 않는다
	;드라이사정·전립선사정
	ELSEIF NOWEX:MASTER:사정 == 1 || NOWEX:MASTER:사정 == 5
		;발기도가 감쇠하기 어렵다
		TCVAR:MASTER:발기도 = TCVAR:MASTER:발기도 / 2 + GET_ABL(TARGET, "기교") * 5
	;사정/사정(조루)/A책사정
	ELSEIF NOWEX:MASTER:사정 == 2 || NOWEX:MASTER:사정 > 3
		;조교자 기교에 의해 발기도 유지
		TCVAR:MASTER:발기도 = TCVAR:MASTER:발기도 / 3 + GET_ABL(TARGET, "기교") * 5
	;대량사정
	ELSEIF NOWEX:MASTER:사정 == 3
		;조교자 기교에 의해 발기도 유지
		TCVAR:MASTER:발기도 = TCVAR:MASTER:발기도 / 5 + GET_ABL(TARGET, "기교") * 3
	ENDIF
	;미약사용시, 발기도 증가
	IF TEQUIP:미약
		TCVAR:MASTER:발기도 = TCVAR:MASTER:발기도 * 5 / 4
	ENDIF
	;뽑지않고중은 발기도 1000이상을 유지
	SIF TEQUIP:성교봉사중 && TFLAG:뽑지않고 && TCVAR:MASTER:발기도 < 1000
		TCVAR:MASTER:발기도 = 1000
ENDIF


;────────────────────────────────────
;피폐의 처리, 게이지의 음수 방지
;────────────────────────────────────
;휴식 따위로 피폐가 음수가 되면 여기서 리셋트
SIF TFLAG:조교자소모도 < 0
	TFLAG:조교자소모도 = 0
SIF TFLAG:소모도 < 0
	TFLAG:소모도 = 0

;조교 대상 체력이 0 이하
IF BASE:MASTER:체력 <= 0
	;지문 표시
	PRINTFORMW （격렬한 조교에 의해%CALLNAME:MASTER%는 꽤 소모하고 있는 듯 하다）
	;소모도를 1～3 증가
	TFLAG:소모도 += 1 + RAND:3
	;체력은 부가 되지 않는다
	BASE:MASTER:체력 = 0
;조교 대상 체력이 200 미만
ELSEIF BASE:MASTER:체력 < 200
	;지문 표시
	PRINTFORMW （격렬한 조교에 의해%CALLNAME:MASTER%는 조금 소모하고 있는 듯 하다）
	;소모도를 1 증가
	TFLAG:소모도 += 1
ENDIF

;조교자 체력이 0 이하
IF BASE:체력 <= 0
	;지문 표시
	PRINTFORMW （격렬한 조교에 의해%CALLNAME:TARGET%는 꽤 소모하고 있는 듯 하다）
	;소모도를 1～3 증가
	TFLAG:조교자소모도 += 1 + RAND:3
	;체력은 부가 되지 않는다
	BASE:체력 = 0
;조교자 체력이 200 미만
ELSEIF BASE:체력 < 200
	;지문 표시
	PRINTFORMW （격렬한 조교에 의해%CALLNAME:TARGET%는 조금 소모하고 있는 듯 하다）
	;소모도를 1 증가
	TFLAG:조교자소모도 += 1
ENDIF

;기력/사정/모유/소변욕/이성/흥미/초조/만족/발기도는 부가 되지 않는다
FOR LCOUNT, 1, 6
	SIF BASE:MASTER:LCOUNT < 0
		BASE:MASTER:LCOUNT = 0
NEXT
SIF TCVAR:MASTER:발기도 < 0
	TCVAR:MASTER:발기도 = 0
FOR LCOUNT, 1, 9
	SIF BASE:LCOUNT < 0
		BASE:LCOUNT = 0
NEXT




;────────────────────────────────────
;조교 대상 기력 0에 의한 저항불능의 처리
;────────────────────────────────────
IF !TFLAG:저항불능 && BASE:MASTER:기력 <= 0
	PRINTFORMW 너무 긴장시켰다%CALLNAME:MASTER%는, 이제(벌써) 기력이 한계인 것 같다…
	TFLAG:저항불능 = 1
ENDIF

;────────────────────────────────────
;체력, 기력, 이성의 자연 회복, 게이지의 한계 돌파 방지
;────────────────────────────────────
;이 턴으로 격렬한 소모가 없으면 체력과 기력은 조금씩 자연 회복

;기초 회복량을 설정
M체력회복 = MAXBASE:MASTER:체력 / 100
M기력회복 = MAXBASE:MASTER:기력 / (50-(TFLAG:저항불능*30))
T체력회복 = MAXBASE:MASTER:체력 / 100
T기력회복 = MAXBASE:MASTER:기력 / 50

;회복빠름/늦은 소질에 의한 보정
IF TALENT:MASTER:회복빠름
	TIMES M체력회복 , 2.00
	TIMES M기력회복 , 1.20
ELSEIF TALENT:MASTER:회복느림
	TIMES M체력회복 , 0.70
	TIMES M기력회복 , 0.90
ENDIF
IF TALENT:회복빠름
	TIMES T체력회복 , 2.00
	TIMES T기력회복 , 1.20
ELSEIF TALENT:회복느림
	TIMES T체력회복 , 0.70
	TIMES T기력회복 , 0.90
ENDIF

;체력 기력 회복
BASE:MASTER:체력 += M체력회복
BASE:MASTER:기력 += M기력회복
BASE:체력 += T체력회복
BASE:기력 += T기력회복


;────────────────────────────────────
;저항불능의 회복 처리
;────────────────────────────────────
IF TFLAG:저항불능 && BASE:MASTER:기력 >= MAXBASE:MASTER:기력/2
	PRINTFORMW 하는대로였다%CALLNAME:MASTER%는, 다소 기력을 되찾은 것 같다…
	TFLAG:저항불능 = 0
ENDIF


;────────────────────────────────────
;대만족 보너스
;────────────────────────────────────
;만족이 상한 돌파
IF BASE:만족 >= MAXBASE:만족
	;지문 표시
	PRINTL 
	;대만족 구상을 부른다
	RESULT = 0
	CALL KOJO_EVENT(21)
	SIF RESULT
		PRINTL 
	PRINTFORMW %CALLNAME:TARGET%님대만족
	;대만족 보너스 발생
	CFLAG:만족보 = 1
	;만족 초기화
	BASE:만족 = 0
	;만족의 게이지를 늘려 다음번의 보너스를 취득하려면 좀 더 노력하지 않으면
	MAXBASE:만족 += (7000 - MAXBASE:만족) / 10
	;대만족 보너스 발생을 기록
	TFLAG:이번에대만족 += 1
	;신규에 날조@/L
	TCVAR:이번에대만족 += 1
;어느 정도 만족을 모으고 있는 경우는 턴 마다 자연 소모
ELSEIF BASE:만족 > MAXBASE:만족 / 3
	;무달성 상태에서는 감쇠 속도가 빨라진다
	IF SOURCE:MASTER:달성 > 0
		BASE:만족 -= MAXBASE:만족 / 50
	ELSE
		BASE:만족 -= MAXBASE:만족 / 30
	ENDIF
	;만족은 부가 되지 않는다
	SIF BASE:만족 < 0
		BASE:만족 = 0
ENDIF

;────────────────────────────────────
;이성의 자연 회복, 게이지의 한계 돌파 방지
;────────────────────────────────────


;지문 표시(이성 묘사)
;저고통 상태
IF TFLAG:고통에의한이성저하 == 0 || ABL:MASTER:마조끼 > 0
	;이성이 200 미만에 저하
	IF BASE:MASTER:이성 < 200 && BASE:MASTER:이성 + DOWNBASE:MASTER:이성 > 200
		PRINTFORML 쾌감의 별로%CALLNAME:MASTER%의 시야는 분홍색의 안개에 희미하게 보여, 머리의 심지는 달콤한 저림에 채워지고 있다.
	;이성이 500 미만에 저하
	ELSEIF BASE:MASTER:이성 < 500 && BASE:MASTER:이성 + DOWNBASE:MASTER:이성 > 500
		PRINTFORML %CALLNAME:TARGET%에게 줄 수 있는 쾌감이 이성을 녹여, 저항하는 기력을 빼앗아 간다…
	ENDIF
;마조끼가 없고 고통
ELSE
	;이성이 200 미만에 저하
	IF BASE:MASTER:이성 < 200 && BASE:MASTER:이성 + DOWNBASE:MASTER:이성 > 200
		PRINTFORML 격통의 별로%CALLNAME:MASTER%의 사고는 완전하게 마비되어, 눈에 이성의 색은 안보인다.
	;이성이 500 미만에 저하
	ELSEIF BASE:MASTER:이성 < 500 && BASE:MASTER:이성 + DOWNBASE:MASTER:이성 > 500
		PRINTFORML %CALLNAME:TARGET%가 주는 격통이%CALLNAME:MASTER%의 이성을 깎아 간다…
	ENDIF
ENDIF

;조교 대상의 이성 회복. 만약 쾌락/고통에의한이성저하의 플래그가 있으면 우선 그것들을 해결합니다. 상태 이상의 경우도 회복하지 않습니다
;고통에의한이성저하. 이성이 단번에 내리지만 1 턴으로 해소합니다. 즉 아픔의 효과는(이성 데미지)+(1 턴 이성 회복 없음)입니다
IF TFLAG:고통에의한이성저하 > 0
	TFLAG:고통에의한이성저하 = 0
;쾌락에의한이성저하. 자연 해소하는 것은 늦습니다만, 절정 하면 단번에 해소합니다. 즉 말하면 다음의 턴으로부터 이성 회복할 수 있습니다
ELSEIF TFLAG:쾌락에의한이성저하 > 0
	;쾌Ｘ가 어느 정도 존재하면 해소 속도 감소(0～1)
	IF CUP:MASTER:쾌Ｃ + CUP:MASTER:쾌Ｖ + CUP:MASTER:쾌Ａ + CUP:MASTER:쾌Ｂ > 50
		TFLAG:쾌락에의한이성저하 -= RAND:2
	;쾌락에의한이성저하 해소(1～2)
	ELSE
		TFLAG:쾌락에의한이성저하 -= 1 + RAND:2
	ENDIF
	;절정시 완전 해소
	SIF NOWEX:MASTER:Ｃ절정 + NOWEX:MASTER:Ｖ절정 + NOWEX:MASTER:Ａ절정 + NOWEX:MASTER:Ｂ절정 > 0
		TFLAG:쾌락에의한이성저하 = 0
;위의 조건+상태변화시에 들어맞지 않으면 이성 회복(60～100)
ELSEIF TCVAR:MASTER:상태변화 == 0
	BASE:MASTER:이성 += 50 + (5 + RAND:6) * (2 + RAND:4)
ENDIF

;조교자의 이성은 상태 이상하지 않으면 자연 회복
SIF TCVAR:상태변화 == 0
	BASE:이성 += 50 + (5 + RAND:6) * (2 + RAND:4)

;발기도의 자연 저하
;남성기 존재시
IF PENIS(MASTER)
	;갈것같다, 성교봉사중, 발기도 증가중, 전립선자극중이 아니면
	IF !TCVAR:MASTER:갈것같다 && !TEQUIP:성교봉사중 && !TCVAR:MASTER:이번발기
		;정욕시 저하 없음
		IF TCVAR:상태변화 == 5
		;피폐/쇠약/무기력시 0.85배
		ELSEIF TCVAR:상태변화 == 1 || TCVAR:상태변화 == 2 || TCVAR:상태변화 == 3
			TIMES TCVAR:MASTER:발기도, 0.85
		;그 외는 0.9배
		ELSE
			TIMES TCVAR:MASTER:발기도, 0.90
		ENDIF
		;요점강제발기시 게다가 0.9배
		SIF TCVAR:MASTER:요점강제발기 == 1
				TIMES TCVAR:MASTER:발기도, 0.90
	ENDIF
;남성기 비존재시는 발기도 초기화
ELSE
	TCVAR:MASTER:발기도 = 0
ENDIF

;조교 대상 체력/기력/사정/모유/소변욕/이성의 상한 돌파를 방지
FOR LCOUNT, 0, 6
	SIF BASE:MASTER:LCOUNT > MAXBASE:MASTER:LCOUNT
		BASE:MASTER:LCOUNT = MAXBASE:MASTER:LCOUNT
NEXT
;발기도의 상한 돌파를 방지
SIF TCVAR:MASTER:발기도 > 1500
	TCVAR:MASTER:발기도 = 1500
;조교자 체력/기력/사정/모유/소변욕/이성/흥미/초조의 상한 돌파를 방지
FOR LCOUNT, 0, 8
	SIF BASE:LCOUNT > MAXBASE:LCOUNT
		BASE:LCOUNT = MAXBASE:LCOUNT
NEXT


;────────────────────────────────────
;조교 대상의 상태변화
;────────────────────────────────────
;이상 상태중(0=통상/1=피폐/2=쇠약/3=무기력/4=몽롱/5=정욕/6=분노/7=싫증/8=울적)
IF TCVAR:MASTER:상태변화 > 0
	IF !(--TCVAR:MASTER:상태변화지속)
		;정상
		TCVAR:MASTER:상태변화 = 0
		PRINTFORMW %CALLNAME:MASTER%는[통상]에 돌아왔다
	ENDIF
	GOTO DECIDE_CONDITION_M
ENDIF

;소모도가 클 때
IF TFLAG:소모도 > 3 + RAND:5
	;소모도가 크면 쇠약, 작으면 피폐
	IF TFLAG:소모도 > 5
		TCVAR:MASTER:상태변화 = 2
	ELSE
		TCVAR:MASTER:상태변화 = 1
	ENDIF
	TCVAR:MASTER:상태변화지속 = 3
	GOTO DECIDE_CONDITION_M
ENDIF

;기력이 없고, 억울PALAM가 높을 때
IF BASE:MASTER:기력 < 0 && (PALAM:MASTER:억울 > 500 + RAND:500 || RAND:3 > 1)
	;무기력
	TCVAR:MASTER:상태변화 = 3
	TCVAR:MASTER:상태변화지속 = 3
	GOTO DECIDE_CONDITION_M
ENDIF

;절정계가 체력계를 웃돌 때
IF BASE:MASTER:체력 + BASE:MASTER:기력 + BASE:MASTER:이성 - NOWEX:MASTER:Ｃ절정 * 100 - NOWEX:MASTER:Ｖ절정 * 150 - NOWEX:MASTER:Ａ절정 * 120 - NOWEX:MASTER:Ｂ절정 * 80 - NOWEX:MASTER:분유 * 80 - NOWEX:MASTER:사정 * 150 < 1000
	;몽롱
	TCVAR:MASTER:상태변화 = 4
	TCVAR:MASTER:상태변화지속 = 3
	GOTO DECIDE_CONDITION_M
ENDIF

;고욕정으로, 욕정 증가+이성 저하가 클 때
IF (PALAM:MASTER:욕정 > 6000 - MARK:MASTER:쾌락각인 * 200 && RAND:3 > 1) || CUP:MASTER:욕정 + DOWNBASE:MASTER:이성 * 2 + TFLAG:쾌락에의한이성저하 * 100 > 2000
	;정욕
	TCVAR:MASTER:상태변화 = 5
	TCVAR:MASTER:상태변화지속 = 3
	GOTO DECIDE_CONDITION_M
ENDIF

;분노 상태변화 판정(기준치가 부가 되면 발생)
;LOCAL 초기화
VARSET LOCAL
;기준치(1000～1200)
LOCAL = 1000 + RAND:5 * 50
;수용력 보정
LOCAL += 400 * POWER(2, TALENT:MASTER:신중 - TALENT:MASTER:조급함 + 10) / 1024
;공순이 클 때에 증가, 반항, 불쾌가 클 때에 감소
LOCAL += CUP:MASTER:공순 - CUP:MASTER:반항 * 2 - CUP:MASTER:불쾌
;굴복각인, 트라우마에 의해 증가, 반항각인에 의해 감소
LOCAL += MARK:굴복각인 * 10 + MARK:MASTER:트라우마 * 10 - MARK:반항각인 * 20
;기준을 채우면
IF LOCAL < 0
	TCVAR:MASTER:상태변화 = 6
	TCVAR:MASTER:상태변화지속 = 3
	GOTO DECIDE_CONDITION_M
ENDIF

;PALAM 상승량이 어릴 때
IF CUP:MASTER:쾌Ｃ + CUP:MASTER:쾌Ｖ + CUP:MASTER:쾌Ａ + CUP:MASTER:쾌Ｂ + CUP:MASTER:욕정 + CUP:MASTER:공포 + CUP:MASTER:고통 + CUP:MASTER:치정 + CUP:MASTER:불쾌 < 500 + BASE:MASTER:이성 + CFLAG:MASTER:조교레벨 * (200 - MARK:굴복각인 * 10) && TFLAG:경과시간 > 15 && RAND:5 > 3
	;지루함
	TCVAR:MASTER:상태변화 = 7
	TCVAR:MASTER:상태변화지속 = 3
	GOTO DECIDE_CONDITION_M
ENDIF

;불쾌+억울PALAM가 클 때
IF PALAM:MASTER:불쾌 + PALAM:MASTER:억울 > 1200 + CFLAG:MASTER:조교레벨 * 100 - MARK:MASTER:트라우마 * (50 + RAND:50) - TALENT:MASTER:광기 * 200
	;울적
	TCVAR:MASTER:상태변화 = 8
	TCVAR:MASTER:상태변화지속 = 3
	GOTO DECIDE_CONDITION_M
ENDIF

$DECIDE_CONDITION_M

;────────────────────────────────────
;조교자의 상태변화
;────────────────────────────────────
;이상 상태중
IF TCVAR:상태변화 > 0
	IF !(--TCVAR:상태변화지속)
		;정상
		TCVAR:상태변화 = 0
		PRINTFORMW %CALLNAME:TARGET%는[통상]에 돌아왔다
	ENDIF
		GOTO DECIDE_CONDITION_T
ENDIF

;소모도를 본다
IF TFLAG:조교자소모도 > 3 + RAND:5
	;소모도가 크면 쇠약, 작으면 피폐
	IF TFLAG:조교자소모도 > 5
		TCVAR:상태변화 = 2
	ELSE
		TCVAR:상태변화 = 1
	ENDIF
	TCVAR:상태변화지속 = 3
	GOTO DECIDE_CONDITION_T
ENDIF

;기력이 없고, 초조가 어릴 때
IF BASE:기력 < 100 && (BASE:초조 < 100 + RAND:100 || RAND:3 > 1)
	;무기력
	TCVAR:상태변화 = 3
	TCVAR:상태변화지속 = 3
	GOTO DECIDE_CONDITION_T
ENDIF

;절정계가 체력계를 웃돌 때
IF BASE:체력 + BASE:기력 + BASE:이성 - NOWEX:Ｃ절정 * 100 - NOWEX:Ｖ절정 * 150 - NOWEX:Ａ절정 * 120 - NOWEX:Ｂ절정 * 80 - NOWEX:분유 * 80 - NOWEX:사정 * 150 < 1000
	;몽롱
	TCVAR:상태변화 = 4
	TCVAR:상태변화지속 = 3
	GOTO DECIDE_CONDITION_T
ENDIF

;고쾌x시
IF PALAM:쾌Ｃ + PALAM:쾌Ｖ + PALAM:쾌Ａ + PALAM:쾌Ｂ > 12000 && RAND:3 > 1
	;정욕
	TCVAR:상태변화 = 5
	TCVAR:상태변화지속 = 3
	GOTO DECIDE_CONDITION_T
ENDIF

;분노 상태변화 판정(기준치가 부가 되면 발생)
;LOCAL 초기화
VARSET LOCAL
;기준치(1000～1200)
LOCAL = 1000 + RAND:5 * 50
;수용력 보정
LOCAL += 400 * POWER(2, TALENT:신중 - TALENT:조급함 + 10) / 1024
;공순 보정
LOCAL += CUP:MASTER:공순 * POWER(2, TALENT:상냥함 - TALENT:심술쟁이 + 10) / 1024
;굴복 보정
LOCAL += (CUP:MASTER:굴복 + MARK:굴복각인 * 50) * POWER(2, TALENT:순수 + 10) / 1024
;반항 보정
LOCAL -= CUP:MASTER:반항 * POWER(2, 1 + TALENT:반항적 - TALENT:겁쟁이 + 10) / 1024
;초조 보정
LOCAL -= BASE:초조 * POWER(2, TALENT:충동적 - TALENT:자제적 + 10) / 1024
;벌보정
LOCAL -= (TFLAG:벌포인트 + TFLAG:벌플래그 * 75) * POWER(2, 1 + TALENT:프라이드높음 - TALENT:프라이드낮음 + 10) / 1024
;기준을 채우면
IF LOCAL < 0
	;분노
	TCVAR:상태변화 = 6
	TCVAR:상태변화지속 = 3
	GOTO DECIDE_CONDITION_T
ENDIF

;저흥미시
IF BASE:흥미 < 100 + (GET_TRAINLV(TARGET) + 1) * (10 + RAND:5) && RAND:3 > 1
	;지루함
	TCVAR:상태변화 = 7
	TCVAR:상태변화지속 = 3
	GOTO DECIDE_CONDITION_T
ENDIF

;초조가 크고, 반항이 어릴 때
IF BASE:초조- CUP:MASTER:반항 * 2 + TFLAG:벌포인트 * 2 + TFLAG:벌플래그 * 150 > 500 + CFLAG:조교레벨 * 100 + (RAND:5 - TALENT:유치 - TALENT:광기 * 2) * 100
	;울적
	TCVAR:상태변화 = 8
	TCVAR:상태변화지속 = 3
	GOTO DECIDE_CONDITION_T
ENDIF

$DECIDE_CONDITION_T


;────────────────────────────────────
;상태변화 구상과 텍스트의 표시
;────────────────────────────────────
;상태변화 구상을 부른다
RESULT = 0
SIF TCVAR:MASTER:상태변화지속 > 2
	;여기는 호출을 분리하고 싶은 감이
	CALL KOJO_EVENT(22)
;지문 표시
SIF RESULT
	PRINTL 
;조교 대상 상태변화시
SIF TCVAR:MASTER:상태변화 && TCVAR:MASTER:상태변화지속 > 2
	PRINTFORMW %CALLNAME:MASTER%는[%CONDITION(TCVAR:MASTER:상태변화)%]가 되었다
	;CALL KOJO_EVENT(22, 1)
;조교자 상태변화시
SIF TCVAR:상태변화 && TCVAR:상태변화지속 > 2
	PRINTFORMW %CALLNAME:TARGET%는[%CONDITION(TCVAR:상태변화)%]가 되었다
	;CALL KOJO_EVENT(22, 0)
;발기 묘사
;강발기
IF TCVAR:MASTER:발기도 >= 1300 && TCVAR:MASTER:전발기도 < 1300
	IF TCVAR:MASTER:전립선자극 > 1
		PRINTFORML 안쪽에서 수컷 기능을 희롱해져%CALLNAME:MASTER%의 남성기가 의사와 무관계하게 반응한다….
		PRINTFORML 검붉게 충혈해 우뚝 솟은 페니스를 봐,%CALLNAME:TARGET%는 혀 핥음을 했다….
	ELSEIF TCVAR:MASTER:전립선자극 == 1
		PRINTFORML 안쪽으로부터 근원의 뒤편을 비비어져%CALLNAME:MASTER%의 남성기는 부풀어 터질 것 같게 맥박치고 있다….
	ELSE
		PRINTFORML %CALLNAME:MASTER%의 남성기는 배를 뒤따를 것 같을 정도 뒤로 젖혀, 격렬하게 맥박치고 있다….
	ENDIF
;안발기
ELSEIF TCVAR:MASTER:발기도 >= 1000 && TCVAR:MASTER:전발기도 < 1000
	IF TCVAR:MASTER:전립선자극
		PRINTFORML 안쪽으로부터 근원의 뒤편을 비비어져%CALLNAME:MASTER%의 남성기가 팽창해 나간다….
	ELSE
		PRINTFORML %CALLNAME:MASTER%의 남성기는 지금 융성하게 머리를 쳐들고 있다….
	ENDIF
;약발기
ELSEIF TCVAR:MASTER:발기도 >= 200 && TCVAR:MASTER:전발기도 < 200
	;급속
	IF TCVAR:MASTER:발기도 - TCVAR:MASTER:전발기도 > 200
		PRINTFORML %CALLNAME:MASTER%는 하반신에 급속히 피가 모이는 것을 느꼈다….
	;상속
	ELSEIF TCVAR:MASTER:발기도 - TCVAR:MASTER:전발기도 > 50
		PRINTFORML %CALLNAME:MASTER%는 하반신에 피가 모이기 시작하는 것을 느꼈다….
	;완속
	ELSE
		PRINTFORML %CALLNAME:MASTER%는 하반신에 천천히 피가 모이기 시작하는 것을 느꼈다….
	ENDIF
;요점강제발기
ELSEIF TCVAR:MASTER:요점강제발기 && TCVAR:MASTER:발기도 <= 200 && TCVAR:MASTER:이번발기
	PRINTFORML 정령 다했다%CALLNAME:MASTER%의 남성기는 흠칫도 반응하지 않는다….
ENDIF
;윤활 묘사(남자 이외)
IF !TALENT:MASTER:남자
	;윤활 충분히
	IF PALAM:MASTER:윤활 >= 1000 && TCVAR:MASTER:전윤활 < 1000
		IF TEQUIP:로션
			PRINTFORML %CALLNAME:MASTER%의 샘으로부터는 끝 없게 애액이 흘러넘쳐, 이미 로션의 의미가 없다….
		ELSE
			PRINTFORML %CALLNAME:MASTER%의 샘으로부터는 끝 없게 애액이 흘러넘쳐, 홍수와 같이 되어 있다….
		ENDIF
	;강윤활
	ELSEIF PALAM:MASTER:윤활 >= 500 && TCVAR:MASTER:전윤활 < 500
		IF TEQUIP:로션
			PRINTFORML %CALLNAME:MASTER%의 여성기는 로션에 애액이 섞여 미끈미끈이 되어 있다….
		ELSEIF MV_PLAYER(1) > 1
			PRINTFORML %CALLNAME:MASTER%의 여성기는 이물을 환영하도록(듯이) 진한 애액을 분비하기 시작했다….
		ELSE
			PRINTFORML %CALLNAME:MASTER%의 여성기는 꿀을 늘어뜨려, 삽입을 기다려 바래 실룩거리고 있다….
		ENDIF
	;안윤활
	ELSEIF PALAM:MASTER:윤활 >= 250 && TCVAR:MASTER:전윤활 < 250
		IF TEQUIP:로션 == 1
			PRINTFORML %CALLNAME:MASTER%의 여성기는 말라 내기의 로션에 대신해 스스로도 애액을 분비하고 있다….
		ELSEIF TEQUIP:로션
			PRINTFORML %CALLNAME:MASTER%의 여성기는 로션 뿐만이 아니라 스스로도 애액을 분비하고 있다….
		ELSEIF MV_PLAYER(1) > 1
			PRINTFORML %CALLNAME:MASTER%의 여성기는 질내의 이물에 따뜻한 미끄러짐을 토해내고 있다….
		ELSE
			PRINTFORML %CALLNAME:MASTER%는 비순으로부터 따뜻한 애액이 늘어지고 떨어지는 것을 느껴 등골을 진동시켰다….
		ENDIF
	;약윤활
	ELSEIF PALAM:MASTER:윤활 >= 100 && TCVAR:MASTER:전윤활 < 100
		IF TEQUIP:로션
			PRINTFORML %CALLNAME:MASTER%의 사타구니는 로션으로 싫은 것 같게 빛나고 있다….
		ELSEIF MV_PLAYER(1) > 1
			PRINTFORML %CALLNAME:MASTER%의 여성기는 땀과는 어긋나는 미끄러짐을 띠기 시작했다….
		ELSE
			PRINTFORML %CALLNAME:MASTER%의 비부는 촉촉히 습기를 띠기 시작했다….
		ENDIF
	ENDIF
ENDIF
;소변욕 묘사(조교 대상으로 소변버릇 소질 존재시)
IF TALENT:MASTER:소변버릇
	;강소변욕
	IF BASE:MASTER:소변욕 > 7 * MAXBASE:MASTER:소변욕 / 8
		PRINTFORML %CALLNAME:MASTER%는 격렬하게 숨을 거칠게 해, 무릎을 강하게 닫아, 부풀어 오르는 소변욕의 결궤을 가까스로 막고 있다….
	;안소변욕
	ELSEIF BASE:MASTER:소변욕 > 2 * MAXBASE:MASTER:소변욕 / 3
		PRINTFORML %CALLNAME:MASTER%는 뺨을 홍조시켜, 떨어지고 도착하고에 넙적다리를 맞대고 비벼 소변욕에 참고 있다….
	;약소변욕
	ELSEIF BASE:MASTER:소변욕 > MAXBASE:MASTER:소변욕 / 2
		PRINTFORML %CALLNAME:MASTER%는 소변욕을 느끼고 있다….
	ENDIF
ENDIF


;────────────────────────────────────
;약력의 소멸, 약제의 쿨 다운
;────────────────────────────────────
;로션
;약물내성 존재시는 추가 감소(1～2)
SIF TEQUIP:로션 > 0 && TALENT:MASTER:약물내성
	TEQUIP:로션 -= 1 + RAND:2
;1/2의 확률로 감소(1～2)
SIF TEQUIP:로션 > 0 && RAND:2
	TEQUIP:로션 -= 1 + RAND:2
;하한 돌파를 방지
SIF TEQUIP:로션 < 0
	TEQUIP:로션 = 0

;미약
;약물내성 존재시는 추가 감소(2～3)
SIF TEQUIP:미약 > 0 && TALENT:MASTER:약물내성
	TEQUIP:미약 -= 2 + RAND:2
;1/2의 확률로 감소(1～2)
SIF TEQUIP:미약 > 0 && RAND:2
	TEQUIP:미약 -= 1 + RAND:2
;하한 돌파를 방지
SIF TEQUIP:미약 < 0
	TEQUIP:미약 = 0

;약제계 히트는 조교중 그다지 쿨 다운하지 않고, 조교 종료후 단번에 쿨 다운한다.
SIF !RAND:5
	FLAG:로시히트 -= 1
SIF !RAND:5
	FLAG:미약히트 -= 1
SIF !RAND:4
	FLAG:영양히트 -= 1
;치료히트는 기력 체력이 충분한 때 마셔 감쇠
SIF BASE:체력 > 1000 && BASE:기력 > 500 && RAND:3
	FLAG:치료히트 -= 1
;약제계 히트의 하한 돌파를 방지
FOR LCOUNT, 20, 25
	SIF LCOUNT == 22
		CONTINUE
	SIF FLAG:LCOUNT < 0
		FLAG:LCOUNT = 0
NEXT


;────────────────────────────────────
;히트 축적과 쿨 다운의 처리. 이 시스템을 성공할 수 있을까의 열쇠입니다만, 이건 또 복잡한 구조가 되었어요…
;────────────────────────────────────
;조교 방침히트 축적
;이번 방침에 대해 처리를 실시한다

;쓰러트리기중은 축적하지 않는다
IF TEQUIP:쓰러트리기중 != 1
	;LOCAL에 히트 상승량을 확보
	LOCAL = 0
	;LOCAL:1에 변수 포인터를 설정
	LOCAL:1 = 70 + TFLAG:턴방침
	;히트치가 존재하면(+4)
	SIF TFLAG:(LOCAL:1) > 0
		LOCAL += 4
	;무조건으로(+8)
	LOCAL += 8
	;연속 실행으로(+5)
	SIF TFLAG:전턴방침 == TFLAG:턴방침
		LOCAL += 5
	;실제의 상승 처리
	TFLAG:(LOCAL:1) += LOCAL
ENDIF

;조교 방침의 쿨 다운(히트치 존재시 처리를 실시한다)
;변수 『쿨치』에 감소량 보존

;휴식의 쿨 다운
IF TFLAG:휴식방침히트 > 0
	;초기치(3)
	쿨치 = 3
	;전회 실행으로(-1)
	SIF TFLAG:전턴방침 == 1
		쿨치 -= 1
	;조교자 및 조교 대상의 체력과 기력의 잔량 총계를 본다(적을 정도 크고 쿨 다운, +0～3)
	쿨치 += 3 - LIMIT(BASE:체력 + BASE:기력 + BASE:MASTER:체력 + BASE:MASTER:기력 / 500, 0, 3)
	;소모도가 크면 크고 쿨 다운
	쿨치 += TFLAG:조교자소모도 / 2
	쿨치 += TFLAG:소모도 / 3
	;조교자 상태변화에 의해 쿨 다운량 변동
	;피폐(+1)
	SIF TCVAR:상태변화 == 1
		쿨치 += 1
	;쇠약(+2)
	SIF TCVAR:상태변화 == 2
		쿨치 += 2
	;무기력(+1)
	SIF TCVAR:상태변화 == 3
		쿨치 += 1
	;조교자/조교 대상의 회복빠름/늦은 소질을 본다(회복 빠르면-1, 늦으면+1)
	SIF TALENT:회복빠름
		쿨치 -= 1
	SIF TALENT:MASTER:회복빠름
		쿨치 -= 1
	SIF TALENT:회복느림
		쿨치 += 1
	SIF TALENT:MASTER:회복느림
		쿨치 += 1
	;쿨치가 정이라면 쿨 다운 실행
	SIF 쿨치 > 0
		TFLAG:휴식방침히트 -= 쿨치
	;하한 돌파를 방지
	SIF TFLAG:휴식방침히트 < 0
		TFLAG:휴식방침히트 = 0
ENDIF

;소프트의 쿨 다운
IF TFLAG:소프트방침히트 > 0
	;초기치(3～4)
	쿨치 = 3 + RAND:2
	;전회 실행으로(-1)
	SIF TFLAG:전턴방침 == 2
		쿨치 -= 1
	;체력과 기력의 잔량을 본다(적을 정도 크고 쿨 다운, +0～2)
	쿨치 += 2 - LIMIT(BASE:체력 + BASE:기력 + BASE:MASTER:체력 + BASE:MASTER:기력 / 500, 0, 2)
	;조교자 상태변화에 의해 쿨 다운량 변동
	;정욕(+1)
	SIF TCVAR:상태변화 == 5
		쿨치 += 1
	;분노(-2)
	SIF TCVAR:상태변화 == 6
		쿨치 -= 2
	;울적(-1)
	SIF TCVAR:상태변화 == 8
		쿨치 -= 1
	;조교자 소질에 의해 쿨 다운량 변동
	SIF TALENT:반항적
		쿨치 -= 1
	SIF TALENT:얌전함
		쿨치 += 2
	SIF TALENT:프라이드높음
		쿨치 -= RAND:2
	SIF TALENT:거만함
		쿨치 -= 1
	SIF TALENT:프라이드낮음
		쿨치 += RAND:2
	SIF TALENT:선을그음
		쿨치 += 1
	SIF TALENT:도M
		쿨치 -= RAND:4
	SIF TALENT:심술쟁이
		쿨치 -= RAND:2
	SIF TALENT:상냥함
		쿨치 += 2
	SIF TALENT:사교적
		쿨치 += RAND:2
	;조교자 조교레벨이 낮으면 추가 쿨 다운(+1)
	SIF CFLAG:조교레벨 < 5
		쿨치 += 1
	;아라이먼트가 정말로 기울면 추가 쿨 다운(+1～2)
	쿨치 += LIMIT(CFLAG:아라이먼트 / 25, 0, 2)
	;쿨치가 정이라면 쿨 다운 실행
	SIF 쿨치 > 0
		TFLAG:소프트방침히트 -= 쿨치
	;하한 돌파를 방지
	SIF TFLAG:소프트방침히트 < 0
		TFLAG:소프트방침히트 = 0
ENDIF

;노멀의 쿨 다운
IF TFLAG:노멀방침히트 > 0
	;초기치(5)
	쿨치 = 5
	;전회비실행으로(+1～3)
	SIF TFLAG:전턴방침 != 3
		쿨치 += 1 + RAND:3
	;조교자 상태변화에 의해 쿨 다운량 변동
	;분노(-1)
	SIF TCVAR:상태변화 == 6
		쿨치 -= 1
	;싫증(-3)
	SIF TCVAR:상태변화 == 7
		쿨치 -= 3
	;울적(-3)
	SIF TCVAR:상태변화 == 8
		쿨치 -= 3
	;초조가 작으면 쿨 다운 억제(-2～0)
	쿨치 -= 2 - LIMIT(BASE:초조 / 200, 0, 2)
	;조교자 소질에 의해 쿨 다운량 변동
	SIF TALENT:얌전함
		쿨치 += 1
	SIF TALENT:선을그음
		쿨치 += 2
	SIF TALENT:도M
		쿨치 -= RAND:2
	SIF TALENT:신중
		쿨치 += 1
	SIF TALENT:유치
		쿨치 += RAND:3
	SIF TALENT:매혹
		쿨치 += RAND:2
	;조교 대상 소질에 의해 쿨 다운량 변동
	SIF TALENT:MASTER:연모
		쿨치 += RAND:3
	;아라이먼트가 0에 가까우면 추가 쿨 다운(+1～2)
	LOCAL = ABS(CFLAG:아라이먼트)
	쿨치 += 2 - LIMIT(LOCAL / 12, 0, 2)
	;쿨치가 정이라면 쿨 다운 실행
	SIF 쿨치 > 0
		TFLAG:노멀방침히트 -= 쿨치
	;하한 돌파를 방지
	SIF TFLAG:노멀방침히트 < 0
		TFLAG:노멀방침히트 = 0
ENDIF

;하드의 쿨 다운
IF TFLAG:하드방침히트 > 0
	;초기치(3～5)
	쿨치 = 3 + RAND:3
	;전회 실행으로(-2)
	SIF TFLAG:전턴방침 == 4
		쿨치 -= 2
	;체력과 기력의 잔량을 본다(적을 정도쿨 다운 억제, -2～0)
	쿨치 -= 2 - LIMIT(BASE:체력 + BASE:기력 + BASE:MASTER:체력 + BASE:MASTER:기력 / 500, 0, 2)
	;조교자 상태변화에 의해 쿨 다운량 변동
	;쇠약(-1)
	SIF TCVAR:상태변화 == 2
		쿨치 -= 1
	;무기력(-2)
	SIF TCVAR:상태변화 == 3
		쿨치 -= 2
	;싫증(+1)
	SIF TCVAR:상태변화 == 7
		쿨치 += 1
	;초조가 크면 추가 쿨 다운(+0～2)
	쿨치 += LIMIT(BASE:초조 / 300, 0, 2)
	;조교자 소질에 의해 쿨 다운량 변동
	SIF TALENT:겁쟁이
		쿨치 -= 1
	SIF TALENT:기장
		쿨치 += 1
	SIF TALENT:프라이드높음
		쿨치 += RAND:3
	SIF TALENT:거만함
		쿨치 += 1
	SIF TALENT:프라이드낮음
		쿨치 -= RAND:3
	SIF TALENT:선을그음
		쿨치 -= 1
	SIF TALENT:억압
		쿨치 -= 1
	SIF TALENT:해방
		쿨치 += 1
	SIF TALENT:새드
		쿨치 += 2
	SIF TALENT:신중 && CFLAG:MASTER:조교레벨 < 6
		쿨치 -= 3 - CFLAG:MASTER:조교레벨 / 2
	SIF TALENT:위압감
		쿨치 += 1
	;조교 대상 소질에 의해 쿨 다운량 변동
	SIF TALENT:MASTER:도M
		쿨치 += 1
	SIF TALENT:MASTER:예속
		쿨치 += 2
	;쿨치가 정이라면 쿨 다운 실행
	SIF 쿨치 > 0
		TFLAG:하드방침히트 -= 쿨치
	;하한 돌파를 방지
	SIF TFLAG:하드방침히트 < 0
		TFLAG:하드방침히트 = 0
ENDIF

;이상의 쿨 다운
IF TFLAG:이상방침히트 > 0
	;초기치(0～5)
	쿨치 = RAND:6
	;전회비실행으로(+0～2)
	SIF TFLAG:전턴방침 != 5
		쿨치 += RAND:3
	;조교자 상태변화에 의해 쿨 다운량 변동
	;몽롱(+1)
	SIF TCVAR:상태변화 == 4
		쿨치 += 1
	;울적(+3)
	SIF TCVAR:상태변화 == 8
		쿨치 += 3
	;조교자 이성이 낮으면 추가 쿨 다운(+0～3)
	쿨치 += 3 - LIMIT(BASE:이성 / 150, 0, 3)
	;조교자 소질에 의해 쿨 다운량 변동
	SIF TALENT:겁쟁이
		쿨치 -= 2
	SIF TALENT:거만함
		쿨치 += 1
	SIF TALENT:자제적
		쿨치 -= 2
	SIF TALENT:충동적
		쿨치 += 2
	SIF TALENT:선을그음
		쿨치 -= 3
	SIF TALENT:억압
		쿨치 -= 1 + RAND:2
	SIF TALENT:해방
		쿨치 += 1 + RAND:2
	SIF TALENT:광기
		쿨치 += 4
	SIF TALENT:불사
		쿨치 += 1 + RAND:2
	SIF TALENT:금단의지식
		쿨치 += 2
	;조교 대상 소질에 의해 쿨 다운량 변동
	SIF TALENT:MASTER:음란
		쿨치 += 1
	;쿨치가 정이라면 쿨 다운 실행
	SIF 쿨치 > 0
		TFLAG:이상방침히트 -= 쿨치
	;하한 돌파를 방지
	SIF TFLAG:이상방침히트 < 0
		TFLAG:이상방침히트 = 0
ENDIF

;조교 메뉴 히트 축적
;이번 ACT분류에 대해 처리를 실시한다

;쓰러트리기중은 축적하지 않는다
;또, 휴식에는 메뉴 히트가 존재하지 않는다
IF TEQUIP:쓰러트리기중 != 1 && TFLAG:ACT분류 != 9
	;LOCAL에 히트 상승량을 확보
	LOCAL = 0
	;LOCAL:1에 이번 ACT분류에 의해 변수 포인터를 설정
	LOCAL:1 = 81 + TFLAG:ACT분류
	;분류가 성교 봉사이면 변수 포인터를 조정
	SIF TFLAG:ACT분류 == 10
		LOCAL:1 = 350
	;히트치가 정이라면(+8)
	SIF TFLAG:(LOCAL:1) > 0
		LOCAL += 8
	;무조건으로(+8)
	LOCAL += 8
	;연속 실행으로(+8)
	IF TFLAG:전ACT분류 == TFLAG:ACT분류
		LOCAL += 8
	ENDIF
	;선택 가능 계열수에 의해 상승량 감소
	LOCAL = LOCAL * LIMIT(TFLAG:가능한ACT계열수 - 1, 0, 4) / 4
	;실제의 상승 처리
	TFLAG:(LOCAL:1) += LOCAL
ENDIF

;조교 메뉴의 쿨 다운(히트치 존재시 처리를 실시한다)
;변수 『쿨치』에 감소량 보존

;회화의 쿨 다운
IF TFLAG:회화히트 > 0
	;기능에 응해 쿨치를 설정(4～)
	쿨치 = 4 + (GET_ABL(TARGET, "회화") + 10) / 20
	;전회 실행으로(-3)
	SIF TFLAG:전ACT분류 == 0
		쿨치 -= 3
	;체력과 기력의 잔량을 본다(적을 정도 추가 쿨 다운, +0～2)
	쿨치 += 2 - LIMIT(BASE:체력 + BASE:기력 / 400, 0, 2)
	;흥미가 낮으면 쿨 다운 억제(-3～+1)
	쿨치 += LIMIT(BASE:흥미 / 200, 0, 4) - 3
	;조교자 소질에 의해 쿨 다운량 변동
	SIF TALENT:무관심
		쿨치 -= 2
	SIF TALENT:호기심
		쿨치 += 2
	SIF TALENT:감정결핍
		쿨치 -= 1
	SIF TALENT:감수성
		쿨치 += 1
	SIF TALENT:낙관적
		쿨치 += RAND:2
	SIF TALENT:비관적
		쿨치 -= RAND:2
	SIF TALENT:사교적
		쿨치 += 1
	SIF TALENT:대인기피
		쿨치 -= 1
	;리액션에 의한 배율
	쿨치 = COOL_BYREACT(쿨치)
	;쿨치가 정이라면 쿨 다운 실행
	SIF 쿨치 > 0
		TFLAG:회화히트 -= 쿨치
	;하한 돌파를 방지
	SIF TFLAG:회화히트 < 0
		TFLAG:회화히트 = 0
ENDIF

;애무의 쿨 다운
IF TFLAG:애무히트 > 0
	;기능에 응해 쿨치를 설정(4～)
	쿨치 = 4 + RAND:3 + (GET_ABL(TARGET, "애무") + 10) / 20
	;전회 실행으로(-1)
	SIF TFLAG:전ACT분류 != 1
		쿨치 -= 1
	;조교자소모도가 높면 쿨치 감소(-2～0)
	SIF TFLAG:조교자소모도 > 0
		쿨치 -= TFLAG:조교자소모도 > 5 ? 2 # 1
	;조교자 상태변화에 의해 쿨 다운량 변동
	;몽롱(+0～1)
	SIF TCVAR:상태변화 == 4
		쿨치 += RAND:2
	;분노(-2)
	SIF TCVAR:상태변화 == 6
		쿨치 -= 2
	;흥미가 낮으면 쿨 다운 억제(-1～+1)
	쿨치 += LIMIT(BASE:흥미 / 400, 0, 2) - 1
	;조교자 소질에 의해 쿨 다운량 변동
	SIF TALENT:무관심
		쿨치 -= 2
	SIF TALENT:호기심
		쿨치 += 2
	SIF TALENT:감정결핍
		쿨치 -= 1
	SIF TALENT:감수성
		쿨치 += 1
	SIF TALENT:낙관적
		쿨치 += RAND:2
	SIF TALENT:비관적
		쿨치 -= RAND:2
	SIF TALENT:혀사용
		쿨치 += 1
	SIF TALENT:기묘한손가락
		쿨치 += 1
	SIF TALENT:헌신적
		쿨치 += 1
	SIF TALENT:수동
		쿨치 -= 1
	;조교자가 남성혐오때, 남자가 조교 대상이라면(-2)
	SIF TALENT:남성혐오 && TALENT:MASTER:남자
		쿨치 -= 2
	;남성혐오, 레즈끼, ＢＬ끼이 모두 없을 때, 조교자·조교 대상이 동성이라면(-2)
	SIF !TALENT:남성혐오 && ABL:레즈끼 < 1 && ABL:ＢＬ끼 < 1 && TALENT:MASTER:남자 == TALENT:남자
			쿨치 -= 2
	;리액션에 의한 배율
	쿨치 = COOL_BYREACT(쿨치)
	;쿨치가 정이라면 쿨 다운 실행
	SIF 쿨치 > 0
		TFLAG:애무히트 -= 쿨치
	;하한 돌파를 방지
	SIF TFLAG:애무히트 < 0
		TFLAG:애무히트 = 0
ENDIF

;도구의 쿨 다운
IF TFLAG:도구히트 > 0
	;기능에 응해 쿨치를 설정(4～)
	쿨치 = 4 + (GET_ABL(TARGET, "도구") + 10) * 2 / 30
	;전회 실행으로(-3)
	SIF TFLAG:전ACT분류 == 2
		쿨치 -= 3
	;조교자 상태변화에 의해 쿨 다운량 변동
	;무기력(-0～1)
	SIF TCVAR:상태변화 == 3
		쿨치 -= RAND:2
	;몽롱(-1)
	SIF TCVAR:상태변화 == 4
		쿨치 -= 1
	;정욕(-1)
	SIF TCVAR:상태변화 == 5
		쿨치 -= 1
	;도구무장착시(+3)
	IF TEQUIP:바이브 + TEQUIP:애널바이브 + TEQUIP:애널비즈 + TEQUIP:관장기＋플러그 + TEQUIP:클리캡 + TEQUIP:오나홀 + TEQUIP:니플캡 == 0
		쿨치 += 3
	;장착시(-1)
	ELSE
		쿨치 -= 1
	ENDIF
	;조교자 소질에 의해 쿨 다운량 변동
	SIF TALENT:해방
		쿨치 += RAND:2
	SIF TALENT:억압
		쿨치 -= RAND:2
	SIF TALENT:도구사용
		쿨치 += 2
	SIF TALENT:헌신적
		쿨치 += 1
	SIF TALENT:수동
		쿨치 -= 1
	;리액션에 의한 배율
	쿨치 = COOL_BYREACT(쿨치)
	;쿨치가 정이라면 쿨 다운 실행
	SIF 쿨치 > 0
		TFLAG:도구히트 -= 쿨치
	;하한 돌파를 방지
	SIF TFLAG:도구히트 < 0
		TFLAG:도구히트 = 0
ENDIF

;성교의 쿨 다운
IF TFLAG:성교히트 > 0
	;기능에 응해 쿨치를 설정(2～)
	쿨치 = 2 + (GET_ABL(TARGET, "성교") + 10) / 20
	;전회 실행으로(-1)
	SIF TFLAG:전ACT분류 == 3
		쿨치 -= 1
	;조교자 상태변화에 의해 쿨 다운량 변동
	;무기력(-0～1)
	SIF TCVAR:상태변화 == 3
		쿨치 -= RAND:2
	;정욕(+2)
	SIF TCVAR:상태변화 == 5
		쿨치 += 2
	;싫증(-1)
	SIF TCVAR:상태변화 == 7
		쿨치 -= 1
	;삽입하고 있는 동안만 빠른 스피드로 쿨 다운한다(+7)
	SIF TEQUIP:성교중 && TFLAG:ACT분류 != 3
		쿨치 += 7
	;조교자가 남성혐오때, 남자가 조교 대상이라면(-3)
	SIF TALENT:남성혐오 && TALENT:MASTER:남자
		쿨치 -= 3
	;남성혐오, 레즈끼, ＢＬ끼이 모두 없을 때, 조교자·조교 대상이 동성이라면(-3)
	SIF !TALENT:남성혐오 && ABL:레즈끼 < 1 && ABL:ＢＬ끼 < 1 && TALENT:MASTER:남자 == TALENT:남자
			쿨치 -= 3
	;리액션에 의한 배율
	쿨치 = COOL_BYREACT(쿨치)
	;쿨치가 정이라면 쿨 다운 실행
	SIF 쿨치 > 0
		TFLAG:성교히트 -= 쿨치
	;하한 돌파를 방지
	SIF TFLAG:성교히트 < 0
		TFLAG:성교히트 = 0
ENDIF

;수치의 쿨 다운
IF TFLAG:수치히트 > 0
	;기능에 응해 쿨치를 설정(4～)
	쿨치 = 4 + (GET_ABL(TARGET, "수치") + 10) / 20
	;전회 실행으로(-2)
	SIF TFLAG:전ACT분류 == 4
		쿨치 -= 2
	;흥미가 낮으면 쿨 다운 억제(-3～+1)
	쿨치 += LIMIT(BASE:흥미 / 200, 0, 4) - 3
	;조교자 상태변화에 의해 쿨 다운량 변동
	;무기력(-2)
	SIF TCVAR:상태변화 == 3
		쿨치 -= 2
	;분노(-2)
	SIF TCVAR:상태변화 == 6
		쿨치 -= 2
	;싫증(-1)
	SIF TCVAR:상태변화 == 7
		쿨치 -= 1
	;아이마스크 장착시는 추가 쿨 다운(+1)
	SIF TEQUIP:아이마스크
		쿨치 += 1
	;조교자 소질에 의해 쿨 다운량 변동
	SIF TALENT:무관심
		쿨치 -= 1
	SIF TALENT:호기심
		쿨치 += 1
	SIF TALENT:튀고싶어함
		쿨치 += 1
	SIF TALENT:억압
		쿨치 -= 2
	SIF TALENT:해방
		쿨치 -= 2
	SIF TALENT:수줍음
		쿨치 -= 2
	SIF TALENT:수치심옅음
		쿨치 += 2
	SIF TALENT:자위쉬움
		쿨치 += 1
	;조교 대상 소질에 의해 쿨 다운량 변동
	SIF TALENT:MASTER:자위쉬움
		쿨치 += 1
	;리액션에 의한 배율
	쿨치 = COOL_BYREACT(쿨치)
	;쿨치가 정이라면 쿨 다운 실행
	SIF 쿨치 > 0
		TFLAG:수치히트 -= 쿨치
	;하한 돌파를 방지
	SIF TFLAG:수치히트 < 0
		TFLAG:수치히트 = 0
ENDIF

;봉사의 쿨 다운
IF TFLAG:봉사히트 > 0
	;기능에 응해 쿨치를 설정(5～)
	쿨치 = 5 + (GET_ABL(TARGET, "봉사") + 10) / 20
	;전회 실행으로(-2)
	SIF TFLAG:전ACT분류 == 5
		쿨치 -= 2
	;조교 대상 기력이 낮으면 쿨 다운 억제(-2～0)
	쿨치 -= 2 - LIMIT(BASE:MASTER:기력 / 200, 0, 2)
	;조교자 상태변화에 의해 쿨 다운량 변동
	;정욕(+2)
	SIF TCVAR:상태변화 == 5
		쿨치 += 2
	;싫증(-2)
	SIF TCVAR:상태변화 == 7
		쿨치 -= 2
	;줄, 볼개그 장착시는 쿨 다운 억제
	SIF TEQUIP:줄 || TEQUIP:볼개그
		쿨치 -= 1
	;조교자 소질에 의해 쿨 다운량 변동
	SIF TALENT:프라이드높음
		쿨치 += 1
	SIF TALENT:프라이드낮음
		쿨치 -= 1
	SIF TALENT:무관심
		쿨치 -= RAND:2
	SIF TALENT:호기심
		쿨치 += RAND:2
	SIF TALENT:튀고싶어함
		쿨치 += 1
	SIF TALENT:수줍음
		쿨치 -= 1
	SIF TALENT:수치심옅음
		쿨치 += 1
	SIF TALENT:헌신적
		쿨치 -= 1
	SIF TALENT:수동
		쿨치 += 1 + RAND:2
	SIF TALENT:쾌감에솔직
		쿨치 += 2
	SIF TALENT:쾌감을부정
		쿨치 -= 2
	;조교 대상 소질에 의해 쿨 다운량 변동
	SIF TALENT:MASTER:연모
		쿨치 += 1
	;조교자가 남성혐오때, 남자가 조교 대상이라면(-1)
	SIF TALENT:남성혐오 && TALENT:MASTER:남자
		쿨치 -= 1
	;남성혐오, 레즈끼, ＢＬ끼이 모두 없을 때, 조교자·조교 대상이 동성이라면(-1)
	SIF !TALENT:남성혐오 && ABL:레즈끼 < 1 && ABL:ＢＬ끼 < 1 && TALENT:MASTER:남자 == TALENT:남자
			쿨치 -= 1
	;리액션에 의한 배율
	쿨치 = COOL_BYREACT(쿨치)
	;쿨치가 정이라면 쿨 다운 실행
	SIF 쿨치 > 0
		TFLAG:봉사히트 -= 쿨치
	;하한 돌파를 방지
	SIF TFLAG:봉사히트 < 0
		TFLAG:봉사히트 = 0
ENDIF

;가학의 쿨 다운
IF TFLAG:가학히트 > 0
	;기능에 응해 쿨치를 설정(4～)
	쿨치 = 4 + (GET_ABL(TARGET, "가학") + 10) / 20
	;전회 실행으로(-2)
	SIF TFLAG:전ACT분류 == 6
		쿨치 -= 2
	;조교 대상 체력이 낮으면(-1)
	SIF BASE:MASTER:체력 < 500
		쿨치 -= 1
	;조교 대상 흥미가 낮으면(-1)
	SIF BASE:MASTER:흥미 < 1000
		쿨치 -= 1
	;초조가 꽤 강하면 추가 쿨 다운(+0～2)
	쿨치 += LIMIT(BASE:초조 / 200, 2, 4) - 2
	;조교자 상태변화에 의해 쿨 다운량 변동
	;쇠약(-1)
	SIF TCVAR:상태변화 == 2
		쿨치 -= 1
	;무기력(-2)
	SIF TCVAR:상태변화 == 3
		쿨치 -= 2
	;분노(+2)
	SIF TCVAR:상태변화 == 6
		쿨치 += 2
	;울적(+1)
	SIF TCVAR:상태변화 == 8
		쿨치 += 1
	;조교자 소질에 의해 쿨 다운량 변동
	SIF TALENT:프라이드높음
		쿨치 += 1
	SIF TALENT:프라이드낮음
		쿨치 -= 1
	SIF TALENT:얌전함
		쿨치 -= 2
	SIF TALENT:거만함
		쿨치 += 2
	SIF TALENT:바느질
		쿨치 += 1
	SIF TALENT:결박능숙
		쿨치 += 1
	SIF TALENT:새드
		쿨치 += 2
	SIF TALENT:심술쟁이
		쿨치 += 1
	SIF TALENT:상냥함
		쿨치 -= 1
	;조교 대상 소질에 의해 쿨 다운량 변동
	SIF TALENT:MASTER:도M
		쿨치 += 2
	;리액션에 의한 배율
	쿨치 = COOL_BYREACT(쿨치)
	;쿨치가 정이라면 쿨 다운 실행
	SIF 쿨치 > 0
		TFLAG:가학히트 -= 쿨치
	;하한 돌파를 방지
	SIF TFLAG:가학히트 < 0
		TFLAG:가학히트 = 0
ENDIF

;이상 행위의 쿨 다운
IF TFLAG:이상히트 > 0
	;기능에 응해 쿨치를 설정(1～)
	쿨치 = 1 + (GET_ABL(TARGET, "사역") + 10) * 2 / 30
	;전회비실행으로(+0～3)
	SIF TFLAG:전ACT분류 != 7
		쿨치 += RAND:4
	;체력과 기력의 잔량을 본다(적을 정도쿨 다운 억제, -2～0)
	쿨치 -= 2 - LIMIT(BASE:체력 + BASE:기력 / 600, 0, 2)
	;만족이 크면 쿨 다운 억제(-2～0)
	쿨치 -= LIMIT(BASE:만족 / 300, 0, 2)
	;조교자 상태변화에 의해 쿨 다운량 변동
	;쇠약(-3)
	SIF TCVAR:상태변화 == 2
		쿨치 -= 3
	;무기력(-1)
	SIF TCVAR:상태변화 == 3
		쿨치 -= 1
	;몽롱(+2)
	SIF TCVAR:상태변화 == 4
		쿨치 += 2
	;울적(+4)
	SIF TCVAR:상태변화 == 8
		쿨치 += 4
	;조교자 소질에 의해 쿨 다운량 변동
	SIF TALENT:겁쟁이
		쿨치 -= 2
	SIF TALENT:기장
		쿨치 += RAND:2
	SIF TALENT:자제적
		쿨치 -= 1
	SIF TALENT:충동적
		쿨치 += 1
	SIF TALENT:억압
		쿨치 -= 2
	SIF TALENT:해방
		쿨치 += RAND:3
	SIF TALENT:유치
		쿨치 += 1
	SIF TALENT:광기
		쿨치 += 3
	SIF TALENT:불사
		쿨치 += RAND:2
	SIF TALENT:금단의지식
		쿨치 += 1
	;리액션에 의한 배율
	쿨치 = COOL_BYREACT(쿨치)
	;쿨치가 정이라면 쿨 다운 실행
	SIF 쿨치 > 0
		TFLAG:이상히트 -= 쿨치
	;하한 돌파를 방지
	SIF TFLAG:이상히트 < 0
		TFLAG:이상히트 = 0
ENDIF

;사역의 쿨 다운
IF TFLAG:사역히트 > 0
	;기능에 응해 쿨치를 설정(1～)
	쿨치 = 1 + RAND:2 + (GET_ABL(TARGET, "사역") + 10) * 2 / 30
	;전회 실행으로(-1)
	SIF TFLAG:전ACT분류 == 8
		쿨치 -= 1
	;체력과 기력의 잔량을 본다(적을 정도쿨 다운 억제, -4～0)
	쿨치 -= 4 - LIMIT(BASE:MASTER:체력 + BASE:MASTER:기력 / 300, 0, 4)
	;흥미가 작으면 추가 쿨 다운(+0～2)
	쿨치 += 2 - LIMIT(BASE:흥미 / 200, 0, 2)
	;조교자 상태변화에 의해 쿨 다운량 변동
	;정욕(-1)
	SIF TCVAR:상태변화 == 5
		쿨치 -= 1
	;싫증(+3)
	SIF TCVAR:상태변화 == 7
		쿨치 += 3
	;울적(+1)
	SIF TCVAR:상태변화 == 8
		쿨치 += 1
	;조교자 소질에 의해 쿨 다운량 변동
	SIF TALENT:무관심
		쿨치 += 1
	SIF TALENT:호기심
		쿨치 -= 1
	SIF TALENT:선을그음
		쿨치 -= 2
	SIF TALENT:유치
		쿨치 += 1
	SIF TALENT:광기
		쿨치 += 2
	SIF TALENT:금단의지식
		쿨치 += 3
	;리액션에 의한 배율
	쿨치 = COOL_BYREACT(쿨치)
	;쿨치가 정이라면 쿨 다운 실행
	SIF 쿨치 > 0
		TFLAG:사역히트 -= 쿨치
	;하한 돌파를 방지
	SIF TFLAG:사역히트 < 0
		TFLAG:사역히트 = 0
ENDIF

;성교(봉사)의 쿨 다운
IF TFLAG:성적봉사히트 > 0
	;초기치를 설정(2)
	쿨치 = 2
	;전회 실행으로(-1)
	SIF TFLAG:전ACT분류 == 10
		쿨치 -= 1
	;조교자 상태변화에 의해 쿨 다운량 변동
	;무기력(+0～1)
	SIF TCVAR:상태변화 == 3
		쿨치 += RAND:2
	;정욕(-3)
	SIF TCVAR:상태변화 == 5
		쿨치 -= 3
	;싫증(+1)
	SIF TCVAR:상태변화 == 7
		쿨치 += 1
	;삽입하고 있는 동안만 빠른 스피드로 쿨 다운한다(+7)
	SIF TEQUIP:성교봉사중 && TFLAG:ACT분류 != 10
		쿨치 += 7
	;조교자가 남성혐오때, 남자가 조교 대상이라면(+3)
	SIF TALENT:남성혐오 && TALENT:MASTER:남자
		쿨치 += 3
	;조교자 소질에 의해 쿨 다운량 변동
	SIF TALENT:연모
		쿨치 -= 1
	;오늘의방침가 아이 만들기라면(+5)
	SIF TFLAG:오늘의방침 == 7
		쿨치 += 5
	;리액션에 의한 배율
	쿨치 = COOL_BYREACT(쿨치)
	;쿨치가 정이라면 쿨 다운 실행
	SIF 쿨치 > 0
		TFLAG:성적봉사히트 -= 쿨치
	;하한 돌파를 방지
	SIF TFLAG:성적봉사히트 < 0
		TFLAG:성적봉사히트 = 0
ENDIF


;────────────────────────────────────
;성교계 동일 체위 억제 처리
;PLAYER_ACTM계로부터 이동@이것 보고
;────────────────────────────────────
;성교계
FOR LCOUNT, 0, 5
	;해당 체위라면+100
	IF TFLAG:전ACT == LCOUNT + 30
		TFLAG:(356 + LCOUNT) += 100
	;그 이외라면 2/3
	ELSE
		TFLAG:(356 + LCOUNT) = TFLAG:(356 + LCOUNT) * 2 / 3
	ENDIF
NEXT

;성교 봉사
LOCAL = 95, 96, 103, 97, 98
FOR LCOUNT, 0, 5
	;해당 체위라면+100
	IF TFLAG:전ACT == LOCAL:LCOUNT
		TFLAG:(351 + LCOUNT) += 100
	;그 이외라면 2/3
	ELSE
		TFLAG:(351 + LCOUNT) = TFLAG:(351 + LCOUNT) * 2 / 3
	ENDIF
NEXT

;────────────────────────────────────
;가게 하는, 초조하게 한다
;────────────────────────────────────
;범용 연속행동 플래그(ACT분류예약, 행동예약이 이미 있는 경우 무시)
IF TFLAG:연속행동 && TFLAG:ACT분류예약 == -1 && TCVAR:행동예약 == -1
	;ACT분류, ACT를 예약한다
	TFLAG:ACT분류예약 = TFLAG:ACT분류
	TCVAR:행동예약 = TFLAG:ACT
ENDIF
;연속행동 플래그 해제
TFLAG:연속행동 = 0

;Ｃ절정시
IF NOWEX:MASTER:사정 || NOWEX:MASTER:Ｃ절정
	;갈것같다·초조하게함의 리셋트
	TCVAR:MASTER:갈것같다 = 0
	TCVAR:MASTER:애태움수 = 0
ENDIF
SIF NOWEX:Ｃ절정
	TCVAR:TARGET:갈것같다 = 0
	
;조교 대상으로 남성기가 존재하지 않을 때에Ｃ절정
IF !PENIS(MASTER) && NOWEX:MASTER:Ｃ절정
	;초조하게함의 리셋트
	TCVAR:MASTER:애태움수 = 0
ENDIF
;벌시, 초조하게함의 리셋트
SIF TFLAG:벌플래그
	TCVAR:MASTER:애태움수 = 0

;────────────────────────────────────
;ACT 보정치 감소, 감쇠 처리
;────────────────────────────────────
CFLAG:(400 + TFLAG:ACT) /= 2

;────────────────────────────────────
;데이타베이스의 등록, 턴의 리셋트
;────────────────────────────────────
;데이타베이스 등록
CALL DATABASE_INPUT

;전CHARA 체위의 보존
FOR LCOUNT, 0, CHARANUM
	FOR LCOUNT:1, 1, 11
		TEQUIP:LCOUNT:(110 + LCOUNT:1) = TEQUIP:LCOUNT:(100 + LCOUNT:1)
	NEXT
NEXT

;TEQUIP의 보존
;성교
TFLAG:전턴성교 = TEQUIP:성교중
TFLAG:전턴성교봉사 = TEQUIP:성교봉사중
;안면승마
TFLAG:전턴안면승마 = TEQUIP:안면승마
TFLAG:전턴Ａ안면승마 = TEQUIP:안면승마애널

;인내 플래그
;인내중은
IF TCVAR:MASTER:인내
	;절정에서 인내 리셋트
	IF NOWEX:MASTER:Ｃ절정 || NOWEX:MASTER:Ｖ절정 || NOWEX:MASTER:Ａ절정 || NOWEX:MASTER:Ｂ절정
		TCVAR:MASTER:인내 = 0
	;그렇지 않으면 카운트를-1
	ELSE
		TCVAR:MASTER:인내 -= 1
	ENDIF
ENDIF

;────────────────────────────────────
;사정 후의 턴수가산에 대해 by/L
;────────────────────────────────────
;2014/11/08시점에서는 TFLAG:사정후경과턴과 TFLAG와 TCVAR가 혼재해 조금 알기 어려운 상태
;라고는 해도, 갑자기 TFLAG:사정후경과턴 등을 삭제해 버릴 수도 없다고 생각하므로
;403xx series 근처까지는 이대로라고 하는 일로. 그리고 장내사정후경과턴이라든지 개인적 취미()로 추가했습니다
;-------------------------------------------------
;━━━━━━━━━━━━━━━━━━━━━━━━
;조교 대상 사정 후의 턴수가산
;━━━━━━━━━━━━━━━━━━━━━━━━
;TFLAG:사정후경과턴은 403xx series로 말소 예정@/L
SIF TFLAG:사정후경과턴
	TFLAG:사정후경과턴 += 1

SIF TCVAR:MASTER:사정후경과턴참조
	TCVAR:MASTER:사정후경과턴 += 1

;━━━━━━━━━━━━━━━━━━━━━━━━
;조교 대상 질내사정 후의 턴수가산
;━━━━━━━━━━━━━━━━━━━━━━━━
;TFLAG:질내사정후경과턴은 403xx series로 말소 예정@/L
SIF TFLAG:질내사정후경과턴
	TFLAG:질내사정후경과턴 += 1

SIF TCVAR:MASTER:질내사정후경과턴참조
	TCVAR:MASTER:질내사정후경과턴 += 1

;━━━━━━━━━━━━━━━━━━━━━━━━
;조교 대상장내 사정 후의 턴수가산
;━━━━━━━━━━━━━━━━━━━━━━━━
SIF TCVAR:MASTER:장내사정후경과턴참조
	TCVAR:MASTER:장내사정후경과턴 += 1

;━━━━━━━━━━━━━━━━━━━━━━━━
;조교자 사정 후의 턴수가산
;━━━━━━━━━━━━━━━━━━━━━━━━
SIF TCVAR:사정후경과턴참조
	TCVAR:사정후경과턴 += 1

;━━━━━━━━━━━━━━━━━━━━━━━━
;조교자 질내사정 후의 턴수가산
;━━━━━━━━━━━━━━━━━━━━━━━━
SIF TCVAR:질내사정후경과턴참조
	TCVAR:질내사정후경과턴 += 1

;━━━━━━━━━━━━━━━━━━━━━━━━
;조교자장내 사정 후의 턴수가산
;━━━━━━━━━━━━━━━━━━━━━━━━
SIF TCVAR:장내사정후경과턴참조
	TCVAR:장내사정후경과턴 += 1

;조교 턴수가산
TFLAG:조교시간 ++

;실금 플래그 리셋트
TFLAG:실금 = 0

;조수연속교합 플래그 가산
SIF TFLAG:조수에삽입중 || TFLAG:조수가삽입중
	TFLAG:조수연속교합 = 1

;이번 커맨드를 「전회의 커맨드」라고 한다
PREVCOM = SELECTCOM
TFLAG:전REACT분류 = TFLAG:REACT분류
TFLAG:전턴방침 = TFLAG:턴방침
TFLAG:전ACT분류 = TFLAG:ACT분류
TFLAG:전ACT = TFLAG:ACT
TFLAG:조수의전행동 = TFLAG:조수의방침

;윤활의 보존
TCVAR:MASTER:전윤활 = PALAM:MASTER:윤활

;조교자행동제 보고의 플래그를 리셋트
TFLAG:조교자행동제 = 0

;실행 가능 반응의 수를 리셋트
TFLAG:가능한COM수 = 0

;행동 패턴을 리셋트
TFLAG:ACT파생 = 0

;조교 대상의 반응에 의한 영향을 리셋트
CALL SET_COMGRO("없음")

;호감도/경험 가격인상승량을 리셋트
TFLAG:호감도업 = 0
TFLAG:경험치업 = 0

;위로돌리고 상태의 재설정
IF TFLAG:위로돌리고 > 0
	IF TEQUIP:쓰러트리기중 || TEQUIP:안면승마 || TEQUIP:안면승마애널
		TFLAG:위로돌리고 = 1
	ELSE
		TFLAG:위로돌리고 = 0
	ENDIF
ENDIF

;SP행동의 리셋트
TFLAG:SP행동 = 0

;범용 연속행동 플래그의 리셋트
TFLAG:연속행동 = 0

;발기 계산의 리셋트
TCVAR:MASTER:이번발기 = 0
TCVAR:MASTER:전립선자극 = 0

;인상의 리셋트
CALL SET_COMIMP("영향없음")

;조수 플레이시의 TEQUIP를 리셋트
TFLAG:Ｐ삽입중 = 0
TFLAG:Ｐ사용중 = 0
TFLAG:Ａ사용중 = 0
TFLAG:입사용중 = 0
TFLAG:가슴사용중 = 0
TFLAG:Ｖ사용중 = 0
TFLAG:발사용중 = 0
TFLAG:손사용중 = 0

;추가행동 플래그의 리셋트
TFLAG:강제탈의 = 0
TFLAG:자주탈의 = 0
TFLAG:미약등사용 = 0
TFLAG:추가행동 = 0

;구상 발생 플래그등의 리셋트
TFLAG:추가행동구상 = 0
TFLAG:추가행동지문스킵 = 0
TFLAG:미약등사용구상 = 0
TFLAG:미약등사용지문스킵 = 0

;조교지령의 기록
TSTR:전조교지령 = %TSTR:조교지령%

;절정의 기록
TCVAR:MASTER:전회턴Ｃ절정 = TCVAR:MASTER:Ｃ절정
TCVAR:MASTER:전회턴Ｖ절정 = TCVAR:MASTER:Ｖ절정
TCVAR:MASTER:전회턴Ａ절정 = TCVAR:MASTER:Ａ절정
TCVAR:MASTER:전회턴Ｂ절정 = TCVAR:MASTER:Ｂ절정
TCVAR:MASTER:전회턴분수 = TCVAR:MASTER:분수
TCVAR:MASTER:전회턴드라이사정 = TCVAR:MASTER:드라이사정
TCVAR:MASTER:전회턴분유   = TCVAR:MASTER:분유
TCVAR:MASTER:전회턴사정   = TCVAR:MASTER:사정
TCVAR:MASTER:전회턴방뇨   = TCVAR:MASTER:방뇨

TCVAR:전회턴Ｃ절정        = TCVAR:Ｃ절정
TCVAR:전회턴Ｖ절정        = TCVAR:Ｖ절정
TCVAR:전회턴Ａ절정        = TCVAR:Ａ절정
TCVAR:전회턴Ｂ절정        = TCVAR:Ｂ절정
TCVAR:전회턴분수        = TCVAR:분수
TCVAR:전회턴드라이사정        = TCVAR:드라이사정
TCVAR:전회턴분유          = TCVAR:분유
TCVAR:전회턴사정          = TCVAR:사정
TCVAR:전회턴방뇨          = TCVAR:방뇨

;사정의 기록
TCVAR:MASTER:전회사정 = NOWEX:MASTER:사정
TCVAR:전회사정 = NOWEX:사정

;절정계 TCVAR의 리셋트
FOR LCOUNT, 0, CHARANUM
	VARSET TCVAR:LCOUNT:0, 0, 70, 80
NEXT

;────────────────────────────────────
;파라미터의 상승＆표시（DOWN도 여기서）
;────────────────────────────────────
;-----------------------------------------
;PALAM 보정치 변동 처리
;CUP가 있던 것은 증가, 없었던 것은 감쇠한다
;-----------------------------------------
;쾌Ｘ/x보정 가격인상승(쾌Ｘ/x상승시, 보정치+10)
FOR LCOUNT, 0, 4
	SIF CUP:MASTER:LCOUNT
		CFLAG:MASTER:(30 + LCOUNT) += 10
	SIF CUP:LCOUNT
		CFLAG:(30 + LCOUNT) += 10
NEXT
	
;쾌Ｘ/x보정치 감쇠(50 초과시, 1 T마다-1)
FOR LCOUNT, 0, CHARANUM
	FOR LCOUNT:1, 30, 34
		SIF CFLAG:LCOUNT:(LCOUNT:1) > 50
			CFLAG:LCOUNT:(LCOUNT:1) -= 1
	NEXT
NEXT

;-----------------------------------------
;PALAM 자연 감쇠 처리
;CUP가 없었던 것은 감쇠한다
;-----------------------------------------
;쾌감의 자연 감쇠
;합계 0의 경우는 5%씩 감쇠(갈 것 같은 경우, 쾌Ｃ는 제외)
;쾌감계 PALAM는 몇개의 증가가 있었을 때는 감쇠하지 않는다

;조교 대상의 쾌감 감쇠(쾌Ｘ상승 총계가 0, 5% 감쇠)
IF !MAX(CUP:MASTER:쾌Ｃ, CUP:MASTER:쾌Ｖ, CUP:MASTER:쾌Ａ, CUP:MASTER:쾌Ｂ)
	;갈것같다인 경우쾌Ｃ를 제외
	SIF TCVAR:MASTER:갈것같다 == 0
		CDOWN:MASTER:쾌Ｃ += PALAM:MASTER:쾌Ｃ * 5 / 100
	CDOWN:MASTER:쾌Ｖ += PALAM:MASTER:쾌Ｖ * 5 / 100
	CDOWN:MASTER:쾌Ａ += PALAM:MASTER:쾌Ａ * 5 / 100
	CDOWN:MASTER:쾌Ｂ += PALAM:MASTER:쾌Ｂ * 5 / 100
ENDIF
;조교자의 쾌감 감쇠(쾌x상승 총계가 0, 5% 감쇠)
IF !MAX(CUP:쾌Ｃ, CUP:쾌Ｖ, CUP:쾌Ａ, CUP:쾌Ｂ)
	;갈것같다인 경우쾌Ｃ를 제외
	SIF TCVAR:TARGET:갈것같다 == 0
		CDOWN:쾌Ｃ += PALAM:쾌Ｃ * 5 / 100
	CDOWN:쾌Ｖ += PALAM:쾌Ｖ * 5 / 100
	CDOWN:쾌Ａ += PALAM:쾌Ａ * 5 / 100
	CDOWN:쾌Ｂ += PALAM:쾌Ｂ * 5 / 100
ENDIF

;갈것같다의 판정
;쾌Ｃ상승 0시는 반감
IF !CUP:MASTER:쾌Ｃ
	TCVAR:MASTER:갈것같다 /= 2
;그 이외에서는 1.2배
ELSE
	TIMES TCVAR:MASTER:갈것같다 , 1.20
ENDIF

;쾌c상승 0시는 반감
IF !CUP:쾌Ｃ
	TCVAR:TARGET:갈것같다 /= 2
;그 이외에서는 1.2배
ELSE
	TIMES TCVAR:TARGET:갈것같다 , 1.20
ENDIF

TCVAR:MASTER:갈것같다（그쳐라） = TCVAR:MASTER:갈것같다 < ABL:MASTER:Ｃ감각 * 2 ? 1 # 0
TCVAR:TARGET:갈것같다（그쳐라） = TCVAR:TARGET:갈것같다 < ABL:TARGET:Ｃ감각 * 2 ? 1 # 0

;로션비사용시는 윤활 감쇠(5%)
CDOWN:MASTER:윤활 += TEQUIP:로션 ? 0 # PALAM:MASTER:윤활 * 5 / 100

;Ｘ절정시
IF MAX(NOWEX:MASTER:Ｃ절정, NOWEX:MASTER:Ｖ절정, NOWEX:MASTER:Ａ절정, NOWEX:MASTER:Ｂ절정)
	;욕정의 30～60% 감쇠(욕정이 큰 만큼 감쇠)
	CDOWN:MASTER:욕정 += PALAM:MASTER:욕정 * LIMIT(PALAM:MASTER:욕정, 4000, 8000) * 3 / 40000
;쾌Ｘ상승 총계가 어릴 때
ELSEIF CUP:MASTER:쾌Ｃ + CUP:MASTER:쾌Ｖ + CUP:MASTER:쾌Ａ + CUP:MASTER:쾌Ｂ < 20
	;욕정의 0～10% 감쇠(욕정이 큰 만큼 감쇠)
	CDOWN:MASTER:욕정 += PALAM:MASTER:욕정 * LIMIT(PALAM:MASTER:욕정, 0, 6000) / 60000
ENDIF

;굴복은 상승 0시 5% 감쇠
CDOWN:MASTER:굴복 += CUP:MASTER:굴복 ? 0 # PALAM:MASTER:굴복 * 5 / 100
;공포는 상승 0시 1/12감쇠
CDOWN:MASTER:공포 += CUP:MASTER:공포 ? 0 # PALAM:MASTER:공포 / 12
;한층 더 공포는 상승 50 미만으로 20% 감쇠 추가
CDOWN:MASTER:공포 += CUP:MASTER:공포 >= 50 ? 0 # PALAM:MASTER:공포 * 20 / 100
;반항은 상승 0시 1/18감쇠
CDOWN:MASTER:반항 += CUP:MASTER:반항 ? 0 # PALAM:MASTER:반항 / 18
;고통은 상승 0시 10% 감쇠
CDOWN:MASTER:고통 += CUP:MASTER:고통 ? 0 # PALAM:MASTER:고통 * 10 / 100
;치정은 상승 0시 10% 감쇠
CDOWN:MASTER:치정 += CUP:MASTER:치정 ? 0 # PALAM:MASTER:치정 * 10 / 100
;불쾌는 상승 0시 1/12감쇠
CDOWN:MASTER:불쾌 += CUP:MASTER:불쾌 ? 0 # PALAM:MASTER:불쾌 / 12
;억울은 상승 0시 5% 감쇠(ACT가 위협하는 것 때는 감쇠하지 않는다)
CDOWN:MASTER:억울 += CUP:MASTER:억울 && !IS_NOWACTNAME("엄격하게위협한다") ? 0 # PALAM:MASTER:억울 * 5 / 100
;한층 더 억울은 위로하는 것으로 따랐을 경우 20% 감쇠
CDOWN:MASTER:억울 += IS_NOWACTNAME("상냥하게위로한다") && TFLAG:REACT분류 < 6 ? 0 # PALAM:MASTER:억울 * 20 / 100

;지문 표시
PRINTL 　
IF FLAG:PALAM애니메이션 == 2
	UPCHECK
ELSE
	CALL UPCHECK_REV
ENDIF

;CUP, CDOWN, SOURCE를 리셋트
FOR LCOUNT, 0, 50
	CVARSET CUP, LCOUNT
	CVARSET CDOWN, LCOUNT
	CVARSET SOURCE, LCOUNT
NEXT
WAIT

;전PALAM에 대해 조사
FOR LCOUNT, 0, 50
	;PALAM가 한계를 돌파했을 경우
	IF PALAM:MASTER:LCOUNT > 9999
		;초과량을 확인해, PALAM를 상한으로 설정
		LOCAL = PALAM:MASTER:LCOUNT - 9999
		PALAM:MASTER:LCOUNT = 9999
		;쾌Ｘ이외의 때, 이상경험이 적으면
		IF LCOUNT > 4 && LOCAL > EXP:MASTER:이상경험 * 50
			;이상경험 가산
			EXP:MASTER:이상경험 += 1
			;지문 표시
			PRINTL （파라미터 한계 돌파에 의해）이상경험+1
		ENDIF
	ENDIF
	;하한 돌파를 방지
	SIF PALAM:MASTER:LCOUNT < 0
		PALAM:MASTER:LCOUNT = 0
NEXT

;파라미터변동을 종료
TFLAG:파라미터변동 = 0

;────────────────────────────────────
;조교 종료의 판정
;────────────────────────────────────

;리셋트 되는 개소가 없기 때문에 여기서 초기화
FLAG:일상제어 = 0

;한번휴식
IF TFLAG:한번휴식 == 1
	;조교 후 플래그에 『한번휴식에 의한 종료』를 건네준다
	FLAG:일상제어 = 3
	;지문 표시
	PRINTL 
	;조교 종료
	BEGIN AFTERTRAIN
;통상의 조교(11 카운트 이후)
ELSEIF TFLAG:경과시간 > 10
	;조교 계속 판단
	CALL EST_T_TRA_END()
	계속기준치 = RESULT
	
	;조교 계속 기준치를 생성한다
	계속기준치 += TFLAG:경과시간 / 10
	계속기준치 += TFLAG:조교자소모도 / 2
	계속기준치 += TFLAG:소모도 / 3
	
	;경과시간에 의해 보정
	;30미만이라면 기준치 감소(-3～0)
	IF TFLAG:경과시간 < 30
		계속기준치 -= 3 - (TFLAG:경과시간 / 10)
	;100미만이라면 기준치 증가(100에가까워짐(정도)만큼 증가 속도 상승, +0～23)
	ELSEIF TFLAG:경과시간 < 100
		계속기준치 += (TFLAG:경과시간 - 30) * (TFLAG:경과시간 / 2) / 150
	;100이상이라면 기준치 증가(+23～)
	ELSE
		계속기준치 += (TFLAG:경과시간 / 10) + 13
	ENDIF
	
	;흥미에 의해 보정(낮을 정도 기준치 증가, -5～10)
	계속기준치 += 10 - LIMIT(BASE:흥미 / 40, 0, 15)
	
	;조교자 상태에 의해 보정
	;무기력(+3)
	SIF TCVAR:상태변화 == 3
		계속기준치 += 3
	;몽롱(+1)
	SIF TCVAR:상태변화 == 4
		계속기준치 += 1
	;정욕(-2)
	SIF TCVAR:상태변화 == 5
		계속기준치 -= 2
	;분노(-5)
	SIF TCVAR:상태변화 == 6
		계속기준치 -= 5
	;싫증(+5)
	SIF TCVAR:상태변화 == 7
		계속기준치 += 5
	
	;이번 조교로 대만족 보너스를 취득했을 경우 기준치 상승(+5)
	SIF TFLAG:이번에대만족 > 0
		계속기준치 += 5
	
	;기준치는 최저 1
	계속기준치 = MAX(계속기준치, 1)
	;B = TFLAG:오늘의방침 == 3? 5 # 0
	
	;기준치가 조교자 조교레벨에 의한 값을 웃돌면
	IF RAND:계속기준치 > 10 + (GET_TRAINLV(TARGET) / 20) + 5
		;정력 0의 경우, 조교 후 플래그에 『정력 0시 종료』를 건네준다
		IF BASE:MASTER:사정 <= 0
			FLAG:일상제어 = 2
		;그 외의 경우, 조교 후 플래그에 『통상의 종료』를 건네준다
		ELSE
			FLAG:일상제어 = 1
		ENDIF
		;지문, 구상 표시
		PRINTL
		CALL KOJO_EVENT(13)
		CALL VOIDLINE_IF(RESULT)
		;조교 종료
		BEGIN AFTERTRAIN
	;소모도 과잉시
	;SANDBOX 한정 처리 체력 0으로 즉실신
	ELSEIF (TFLAG:소모도 > 5 + TALENT:MASTER:작은체형 - TALENT:MASTER:장신 + TALENT:MASTER:정력절륜 - TALENT:MASTER:정력박약 + CFLAG:MASTER:조교레벨 / 3 + RAND:5) || (BASE:MASTER:체력 == 0 && FLAG:모드 == 1)
		;조교자 체력+기력은 충분히
		IF BASE:MASTER:체력 + BASE:MASTER:기력 > 600
			;지문, 구상 표시
			PRINTFORML 조교가 너무 격렬해%CALLNAME:MASTER%는 피로가 모여 있는 것 같다,%CALLNAME:TARGET%는 조교를 끝맺었다
			PRINTL
			CALL KOJO_EVENT(13, 1)
		;조교자 체력+기력이 부족
		ELSE
			;지문, 구상 표시
			PRINTFORML 조교가 너무 격렬해%CALLNAME:MASTER%는 실신했다
			CALL MARK_CHECK_GET("실신")	;【굴복:56】실신 신사양으로 변경@revkoishi(14/05/26)
			PRINTL 
			CALL KOJO_EVENT(13, 2)
		ENDIF
		;개별적으로 처리하고 있던 것을 통합. EVENT13_1은 아마 버그를 일으키고 있었을 것@/L
		;RESULT치가 0이상이라면 공행
		CALL VOIDLINE_IF(RESULT)
		;조교 후 플래그에 『실신에 의한 종료』를 건네준다
		FLAG:일상제어 = 4
		;조교 종료
		BEGIN AFTERTRAIN
	ENDIF
ENDIF

;────────────────────────────────────
;절정이나 시간 경과등에 의한 TEQUIP의 해제
;────────────────────────────────────
;※BEGIN의 해결은 그 시점에서 실행할 수 있는 모든 코드의 처리가 끝난 다음에 처음으로 행해집니다
;※TEQUIP 해제는 이 타이밍이 아니면, 조교 종료 구상으로부터 체위의 참조등을 할 수 없습니다
;※기본적으로는 전부의 TEQUIP 해제는 빠듯이까지 이끄는 것이 편리할지도 모릅니다만…
;※각처를 정합하는 것도 귀찮은 것으로 우선 필요라고 생각되는 분만큼. 나머지는 가까운 시일내에

;조교자 사정에 의한 성교 해제
TEQUIP:성교중 = NOWEX:사정 ? 0 # TEQUIP:성교중
;조교 대상 사정(계속 없음)에 의한 성교 봉사 해제
TEQUIP:성교봉사중 = NOWEX:MASTER:사정 && !TFLAG:뽑지않고 ? 0 # TEQUIP:성교봉사중
;조교자가 페니스밴드 사용시, 각종 절정에 의한 성교 해제
TEQUIP:성교중 = TEQUIP:조교자페니스밴드 && (NOWEX:Ｃ절정 || NOWEX:Ｖ절정) ? 0 # TEQUIP:성교중
;조교 대상이 페니스밴드 사용시, 각종 절정에 의한 성교 해제
TEQUIP:성교봉사중 = TEQUIP:페니스밴드 && (NOWEX:MASTER:Ｃ절정 || NOWEX:MASTER:Ｖ절정) && !TFLAG:뽑지않고 ? 0 # TEQUIP:성교봉사중

;비성교 봉사시, 뽑지않고를 해제
TFLAG:뽑지않고 = !TEQUIP:성교봉사중 ? 0 # TFLAG:뽑지않고
;조교 대상 사정(계속 없음)에 의한 C애무 해제
IF TEQUIP:조교대상Ｃ사용 && NOWEX:MASTER:사정
	SELECTCASE TEQUIP:조교대상Ｃ사용
		;자위시
		CASE 1
			;드라이사정시
			IF NOWEX:MASTER:사정 == 1
				TEQUIP:조교대상Ｃ사용 = 0
				TEQUIP:접촉손가락 = 0
			;1/2의 확률로 종료
			ELSEIF RAND:2
				TEQUIP:조교대상Ｃ사용 = 0
				TEQUIP:접촉손가락 = 0
				PRINTFORMW %CALLNAME:TARGET%는 손에 부착한다%CALLNAME:MASTER%의 정액을 맛있을 것 같게 빨아내고 있다…
			;계속
			ELSE
				PRINTFORML %CALLNAME:TARGET%는 페니스 전체에 정액을 펴발라,
				PRINTFORMW 느긋한 자위로%CALLNAME:MASTER%에 다음의 정액을 재촉 하고 있다…
			ENDIF
		;펠라치오시
		CASE 2
			;드라이사정시
			IF NOWEX:MASTER:사정 == 1
				TEQUIP:조교대상Ｃ사용 = 0
				TEQUIP:접촉입 = 0
			;1/2의 확률로 종료
			ELSEIF RAND:2
				TEQUIP:조교대상Ｃ사용 = 0
				TEQUIP:접촉입 = 0
				PRINTFORML %CALLNAME:TARGET%는 입술을 사그러들 수 있어 천천히 페니스로부터 입을 떼어 놓으면
				{
				PRINTFORMW \@ DEEP_THROAT 
				? 입안에 부착한 정액을 손가락끝 (로) 건져 취해,%CALLNAME:MASTER%에 과시하도록(듯이) 혀로 빨아냈다 
				# %CALLNAME:MASTER%에 입안의 정액을 과시하고 나서 목을 울려 삼켰다 \@…
				}
			;계속
			ELSE
				PRINTFORML %CALLNAME:TARGET%는 입안에 발해진 정액을 삼키면
				PRINTFORMW 귀두에 입맞춤 분뇨 도내의 정액을 빨아 올리고 있다…
			ENDIF
	ENDSELECT
;여자 아이 조교 대상Ｃ절정에 의한다Ｃ애무 해제
ELSEIF TEQUIP:조교대상Ｃ사용 && NOWEX:MASTER:Ｃ절정 && !PENIS(MASTER)
	SELECTCASE TEQUIP:조교대상Ｃ사용
		;애무시
		CASE 1
			;페니스밴드 자위
			IF TEQUIP:페니스밴드
				;1/2의 확률로 종료
				IF RAND:2
					TEQUIP:조교대상Ｃ사용 = 0
					TEQUIP:접촉손가락 = 0
					PRINTFORMW %CALLNAME:TARGET%는 진짜의 페니스와 같이 뛴다%CALLNAME:MASTER%의 장형으로부터 손을 떼어 놓았다…
				;계속
				ELSE
					PRINTFORML %CALLNAME:TARGET%는 절정에서 단단히 조일 수 있는 질내를 페니스밴드로 풀어,
					PRINTFORMW 느긋한 전후 운동으로%CALLNAME:MASTER%에 절정의 여운을 주고 있다…
				ENDIF
			;애무
			ELSE
				;1/2의 확률로 종료
				IF RAND:2
					TEQUIP:조교대상Ｃ사용 = 0
					TEQUIP:접촉손가락 = 0
					PRINTFORMW %CALLNAME:TARGET%는 손에 부착한다%CALLNAME:MASTER%의 애액을 맛있을 것 같게 빨아내고 있다…
				;계속
				ELSE
					PRINTFORML %CALLNAME:TARGET%는 바기나 전체에 애액을 펴발라,
					PRINTFORMW 느긋한 애무로%CALLNAME:MASTER%에 절정의 여운을 주고 있다…
				ENDIF
			ENDIF
		;쿤닐링구스시
		CASE 2
			;1/2의 확률로 종료
			IF RAND:2
				TEQUIP:조교대상Ｃ사용 = 0
				TEQUIP:접촉입 = 0
				PRINTFORML %CALLNAME:TARGET%는 키스를 하도록(듯이) 천천히 클리토리스를 빨아
				PRINTFORMW 서운한 듯이 간신히 입을 떼어 놓았다…
			;계속
			ELSE
				PRINTFORML %CALLNAME:TARGET%는 클리토리스를 들이마시면서 졸졸 혀끝에서 계속 빨고
				PRINTFORMW 강제적으로%CALLNAME:MASTER%에 깊은 절정감을 보내려고 하고 있다…
			ENDIF
	ENDSELECT
ENDIF
;조교 대상 절정에 의한 조교자 자위 해제
SIF NOWEX:MASTER:Ｃ절정 || NOWEX:MASTER:Ｖ절정 || NOWEX:MASTER:Ａ절정 || NOWEX:MASTER:Ｂ절정 || NOWEX:MASTER:사정
	CLEARBIT TEQUIP:자위중, 0
;조교자 절정에 의한 조교자 자위 해제
SIF NOWEX:Ｃ절정 || NOWEX:Ｖ절정 || NOWEX:Ａ절정 || NOWEX:Ｂ절정
	CLEARBIT TEQUIP:자위중, 1
IF FLAG:오토모드표시설정 == 1 && FLAG:오토모드 > 0
	SKIPDISP 0
	PRINTFORM [{TFLAG:경과시간,3}] %NOWACTNAME(), 24, LEFT%%TRAINNAME:SELECTCOM,24,LEFT%
	CALL PRINT_COLORBAR(BASE:MASTER:체력, MAXBASE:MASTER:체력, 16, UNICODE(0x2584), UNICODE(0x2584), 0xC07070, 0x502020)
	PRINT 　
	CALL PRINT_COLORBAR(BASE:MASTER:기력, MAXBASE:MASTER:기력, 16, UNICODE(0x2584), UNICODE(0x2584), 0x70C070, 0x205020)
	PRINTL 
	SKIPDISP 1
ENDIF
[SKIPSTART]
구상측을 대응시킬 필요가 있으므로 당분간은 comment out
v1. 020혹은 1.010용 패치 대응 예정. 여유 없지요, 지금 w(2015/01/09)
;NOWEX의 리셋트
FOR LCOUNT, 0, CHARANUM
	VARSET NOWEX:LCOUNT:0
NEXT
[SKIPEND]
CALL SHOW_AUTOMODE_UPCHECK

@SHOW_AUTOMODE_UPCHECK, ARG
#DIM ABL_M, 100
#DIM ABL_T, 100
#DIM MARK_T, 10
#DIM 성교유무, 1
#LOCALSIZE 2
SIF FLAG:오토모드표시설정 != 1
	RETURN 0
SIF FLAG:오토모드 <= 0
	RETURN 0
IF ARG
	FOR LOCAL, 0, 100
		ABL_M:LOCAL = ABL:MASTER:LOCAL
	NEXT
	FOR LOCAL, 0, 100
		ABL_T:LOCAL = ABL:TARGET:LOCAL
	NEXT
	FOR LOCAL, 0, 10
		MARK_T:LOCAL = MARK:TARGET:LOCAL
	NEXT
	성교유무 = EXP:TARGET:성교경험 == CSVEXP(NO:TARGET, GETNUM(EXP, "성교경험"), 0) ? 1 # 0
ELSE
	SKIPDISP 0
	SETCOLOR 0xC07070
	FOR LOCAL, 0, 100
		SIF ABL_M:LOCAL != ABL:MASTER:LOCAL
			PRINTFORML %CALLNAME:MASTER%의[%ABLNAME:LOCAL%]가 Lv{ABL:MASTER:LOCAL}로 상승
		ABL_M:LOCAL = ABL:MASTER:LOCAL
	NEXT
	FOR LOCAL, 0, 100
		SIF ABL_T:LOCAL != ABL:TARGET:LOCAL
			PRINTFORML %CALLNAME:TARGET%의[%ABLNAME:LOCAL%]가 Lv{ABL:TARGET:LOCAL}로 상승
		ABL_T:LOCAL = ABL:TARGET:LOCAL
	NEXT
	FOR LOCAL, 0, 10
		SIF MARK_T:LOCAL != MARK:TARGET:LOCAL
			PRINTFORML %CALLNAME:TARGET%에 의한다[%MARKNAME:LOCAL%]가 Lv{MARK:TARGET:LOCAL}로 상승
		MARK_T:LOCAL = MARK:TARGET:LOCAL
	NEXT
	IF 성교유무 && EXP:TARGET:성교경험 != CSVEXP(NO:TARGET, GETNUM(EXP, "성교경험"), 0)
		성교유무 = 0
		PRINTFORML 첫성교 발생!
	ENDIF
	RESETCOLOR
	SKIPDISP 1
ENDIF


;────────────────────────────────────
;쿨치의 계산
;────────────────────────────────────
;리액션 분류에 의한 쿨치 배율의 처리
;원칙, 호의적인 반응을 하고 있으면 히트치가 모이기 어렵다.
;주인님이 새드나 충분하고 욕구 불만이라면 심술쟁이인 대응이 되도록(듯이)해도 될지도?
;잠정적인 것으로 요점 조정인가. REACT분류를 사용해도 괜찮은가?
@COOL_BYREACT(ARG)
#FUNCTION
SELECTCASE TFLAG:REACT인상
	;좋은인상
	CASE 1, 5
		TIMES ARG, 2.00
	;약간좋은인상
	CASE 2
		TIMES ARG, 1.50
	;나쁜인상
	CASE 3
		TIMES ARG, 0.75
	;약간나쁜인상
	CASE 4, 6
		TIMES ARG, 0.50
ENDSELECT
RETURNF ARG
