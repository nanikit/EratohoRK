;─
;000, 침묵한다(벌포인트)
;─
@COM0
;질문받아 대답하지 않는 경우는 벌포인트에 가산
IF IS_NOWACTNAME("지금의기분을얘기한다/자신에게로의기분을얘기한다")
	TFLAG:벌포인트 += 1 + RAND:3 + MARK:반항각인 / 2 + BASE:초조 / 100
	;파생:질문에 답하지 않는다
	TFLAG:REACT파생 = 1
ELSE
	;파생:단순한 침묵
	TFLAG:REACT파생 = 0
ENDIF
;COM 인상
CALL SET_COMIMP("나쁜인상")
;의존도 변화
CALL ADD_DEPEND(-1, -1)
RETURN 1


;─
;001, 여리게대답한다(트라우마, 굴종, 공포, 소극적 따른다, 애원)
;─
@COM1
;트라우마화 가속
TIMES SOURCE:MASTER:트라우마 , 1.20
;굴종 SOURCE 증가
SOURCE:MASTER:굴종 += 100 + MARK:굴복각인 * 20
;ACT 조건에 의해 공포 SOURCE 증가
SIF IS_NOWACTNAME("엄격하게위협한다/벌이라고선언한다/고함친다/기분나쁘게웃는다/매도")
	SOURCE:MASTER:공포 += 200 + MARK:MASTER:트라우마 * 50
;대개의 상황은 소극적 따르는이 되지만, 특정의 상황으로 애원이 됩니다
LOCAL = PALAM:MASTER:굴복 / 1000 + PALAM:MASTER:공순 / 1000 + PALAM:MASTER:공포 / 1000 + PALAM:MASTER:고통 / 1500 + PALAM:MASTER:불쾌 / 2000
LOCAL -= PALAM:MASTER:반항 / 200 - MARK:반항각인 * 2
PRINTL 
IF (IS_NOWACTNAME("엄격하게위협한다/벌이라고선언한다/고함친다/밀어넘어뜨린다/매도")) && LOCAL > 0
	;COM 분류
	CALL SET_COMGRO("용서를빈다")
	;파생:애원 한다
	TFLAG:REACT파생 = 1
ELSE
	;COM 분류
	CALL SET_COMGRO("소극적 따른다")
	;파생:여리게대답한다
	TFLAG:REACT파생 = 0
ENDIF
PRINTL 
;COM 인상
CALL SET_COMIMP("약간좋은인상")
;의존도 변화
CALL ADD_DEPEND(0, +1)
RETURN 1


;─
;002, 무뚝뚝하게대답한다(반항, 벌포인트)
;─
@COM2
;반항 SOURCE 증가
SOURCE:MASTER:반항 += 200
;벌포인트 가산
TFLAG:벌포인트 += RAND:5 + MARK:반항각인 / 2 + BASE:초조 / 100 - BASE:이성 / 100
;COM 인상
CALL SET_COMIMP("나쁜인상")
;의존도 변화
CALL ADD_DEPEND(-2, -2)
RETURN 1


;─
;003, 붙임성좋게대답한다(달성, 열락, 적극적 따른다)
;─
@COM3
;달성, 열락 SOURCE 증가
SOURCE:MASTER:달성 += 200
SOURCE:MASTER:열락 += 50 + MARK:MASTER:쾌락각인 * 20 + SOURCE:MASTER:쾌C / 8 + SOURCE:MASTER:쾌V / 8 + SOURCE:MASTER:쾌A / 8 + SOURCE:MASTER:쾌B / 8
;COM 분류
CALL SET_COMGRO("적극적 따른다")
;COM 인상
CALL SET_COMIMP("좋은인상")
;의존도 변화
CALL ADD_DEPEND(+3, +1)
RETURN 1


;─
;004, 강하게대답한다(반항, 벌포인트)
;─
@COM4
;반항 SOURCE 증가
SOURCE:MASTER:반항 += 500 + MARK:반항각인 * 10
;벌포인트 가산
TFLAG:벌포인트 += 5 + MARK:반항각인 / 2 + BASE:초조 / 100 - BASE:흥미 / 100
;ACT 조건에 의해 파생
IF IS_NOWACTNAME("밀어넘어뜨린다")
	;파생:밀어넘어뜨리기에 굽히지 않는다
	TFLAG:REACT파생 = 1
ELSE
	;파생:강하게 응답
	TFLAG:REACT파생 = 0
ENDIF
;COM 인상
CALL SET_COMIMP("나쁜인상")
;의존도 변화
CALL ADD_DEPEND(-1, -3)
RETURN 1


;─
;005, 빈정거린다(굴종, 반항, 소극적 따른다, 벌포인트)
;─
@COM5
;굴종, 반항 SOURCE 증가
SOURCE:MASTER:굴종 += 250 - MARK:반항각인 * 20
SOURCE:MASTER:반항 += 250 - MARK:굴복각인 * 20
;벌포인트 가산
TFLAG:벌포인트 += 3 + RAND:5 + MARK:반항각인 / 5
;질문이 아니면 태연하게 따르는 분류
SIF !IS_NOWACTNAME("지금의기분을얘기한다/자신에게로의기분을얘기한다")
	CALL SET_COMGRO("태연하게 따른다")
;COM 인상
CALL SET_COMIMP("나쁜인상")
;의존도 변화
CALL ADD_DEPEND(-3, -1)
RETURN 1


;─
;006, 더하드하게(굴종, 벌포인트)
;─
@COM6
;굴종 SOURCE 증가
SOURCE:MASTER:굴종 += 50 + MARK:MASTER:고통각인 * 20 + GET_ABL(MASTER, "마조끼") * CFLAG:MASTER:조교레벨 * 2
;이 철면피의 암퇘지놈! 라든지 말하면서 벌이 됩니다
LOCAL = BASE:초조 / 100 + TALENT:새디즘 * 3 + TALENT:심술쟁이 * 5 - TALENT:상냥함 * 5 - MARK:굴복각인 - CFLAG:아라이먼트 / 10 + RAND:5
IF LOCAL > 0
	;벌포인트 가산
	TFLAG:벌포인트 += LOCAL
	;파생:「이 철면피의 암퇘지놈!」
	TFLAG:REACT파생 = 1
ELSE
	;파생:「흐음」
	TFLAG:REACT파생 = 0
ENDIF
CALL SET_COMGRO("받아들인다")
;COM 인상
CALL SET_COMIMP("0에서멀어짐")
;의존도 변화
CALL ADD_DEPEND(0, +3)
RETURN 1
;쾌SOURCE에 응해 REACTION_MESSAGE로 분기 시킨다면, REACT파생에도 반영시켜야 할 생각이 든다
;조건 그대로 쓰면 참조 가능하지만, 그러한 문제는 아니라고 생각합니다@/L

;─
;007, 용서를빈다(굴종, 공포, 애원, 벌포인트)
;─
@COM7
;굴종, 공포 SOURCE 증가
SOURCE:MASTER:굴종 += 200 + MARK:굴복각인 * MARK:MASTER:트라우마 * 10
SOURCE:MASTER:공포 += SOURCE:MASTER:아픔 / 3 + SOURCE:MASTER:구속 / 5 + SOURCE:MASTER:일탈 / 4 + SOURCE:MASTER:트라우마 / 3
;의견을 내다니 좋은 담력이구나! 라든지 말하면서 벌이 됩니다
LOCAL = BASE:초조 / 80 - BASE:이성 / 100 - CFLAG:아라이먼트 / 10 + PALAM:MASTER:반항 / 1000 + TALENT:새디즘 * 5 + TALENT:심술쟁이 * 5 - TALENT:상냥함 * 5 - MARK:MASTER:트라우마 / 2
SIF IS_POLICY("지금", "이상")
	LOCAL += RAND:7
;COM 인상이 공통된 것은 납득 가지 않았기 때문에를 파생마다별 찬다@/L
IF LOCAL > 0 && !IS_NOWACTNAME("공중육변기플레이")
	;벌포인트 가산
	TFLAG:벌포인트 += LOCAL
	;파생:「의견을 내다니 좋은 담력이구나!」
	TFLAG:REACT파생 = 1
	;COM 인상
	CALL SET_COMIMP("0에서멀어짐")
ELSE
	;파생:「흐음」
	TFLAG:REACT파생 = 0
	;COM 분류
	CALL SET_COMGRO("용서를빈다")
	;COM 인상
	CALL SET_COMIMP("0에가까워짐")
ENDIF
;의존도 변화
CALL ADD_DEPEND(0, +2)
RETURN 1


;─
;008, 기분좋게해줘(열락, 받아들인다, 벌포인트)
;─
@COM8
;열락 SOURCE 증가
SOURCE:MASTER:열락 += PALAM:MASTER:욕정 / 5 + CFLAG:MASTER:조교레벨 * GET_ABL(MASTER, "욕망") * MARK:MASTER:쾌락각인 / 10
;이 철면피의 암퇘지놈! 라든지 말하면서 벌이 됩니다
LOCAL = BASE:초조 / 100 - CFLAG:아라이먼트 / 10 - CFLAG:MASTER:조교레벨 + TALENT:새디즘 * 3 + TALENT:심술쟁이 * 5 + MARK:MASTER:쾌락각인 / 2 + RAND:5
IF LOCAL > 0
	;벌포인트 가산
	TFLAG:벌포인트 += LOCAL
	;파생:「이 철면피의 암퇘지놈!」
	TFLAG:REACT파생 = 1
ELSE
	;파생:「흐음」
	TFLAG:REACT파생 = 0
ENDIF
;COM 분류
CALL SET_COMGRO("받아들인다")
;COM 인상
CALL SET_COMIMP("0에서멀어짐")
;의존도 변화
CALL ADD_DEPEND(+2, +1)
RETURN 1
