;────────────────────────────────────
;020, 허리를흔드는（쾌ＣＶＡ보정, 열락, 달성）
;────────────────────────────────────
@COM20
;자신이 탓하는 측의 경우（성교 봉사, 기승위등으로 허리를 사용하는 느끼게 한다）
IF SEX_INITIATIVE(MASTER)
	;COM 분류
	CALL SET_COMGRO("적극적으로 한다")
	;달성 SOURCE 증가
	SOURCE:MASTER:달성 += (CFLAG:MASTER:조교레벨 + 5) * (GET_ABL(MASTER, "봉사정신") + 50) + (GET_ABL(MASTER, "기교") + 50) * 20 / 100
	;의존도 변화
	CALL ADD_DEPEND(+5, +3)
;자신이 탓할 수 있는 측의 경우（역강간, 성교계등으로 마중허리를 사용해 쾌감을 탐내는, 파이즈리나 가랑이로 쾌감을 추구한다）
ELSE
	;COM 분류
	CALL SET_COMGRO("받아들인다")
	;열락 SOURCE 증가
	SOURCE:MASTER:열락 += 250 + SOURCE:MASTER:쾌Ｃ / 3 + SOURCE:MASTER:쾌Ｖ / 3 + SOURCE:MASTER:쾌Ａ / 3 + (CFLAG:MASTER:조교레벨 + 1) * (GET_ABL(MASTER, "기교") + 10) * 1
	;의존도 변화
	CALL ADD_DEPEND(+4, +8)
ENDIF
;COM 인상
CALL SET_COMIMP("좋은인상")
;인내 플래그 해소
TCVAR:MASTER:인내 = 0
RETURN 1

;────────────────────────────────────
;021, 안에내！（달성, 열락, 적극적으로 한다）
;────────────────────────────────────
@COM21
LOCAL = (CFLAG:M호의 - 500) / 20
LOCAL = LIMIT(LOCAL, 1, 200)
;달성 SOURCE 증가
SOURCE:MASTER:달성 += 100 + LOCAL * (SOURCE:MASTER:쾌Ｃ + SOURCE:MASTER:쾌Ｖ + SOURCE:MASTER:쾌Ａ) / 1000
;열락 SOURCE 증가
IF TEQUIP:성교중 == 6
	SOURCE:MASTER:열락 += 100 + SOURCE:MASTER:쾌Ａ / 4 + SOURCE:쾌Ｃ / 3 + (CFLAG:MASTER:조교레벨 + 1) * (GET_ABL(MASTER, "기교") + 10) * CFLAG:M호의 / 1500
ELSE
	SOURCE:MASTER:열락 += 250 + SOURCE:MASTER:쾌Ｖ / 2 + SOURCE:쾌Ｃ / 3 + (CFLAG:MASTER:조교레벨 + 1) * (GET_ABL(MASTER, "기교") + 10) * CFLAG:M호의 / 1000
ENDIF
;조교 대상이 남자 혹은Ａ성교중
IF TALENT:MASTER:남자 || TEQUIP:성교중 == 6
	;파생:Ａ에 내
	TFLAG:REACT파생 = 1
ELSE
	;파생:Ｖ에 내
	TFLAG:REACT파생 = 0
ENDIF
;COM 분류
CALL SET_COMGRO("받아들인다")
;COM 인상
CALL SET_COMIMP("좋은인상")
;인내 플래그 해소
TCVAR:MASTER:인내 = 0
;의존도 변화
CALL ADD_DEPEND(+10, +10)
RETURN 1

;────────────────────────────────────
;022, 안에내지마！（반항, 공포, 애원, 벌포인트）
;────────────────────────────────────
@COM22
;반항, 공포 SOURCE 증가
SOURCE:MASTER:반항 += 300 + (MARK:반항각인 + 5) * 50
SOURCE:MASTER:공포 += (100 + SOURCE:MASTER:쾌Ｖ / 2 + SOURCE:MASTER:쾌Ａ / 10) * (MARK:MASTER:트라우마 + 10) / 10
;소원을 들어줄까의 체크. 꽤 어바웃
IF BASE:만족 / 100 + CFLAG:아라이먼트 / 10 + (CFLAG:M호의 - 500) / 100 > BASE:초조 / 100 + RAND:5
	;COM 분류
	CALL SET_COMGRO("용서를빈다")
	;파생:중에 내지 않는다
	TFLAG:REACT파생 = 0
ELSE
	;벌포인트 가산
	TFLAG:벌포인트 += 1 + RAND:3 + BASE:초조 / 200
	;파생:중에 낸다
	TFLAG:REACT파생 = 1
ENDIF
;COM 인상
CALL SET_COMIMP("나쁜인상")
;의존도 변화
CALL ADD_DEPEND(0, -10)
RETURN 1
